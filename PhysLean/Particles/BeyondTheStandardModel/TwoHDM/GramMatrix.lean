/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.BeyondTheStandardModel.TwoHDM.Basic
/-!

# The gram matrix for the two Higgs doublet model

The main reference for material in this section is https://arxiv.org/pdf/hep-ph/0605184.

We will show that the gram matrix of the two Higgs doublet model
describes the gauge orbits of the configuration space.

-/
namespace TwoHiggsDoublet

open InnerProductSpace
open StandardModel

/-!

## C. The Gram matrix

-/

/-- The Gram matrix of the two Higgs doublet.
  This matrix is used in https://arxiv.org/abs/hep-ph/0605184. -/
noncomputable def gramMatrix (H : TwoHiggsDoublet) : Matrix (Fin 2) (Fin 2) ‚ÑÇ :=
  !![‚ü™H.Œ¶1, H.Œ¶1‚ü´_‚ÑÇ, ‚ü™H.Œ¶2, H.Œ¶1‚ü´_‚ÑÇ; ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ, ‚ü™H.Œ¶2, H.Œ¶2‚ü´_‚ÑÇ]

lemma gramMatrix_selfAdjoint (H : TwoHiggsDoublet) :
    IsSelfAdjoint (gramMatrix H) := by
  rw [gramMatrix]
  ext i j
  fin_cases i <;> fin_cases j <;> simp [inner_conj_symm]

lemma eq_fst_norm_of_eq_gramMatrix {H1 H2 : TwoHiggsDoublet}
    (h : H1.gramMatrix = H2.gramMatrix) : ‚ÄñH1.Œ¶1‚Äñ = ‚ÄñH2.Œ¶1‚Äñ := by
  rw [gramMatrix, gramMatrix] at h
  have h1 := congrArg (fun x => x 0 0) h
  simp only [Matrix.of_apply, Matrix.cons_val', Matrix.cons_val_zero, Fin.isValue] at h1
  rw [inner_self_eq_norm_sq_to_K, inner_self_eq_norm_sq_to_K] at h1
  rw [sq_eq_sq_iff_eq_or_eq_neg] at h1
  rcases h1 with h1 | h1
  ¬∑ simpa using h1
  ¬∑ rw [‚Üê RCLike.ofReal_neg] at h1
    have hnorm1 : 0 ‚â§ ‚ÄñH1.Œ¶1‚Äñ := norm_nonneg H1.Œ¶1
    have hnorm2 : 0 ‚â§ ‚ÄñH2.Œ¶1‚Äñ := norm_nonneg H2.Œ¶1
    have hl : ‚ÄñH1.Œ¶1‚Äñ = (-‚ÄñH2.Œ¶1‚Äñ) := Eq.symm
      ((fun {z w} => Complex.ofReal_inj.mp) (id (Eq.symm h1)))
    grind

lemma eq_snd_norm_of_eq_gramMatrix {H1 H2 : TwoHiggsDoublet}
    (h : H1.gramMatrix = H2.gramMatrix) : ‚ÄñH1.Œ¶2‚Äñ = ‚ÄñH2.Œ¶2‚Äñ := by
  rw [gramMatrix, gramMatrix] at h
  have h1 := congrArg (fun x => x 1 1) h
  simp [Matrix.of_apply, Matrix.cons_val', Matrix.cons_val_one, Fin.isValue] at h1
  rw [sq_eq_sq_iff_eq_or_eq_neg] at h1
  rcases h1 with h1 | h1
  ¬∑ simpa using h1
  ¬∑ erw [‚Üê RCLike.ofReal_neg] at h1
    have hnorm1 : 0 ‚â§ ‚ÄñH1.Œ¶2‚Äñ := norm_nonneg H1.Œ¶2
    have hnorm2 : 0 ‚â§ ‚ÄñH2.Œ¶2‚Äñ := norm_nonneg H2.Œ¶2
    have hl : ‚ÄñH1.Œ¶2‚Äñ = (-‚ÄñH2.Œ¶2‚Äñ) := Eq.symm
      ((fun {z w} => Complex.ofReal_inj.mp) (id (Eq.symm h1)))
    grind

@[simp]
lemma gaugeGroupI_smul_gramMatrix (g : StandardModel.GaugeGroupI) (H : TwoHiggsDoublet) :
    (g ‚Ä¢ H).gramMatrix = H.gramMatrix := by
  rw [gramMatrix, gramMatrix, gaugeGroupI_smul_fst, gaugeGroupI_smul_snd]
  ext i j
  fin_cases i <;> fin_cases j <;> simp

lemma gramMatrix_det_eq (H : TwoHiggsDoublet) :
    H.gramMatrix.det = ‚ÄñH.Œ¶1‚Äñ ^ 2 * ‚ÄñH.Œ¶2‚Äñ ^ 2 - ‚Äñ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ‚Äñ ^ 2 := by
  rw [gramMatrix, Matrix.det_fin_two]
  simp only [inner_self_eq_norm_sq_to_K, Complex.coe_algebraMap, Fin.isValue, Matrix.of_apply,
    Matrix.cons_val', Matrix.cons_val_zero, Matrix.cons_val_fin_one, Matrix.cons_val_one,
    sub_right_inj]
  rw [‚Üê Complex.conj_mul']
  simp only [inner_conj_symm]

lemma gramMatrix_det_eq_real (H : TwoHiggsDoublet) :
    H.gramMatrix.det.re = ‚ÄñH.Œ¶1‚Äñ ^ 2 * ‚ÄñH.Œ¶2‚Äñ ^ 2 - ‚Äñ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ‚Äñ ^ 2 := by
  rw [gramMatrix_det_eq]
  simp [‚Üê Complex.ofReal_pow, Complex.ofReal_im]

lemma gramMatrix_det_nonneg (H : TwoHiggsDoublet) :
    0 ‚â§ H.gramMatrix.det.re := by
  rw [gramMatrix_det_eq_real]
  simp only [sub_nonneg]
  convert inner_mul_inner_self_le (ùïú := ‚ÑÇ) H.Œ¶1 H.Œ¶2
  ¬∑ simp [sq, norm_inner_symm]
  ¬∑ exact norm_sq_eq_re_inner H.Œ¶1
  ¬∑ exact norm_sq_eq_re_inner H.Œ¶2

lemma gramMatrix_tr_nonneg (H : TwoHiggsDoublet) :
    0 ‚â§ H.gramMatrix.trace.re := by
  rw [gramMatrix, Matrix.trace_fin_two]
  simp only [inner_self_eq_norm_sq_to_K, Complex.coe_algebraMap, Fin.isValue, Matrix.of_apply,
    Matrix.cons_val', Matrix.cons_val_zero, Matrix.cons_val_fin_one, Matrix.cons_val_one,
    Complex.add_re]
  apply add_nonneg
  ¬∑ rw [‚Üê Complex.ofReal_pow, Complex.ofReal_re]
    exact sq_nonneg ‚ÄñH.Œ¶1‚Äñ
  ¬∑ rw [‚Üê Complex.ofReal_pow, Complex.ofReal_re]
    exact sq_nonneg ‚ÄñH.Œ¶2‚Äñ

lemma gaugeGroupI_exists_fst_eq {H : TwoHiggsDoublet} (h1 : H.Œ¶1 ‚â† 0) :
    ‚àÉ g : StandardModel.GaugeGroupI,
      g ‚Ä¢ H.Œ¶1 = (!‚ÇÇ[‚ÄñH.Œ¶1‚Äñ, 0] : HiggsVec) ‚àß
      (g ‚Ä¢ H.Œ¶2) 0 = ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ / ‚ÄñH.Œ¶1‚Äñ ‚àß
      ‚Äñ(g ‚Ä¢ H.Œ¶2) 1‚Äñ = Real.sqrt (H.gramMatrix.det.re) / ‚ÄñH.Œ¶1‚Äñ := by
  rw [gramMatrix_det_eq_real]
  obtain ‚ü®g, h‚ü© := (HiggsVec.mem_orbit_gaugeGroupI_iff (H.Œ¶1) (!‚ÇÇ[‚ÄñH.Œ¶1‚Äñ, 0] : HiggsVec)).mpr
    (by simp [@PiLp.norm_eq_of_L2])
  use g
  simp at h
  simp [h]
  have h_fst : (g ‚Ä¢ H.Œ¶2).ofLp 0 = ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ / ‚ÄñH.Œ¶1‚Äñ := by
    have h2 : ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ = ‚ü™g ‚Ä¢ H.Œ¶1, g ‚Ä¢ H.Œ¶2‚ü´_‚ÑÇ := by
      simp
    rw [h] at h2
    conv_rhs at h2 =>
      simp [PiLp.inner_apply]
    rw [h2]
    have hx : (‚ÄñH.Œ¶1‚Äñ : ‚ÑÇ) ‚â† 0 := by
      simp_all
    field_simp
  apply And.intro h_fst
  have hx : ‚Äñg ‚Ä¢ H.Œ¶2‚Äñ ^ 2 = ‚ÄñH.Œ¶2‚Äñ ^ 2 := by
    simp
  rw [PiLp.norm_sq_eq_of_L2] at hx
  simp at hx
  have hx0 : ‚Äñ(g ‚Ä¢ H.Œ¶2).ofLp 1‚Äñ ^ 2 = ‚ÄñH.Œ¶2‚Äñ ^ 2 - ‚Äñ(g ‚Ä¢ H.Œ¶2).ofLp 0‚Äñ ^ 2 := by
    rw [‚Üê hx]
    simp
  have h0 : ‚Äñ(g ‚Ä¢ H.Œ¶2) 1‚Äñ ^ 2 = (‚ÄñH.Œ¶1‚Äñ ^ 2 * ‚ÄñH.Œ¶2‚Äñ ^ 2 - ‚Äñ‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ‚Äñ ^ 2) / ‚ÄñH.Œ¶1‚Äñ ^ 2 := by
    field_simp
    rw [hx0, h_fst]
    simp only [Fin.isValue, Complex.norm_div, Complex.norm_real, norm_norm]
    ring_nf
    field_simp
  have habc (a b c : ‚Ñù) (ha : 0 ‚â§ a) (hx : a ^ 2 = b / c ^2) (hc : c ‚â† 0) (hc : 0 < c) :
      a = Real.sqrt b / c := by
    field_simp
    symm
    have hb : b = a ^ 2 * c ^ 2 := by
      rw [hx]
      field_simp
    subst hb
    rw [Real.sqrt_eq_iff_eq_sq]
    ¬∑ ring
    ¬∑ positivity
    ¬∑ positivity
  apply habc
  rw [h0]
  ring_nf
  ¬∑ exact norm_ne_zero_iff.mpr h1
  ¬∑ simpa using h1
  ¬∑ exact norm_nonneg ((g ‚Ä¢ H.Œ¶2).ofLp 1)

lemma gaugeGroupI_exists_fst_eq_snd_eq {H : TwoHiggsDoublet} (h1 : H.Œ¶1 ‚â† 0) :
    ‚àÉ g : StandardModel.GaugeGroupI,
      g ‚Ä¢ H.Œ¶1 = (!‚ÇÇ[‚ÄñH.Œ¶1‚Äñ, 0] : HiggsVec) ‚àß
      g ‚Ä¢ H.Œ¶2 = (!‚ÇÇ[‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ / ‚ÄñH.Œ¶1‚Äñ, ‚àö(H.gramMatrix.det.re) / ‚ÄñH.Œ¶1‚Äñ] : HiggsVec) := by
  obtain ‚ü®g, h_fst, h_snd_0, h_snd_1‚ü© := gaugeGroupI_exists_fst_eq h1
  obtain ‚ü®k, h1, h2, h3‚ü© := HiggsVec.gaugeGroupI_smul_phase_snd (g ‚Ä¢ H.Œ¶2)
  use k * g
  apply And.intro
  ¬∑ rw [mul_smul, h_fst, h3]
  ¬∑ rw [mul_smul]
    ext i
    fin_cases i
    ¬∑ simp
      rw [h2, h_snd_0]
    ¬∑ simp
      rw [h1, h_snd_1]
      simp

lemma mem_orbit_gaugeGroupI_iff_gramMatrix (H1 H2 : TwoHiggsDoublet) :
    H1 ‚àà MulAction.orbit GaugeGroupI H2 ‚Üî H1.gramMatrix = H2.gramMatrix := by
  apply Iff.intro
  ¬∑ intro h
    obtain ‚ü®g, hg‚ü© := h
    simp at hg
    simp [‚Üê hg]
  by_cases Œ¶1_zero : H1.Œ¶1 = 0
  ¬∑ intro h
    obtain ‚ü®g1, hg1‚ü© := (HiggsVec.mem_orbit_gaugeGroupI_iff (H1.Œ¶2) (!‚ÇÇ[‚ÄñH1.Œ¶2‚Äñ, 0] : HiggsVec)).mpr
      (by simp [@PiLp.norm_eq_of_L2])
    obtain ‚ü®g2, hg2‚ü© := (HiggsVec.mem_orbit_gaugeGroupI_iff (H2.Œ¶2) (!‚ÇÇ[‚ÄñH2.Œ¶2‚Äñ, 0] : HiggsVec)).mpr
      (by simp [@PiLp.norm_eq_of_L2])
    use g1‚Åª¬π * g2
    simp only
    ext:1
    ¬∑ simp [Œ¶1_zero]
      have hnorm : ‚ÄñH2.Œ¶1‚Äñ = ‚ÄñH1.Œ¶1‚Äñ := by
        symm
        rw [‚Üê eq_fst_norm_of_eq_gramMatrix h]
      simp [Œ¶1_zero] at hnorm
      simp [hnorm]
    ¬∑ simp [mul_smul]
      refine inv_smul_eq_iff.mpr ?_
      simp at hg1 hg2
      simp [hg1, hg2]
      exact eq_snd_norm_of_eq_gramMatrix (id (Eq.symm h))
  ¬∑ intro h
    obtain ‚ü®g1, H1_Œ¶1, H1_Œ¶2‚ü© := gaugeGroupI_exists_fst_eq_snd_eq (H := H1) Œ¶1_zero
    have Œ¶2_nezero : H2.Œ¶1 ‚â† 0 := by
      intro hzero
      have hnorm : ‚ÄñH1.Œ¶1‚Äñ = ‚ÄñH2.Œ¶1‚Äñ := by
        rw [‚Üê eq_fst_norm_of_eq_gramMatrix h]
      simp [hzero] at hnorm
      simp [hnorm] at Œ¶1_zero
    obtain ‚ü®g2, H2_Œ¶1, H2_Œ¶2‚ü© := gaugeGroupI_exists_fst_eq_snd_eq (H := H2) Œ¶2_nezero
    use g1‚Åª¬π * g2
    simp only
    ext:1
    ¬∑ simp [mul_smul]
      refine inv_smul_eq_iff.mpr ?_
      simp [H1_Œ¶1, H2_Œ¶1]
      apply eq_fst_norm_of_eq_gramMatrix (id (Eq.symm h))
    ¬∑ simp [mul_smul]
      refine inv_smul_eq_iff.mpr ?_
      simp [H1_Œ¶2, H2_Œ¶2]
      apply And.intro
      ¬∑ congr 1
        ¬∑ symm
          exact congrArg (fun x => x 1 0) h
        ¬∑ simp only [Complex.ofReal_inj]
          exact eq_fst_norm_of_eq_gramMatrix (id (Eq.symm h))
      ¬∑ congr 2
        ¬∑ simp [h]
        ¬∑ exact eq_fst_norm_of_eq_gramMatrix (id (Eq.symm h))

/-!

### C.2. The Gram vector

-/

/-- A real vector containing the components of the Gram matrix in the Pauli basis. -/
noncomputable def gramVector (H : TwoHiggsDoublet) : Fin 1 ‚äï Fin 3 ‚Üí ‚Ñù := fun Œº =>
  2 * PauliMatrix.pauliBasis.repr ‚ü®gramMatrix H, gramMatrix_selfAdjoint H‚ü© Œº

@[simp]
lemma gaugeGroupI_smul_fst_gramVector (g : StandardModel.GaugeGroupI)
    (H : TwoHiggsDoublet) (Œº : Fin 1 ‚äï Fin 3) :
    (g ‚Ä¢ H).gramVector Œº = H.gramVector Œº := by
  rw [gramVector, gramVector]
  congr 1
  simp

lemma gramMatrix_eq_gramVector_sum_pauliMatrix (H : TwoHiggsDoublet) :
    gramMatrix H = (1 / 2 : ‚Ñù) ‚Ä¢ ‚àë Œº, H.gramVector Œº ‚Ä¢ PauliMatrix.pauliMatrix Œº := by
  have h1 := congrArg (fun x => x.1) <|
    PauliMatrix.pauliBasis.sum_repr ‚ü®gramMatrix H, gramMatrix_selfAdjoint H‚ü©
  simp [-Module.Basis.sum_repr] at h1
  rw [‚Üê h1]
  simp [gramVector, smul_smul, Finset.smul_sum]
  congr 1
  ¬∑ simp [PauliMatrix.pauliBasis, PauliMatrix.pauliSelfAdjoint]
  ¬∑ simp [PauliMatrix.pauliBasis, PauliMatrix.pauliSelfAdjoint]

lemma gramMatrix_eq_component_gramVector (H : TwoHiggsDoublet) :
    gramMatrix H =
    !![(1 / 2 : ‚ÑÇ) * (H.gramVector (Sum.inl 0) + H.gramVector (Sum.inr 2)),
      (1 / 2 : ‚ÑÇ) * (H.gramVector (Sum.inr 0) - Complex.I * H.gramVector (Sum.inr 1));
      (1 / 2 : ‚ÑÇ) * (H.gramVector (Sum.inr 0) + Complex.I * H.gramVector (Sum.inr 1)),
      (1 / 2 : ‚ÑÇ) * (H.gramVector (Sum.inl 0) - H.gramVector (Sum.inr 2))] := by
  rw [gramMatrix_eq_gramVector_sum_pauliMatrix]
  simp only [one_div, PauliMatrix.pauliMatrix, Matrix.one_fin_two, Fintype.sum_sum_type,
    Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Finset.sum_singleton, Matrix.smul_of,
    Matrix.smul_cons, Complex.real_smul, mul_one, smul_zero, Matrix.smul_empty, Fin.sum_univ_three,
    smul_neg, Matrix.of_add_of, Matrix.add_cons, Matrix.head_cons, add_zero, Matrix.tail_cons,
    Matrix.empty_add_empty, zero_add, smul_add, Complex.ofReal_inv, Complex.ofReal_ofNat,
    EmbeddingLike.apply_eq_iff_eq, Matrix.vecCons_inj, and_true]
  ring_nf
  simp

lemma gramVector_inl_eq_trace_gramMatrix (H : TwoHiggsDoublet) :
    H.gramVector (Sum.inl 0) = H.gramMatrix.trace := by
  rw [gramMatrix_eq_component_gramVector, Matrix.trace_fin_two]
  simp only [Fin.isValue, one_div, Matrix.of_apply, Matrix.cons_val', Matrix.cons_val_zero,
    Matrix.cons_val_fin_one, Matrix.cons_val_one]
  ring

lemma normSq_Œ¶1_eq_gramVector (H : TwoHiggsDoublet) :
    ‚ÄñH.Œ¶1‚Äñ ^ 2 = (1/2 : ‚Ñù) * (H.gramVector (Sum.inl 0) + H.gramVector (Sum.inr 2)) := by
  trans (gramMatrix H 0 0).re
  ¬∑ simp [gramMatrix]
    rw [‚Üê Complex.ofReal_pow, Complex.ofReal_re]
  ¬∑ rw [gramMatrix_eq_component_gramVector]
    simp

lemma normSq_Œ¶2_eq_gramVector (H : TwoHiggsDoublet) :
    ‚ÄñH.Œ¶2‚Äñ ^ 2 = (1/2 : ‚Ñù) * (H.gramVector (Sum.inl 0) - H.gramVector (Sum.inr 2)) := by
  trans (gramMatrix H 1 1).re
  ¬∑ simp [gramMatrix]
    rw [‚Üê Complex.ofReal_pow, Complex.ofReal_re]
  ¬∑ rw [gramMatrix_eq_component_gramVector]
    simp

lemma Œ¶1_inner_Œ¶2_eq_gramVector (H : TwoHiggsDoublet) :
    (‚ü™H.Œ¶1, H.Œ¶2‚ü´_‚ÑÇ) = (1/2 : ‚Ñù) * (H.gramVector (Sum.inr 0) +
    Complex.I * H.gramVector (Sum.inr 1)) := by
  trans (gramMatrix H 1 0)
  ¬∑ simp [gramMatrix]
  ¬∑ simp [gramMatrix_eq_component_gramVector]

lemma Œ¶2_inner_Œ¶1_eq_gramVector (H : TwoHiggsDoublet) :
    (‚ü™H.Œ¶2, H.Œ¶1‚ü´_‚ÑÇ) = (1/2 : ‚Ñù) * (H.gramVector (Sum.inr 0) -
    Complex.I * H.gramVector (Sum.inr 1)) := by
  trans (gramMatrix H 0 1)
  ¬∑ simp [gramMatrix]
  ¬∑ simp [gramMatrix_eq_component_gramVector]

end TwoHiggsDoublet
