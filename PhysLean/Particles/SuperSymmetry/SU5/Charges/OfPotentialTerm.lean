/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.Charges.OfFieldLabel
import Mathlib.Tactic.Abel
/-!

# Charges associated with a potential term

Given a potential term `T`, and a charge `x : Charges`,
we can extract the set of charges associated with instances of that potential term.

-/

namespace SuperSymmetry
namespace SU5

namespace Charges
open PotentialTerm

variable {ğ“© : Type} [AddCommGroup ğ“©]

/-- Given a charges `x : Charges` associated to the representations, and a potential
  term `T`, the charges associated with instances of that potential term. -/
def ofPotentialTerm (x : Charges ğ“©) (T : PotentialTerm) : Multiset ğ“© :=
  let add : Multiset ğ“© â†’ Multiset ğ“© â†’ Multiset ğ“© := fun a b => (a.product b).map
      fun (x, y) => x + y
  (T.toFieldLabel.map fun F => (ofFieldLabel x F).val).foldl add {0}

lemma ofPotentialTerm_mono {x y : Charges ğ“©} (h : x âŠ† y) (T : PotentialTerm) :
    x.ofPotentialTerm T âŠ† y.ofPotentialTerm T := by
  have h1 {S1 S2 T1 T2 : Multiset ğ“©} (h1 : S1 âŠ† S2) (h2 : T1 âŠ† T2) :
      (S1.product T1) âŠ† S2.product T2 :=
    Multiset.subset_iff.mpr (fun x => by simpa using fun h1' h2' => âŸ¨h1 h1', h2 h2'âŸ©)
  rw [subset_def] at h
  cases T
  all_goals
    simp [ofPotentialTerm, PotentialTerm.toFieldLabel]
    repeat'
      apply Multiset.map_subset_map <| Multiset.subset_iff.mpr <|
        h1 _ (Finset.subset_def.mp (ofFieldLabel_mono h _))
    simp

@[simp]
lemma ofPotentialTerm_empty (T : PotentialTerm) :
    ofPotentialTerm (âˆ… : Charges ğ“©) T = âˆ… := by
  cases T
  all_goals
    rfl

/-- Given a charges `x : Charges` associated to the representations, and a potential
  term `T`, the charges associated with instances of that potential term.

  This is a more explicit form of `PotentialTerm`, which has the benifit that
  it is quick with `decide`, but it is not defined based on more fundamental
  concepts, like `ofPotentialTerm` is. -/
def ofPotentialTerm' (y : Charges ğ“©) (T : PotentialTerm) : Multiset ğ“© :=
  let qHd := y.1
  let qHu := y.2.1
  let Q5 := y.2.2.1
  let Q10 := y.2.2.2
  match T with
  | Î¼ =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => {qHd - qHu}
  | Î² =>
    match qHu with
    | none => âˆ…
    | some qHu => Q5.val.map (fun x => - qHu + x)
  | Î› => (Q5.product <| Q5.product <| Q10).val.map (fun x => x.1 + x.2.1 + x.2.2)
  | W1 => (Q5.product <| Q10.product <| Q10.product <| Q10).val.map
    (fun x => x.1 + x.2.1 + x.2.2.1 + x.2.2.2)
  | W2 =>
    match qHd with
    | none => âˆ…
    | some qHd =>
      (Q10.product <| Q10.product <| Q10).val.map (fun x => qHd + x.1 + x.2.1 + x.2.2)
  | W3 =>
    match qHu with
    | none => âˆ…
    | some qHu => (Q5.product <| Q5).val.map (fun x => -qHu - qHu + x.1 + x.2)
  | W4 =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => Q5.val.map (fun x => qHd - qHu - qHu + x)
  | K1 => (Q5.product <| Q10.product <| Q10).val.map
    (fun x => - x.1 + x.2.1 + x.2.2)
  | K2 =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => Q10.val.map (fun x => qHd + qHu + x)
  | topYukawa =>
    match qHu with
    | none => âˆ…
    | some qHu => (Q10.product <| Q10).val.map (fun x => - qHu + x.1 + x.2)
  | bottomYukawa =>
    match qHd with
    | none => âˆ…
    | some qHd => (Q5.product <| Q10).val.map (fun x => qHd + x.1 + x.2)

lemma ofPotentialTerm'_Î¼_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' Î¼ =
    (x.1.toFinset.product <| x.2.1.toFinset).val.map (fun x => x.1 - x.2) := by
  match x with
  | (none, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_Î²_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' Î² =
    (x.2.1.toFinset.product <| x.2.2.1).val.map (fun x => - x.1 + x.2) := by
  match x with
  | (qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W2_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' W2 = (x.1.toFinset.product <|
      x.2.2.2.product <| x.2.2.2.product <| x.2.2.2).val.map
    (fun x => x.1 + x.2.1 + x.2.2.1 + x.2.2.2) := by
  match x with
  | (none, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W3_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' W3 = (x.2.1.toFinset.product <| x.2.2.1.product <| x.2.2.1).val.map
    (fun x => -x.1 - x.1 + x.2.1 + x.2.2) := by
  match x with
  | (qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W4_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' W4 = (x.1.toFinset.product <|
      x.2.1.toFinset.product <| x.2.2.1).val.map
    (fun x => x.1 - x.2.1 - x.2.1 + x.2.2) := by
  match x with
  | (none, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_K2_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' K2 = (x.1.toFinset.product <|
      x.2.1.toFinset.product <| x.2.2.2).val.map
    (fun x => x.1 + x.2.1 + x.2.2) := by
  match x with
  | (none, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_topYukawa_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' topYukawa = (x.2.1.toFinset.product <|
      x.2.2.2.product <| x.2.2.2).val.map
    (fun x => -x.1 + x.2.1 + x.2.2) := by
  match x with
  | (qHd, none, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (qHd, some qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_bottomYukawa_finset {x : Charges ğ“©} :
    x.ofPotentialTerm' bottomYukawa = (x.1.toFinset.product <|
      x.2.2.1.product <| x.2.2.2).val.map
    (fun x => x.1 + x.2.1 + x.2.2) := by
  match x with
  | (none, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']
  | (some qHd, qHu, Q5, Q10) =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm_subset_ofPotentialTerm' {x : Charges ğ“©} (T : PotentialTerm) :
    x.ofPotentialTerm T âŠ† x.ofPotentialTerm' T := by
  refine Multiset.subset_iff.mpr (fun n h => ?_)
  simp [ofPotentialTerm] at h
  cases T
  all_goals
    simp [PotentialTerm.toFieldLabel] at h
    obtain âŸ¨f1, f2, âŸ¨âŸ¨f3, f4, âŸ¨h3, f4_memâŸ©, rflâŸ©, f2_memâŸ©, f1_add_f2_eq_zeroâŸ© := h
  case' Î¼ | Î² => obtain âŸ¨rflâŸ© := h3
  case' Î› | W1 | W2 | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
    obtain âŸ¨f5, f6, âŸ¨h4, f6_memâŸ©, rflâŸ© := h3
  case' Î› | K1 | K2 | topYukawa | bottomYukawa => obtain âŸ¨rflâŸ© := h4
  case' W1 | W2 | W3 | W4 => obtain âŸ¨f7, f8, âŸ¨rfl, f8_memâŸ©, rflâŸ© := h4
  all_goals
    try simp [ofPotentialTerm'_W2_finset, ofPotentialTerm'_W3_finset,
      ofPotentialTerm'_Î²_finset, ofPotentialTerm'_Î¼_finset,
      ofPotentialTerm'_W4_finset, ofPotentialTerm'_K2_finset,
      ofPotentialTerm'_topYukawa_finset, ofPotentialTerm'_bottomYukawa_finset]
    try simp [ofPotentialTerm']
    simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product]
    simp_all [ofFieldLabel]
  case' W1 => use f2, f4, f6, f8
  case' W2 => use f2, f4, f6, f8
  case' W3 => use (-f2), f6, f8
  case' W4 => use f6, (-f4), f8
  case' K1 => use (-f2), f4, f6
  case' K2 => use f4, f6, f2
  case' Î› => use f4, f6, f2
  case' topYukawa => use (-f2), f4, f6
  case' bottomYukawa => use f2, f4, f6
  case' Î² => use (-f4), f2
  all_goals simp_all
  all_goals
    rw [â† f1_add_f2_eq_zero]
    abel

lemma ofPotentialTerm'_subset_ofPotentialTerm [DecidableEq ğ“©] {x : Charges ğ“©} (T : PotentialTerm) :
    x.ofPotentialTerm' T âŠ† x.ofPotentialTerm T := by
  refine Multiset.subset_iff.mpr (fun n h => ?_)
  cases T
  all_goals
    try simp [ofPotentialTerm'_W2_finset, ofPotentialTerm'_W3_finset,
      ofPotentialTerm'_Î²_finset, ofPotentialTerm'_Î¼_finset,
      ofPotentialTerm'_W4_finset, ofPotentialTerm'_K2_finset,
      ofPotentialTerm'_topYukawa_finset, ofPotentialTerm'_bottomYukawa_finset] at h
    try simp [ofPotentialTerm'] at h
    simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product] at h
  case' Î¼ | Î² =>
    obtain âŸ¨q1, q2, âŸ¨q1_mem, q2_memâŸ©, q_sumâŸ© := h
  case' Î› | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
    obtain âŸ¨q1, q2, q3, âŸ¨q1_mem, q2_mem, q3_memâŸ©, q_sumâŸ© := h
  case' W1 | W2 =>
    obtain âŸ¨q1, q2, q3, q4, âŸ¨q1_mem, q2_mem, q3_mem, q4_memâŸ©, q_sumâŸ© := h
  case' Î¼ => refine ofPotentialTerm_mono (x := (q1, q2, âˆ…, âˆ…)) ?Î¼Sub _ ?Î¼P
  case' Î² => refine ofPotentialTerm_mono (x := (none, q1, {q2}, âˆ…)) ?Î²Sub _ ?Î²P
  case' Î› =>
    refine ofPotentialTerm_mono (x := (none, none, {q1, q2}, {q3})) ?Î›Sub _ ?Î›P
  case' W1 =>
    refine ofPotentialTerm_mono (x := (none, none, {q1}, {q2, q3, q4})) ?W1Sub _ ?W1P
  case' W2 =>
    refine ofPotentialTerm_mono (x := (q1, none, âˆ…, {q2, q3, q4})) ?W2Sub _ ?W2P
  case' W3 => refine ofPotentialTerm_mono (x := (none, q1, {q2, q3}, âˆ…)) ?W3Sub _ ?W3P
  case' W4 => refine ofPotentialTerm_mono (x := (q1, q2, {q3}, âˆ…)) ?W4Sub _ ?W4P
  case' K1 =>
    refine ofPotentialTerm_mono (x := (none, none, {q1}, {q2, q3}))
      ?K1Sub _ ?K1P
  case' K2 => refine ofPotentialTerm_mono (x := (q1, q2, âˆ…, {q3})) ?K2Sub _ ?K2P
  case' topYukawa =>
    refine ofPotentialTerm_mono (x := (none, q1, âˆ…, {q2, q3}))
      ?topYukawaSub _ ?topYukawaP
  case' bottomYukawa =>
    refine ofPotentialTerm_mono (x := (q1, none, {q2}, {q3}))
      ?bottomYukawaSub _ ?bottomYukawaP
  case' Î¼Sub | Î²Sub | Î›Sub | W1Sub | W2Sub | W3Sub | W4Sub | K1Sub | K2Sub |
      topYukawaSub | bottomYukawaSub =>
    rw [subset_def]
    simp_all [Finset.insert_subset]
  all_goals
    simp [ofPotentialTerm, PotentialTerm.toFieldLabel, ofFieldLabel]
  case' Î›P =>
    use - q3 + n
    simp only [neg_add_cancel_comm, and_true]
    use - q1 - q3 + n, q1
    apply And.intro ?_ (by abel)
    simp only [true_or, and_true]
    use 0
    simp only [true_and, zero_add, exists_eq_right]
    right
    rw [â† q_sum]
    abel
  case' W3P =>
    use 2 â€¢ q1 + n
    apply And.intro ?_ (by abel)
    use - q2 + 2 â€¢ q1 + n, q2
    apply And.intro ?_ (by abel)
    simp only [true_or, and_true]
    use 0, q3
    simp only [or_true, and_self, zero_add, true_and]
    rw [â† q_sum]
    abel
  case' K1P =>
    use q1 + n
    apply And.intro ?_ (by abel)
    use q1 - q2 + n, q2
    apply And.intro ?_ (by abel)
    simp only [true_or, and_true]
    use 0, q3
    simp only [or_true, and_self, zero_add, true_and]
    rw [â† q_sum]
    abel
  case' topYukawaP =>
    use q1 + n
    apply And.intro ?_ (by abel)
    use q1 - q2 + n, q2
    apply And.intro ?_ (by abel)
    simp only [true_or, and_true]
    use 0, q3
    simp only [or_true, and_self, zero_add, true_and]
    rw [â† q_sum]
    abel
  case' W1P | W2P =>
    use - q1 + n
    apply And.intro ?_ (by abel)
    use - q1 - q2 + n, q2
    apply And.intro ?_ (by abel)
    simp only [true_or, and_true]
    use -q1 - q2 - q3 + n, q3
    apply And.intro ?_ (by abel)
    simp only [true_or, or_true, and_true]
    use 0, q4
    simp only [or_true, and_self, zero_add, true_and]
    rw [â† q_sum]
    abel
  all_goals
    rw [â† q_sum]
    try abel

lemma mem_ofPotentialTerm_iff_mem_ofPotentialTerm [DecidableEq ğ“©]
    {T : PotentialTerm} {n : ğ“©} {y : Charges ğ“©} :
    n âˆˆ y.ofPotentialTerm T â†” n âˆˆ y.ofPotentialTerm' T := by
  constructor
  Â· exact fun h => ofPotentialTerm_subset_ofPotentialTerm' T h
  Â· exact fun h => ofPotentialTerm'_subset_ofPotentialTerm T h

lemma ofPotentialTerm'_mono [DecidableEq ğ“©] {x y : Charges ğ“©} (h : x âŠ† y) (T : PotentialTerm) :
    x.ofPotentialTerm' T âŠ† y.ofPotentialTerm' T := by
  intro i
  rw [â† mem_ofPotentialTerm_iff_mem_ofPotentialTerm, â† mem_ofPotentialTerm_iff_mem_ofPotentialTerm]
  exact fun a => ofPotentialTerm_mono h T a

@[simp]
lemma ofPotentialTerm'_empty (T : PotentialTerm) :
    ofPotentialTerm' (âˆ… : Charges ğ“©) T = âˆ… := by
  cases T
  all_goals
    simp [ofPotentialTerm']

end Charges

end SU5
end SuperSymmetry
