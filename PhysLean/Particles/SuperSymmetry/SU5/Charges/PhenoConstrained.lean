/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.Charges.AllowsTerm
import Mathlib.Tactic.Polyrith
/-!

# Pheno constrained charges

We say a charge is pheno-constrained if it leads to allows proton decay or
R-parity violating terms.

We are actually intrested in the charges which are not pheno-constrained.

-/
namespace SuperSymmetry

namespace SU5

namespace Charges
open SuperSymmetry.SU5
open PotentialTerm

variable {ğ“© : Type} [AddCommGroup ğ“©]

/-- A charge is pheno-constrained if it leads to the presence of any term causing proton decay
  ` {W1, Î›, W2, K1}` or R-parity violation `{Î², Î›, W2, W4, K1, K2}`. -/
def IsPhenoConstrained (x : Charges ğ“©) : Prop :=
  x.AllowsTerm Î¼ âˆ¨ x.AllowsTerm Î² âˆ¨ x.AllowsTerm Î› âˆ¨ x.AllowsTerm W2 âˆ¨ x.AllowsTerm W4 âˆ¨
  x.AllowsTerm K1 âˆ¨ x.AllowsTerm K2 âˆ¨ x.AllowsTerm W1

instance decidableIsPhenoConstrained [DecidableEq ğ“©] (x : Charges ğ“©) :
    Decidable x.IsPhenoConstrained :=
  inferInstanceAs (Decidable (x.AllowsTerm Î¼ âˆ¨ x.AllowsTerm Î² âˆ¨ x.AllowsTerm Î› âˆ¨ x.AllowsTerm W2
    âˆ¨ x.AllowsTerm W4 âˆ¨ x.AllowsTerm K1 âˆ¨ x.AllowsTerm K2 âˆ¨ x.AllowsTerm W1))

@[simp]
lemma not_isPhenoConstrained_empty :
    Â¬ IsPhenoConstrained (âˆ… : Charges ğ“©) := by
  simp [IsPhenoConstrained, AllowsTerm, ofPotentialTerm_empty]

lemma isPhenoConstrained_mono {x y : Charges ğ“©} (h : x âŠ† y)
    (hx : x.IsPhenoConstrained) : y.IsPhenoConstrained := by
  simp [IsPhenoConstrained] at *
  rcases hx with hr | hr | hr | hr | hr | hr | hr | hr
  all_goals
    have h' := allowsTerm_mono h hr
    simp_all

/-- The collection of charges of super-potential terms leading to a pheno-constrained model. -/
def phenoConstrainingChargesSP (x : Charges ğ“©) : Multiset ğ“© :=
  x.ofPotentialTerm' Î¼ + x.ofPotentialTerm' Î² + x.ofPotentialTerm' Î› +
  x.ofPotentialTerm' W2 + x.ofPotentialTerm' W4 + x.ofPotentialTerm' W1

@[simp]
lemma phenoConstrainingChargesSP_empty :
    phenoConstrainingChargesSP (âˆ… : Charges ğ“©) = âˆ… := by
  simp [phenoConstrainingChargesSP]

lemma phenoConstrainingChargesSP_mono [DecidableEq ğ“©] {x y : Charges ğ“©} (h : x âŠ† y) :
    x.phenoConstrainingChargesSP âŠ† y.phenoConstrainingChargesSP := by
  simp only [phenoConstrainingChargesSP]
  refine Multiset.subset_iff.mpr ?_
  intro z
  simp [or_assoc]
  intro hr
  rcases hr with hr | hr | hr | hr | hr | hr
  all_goals
    have h' := ofPotentialTerm'_mono h _ hr
    simp_all

/-!

## Is Pheno constrained Q5 addition

-/

/-- The proposition which is true if the addition of a charge `q5` to a set of charegs `x` leads
  `x` to being phenomenologically constrained. -/
def IsPhenoConstrainedQ5 [DecidableEq ğ“©] (x : Charges ğ“©) (q5 : ğ“©) : Prop :=
  x.AllowsTermQ5 q5 Î¼ âˆ¨ x.AllowsTermQ5 q5 Î² âˆ¨ x.AllowsTermQ5 q5 Î› âˆ¨ x.AllowsTermQ5 q5 W2 âˆ¨
  x.AllowsTermQ5 q5 W4 âˆ¨
  x.AllowsTermQ5 q5 K1 âˆ¨ x.AllowsTermQ5 q5 K2 âˆ¨ x.AllowsTermQ5 q5 W1

lemma isPhenoConstrainedQ5_iff [DecidableEq ğ“©] (x : Charges ğ“©) (q5 : ğ“©) :
    x.IsPhenoConstrainedQ5 q5 â†”
    x.AllowsTermQ5 q5 Î² âˆ¨ x.AllowsTermQ5 q5 Î› âˆ¨ x.AllowsTermQ5 q5 W4 âˆ¨
    x.AllowsTermQ5 q5 K1 âˆ¨ x.AllowsTermQ5 q5 W1 := by
  simp [IsPhenoConstrainedQ5, AllowsTermQ5]

instance decidableIsPhenoConstrainedQ5 [DecidableEq ğ“©] (x : Charges ğ“©) (q5 : ğ“©) :
    Decidable (x.IsPhenoConstrainedQ5 q5) :=
  decidable_of_iff _ (isPhenoConstrainedQ5_iff x q5).symm

lemma isPhenoConstrained_insertQ5_iff_isPhenoConstrainedQ5 [DecidableEq ğ“©] {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q5 : ğ“©} :
    IsPhenoConstrained (qHd, qHu, insert q5 Q5, Q10) â†”
    IsPhenoConstrainedQ5 (qHd, qHu, Q5, Q10) q5 âˆ¨
    IsPhenoConstrained (qHd, qHu, Q5, Q10) := by
  constructor
  Â· intro hr
    rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
    all_goals
      rw [allowsTerm_insertQ5_iff_allowsTermQ5] at hr
      rcases hr with hr | hr
      all_goals
        simp_all [IsPhenoConstrainedQ5, IsPhenoConstrained]
  Â· intro hr
    rcases hr with hr | hr
    Â· simp [IsPhenoConstrainedQ5] at hr
      rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
      all_goals
        have hr' := allowsTerm_insertQ5_of_allowsTermQ5 _ hr
        simp_all [IsPhenoConstrained]
    Â· apply isPhenoConstrained_mono _ hr
      simp [subset_def]

/-- The proposition which is true if the addition of a charge `q10` to a set of charegs `x` leads
  `x` to being phenomenologically constrained. -/
def IsPhenoConstrainedQ10 [DecidableEq ğ“©] (x : Charges ğ“©) (q10 : ğ“©) : Prop :=
  x.AllowsTermQ10 q10 Î¼ âˆ¨ x.AllowsTermQ10 q10 Î² âˆ¨ x.AllowsTermQ10 q10 Î› âˆ¨ x.AllowsTermQ10 q10 W2 âˆ¨
  x.AllowsTermQ10 q10 W4 âˆ¨
  x.AllowsTermQ10 q10 K1 âˆ¨ x.AllowsTermQ10 q10 K2 âˆ¨ x.AllowsTermQ10 q10 W1

lemma isPhenoConstrainedQ10_iff [DecidableEq ğ“©] (x : Charges ğ“©) (q10 : ğ“©) :
    x.IsPhenoConstrainedQ10 q10 â†” x.AllowsTermQ10 q10 Î› âˆ¨ x.AllowsTermQ10 q10 W2 âˆ¨
    x.AllowsTermQ10 q10 K1 âˆ¨ x.AllowsTermQ10 q10 K2 âˆ¨ x.AllowsTermQ10 q10 W1 := by
  simp [IsPhenoConstrainedQ10, AllowsTermQ10]

instance decidableIsPhenoConstrainedQ10 [DecidableEq ğ“©] (x : Charges ğ“©) (q10 : ğ“©) :
    Decidable (x.IsPhenoConstrainedQ10 q10) :=
  decidable_of_iff _ (isPhenoConstrainedQ10_iff x q10).symm

lemma isPhenoConstrained_insertQ10_iff_isPhenoConstrainedQ10 [DecidableEq ğ“©] {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q10 : ğ“©} :
    IsPhenoConstrained (qHd, qHu, Q5, insert q10 Q10) â†”
    IsPhenoConstrainedQ10 (qHd, qHu, Q5, Q10) q10 âˆ¨
    IsPhenoConstrained (qHd, qHu, Q5, Q10) := by
  constructor
  Â· intro hr
    rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
    all_goals
      rw [allowsTerm_insertQ10_iff_allowsTermQ10] at hr
      rcases hr with hr | hr
      all_goals
        simp_all [IsPhenoConstrainedQ10, IsPhenoConstrained]
  Â· intro hr
    rcases hr with hr | hr
    Â· simp [IsPhenoConstrainedQ10] at hr
      rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
      all_goals
        have hr' := allowsTerm_insertQ10_of_allowsTermQ10 _ hr
        simp_all [IsPhenoConstrained]
    Â· apply isPhenoConstrained_mono _ hr
      simp [subset_def]

end Charges

end SU5

end SuperSymmetry
