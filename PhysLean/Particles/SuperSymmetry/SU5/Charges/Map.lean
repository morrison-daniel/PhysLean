/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.Charges.Yukawa
import Mathlib.Tactic.FinCases
/-!

# Mapping charges from different sets

In this module we define a function `map` which takes an additive monoid homomorphism
`f : ğ“© â†’+ ğ“©1` and a charge `x : Charges ğ“©`, and returns the charge
`x.map f : Charges ğ“©1` obtained by mapping the elements of `x` by `f`.

We prove that various properties of charges are preserved under this mapping.

We define the preimage of this mapping within a subset `ofFinset S5 S10` of `Charges ğ“©` in
a computationaly efficient way.

-/

namespace SuperSymmetry

namespace SU5
namespace Charges

variable {ğ“© ğ“©1 ğ“©2 : Type} [AddCommGroup ğ“©] [AddCommGroup ğ“©1] [DecidableEq ğ“©1]
  [AddCommGroup ğ“©2] [DecidableEq ğ“©2]

/-- Given an additive monoid homomorphisms `f : ğ“© â†’+ ğ“©1`, for a charge
  `x : Charges ğ“©`, `x.map f` is the charge of `Charges ğ“©1` obtained by mapping the elements
  of `x` by `f`. -/
def map (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©) : Charges ğ“©1 :=
  (f <$> x.1, f <$> x.2.1, x.2.2.1.image f, x.2.2.2.image f)

@[simp]
lemma map_empty (f : ğ“© â†’+ ğ“©1) : map f (âˆ… : Charges ğ“©) = âˆ… := by
  simp only [map, empty_qHd, Option.map_eq_map, Option.map_none, empty_qHu, empty_Q5,
    Finset.image_empty, empty_Q10]
  rfl

lemma map_map (f : ğ“© â†’+ ğ“©1) (g : ğ“©1 â†’+ ğ“©2) (x : Charges ğ“©) :
    map g (map f x) = map (g.comp f) x := by
  simp [map, Function.comp, Option.map_map, Finset.image_image]

@[simp]
lemma map_id [DecidableEq ğ“©] (x : Charges ğ“©) : map (AddMonoidHom.id ğ“©) x = x := by
  simp [map, Option.map_id, Finset.image_id]

lemma map_ofFieldLabel (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©) (F : FieldLabel) :
    ofFieldLabel (map f x) F = (ofFieldLabel x F).image f := by
  simp [ofFieldLabel, map]
  match x with
  | (qHd, qHu, Q5, Q10) =>
  fin_cases F
  all_goals simp
  case Â«0Â» | Â«1Â» =>
    match qHu with
    | some a => simp
    | none => simp
  case Â«2Â» | Â«3Â» =>
    match qHd with
    | some a => simp
    | none => simp
  Â· trans (Finset.image (â‡‘f) Q5).image Neg.neg
    Â· ext a
      simp
    Â· rw [Finset.image_image]
      symm
      trans Finset.image (â‡‘f âˆ˜ Neg.neg) (Q5)
      Â· ext a
        simp
      congr
      funext a
      simp

lemma map_ofPotentialTerm_toFinset [DecidableEq ğ“©]
    (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©) (T : PotentialTerm) :
    (ofPotentialTerm (map f x) T).toFinset = (ofPotentialTerm x T).toFinset.image f := by
  ext i
  simp [Multiset.mem_toFinset]
  rw [mem_ofPotentialTerm_iff_mem_ofPotentialTerm]
  conv_rhs =>
    enter [1, a]
    rw [mem_ofPotentialTerm_iff_mem_ofPotentialTerm]
  constructor
  Â· intro h
    cases T
    all_goals
      simp [ofPotentialTerm'] at h
      simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product] at h
    case' Î¼ | Î² =>
      obtain âŸ¨q1, q2, âŸ¨q1_mem, q2_memâŸ©, q_sumâŸ© := h
      simp [map] at q1_mem q2_mem
      obtain âŸ¨q1, q1_mem, rflâŸ© := q1_mem
      obtain âŸ¨q2, q2_mem, rflâŸ© := q2_mem
    case' Î¼ => use q1 - q2
    case' Î² => use -q1 + q2
    case' Î› | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
      obtain âŸ¨q1, q2, q3, âŸ¨q1_mem, q2_mem, q3_memâŸ©, q_sumâŸ© := h
      simp [map] at q1_mem q2_mem q3_mem
      obtain âŸ¨q1, q1_mem, rflâŸ© := q1_mem
      obtain âŸ¨q2, q2_mem, rflâŸ© := q2_mem
      obtain âŸ¨q3, q3_mem, rflâŸ© := q3_mem
    case' Î› | K2 | bottomYukawa => use q1 + q2 + q3
    case' W3 => use - q1 - q1 + q2 + q3
    case' W4 => use q1 - q2 - q2 + q3
    case' K1 | topYukawa => use - q1 + q2 + q3
    case' W1 | W2 =>
      obtain âŸ¨q1, q2, q3, q4, âŸ¨q1_mem, q2_mem, q3_mem, q4_memâŸ©, q_sumâŸ© := h
      simp [map] at q1_mem q2_mem q3_mem q4_mem
      obtain âŸ¨q1, q1_mem, rflâŸ© := q1_mem
      obtain âŸ¨q2, q2_mem, rflâŸ© := q2_mem
      obtain âŸ¨q3, q3_mem, rflâŸ© := q3_mem
      obtain âŸ¨q4, q4_mem, rflâŸ© := q4_mem
      use q1 + q2 + q3 + q4
    all_goals
      subst i
      simp [ofPotentialTerm']
      simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product]
      use q1, q2
      simp_all
    Â· use q3, q4
    Â· use q3, q4
  Â· intro h
    obtain âŸ¨a, h, rflâŸ© := h
    cases T
    all_goals
      simp [ofPotentialTerm'] at h
      simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product] at h
      simp [ofPotentialTerm']
    case' Î¼ | Î² =>
      obtain âŸ¨q1, q2, âŸ¨q1_mem, q2_memâŸ©, q_sumâŸ© := h
      use f q1, f q2
    case' Î› | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
      obtain âŸ¨q1, q2, q3, âŸ¨q1_mem, q2_mem, q3_memâŸ©, q_sumâŸ© := h
      use f q1, f q2, f q3
    case' W1 | W2 =>
      obtain âŸ¨q1, q2, q3, q4, âŸ¨q1_mem, q2_mem, q3_mem, q4_memâŸ©, q_sumâŸ© := h
      use f q1, f q2, f q3, f q4
    all_goals
      simp only [SProd.sprod, Multiset.instSProd, Multiset.mem_product, map]
      subst a
      simp_all
    case W1 => refine âŸ¨âŸ¨q1, q1_mem, rflâŸ©, âŸ¨q2, q2_mem, rflâŸ©, âŸ¨q3, q3_mem, rflâŸ©, âŸ¨q4, q4_mem, rflâŸ©âŸ©
    case W2 => refine âŸ¨âŸ¨q2, q2_mem, rflâŸ©, âŸ¨q3, q3_mem, rflâŸ©, âŸ¨q4, q4_mem, rflâŸ©âŸ©
    case Î› | K1 => refine âŸ¨âŸ¨q1, q1_mem, rflâŸ©, âŸ¨q2, q2_mem, rflâŸ©, âŸ¨q3, q3_mem, rflâŸ©âŸ©
    case W3 | topYukawa | bottomYukawa => refine âŸ¨âŸ¨q2, q2_mem, rflâŸ©, âŸ¨q3, q3_mem, rflâŸ©âŸ©
    case W4 | K2 => refine âŸ¨q3, q3_mem, rflâŸ©
    case Î² => refine âŸ¨q2, q2_mem, rflâŸ©

lemma mem_map_ofPotentialTerm_iff [DecidableEq ğ“©]
    (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©) (T : PotentialTerm) :
    i âˆˆ (ofPotentialTerm (map f x) T) â†” i âˆˆ (ofPotentialTerm x T).map f := by
  trans i âˆˆ (ofPotentialTerm (map f x) T).toFinset
  Â· simp
  rw [map_ofPotentialTerm_toFinset]
  simp

lemma map_subset {f : ğ“© â†’+ ğ“©1} {x y : Charges ğ“©} (h : x âŠ† y) :
    map f x âŠ† map f y := by
  simp [map, subset_def] at *
  obtain âŸ¨hHd, hHu, hQ5, hQ10âŸ© := h
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· match x, y with
    | (a, _, _, _), (b, _, _, _) =>
      cases a
      all_goals cases b
      all_goals simp [hHd]
      all_goals simp at hHd
      subst hHd
      rfl
  Â· match x, y with
    | (_, a, _, _), (_, b, _, _) =>
      cases a
      all_goals cases b
      all_goals simp [hHu]
      all_goals simp at hHu
      subst hHu
      rfl
  Â· exact Finset.image_subset_image hQ5
  Â· exact Finset.image_subset_image hQ10

variable [DecidableEq ğ“©]

lemma allowsTermForm_map {T} {f : ğ“© â†’+ ğ“©1} {a b c : ğ“©} :
    (allowsTermForm a b c T).map f = allowsTermForm (f a) (f b) (f c) T := by
  cases T
  all_goals simp [allowsTermForm, map]

lemma map_allowsTerm {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} {T : PotentialTerm}
    (h : x.AllowsTerm T) : (map f x).AllowsTerm T := by
  rw [allowsTerm_iff_subset_allowsTermForm] at âŠ¢ h
  obtain âŸ¨a, b, c, h1âŸ© := h
  use f a, f b, f c
  rw [â† allowsTermForm_map]
  exact map_subset h1

lemma map_isPhenoConstrained (f : ğ“© â†’+ ğ“©1) {x : Charges ğ“©}
    (h : x.IsPhenoConstrained) : (map f x).IsPhenoConstrained := by
  simp [IsPhenoConstrained] at âŠ¢ h
  rcases h with h | h | h | h | h | h | h | h
  Â· exact Or.inl (map_allowsTerm h)
  Â· exact Or.inr (Or.inl (map_allowsTerm h))
  Â· exact Or.inr (Or.inr (Or.inl (map_allowsTerm h)))
  Â· exact Or.inr (Or.inr (Or.inr (Or.inl (map_allowsTerm h))))
  Â· exact Or.inr (Or.inr (Or.inr (Or.inr (Or.inl (map_allowsTerm h)))))
  Â· exact Or.inr (Or.inr (Or.inr (Or.inr (Or.inr (Or.inl (map_allowsTerm h))))))
  Â· exact Or.inr (Or.inr (Or.inr (Or.inr (Or.inr (Or.inr (Or.inl (map_allowsTerm h)))))))
  Â· exact Or.inr (Or.inr (Or.inr (Or.inr (Or.inr (Or.inr (Or.inr ((map_allowsTerm h))))))))

lemma not_isPhenoConstrained_of_map {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©}
    (h : Â¬ (map f x).IsPhenoConstrained) : Â¬ x.IsPhenoConstrained :=
  fun hn => h (map_isPhenoConstrained f hn)

omit [DecidableEq ğ“©] in
lemma map_isComplete_iff {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} :
    (map f x).IsComplete â†” x.IsComplete := by
  simp [IsComplete, map]

lemma map_ofYukawaTerms_toFinset {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} :
    (map f x).ofYukawaTerms.toFinset = x.ofYukawaTerms.toFinset.image f := by
  simp [ofYukawaTerms]
  ext i
  rw [Finset.image_union]
  simp only [Finset.mem_union, Multiset.mem_toFinset]
  rw [mem_map_ofPotentialTerm_iff, mem_map_ofPotentialTerm_iff]
  simp [Multiset.mem_map]

lemma mem_map_ofYukawaTerms_iff {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} {i} :
    i âˆˆ (map f x).ofYukawaTerms â†” i âˆˆ x.ofYukawaTerms.map f := by
  trans i âˆˆ (map f x).ofYukawaTerms.toFinset
  Â· simp
  rw [map_ofYukawaTerms_toFinset]
  simp

lemma map_ofYukawaTermsNSum_toFinset {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} {n : â„•}:
    ((map f x).ofYukawaTermsNSum n).toFinset = (x.ofYukawaTermsNSum n).toFinset.image f:= by
  induction n with
  | zero => simp [ofYukawaTermsNSum, map]
  | succ n ih =>
    simp [ofYukawaTermsNSum]
    rw [Finset.image_union]
    congr 1
    ext i
    simp only [Multiset.mem_toFinset, Multiset.mem_bind, Multiset.mem_map, Finset.mem_image,
      exists_exists_and_exists_and_eq_and, map_add]
    constructor
    Â· intro h
      obtain âŸ¨a, a_mem, b, b_mem, hâŸ© := h
      have a_mem' : a âˆˆ ((map f x).ofYukawaTermsNSum n).toFinset := by simpa using a_mem
      rw [ih] at a_mem'
      rw [mem_map_ofYukawaTerms_iff] at b_mem
      simp at a_mem' b_mem
      obtain âŸ¨a, a_mem', rflâŸ© := a_mem'
      obtain âŸ¨b, b_mem', rflâŸ© := b_mem
      exact âŸ¨a, a_mem', b, b_mem', hâŸ©
    Â· intro h
      obtain âŸ¨a, a_mem, b, b_mem, hâŸ© := h
      use f a
      apply And.intro
      Â· rw [â† Multiset.mem_toFinset, ih]
        simp only [Finset.mem_image, Multiset.mem_toFinset]
        use a
      use f b
      apply And.intro
      Â· rw [mem_map_ofYukawaTerms_iff]
        simp only [Multiset.mem_map]
        use b
      exact h

lemma mem_map_ofYukawaTermsNSum_iff {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} {n i} :
    i âˆˆ (map f x).ofYukawaTermsNSum n â†” i âˆˆ (x.ofYukawaTermsNSum n).map f := by
  trans i âˆˆ ((map f x).ofYukawaTermsNSum n).toFinset
  Â· simp
  rw [map_ofYukawaTermsNSum_toFinset]
  simp

lemma map_phenoConstrainingChargesSP_toFinset {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©} :
    (map f x).phenoConstrainingChargesSP.toFinset =
    x.phenoConstrainingChargesSP.toFinset.image f := by
  simp [phenoConstrainingChargesSP, map_ofPotentialTerm_toFinset, Finset.image_union]

lemma map_yukawaGeneratesDangerousAtLevel (f : ğ“© â†’+ ğ“©1) {x : Charges ğ“©} (n : â„•)
    (h : x.YukawaGeneratesDangerousAtLevel n) : (map f x).YukawaGeneratesDangerousAtLevel n := by
  rw [YukawaGeneratesDangerousAtLevel]
  rw [map_phenoConstrainingChargesSP_toFinset, map_ofYukawaTermsNSum_toFinset]
  rw [â† Finset.nonempty_iff_ne_empty, â† Finset.not_disjoint_iff_nonempty_inter]
  apply Disjoint.of_image_finset.mt
  rw [Finset.not_disjoint_iff_nonempty_inter, Finset.nonempty_iff_ne_empty]
  exact h

lemma not_yukawaGeneratesDangerousAtLevel_of_map {f : ğ“© â†’+ ğ“©1} {x : Charges ğ“©}
    (n : â„•) (h : Â¬ (map f x).YukawaGeneratesDangerousAtLevel n) :
    Â¬ x.YukawaGeneratesDangerousAtLevel n :=
  fun hn => h (map_yukawaGeneratesDangerousAtLevel f n hn)

/-!

## Preimage

-/

/-- The preimage of a charge `Charges ğ“©1` in `ofFinset S5 S10 âŠ† Charges ğ“©` under
  mapping charges through `f : ğ“© â†’+ ğ“©1`. -/
def preimageOfFinset (S5 S10 : Finset ğ“©) (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©1) : Finset (Charges ğ“©) :=
  let SHd := (S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ© âˆª {none} : Finset (Option ğ“©)).filter
    fun y => f <$> y = x.1
  let SHu := (S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ© âˆª {none} : Finset (Option ğ“©)).filter
    fun y => f <$> y = x.2.1
  let SQ5' := S5.filter fun y => f y âˆˆ x.2.2.1
  let SQ5 : Finset (Finset ğ“©) := SQ5'.powerset.filter fun y => y.image f = x.2.2.1
  let SQ10' := S10.filter fun y => f y âˆˆ x.2.2.2
  let SQ10 : Finset (Finset ğ“©) := SQ10'.powerset.filter fun y => y.image f = x.2.2.2
  SHd.product <| SHu.product <| SQ5.product SQ10

lemma preimageOfFinset_eq (S5 S10 : Finset ğ“©) (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©1) :
    preimageOfFinset S5 S10 f x = {y : Charges ğ“© | y.map f = x âˆ§ y âˆˆ ofFinset S5 S10} := by
  ext y
  match y, x with
  | (yHd, yHu, y5, y10), (xHd, xHu, x5, x10) =>
  simp [preimageOfFinset]
  repeat rw [Finset.mem_product]
  simp [map]
  constructor
  Â· intro âŸ¨âŸ¨h1, rflâŸ©, âŸ¨h2, rflâŸ©, âŸ¨h3, rflâŸ©, âŸ¨h4, rflâŸ©âŸ©
    simp only [true_and]
    rw [mem_ofFinset_iff]
    simp only
    refine âŸ¨?_, ?_, ?_, ?_âŸ©
    Â· match yHd with
      | some a => simpa using h1
      | none => simp
    Â· match yHu with
      | some a => simpa using h2
      | none => simp
    Â· exact h3.trans <| Finset.filter_subset (fun y => f y âˆˆ Finset.image (â‡‘f) y5) S5
    Â· apply h4.trans <| Finset.filter_subset (fun y => f y âˆˆ Finset.image (â‡‘f) y10) S10
  Â· rw [eq_iff]
    simp only
    intro âŸ¨âŸ¨rfl, rfl, rfl, rflâŸ©, h2âŸ©
    simp only [and_true, Finset.mem_image]
    rw [mem_ofFinset_iff] at h2
    simp at h2
    refine âŸ¨?_, ?_, ?_, ?_âŸ©
    Â· match yHd with
      | some a =>
        simp at h2
        simpa using h2.1
      | none => simp
    Â· match yHu with
      | some a =>
        simp at h2
        simpa using h2.2.1
      | none => simp
    Â· refine Finset.subset_iff.mpr ?_
      intro x hx
      simp only [Finset.mem_filter]
      refine âŸ¨h2.2.2.1 hx, ?_âŸ©
      use x
    Â· refine Finset.subset_iff.mpr ?_
      intro x hx
      simp only [Finset.mem_filter]
      refine âŸ¨h2.2.2.2 hx, ?_âŸ©
      use x

/-- The cardiniality of the
  preimage of a charge `Charges ğ“©1` in `ofFinset S5 S10 âŠ† Charges ğ“©` under
  mapping charges through `f : ğ“© â†’+ ğ“©1`. -/
def preimageOfFinsetCard (S5 S10 : Finset ğ“©) (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©1) : â„• :=
  let SHd := (S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ© âˆª {none} : Finset (Option ğ“©)).filter
    fun y => f <$> y = x.1
  let SHu := (S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ© âˆª {none} : Finset (Option ğ“©)).filter
    fun y => f <$> y = x.2.1
  let SQ5' := S5.filter fun y => f y âˆˆ x.2.2.1
  let SQ5 : Finset (Finset ğ“©) := SQ5'.powerset.filter fun y => y.image f = x.2.2.1
  let SQ10' := S10.filter fun y => f y âˆˆ x.2.2.2
  let SQ10 : Finset (Finset ğ“©) := SQ10'.powerset.filter fun y => y.image f = x.2.2.2
  SHd.card * SHu.card * SQ5.card * SQ10.card

lemma preimageOfFinset_card_eq (S5 S10 : Finset ğ“©) (f : ğ“© â†’+ ğ“©1) (x : Charges ğ“©1) :
    preimageOfFinsetCard S5 S10 f x =
    (preimageOfFinset S5 S10 f x).card := by
  rw [preimageOfFinset]
  simp only [Option.map_eq_map, Finset.product_eq_sprod]
  repeat rw [Finset.card_product]
  simp [preimageOfFinsetCard, mul_assoc]

end Charges
end SU5

end SuperSymmetry
