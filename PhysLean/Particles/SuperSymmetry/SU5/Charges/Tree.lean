/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Mathematics.DataStructures.FourTree.UniqueMap
import PhysLean.Particles.SuperSymmetry.SU5.Charges.Completions
import PhysLean.Particles.SuperSymmetry.SU5.Charges.MinimalSuperSet
/-!

# Trees of charges

It is convient to use `FourTree` to store charges. In this file we make
some auxillary definitions and lemmas which will help us in proofs
and make things faster.

In particular we define the `root`, `trunk`, `branch`, `twig` and `leaf`
specifically for `FourTree (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤)`, this
allows Lean to read in large trees of charges more quickly.

We also prove some results related to `(T.uniqueMap3 (insert q5.1))` and
`(T.uniqueMap4 (insert q10.1))`, and their filters by a predicate `p : Charges â†’ Prop`.

-/
namespace SuperSymmetry

namespace SU5

namespace Charges

namespace Tree
open PhysLean FourTree

instance (T : FourTree (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤)) (x : Charges) :
    Decidable (x âˆˆ T) :=
  inferInstanceAs (Decidable (x.toProd âˆˆ T))

/-- An explicit form of `FourTree.root` for charges. -/
def root : Multiset (Trunk (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤)) â†’
    FourTree (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤) := fun b => .root b

/-- An explicit form of `FourTree.Trunk.trunk` for charges. -/
def trunk : (Option â„¤) â†’ Multiset (Branch (Option â„¤) (Finset â„¤) (Finset â„¤)) â†’
    Trunk (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤) := fun qHd b => .trunk qHd b

/-- An explicit form of `FourTree.Branch.branch` for charges. -/
def branch : (Option â„¤) â†’ Multiset (Twig (Finset â„¤) (Finset â„¤)) â†’
    Branch (Option â„¤) (Finset â„¤) (Finset â„¤) := fun qHu b => .branch qHu b

/-- An explicit form of `FourTree.Twig.twig` for charges. -/
def twig : Finset â„¤ â†’ Multiset (Leaf (Finset â„¤)) â†’ Twig (Finset â„¤) (Finset â„¤) :=
  fun q5 q10s => .twig q5 q10s

/-- An explicit form of `FourTree.Leaf.leaf` for charges. -/
def leaf : Finset â„¤ â†’ Leaf (Finset â„¤) := fun q10 => .leaf q10

/-!

## Inserting charges and minimal super sets

-/

variable {ð“© : Type} [DecidableEq ð“©]

lemma insert_filter_card_zero
    (T : FourTree (Option ð“©) (Option ð“©) (Finset ð“©) (Finset ð“©)) (S5 S10 : Finset ð“©)
    (p : Charges ð“© â†’ Prop) [DecidablePred p]
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (h10 : âˆ€ q10 : S10, (T.uniqueMap4 (insert q10.1)).toMultiset.filter p = âˆ…)
    (h5 : âˆ€ q5 : S5, (T.uniqueMap3 (insert q5.1)).toMultiset.filter p = âˆ…) :
    âˆ€ x âˆˆ T, âˆ€ y âˆˆ minimalSuperSet S5 S10 x, y âˆ‰ T â†’ Â¬ p y := by
  intro (xqHd, xqHu, xQ5, xQ10) x_mem_T y y_mem_minimalSuperSet y_not_in_T
  have x_isComplete : IsComplete (xqHd, xqHu, xQ5, xQ10) := hComplet _ x_mem_T
  have xqHd_isSome : xqHd.isSome := by
    simp [IsComplete] at x_isComplete
    exact x_isComplete.1
  rw [Option.isSome_iff_exists] at xqHd_isSome
  obtain âŸ¨xqHd, rflâŸ© := xqHd_isSome
  have xqHu_isSome : xqHu.isSome := by
    simp [IsComplete] at x_isComplete
    exact x_isComplete.1
  rw [Option.isSome_iff_exists] at xqHu_isSome
  obtain âŸ¨xqHu, rflâŸ© := xqHu_isSome
  simp [minimalSuperSet] at y_mem_minimalSuperSet
  simp_all
  rcases y_mem_minimalSuperSet with âŸ¨y_neq_x, âŸ¨q5, q5_mem_S5, rflâŸ© | âŸ¨q10, q10_mem_S10, rflâŸ©âŸ©
  Â· have h5' := h5 q5 q5_mem_S5
    rw [Multiset.filter_eq_nil] at h5'
    apply h5'
    rw [â† mem_iff_mem_toMultiset]
    have h1 := map_mem_uniqueMap3 (some xqHd, some xqHu, xQ5, xQ10) x_mem_T (insert q5)
    simp_all
  Â· have h10' := h10 q10 q10_mem_S10
    rw [Multiset.filter_eq_nil] at h10'
    apply h10'
    rw [â† mem_iff_mem_toMultiset]
    have h1 := map_mem_uniqueMap4 (some xqHd, some xqHu, xQ5, xQ10) x_mem_T (insert q10)
    simp_all

lemma subset_insert_filter_card_zero_inductive
    (T : FourTree (Option ð“©) (Option ð“©) (Finset ð“©) (Finset ð“©))
    (S5 S10 : Finset ð“©)
    (p : Charges ð“© â†’ Prop) [DecidablePred p]
    (hnotSubset : âˆ€ (x y : Charges ð“©), x âŠ† y â†’ Â¬ p x â†’ Â¬ p y)
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (x : Charges ð“©)
    (hx : x âˆˆ T) (y : Charges ð“©) (hsubset : x âŠ† y)
    (hy : y âˆˆ ofFinset S5 S10)
    (h10 : âˆ€ q10 : S10, (T.uniqueMap4 (insert q10.1)).toMultiset.filter p = âˆ…)
    (h5 : âˆ€ q5 : S5, (T.uniqueMap3 (insert q5.1)).toMultiset.filter p = âˆ…) :
    (n : â„•) â†’ (hn : n = y.card - x.card) â†’ y âˆ‰ T â†’ Â¬ p y
  | 0, hn, hnot_in_T => by
    have hxy : x = y := by
      refine eq_of_subset_card hsubset ?_
      have hl : x.card â‰¤ y.card := card_mono hsubset
      omega
    subst hxy
    simp_all
  | Nat.succ n, hn, hnot_in_T => by
    have hxy : x â‰  y := by
      intro h
      subst h
      simp at hn
    obtain âŸ¨z, hz, hsubsetzâŸ© := exists_minimalSuperSet S5 S10 hy hsubset hxy
    have hz' := insert_filter_card_zero T S5 S10 p hComplet h10 h5 x hx z hz
    by_cases hz_not_in_T : z âˆ‰ T
    Â· apply hnotSubset
      Â· exact hsubsetz
      Â· exact hz' hz_not_in_T
    apply subset_insert_filter_card_zero_inductive T S5 S10 p hnotSubset hComplet z (n := n)
    Â· simpa using hz_not_in_T
    Â· exact hsubsetz
    Â· exact hy
    Â· exact fun q10 => h10 q10
    Â· exact fun q5 => h5 q5
    Â· rw [card_of_mem_minimalSuperSet z hz]
      omega
    Â· exact hnot_in_T

/-- For a proposition `p` if `(T.uniqueMap4 (insert q10.1)).toMultiset.filter p`
  and `(T.uniqueMap3 (insert q5.1)).toMultiset.filter p` for all `q5 âˆˆ S5` and `q10 âˆˆ S10` then
  if `x âˆˆ T` and `x âŠ† y` if `y âˆ‰ T` then `Â¬ p y`.
  This assumes that all charges in `T` are complete, and that `p` satisfies
  `x âŠ† y â†’ Â¬ p x â†’ Â¬ p y`. -/
lemma subset_insert_filter_card_zero
    (T : FourTree (Option â„¤) (Option â„¤) (Finset â„¤) (Finset â„¤))
    (S5 S10 : Finset â„¤)
    (p : Charges â†’ Prop) [DecidablePred p]
    (hnotSubset : âˆ€ (x y : Charges), x âŠ† y â†’ Â¬ p x â†’ Â¬ p y)
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (x : Charges)
    (hx : x âˆˆ T) (y : Charges) (hsubset : x âŠ† y)
    (hy : y âˆˆ ofFinset S5 S10)
    (h10 : âˆ€ q10 : S10, (T.uniqueMap4 (insert q10.1)).toMultiset.filter p = âˆ…)
    (h5 : âˆ€ q5 : S5, (T.uniqueMap3 (insert q5.1)).toMultiset.filter p = âˆ…) :
      y âˆ‰ T â†’ Â¬ p y :=
  subset_insert_filter_card_zero_inductive T S5 S10 p hnotSubset hComplet x hx y hsubset hy h10 h5
    (y.card - x.card) rfl

end Tree

end Charges

end SU5

end SuperSymmetry
