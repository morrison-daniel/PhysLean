/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.Basic
/-!

# Charges assocaited with a field label

## i. Overview

Recall that a `FieldLabel` is one of the seven possible superfields in the SU(5) GUT,
corresponding to the fields present and their conjugates.

Given a charge spectrum `x : ChargeSpectrum ùì©`, we are intrested in the finite set of
charges carried by representations associated with a given `FieldLabel`.

Results in this module will be used to find the charges associated with
terms in the potential.

## ii. Key results

- `ofFieldLabel` : Given a charge spectrum `x : ChargeSpectrum ùì©`,
  `ofFieldLabel x F` is the finite set of charges associated with representations
  corresponding to the field label `F`.

## iii. Table of contents

- A. Charges associated with a field label
  - A.1. The field labels for the empty charge spectrum
  - A.2. Monotonicity of `ofFieldLabel`
  - A.3. Membership of conjugate charegs
  - A.4. Extensionality of charge spectra via `ofFieldLabel`

## iv. References

There are no known references for the results in this file.

-/

namespace SuperSymmetry
namespace SU5

namespace ChargeSpectrum
open SuperSymmetry.SU5

variable {ùì© : Type} [InvolutiveNeg ùì©]

/-!

## A. Charges associated with a field label

We first define `ofFieldLabel`, which given a charge spectrum `x : ChargeSpectrum ùì©` and
a `FieldLabel`, returns the finite set of charges associated with representations
corresponding to that `FieldLabel`.

-/
/-- Given an `x : Charges`, the charges associated with a given `FieldLabel`. -/
def ofFieldLabel (x : ChargeSpectrum ùì©) : FieldLabel ‚Üí Finset ùì©
  | .fiveBarHd => x.qHd.toFinset
  | .fiveBarHu => x.qHu.toFinset
  | .fiveBarMatter => x.Q5
  | .tenMatter => x.Q10
  | .fiveHd => x.qHd.toFinset.map ‚ü®Neg.neg, neg_injective‚ü©
  | .fiveHu => x.qHu.toFinset.map ‚ü®Neg.neg, neg_injective‚ü©
  | .fiveMatter => x.Q5.map ‚ü®Neg.neg, neg_injective‚ü©

/-!

### A.1. The field labels for the empty charge spectrum

We show that the charges associated with any field label for the empty charge spectrum is empty.
This follows directly from the definition.

-/

/-- `ofFieldLabel ‚àÖ F` is empty for any field label `F`. -/
@[simp]
lemma ofFieldLabel_empty (F : FieldLabel) :
    ofFieldLabel (‚àÖ : ChargeSpectrum ùì©) F = ‚àÖ := by
  cases F
  all_goals
    rfl

/-!

### A.2. Monotonicity of `ofFieldLabel`

We show that the function `ofFieldLabel` is monotone in the charge spectrum, with relation to
the subset relation. That is for a fixed field label `F`, if `x ‚äÜ y` are charge spectra,
then `ofFieldLabel x F ‚äÜ ofFieldLabel y F`.

-/

/-- The function `ofFieldLabel` is monotone in the charge spectrum. -/
lemma ofFieldLabel_mono {x y : ChargeSpectrum ùì©} (h : x ‚äÜ y) (F : FieldLabel) :
    x.ofFieldLabel F ‚äÜ y.ofFieldLabel F := by
  rw [subset_def] at h
  obtain ‚ü®h1, h2, h3, h4‚ü© := h
  cases F
  all_goals simp_all [ofFieldLabel]

/-!

### A.3. Membership of conjugate charegs

We show that a charge is a member of the finite sets associated with a field label if and only if
its negative is a member of the finite set associated with the conjugate field label.

-/

@[simp]
lemma mem_ofFieldLabel_fiveHd (x : ùì©) (y : ChargeSpectrum ùì©) :
    x ‚àà y.ofFieldLabel FieldLabel.fiveHd ‚Üî -x ‚àà y.ofFieldLabel .fiveBarHd := by
  simp [ofFieldLabel]
  aesop

@[simp]
lemma mem_ofFieldLabel_fiveHu (x : ùì©) (y : ChargeSpectrum ùì©) :
    x ‚àà y.ofFieldLabel FieldLabel.fiveHu ‚Üî -x ‚àà y.ofFieldLabel .fiveBarHu := by
  simp [ofFieldLabel]
  aesop

@[simp]
lemma mem_ofFieldLabel_fiveMatter (x : ùì©) (y : ChargeSpectrum ùì©) :
    x ‚àà y.ofFieldLabel FieldLabel.fiveMatter ‚Üî -x ‚àà y.ofFieldLabel .fiveBarMatter := by
  simp [ofFieldLabel]
  aesop

/-!

### A.4. Extensionality of charge spectra via `ofFieldLabel`

We whow that two charge spectra are equal if they are equal on all field labels.

This extensionality lemma is actually overkill in most cases, as there are a lot more
direct ways to show that two charge spectra are equal.

-/

/-- Two charges are equal if they are equal on all field labels. -/
lemma ext_ofFieldLabel {x y : ChargeSpectrum ùì©} (h : ‚àÄ F, x.ofFieldLabel F = y.ofFieldLabel F) :
    x = y := by
  match x, y with
  | ‚ü®x1, x2, x3, x4‚ü©, ‚ü®y1, y2, y3, y4‚ü© =>
  have h1 := h FieldLabel.fiveBarHd
  have h2 := h FieldLabel.fiveBarHu
  have h3 := h FieldLabel.fiveBarMatter
  have h4 := h FieldLabel.tenMatter
  clear h
  simp_all [ofFieldLabel]
  rw [‚Üê Option.toFinset_inj] at h1 h2
  simp_all

end ChargeSpectrum

end SU5
end SuperSymmetry
