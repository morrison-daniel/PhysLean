/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.Completions
/-!
# Minimal super set

## i. Overview

The minimally super set of a spectrum of charges `x` is the finite set of
spectrums of charges `y` such that `x âŠ† y` and there is no `z` such that `x âŠ† z âŠ‚ y`.
The minimal super set is defined using a finite set of possible charges in the `5`-bar and `10`
representations of `su(5)`. This is to ensure that the minimal super set is itself finite.

In this file we define the minimal super set and prove some basic properties of it.

## ii. Key results

- `minimalSuperSet`: the minimal super set of a charge spectrum.
- `exists_minimalSuperSet`: the existence of a member of the minimal super set
  between two charge spectra.
- `subset_insert_filter_card_zero`: a statement related to closure properties of multisets
  of charge spectra under a proposition `p` satisfying certain properties. The proof
  of this result relies on induction on minimal super sets.

## iii. Table of contents

- A. The minimal super set
  - A.1. Members of the minimal super set are super sets
  - A.2. Self is not a member of the minimal super set
  - A.3. Cardinality of member of the minimal super set
  - A.4. Inserting charges and minimal super sets
  - A.5. Existence of a minimal super set member between two charges
- B. Induction properties on the minimal super set
  - B.1. Lifting propositions from minimal super sets to super sets
  - B.2. Closure of multisets based on proposition for minimal super sets
  - B.3. Closure of multisets based on propositions

## iv. References

There are no known references for the material in this file.

-/

namespace SuperSymmetry

namespace SU5

namespace ChargeSpectrum

variable {ð“© : Type} [DecidableEq ð“©]

/-!

## A. The minimal super set

We define the minimal super set.

-/
/-- Given a collection of charges `x` in `ofFinset S5 S10`,
  the minimal charges `y` in `ofFinset S5 S10` which are a super sets of `x`. -/
def minimalSuperSet (S5 S10 : Finset ð“©) (x : ChargeSpectrum ð“©) : Finset (ChargeSpectrum ð“©) :=
  let SqHd := if x.qHd.isSome then âˆ… else S5.map âŸ¨fun y => âŸ¨some y, x.qHu, x.Q5, x.Q10âŸ©,
    by intro y1 y2; simpâŸ©
  let SqHu := if x.qHu.isSome then âˆ… else S5.image fun y => âŸ¨x.qHd, some y, x.Q5, x.Q10âŸ©
  let SQ5 := (S5 \ x.Q5).image (fun y => âŸ¨x.qHd, x.qHu, insert y x.Q5, x.Q10âŸ©)
  let SQ10 := (S10 \ x.Q10).image (fun y => âŸ¨x.qHd, x.qHu, x.Q5, insert y x.Q10âŸ©)
  (SqHd âˆª SqHu âˆª SQ5 âˆª SQ10).erase x

/-!

### A.1. Members of the minimal super set are super sets

We show the basic property of a member `y âˆˆ minimalSuperSet S5 S10 x`, that is
that they are indeed super sets, namely `x âŠ† y`.

-/

lemma self_subset_mem_minimalSuperSet (S5 S10 : Finset ð“©) (x y : ChargeSpectrum ð“©)
    (hy : y âˆˆ minimalSuperSet S5 S10 x) : x âŠ† y := by
  simp [minimalSuperSet] at hy
  rcases hy with âŸ¨hy1, hr | hr | hr | hrâŸ©
  Â· match x with
    | âŸ¨none, _, _, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      rw [Subset]
      simp [hasSubset]
    | âŸ¨some x1, _, _, _âŸ© =>
      simp at hr
  Â· match x with
    | âŸ¨_, none, _, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      rw [Subset]
      simp [hasSubset]
    | âŸ¨_, some x2, _, _âŸ© =>
      simp at hr
  Â· match x with
    | âŸ¨_, _, Q5, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      rw [Subset]
      simp [hasSubset]
  Â· match x with
    | âŸ¨_, _, _, Q10âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      rw [Subset]
      simp [hasSubset]

/-!

### A.2. Self is not a member of the minimal super set

A charge spectrum is not a member of its own minimal super set. We give
two different forms of this result.

-/

@[simp]
lemma self_not_mem_minimalSuperSet (S5 S10 : Finset ð“©) (x : ChargeSpectrum ð“©) :
    x âˆ‰ minimalSuperSet S5 S10 x := by
  simp [minimalSuperSet]

lemma self_neq_mem_minimalSuperSet (S5 S10 : Finset ð“©) (x y : ChargeSpectrum ð“©)
    (hy : y âˆˆ minimalSuperSet S5 S10 x) : x â‰  y := by
  by_contra h
  subst h
  simp at hy

/-!

### A.3. Cardinality of member of the minimal super set

We show that any member `y` of the minimal super set of `x` has cardinality one more than
that of `x`. I.e. it contains exactly one more unique charge.

-/

lemma card_of_mem_minimalSuperSet {S5 S10 : Finset ð“©} {x : ChargeSpectrum ð“©}
    (y : ChargeSpectrum ð“©) (hy : y âˆˆ minimalSuperSet S5 S10 x) :
    card y = card x + 1 := by
  simp [minimalSuperSet] at hy
  rcases hy with âŸ¨hy1, hr | hr | hr | hrâŸ©
  Â· match x with
    | âŸ¨none, _, _, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      simp [card]
      omega
    | âŸ¨some x1, _, _, _âŸ© =>
      simp at hr
  Â· match x with
    | âŸ¨_, none, _, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      simp [card]
      omega
    | âŸ¨_, some x2, _, _âŸ© =>
      simp at hr
  Â· match x with
    | âŸ¨_, _, Q5, _âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      simp [card]
      rw [Finset.card_insert_of_notMem]
      omega
      by_contra h
      rw [Finset.insert_eq_of_mem h] at hy1
      simp at hy1
  Â· match x with
    | âŸ¨_, _, _, Q10âŸ© =>
      simp at hr
      obtain âŸ¨a, ha, rflâŸ© := hr
      simp [card]
      rw [Finset.card_insert_of_notMem]
      omega
      by_contra h
      rw [Finset.insert_eq_of_mem h] at hy1
      simp at hy1

/-!

### A.4. Inserting charges and minimal super sets

We show that inserting a charge from `S5` or `S10` into `x`'s `Q5` or `Q10` respectively
which is not already present in `x` gives a member of the minimal super set.

Likewise we show that if `x` has no `qHd` or `qHu` charge, then inserting a charge from `S5`
into `qHd` or `qHu` respectively gives a member of the minimal super set.

-/

lemma insert_Q5_mem_minimalSuperSet {S5 S10 : Finset ð“©} {x : ChargeSpectrum ð“©}
    (z : ð“©) (hz : z âˆˆ S5) (hznot : z âˆ‰ x.Q5) :
    âŸ¨x.qHd, x.qHu, insert z x.Q5, x.Q10âŸ© âˆˆ minimalSuperSet S5 S10 x := by
  simp [minimalSuperSet]
  match x with
  | âŸ¨qHd, qHu, Q5, Q10âŸ© =>
  apply And.intro
  Â· simpa using hznot
  Â· right
    right
    left
    use z

lemma insert_Q10_mem_minimalSuperSet {S5 S10 : Finset ð“©} {x : ChargeSpectrum ð“©}
    (z : ð“©) (hz : z âˆˆ S10) (hznot : z âˆ‰ x.Q10) :
    âŸ¨x.qHd, x.qHu, x.Q5, insert z x.Q10âŸ© âˆˆ minimalSuperSet S5 S10 x := by
  simp [minimalSuperSet]
  match x with
  | âŸ¨qHd, qHu, Q5, Q10âŸ© =>
  apply And.intro
  Â· simpa using hznot
  Â· right
    right
    right
    use z

lemma some_qHd_mem_minimalSuperSet_of_none {S5 S10 : Finset ð“©}
    {x2 : Option ð“© Ã— Finset ð“© Ã— Finset ð“©} (z : ð“©) (hz : z âˆˆ S5) :
    âŸ¨some z, x2.1, x2.2.1, x2.2.2âŸ© âˆˆ minimalSuperSet S5 S10 âŸ¨none, x2.1, x2.2.1, x2.2.2âŸ© := by
  simp_all [minimalSuperSet]

lemma some_qHu_mem_minimalSuperSet_of_none {S5 S10 : Finset ð“©}
    {x1 : Option ð“©} {x2 : Finset ð“© Ã— Finset ð“©} (z : ð“©) (hz : z âˆˆ S5) :
    âŸ¨x1, some z, x2.1,x2.2âŸ© âˆˆ minimalSuperSet S5 S10 âŸ¨x1, none, x2.1, x2.2âŸ© := by
  simp_all [minimalSuperSet]

/-!

### A.5. Existence of a minimal super set member between two charges

We show that if `y` has charges from `S5` and `S10` and is a super set of `x` but not equal to `x`
then there is a `z` in the minimal super set of `x` which is a subset of `y`.

This shows, in a sense, that `minimalSuperSet` is "minimal", although it does not
go all the way to doing that. In particular, it does show that every minimal super set
is a member of `minimalSuperSet`.

-/

lemma exists_minimalSuperSet (S5 S10 : Finset ð“©) {x y : ChargeSpectrum ð“©}
    (hy : y âˆˆ ofFinset S5 S10) (hsubset : x âŠ† y)
    (hxneqy : x â‰  y) : âˆƒ z âˆˆ minimalSuperSet S5 S10 x, z âŠ† y := by
  rw [Subset] at hsubset
  dsimp [hasSubset] at hsubset
  match x, y with
  | âŸ¨x1, x2, x3, x4âŸ©, âŸ¨y1, y2, y3, y4âŸ© =>
  simp at hxneqy
  by_cases h3 : x3 â‰  y3
  Â· have h3Strict : x3 âŠ‚ y3 := by
      refine Finset.ssubset_iff_subset_ne.mpr ?_
      simp_all
    rw [Finset.ssubset_iff_of_subset (by simp_all)] at h3Strict
    obtain âŸ¨z3, hz3, h3âŸ© := h3Strict
    use âŸ¨x1, x2, insert z3 x3, x4âŸ©
    constructor
    Â· apply insert_Q5_mem_minimalSuperSet
      Â· simp [mem_ofFinset_iff] at hy
        apply hy.2.2.1 hz3
      Â· exact h3
    Â· rw [Subset]
      dsimp [hasSubset]
      simp_all
      rw [@Finset.insert_subset_iff]
      simp_all
  simp at h3
  subst h3
  by_cases h4 : x4 â‰  y4
  Â· have h4Strict : x4 âŠ‚ y4 := by
      refine Finset.ssubset_iff_subset_ne.mpr ?_
      simp_all
    rw [Finset.ssubset_iff_of_subset (by simp_all)] at h4Strict
    obtain âŸ¨z4, hz4, h4âŸ© := h4Strict
    use âŸ¨x1, x2, x3, insert z4 x4âŸ©
    constructor
    Â· apply insert_Q10_mem_minimalSuperSet
      Â· simp [mem_ofFinset_iff] at hy
        apply hy.2.2.2 hz4
      Â· exact h4
    Â· rw [Subset]
      dsimp [hasSubset]
      simp_all
      rw [@Finset.insert_subset_iff]
      simp_all
  simp at h4
  subst h4
  simp_all
  match x1, y1, x2, y2 with
  | some x1, none, x2, y2 =>
    simp at hsubset
  | none, some y1, x2, y2 =>
    simp at hsubset
    use âŸ¨some y1, x2, x3, x4âŸ©
    constructor
    Â· have h0 := (some_qHd_mem_minimalSuperSet_of_none (S5 := S5) (S10 := S10) y1
        (by simp_all [mem_ofFinset_iff]) (x2 := (x2, x3, x4)))
      simpa using h0
    Â· simp_all [hasSubset]
  | x1, y1, some x2, none =>
    simp at hsubset
  | x1, y1, none, some y2 =>
    simp at hsubset
    use âŸ¨x1, some y2, x3, x4âŸ©
    constructor
    Â· have h0 := (some_qHu_mem_minimalSuperSet_of_none (x1 := x1) (S5 := S5) (S10 := S10) y2
        (by simp_all [mem_ofFinset_iff]) (x2 := (x3, x4)))
      simpa using h0
    Â· simp_all [hasSubset]
  | none, none, none, none =>
    simp_all
  | some x1, some y1, none, none =>
    simp_all
  | none, none, some x2, some y2 =>
    simp_all
  | some x1, some y1, some x2, some y2 =>
    simp_all

/-!

## B. Induction properties on the minimal super set

We now prove a number of induction properties related to minimal super sets.

-/

/-!

### B.1. Lifting propositions from minimal super sets to super sets

We show that for a proposition `p` on charge spectra with the property that
it is true on all minimal super sets of `x` if it true on `x` itself, then it is true on all
super sets of `x` if it is true for `x` itself.

-/

lemma minimalSuperSet_induction_on_inductive {S5 S10 : Finset ð“©}
    (p : ChargeSpectrum ð“© â†’ Prop)
    (hp : (x : ChargeSpectrum ð“©) â†’ p x â†’ âˆ€ y âˆˆ minimalSuperSet S5 S10 x, p y)
    (x : ChargeSpectrum ð“©) (hbase : p x)
    (y : ChargeSpectrum ð“©) (hy : y âˆˆ ofFinset S5 S10) (hsubset : x âŠ† y) :
    (n : â„•) â†’ (hn : n = y.card - x.card) â†’ p y
  | 0, hn => by
    have hxy : x = y := by
      refine eq_of_subset_card hsubset ?_
      have hl : card x â‰¤ card y := card_mono hsubset
      omega
    subst hxy
    simp_all
  | Nat.succ n, hn => by
    have hxy : x â‰  y := by
      intro h
      subst h
      simp at hn
    obtain âŸ¨z, hz, hsubsetzâŸ© := exists_minimalSuperSet S5 S10 hy hsubset hxy
    refine minimalSuperSet_induction_on_inductive p hp z ?_ y hy ?_ n ?_
    Â· exact hp x hbase z hz
    Â· exact hsubsetz
    Â· rw [card_of_mem_minimalSuperSet z hz]
      omega

/-!

### B.2. Closure of multisets based on proposition for minimal super sets

We show that for a predicate `p` on charge spectrum,
if a multiset `T` of complete charge spectra has the property that
- all insertions of a `q10` charge either ends in `T` or fails `p`.
- all insertions of a `q5` charge either ends in `T` or fails `p`.
Then if `x` is in `T` then all members of the minimal super set of `x` either
are in `T` or fail `p`.

-/
variable {ð“© : Type} [DecidableEq ð“©]

lemma insert_filter_card_zero
    (T : Multiset (ChargeSpectrum ð“©)) (S5 S10 : Finset ð“©)
    (p : ChargeSpectrum ð“© â†’ Prop) [DecidablePred p]
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (h10 : âˆ€ q10 : S10, ((T.map fun x => âŸ¨x.qHd, x.qHu, x.Q5, insert q10.1 x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…)
    (h5 : âˆ€ q5 : S5, ((T.map fun x => âŸ¨x.qHd, x.qHu, insert q5.1 x.Q5, x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…) :
    âˆ€ x âˆˆ T, âˆ€ y âˆˆ minimalSuperSet S5 S10 x, y âˆ‰ T â†’ Â¬ p y := by
  intro âŸ¨xqHd, xqHu, xQ5, xQ10âŸ© x_mem_T y y_mem_minimalSuperSet y_not_in_T
  have x_isComplete : IsComplete âŸ¨xqHd, xqHu, xQ5, xQ10âŸ© := hComplet _ x_mem_T
  have xqHd_isSome : xqHd.isSome := by
    simp [IsComplete] at x_isComplete
    exact x_isComplete.1
  rw [Option.isSome_iff_exists] at xqHd_isSome
  obtain âŸ¨xqHd, rflâŸ© := xqHd_isSome
  have xqHu_isSome : xqHu.isSome := by
    simp [IsComplete] at x_isComplete
    exact x_isComplete.1
  rw [Option.isSome_iff_exists] at xqHu_isSome
  obtain âŸ¨xqHu, rflâŸ© := xqHu_isSome
  simp [minimalSuperSet] at y_mem_minimalSuperSet
  simp_all
  rcases y_mem_minimalSuperSet with âŸ¨q5, q5_mem_S5, rflâŸ© | âŸ¨q10, q10_mem_S10, rflâŸ©
  Â· have h5' := h5 q5 q5_mem_S5.1
    exact h5' âŸ¨some xqHd, some xqHu, xQ5, xQ10âŸ© x_mem_T y_not_in_T
  Â· have h10' := h10 q10 q10_mem_S10.1
    exact h10' âŸ¨some xqHd, some xqHu, xQ5, xQ10âŸ© x_mem_T y_not_in_T

/-!

### B.3. Closure of multisets based on propositions

We show that for a predicate `p` on charge spectrum which if false on a charge spectrum
is also false on all its super sets,
if a multiset `T` of complete charge spectra has the property that
- all insertions of a `q10` charge either ends in `T` or fails `p`.
- all insertions of a `q5` charge either ends in `T` or fails `p`.
Then if `y` is not in `T` then it does not satisfy `p`.

We first prove this with an explicit induction argument, `n`, and then
we prove it in a more user friendly way.

-/

lemma subset_insert_filter_card_zero_inductive
    (T : Multiset (ChargeSpectrum ð“©))
    (S5 S10 : Finset ð“©)
    (p : ChargeSpectrum ð“© â†’ Prop) [DecidablePred p]
    (hnotSubset : âˆ€ (x y : ChargeSpectrum ð“©), x âŠ† y â†’ Â¬ p x â†’ Â¬ p y)
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (x : ChargeSpectrum ð“©)
    (hx : x âˆˆ T) (y : ChargeSpectrum ð“©) (hsubset : x âŠ† y)
    (hy : y âˆˆ ofFinset S5 S10)
    (h10 : âˆ€ q10 : S10, ((T.map fun x => âŸ¨x.qHd, x.qHu, x.Q5, insert q10.1 x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…)
    (h5 : âˆ€ q5 : S5, ((T.map fun x => âŸ¨x.qHd, x.qHu, insert q5.1 x.Q5, x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…) :
    (n : â„•) â†’ (hn : n = y.card - x.card) â†’ y âˆ‰ T â†’ Â¬ p y
  | 0, hn, hnot_in_T => by
    have hxy : x = y := by
      refine eq_of_subset_card hsubset ?_
      have hl : x.card â‰¤ y.card := card_mono hsubset
      omega
    subst hxy
    simp_all
  | Nat.succ n, hn, hnot_in_T => by
    have hxy : x â‰  y := by
      intro h
      subst h
      simp at hn
    obtain âŸ¨z, hz, hsubsetzâŸ© := exists_minimalSuperSet S5 S10 hy hsubset hxy
    have hz' := insert_filter_card_zero T S5 S10 p hComplet h10 h5 x hx z hz
    by_cases hz_not_in_T : z âˆ‰ T
    Â· apply hnotSubset
      Â· exact hsubsetz
      Â· exact hz' hz_not_in_T
    apply subset_insert_filter_card_zero_inductive T S5 S10 p hnotSubset hComplet z (n := n)
    Â· simpa using hz_not_in_T
    Â· exact hsubsetz
    Â· exact hy
    Â· exact fun q10 => h10 q10
    Â· exact fun q5 => h5 q5
    Â· rw [card_of_mem_minimalSuperSet z hz]
      omega
    Â· exact hnot_in_T

/-- For a proposition `p` if `(T.uniqueMap4 (insert q10.1)).toMultiset.filter p`
  and `(T.uniqueMap3 (insert q5.1)).toMultiset.filter p` for all `q5 âˆˆ S5` and `q10 âˆˆ S10` then
  if `x âˆˆ T` and `x âŠ† y` if `y âˆ‰ T` then `Â¬ p y`.
  This assumes that all charges in `T` are complete, and that `p` satisfies
  `x âŠ† y â†’ Â¬ p x â†’ Â¬ p y`. -/
lemma subset_insert_filter_card_zero
    (T : Multiset (ChargeSpectrum ð“©))
    (S5 S10 : Finset ð“©)
    (p : ChargeSpectrum ð“© â†’ Prop) [DecidablePred p]
    (hnotSubset : âˆ€ (x y : ChargeSpectrum ð“©), x âŠ† y â†’ Â¬ p x â†’ Â¬ p y)
    (hComplet : âˆ€ x âˆˆ T, IsComplete x)
    (x : ChargeSpectrum ð“©)
    (hx : x âˆˆ T) (y : ChargeSpectrum ð“©) (hsubset : x âŠ† y)
    (hy : y âˆˆ ofFinset S5 S10)
    (h10 : âˆ€ q10 : S10, ((T.map fun x => âŸ¨x.qHd, x.qHu, x.Q5, insert q10.1 x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…)
    (h5 : âˆ€ q5 : S5, ((T.map fun x => âŸ¨x.qHd, x.qHu, insert q5.1 x.Q5, x.Q10âŸ©).filter
      fun y => (y âˆ‰ T âˆ§ p y)) = âˆ…) :
      y âˆ‰ T â†’ Â¬ p y :=
  subset_insert_filter_card_zero_inductive T S5 S10 p hnotSubset hComplet x hx y hsubset hy h10 h5
    (y.card - x.card) rfl

end ChargeSpectrum

end SU5

end SuperSymmetry
