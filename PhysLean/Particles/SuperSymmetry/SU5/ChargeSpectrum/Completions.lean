/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.MinimallyAllowsTerm.OfFinset
/-!

# Completions of charges

## i. Overview

Recall that a charge spectrum has optional `Hu` and `Hd` charges, and
can have an empty set of `5`-bar or `10` charges.

We say a charge spectrum is complete if it has all types of fields present, i.e.
the `Hd` and `Hu` charges are present, and the sets of `5`-bar and `10` charges are non-empty.

Given a non-complete charge spectrum `x` we can find all the completions of `x`,
which charges in given subsets. That is, all charge spectra `y` which are a super set of `x`,
are complete, and have their charges in the given subsets.

## ii. Key results

- `IsComplete` : A predicate saying a charge spectrum is complete.
- `completions` : Given a charge spectrum `x` and finite sets of charges `S5` and `S10`,
  the multiset of completions of `x` with charges in `S5` and `S10`.
- `completionsTopYukawa` : A fast version of `completions` for an `x` which is in
  `minimallyAllowsTermsOfFinset S5 S10 .topYukawa`, or in other words has a `qHu` charge,
  a non-empty set of `10` charges, but does not have a `qHd` charge or any `5`-bar charges.

## iii. Table of contents

- A. The IsComplete predicate
  - A.1. The empty spectrum is not complete
  - A.2. The predicate `IsCompelete` is monotonic
- B. Multiset of completions
  - B.1. A membership condition
  - B.2. No duplicate condition
  - B.3. Completions of a complete charge spectrum
  - B.4. Membership of own completions
  - B.5. Completeness of members of completions
  - B.6. Subset of members of completions
- C. Completions for top Yukawa
  - C.1. No duplicates in completionsTopYukawa
  - C.2. Equivalence of completions and completionsTopYukawa

## iv. References

There are no known references for the material in this module.

-/

namespace SuperSymmetry

namespace SU5

namespace ChargeSpectrum

variable {ð“© : Type}
/-!

## A. The IsComplete predicate

We say a charge spectrum is complete if it has all types of fields present, i.e.
the `Hd` and `Hu` charges are present, and the sets of `5`-bar and `10` charges are non-empty.

-/

/-- A charge spectrum is complete if it has all types of fields. -/
def IsComplete (x : ChargeSpectrum ð“©) : Prop :=
  x.qHd.isSome âˆ§ x.qHu.isSome âˆ§ x.Q5 â‰  âˆ… âˆ§ x.Q10 â‰  âˆ…

instance [DecidableEq ð“©] (x : ChargeSpectrum ð“©) : Decidable (IsComplete x) :=
  inferInstanceAs (Decidable (x.qHd.isSome âˆ§ x.qHu.isSome âˆ§ x.Q5 â‰  âˆ… âˆ§ x.Q10 â‰  âˆ…))

/-!

### A.1. The empty spectrum is not complete

The empty charge spectrum is not complete, since it has no charges present.

-/

@[simp]
lemma not_isComplete_empty : Â¬ IsComplete (âˆ… : ChargeSpectrum ð“©) := by
  simp [IsComplete]

/-!

### A.2. The predicate `IsCompelete` is monotonic

The predicate `IsComplete` is monotonic with respect to the subset relation. That is, if `x` is a
subset of `y` and `x` is complete, then `y` is also complete.

-/

lemma isComplete_mono {x y : ChargeSpectrum ð“©} (h : x âŠ† y) (hx : IsComplete x) :
    IsComplete y := by
  simp [IsComplete] at *
  rw [subset_def] at h
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· by_contra hn
    simp only [Bool.not_eq_true, Option.isSome_eq_false_iff, Option.isNone_iff_eq_none] at hn
    have h1 := h.1
    have hx1 := hx.1
    rw [Option.isSome_iff_exists] at hx1
    obtain âŸ¨a, haâŸ© := hx1
    rw [hn, ha] at h1
    simp at h1
  Â· by_contra hn
    simp only [Bool.not_eq_true, Option.isSome_eq_false_iff, Option.isNone_iff_eq_none] at hn
    have h1 := h.2.1
    have hx1 := hx.2.1
    rw [Option.isSome_iff_exists] at hx1
    obtain âŸ¨a, haâŸ© := hx1
    rw [hn, ha] at h1
    simp at h1
  Â· by_contra hn
    simp_all
  Â· by_contra hn
    simp_all

/-!

## B. Multiset of completions

The completions of a charge spectrum `x` with charges in given finite sets `S5` and `S10`
are all the charge spectra `y` which are a super set of `x`, are complete, and have
their charges in `S5` and `S10`.

-/

variable [DecidableEq ð“©]

/-- Given a collection of charges `x` in `ofFinset S5 S10`,
  the minimal charges `y` in `ofFinset S5 S10` which are a super sets of `x` and are
  complete. -/
def completions (S5 S10 : Finset ð“©) (x : ChargeSpectrum ð“©) : Multiset (ChargeSpectrum ð“©) :=
  let SqHd := if x.qHd.isSome then {x.qHd} else S5.val.map fun y => some y
  let SqHu := if x.qHu.isSome then {x.qHu} else S5.val.map fun y => some y
  let SQ5 := if x.Q5 â‰  âˆ… then {x.Q5} else S5.val.map fun y => {y}
  let SQ10 := if x.Q10 â‰  âˆ… then {x.Q10} else S10.val.map fun y => {y}
  (SqHd Ã—Ë¢ SqHu Ã—Ë¢ SQ5 Ã—Ë¢ SQ10).map (toProd).symm

/-!

### B.1. A membership condition

A simple relation for membership of a charge spectrum in the completions of another.

-/

lemma mem_completions_iff {S5 S10 : Finset ð“©} {x y : ChargeSpectrum ð“©} :
    y âˆˆ completions S5 S10 x â†”
    y.qHd âˆˆ (if x.qHd.isSome then {x.qHd} else S5.val.map fun y => some y) âˆ§
    y.qHu âˆˆ (if x.qHu.isSome then {x.qHu} else S5.val.map fun y => some y) âˆ§
    y.Q5 âˆˆ (if x.Q5 â‰  âˆ… then {x.Q5} else S5.val.map fun y => {y}) âˆ§
    y.Q10 âˆˆ (if x.Q10 â‰  âˆ… then {x.Q10} else S10.val.map fun y => {y}) := by
  rw [completions]
  rw [Multiset.mem_map]
  constructor
  Â· rintro âŸ¨a, h, hyâŸ©
    have ha : a = toProd y := by subst hy; simp
    subst ha
    simpa [toProd] using h
  Â· intro h
    use toProd y
    simpa [toProd] using h

/-!

### B.2. No duplicate condition

-/

/-- For speed we define `completions` to be a multiset, but in fact it has no duplicates,
so it could be defined as a finite set. -/
lemma completions_nodup (S5 S10 : Finset ð“©) (x : ChargeSpectrum ð“©) :
    (completions S5 S10 x).Nodup := by
  simp [completions]
  split_ifs
  all_goals
    refine Multiset.Nodup.map toProd.symm.injective ?_
    refine Multiset.Nodup.product ?_ (Multiset.Nodup.product ?_ (Multiset.Nodup.product ?_ ?_))
  any_goals exact Multiset.nodup_singleton _
  any_goals exact Finset.nodup_map_iff_injOn.mpr (by simp)

/-!

### B.3. Completions of a complete charge spectrum

-/

/-- The completions of a complete charge spectrum is just the singleton containing itself. -/
lemma completions_eq_singleton_of_complete {S5 S10 : Finset ð“©} (x : ChargeSpectrum ð“©)
    (hcomplete : IsComplete x) :
    completions S5 S10 x = {x} := by
  simp [completions]
  simp [IsComplete] at hcomplete
  by_cases h1 : x.qHd.isSome
  case' neg => simp_all
  by_cases h2 : x.qHu.isSome
  case' neg => simp_all
  by_cases h3 : x.Q5 â‰  âˆ…
  case' neg => simp_all
  by_cases h4 : x.Q10 â‰  âˆ…
  case' neg => simp_all
  simp_all
  rfl

/-!

### B.4. Membership of own completions

-/

/-- If a charge spectrum `x` is a member of its own completion then it is complete,
  and vice versa. -/
@[simp]
lemma self_mem_completions_iff_isComplete {S5 S10 : Finset ð“©} (x : ChargeSpectrum ð“©) :
    x âˆˆ completions S5 S10 x â†” IsComplete x := by
  simp [mem_completions_iff, IsComplete]
  by_cases h1 : x.qHd.isSome
  case neg => simp_all
  by_cases h2 : x.qHu.isSome
  case' neg => simp_all
  by_cases h3 : x.Q5 â‰  âˆ…
  case' neg => simp_all
  by_cases h4 : x.Q10 â‰  âˆ…
  case' neg => simp_all
  simp_all

/-!

### B.5. Completeness of members of completions

We now show that any member of the completions of a charge spectrum is complete.

-/

/-- A charge spectrum which is a member of the completions of another charge
  spectrum is complete. -/
lemma mem_completions_isComplete {S5 S10 : Finset ð“©} {x y : ChargeSpectrum ð“©}
    (hx : y âˆˆ completions S5 S10 x) : IsComplete y := by
  match y with
  | âŸ¨qHd, qHu, Q5, Q10âŸ© =>
  simp [mem_completions_iff] at hx
  match x with
  | âŸ¨x1, x2, x3, x4âŸ© =>
  simp_all
  rw [IsComplete]
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· simp
    by_cases hs : x1.isSome
    Â· simp_all
    Â· simp_all
      obtain âŸ¨a, h, rflâŸ© := hx.1
      simp
  Â· simp
    by_cases hs : x2.isSome
    Â· simp_all
    Â· simp_all
      obtain âŸ¨a, h, rflâŸ© := hx.2.1
      simp
  Â· simp
    by_cases hs : x3 â‰  âˆ…
    Â· simp_all
    Â· simp_all
      obtain âŸ¨a, h, rflâŸ© := hx.2.2.1
      simp
  Â· simp
    by_cases hs : x4 â‰  âˆ…
    Â· simp_all
    Â· simp_all
      obtain âŸ¨a, h, rflâŸ© := hx.2.2.2
      simp

/-!

### B.6. Subset of members of completions

We show that any member of the completions of a charge spectrum is a super set of that
charge spectrum.

-/

/-- If `y` is in the completions of `x` then `x âŠ† y`. -/
lemma self_subset_mem_completions (S5 S10 : Finset ð“©) (x y : ChargeSpectrum ð“©)
    (hy : y âˆˆ completions S5 S10 x) : x âŠ† y := by
  simp [mem_completions_iff] at hy
  rw [Subset]
  dsimp [hasSubset]
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· by_cases h : x.qHd.isSome
    Â· simp_all
    Â· simp_all
  Â· by_cases h : x.qHu.isSome
    Â· simp_all
    Â· simp_all
  Â· by_cases h : x.Q5 â‰  âˆ…
    Â· simp_all
    Â· simp_all
  Â· by_cases h : x.Q10 â‰  âˆ…
    Â· simp_all
    Â· simp_all

/-- If `x` is a subset of `y` and `y` is complete, then there is a completion of `x` which is also
  a subset of `y`. -/
lemma exist_completions_subset_of_complete (S5 S10 : Finset ð“©) (x y : ChargeSpectrum ð“©)
    (hsubset : x âŠ† y) (hy : y âˆˆ ofFinset S5 S10) (hycomplete : IsComplete y) :
    âˆƒ z âˆˆ completions S5 S10 x, z âŠ† y := by
  by_cases hx : IsComplete x
  Â· use x
    simp_all
  rw [Subset] at hsubset
  dsimp [hasSubset] at hsubset
  match x, y with
  | âŸ¨x1, x2, x3, x4âŸ©, âŸ¨y1, y2, y3, y4âŸ© =>
  simp [IsComplete] at hycomplete
  rw [Option.isSome_iff_exists, Option.isSome_iff_exists] at hycomplete
  obtain âŸ¨y1, rflâŸ© := hycomplete.1
  obtain âŸ¨y2, rflâŸ© := hycomplete.2.1
  rw [Finset.eq_empty_iff_forall_notMem, Finset.eq_empty_iff_forall_notMem] at hycomplete
  simp at hycomplete
  obtain âŸ¨z3, hz3âŸ© := hycomplete.1
  obtain âŸ¨z4, hz4âŸ© := hycomplete.2
  simp [mem_ofFinset_iff] at hy
  have hz3Mem : z3 âˆˆ S5 := by
    apply hy.2.2.1
    simp_all
  have hz4Mem : z4 âˆˆ S10 := by
    apply hy.2.2.2
    simp_all
  have hy1' : some y1 âˆˆ if x1.isSome = true then {x1} else
      Multiset.map (fun y => some y) S5.val := by
    by_cases h1 : x1.isSome
    Â· simp_all
      rw [Option.isSome_iff_exists] at h1
      obtain âŸ¨a, rflâŸ© := h1
      simp_all
    Â· simp_all
  have hy2' : some y2 âˆˆ if x2.isSome = true then {x2} else
      Multiset.map (fun y => some y) S5.val := by
    by_cases h2 : x2.isSome
    Â· simp_all
      rw [Option.isSome_iff_exists] at h2
      obtain âŸ¨a, rflâŸ© := h2
      simp_all
    Â· simp_all
  simp_all
  by_cases h3 : x3 â‰  âˆ…
  Â· by_cases h4 : x4 â‰  âˆ…
    Â· use âŸ¨y1, y2, x3, x4âŸ©
      constructor
      Â· simp_all [mem_completions_iff]
      Â· rw [Subset]
        dsimp [hasSubset]
        simp_all
    Â· simp at h4
      subst h4
      use âŸ¨y1, y2, x3, {z4}âŸ©
      constructor
      Â· simp_all [mem_completions_iff]
      Â· rw [Subset]
        dsimp [hasSubset]
        simp_all
  Â· simp at h3
    subst h3
    by_cases h4 : x4 â‰  âˆ…
    Â· use âŸ¨y1, y2, {z3}, x4âŸ©
      constructor
      Â· simp_all [mem_completions_iff]
      Â· rw [Subset]
        dsimp [hasSubset]
        simp_all
    Â· simp at h4
      subst h4
      use âŸ¨y1, y2, {z3}, {z4}âŸ©
      constructor
      Â· simp_all [mem_completions_iff]
      Â· rw [Subset]
        dsimp [hasSubset]
        simp_all

/-!

## C. Completions for top Yukawa

We give a fast version of `completions` in the case when `x` has a `qHu` charge,
and a non-empty set of `10` charges, but does not have a `qHd` charge or any `5`-bar charges.
Here we only need to specify the allowed `5`-bar charges, not the allowed `10` charges.

This is the case for charges which minimally allow the top Yukawa coupling.

These definitions are primarily for speed, as this is the most common case we will
look at.

-/

/-- A fast version of `completions` for an `x` which is in
  `minimallyAllowsTermsOfFinset S5 S10 .topYukawa`. -/
def completionsTopYukawa (S5 : Finset ð“©) (x : ChargeSpectrum ð“©) :
    Multiset (ChargeSpectrum ð“©) :=
  (S5.val Ã—Ë¢ S5.val).map fun (qHd, q5) => âŸ¨qHd, x.qHu, {q5}, x.Q10âŸ©

/-!

### C.1. No duplicates in completionsTopYukawa

Like for `completions`, we define `completionsTopYukawa` as a multiset for speed,
however, we can show it has no duplicates.

-/

omit [DecidableEq ð“©] in
/-- The multiset `completionsTopYukawa S5 x` has no duplicates. -/
lemma completionsTopYukawa_nodup {S5 : Finset ð“©} (x : ChargeSpectrum ð“©) :
    (completionsTopYukawa S5 x).Nodup := by
  simp [completionsTopYukawa]
  refine Multiset.Nodup.map_on ?_ ?_
  intro (z1, z2) hz (y1, y2) hy h
  simp [eq_iff] at h
  simp_all
  exact (S5.product S5).nodup

/-!

### C.2. Equivalence of completions and completionsTopYukawa

For charges `x` which are in `minimallyAllowsTermsOfFinset S5 S10 .topYukawa`,
we show that `completions S5 S10 x` and `completionsTopYukawa S5 x` are equal multisets.

-/

/-- The multisets `completions S5 S10 x` and `completionsTopYukawa S5 x` are equivalent if
  `x` minimally allows the top Yukawa. -/
lemma completions_eq_completionsTopYukawa_of_mem_minimallyAllowsTermsOfFinset [AddCommGroup ð“©]
    {S5 S10 : Finset ð“©} (x : ChargeSpectrum ð“©)
    (hx : x âˆˆ minimallyAllowsTermsOfFinset S5 S10 .topYukawa) :
    completions S5 S10 x = completionsTopYukawa S5 x := by
  refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
  Â· exact completions_nodup S5 S10 x
  Â· exact completionsTopYukawa_nodup x
  intro a
  simp [minimallyAllowsTermsOfFinset] at hx
  obtain âŸ¨qHu, Q10, âŸ¨âŸ¨h1, âŸ¨h2, hcardâŸ©âŸ©, h3âŸ©, rflâŸ© := hx
  simp [completions, completionsTopYukawa]
  have Q10_neq_zero : Q10 â‰  0 := by
    by_contra hn
    subst hn
    simp at hcard
  simp [Q10_neq_zero]
  match a with
  | âŸ¨xqHd, xqHu, xQ5, xQ10âŸ© =>
  simp [eq_iff]
  aesop

end ChargeSpectrum

end SU5

end SuperSymmetry
