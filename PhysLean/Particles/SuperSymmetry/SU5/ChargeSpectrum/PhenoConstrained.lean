/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.AllowsTerm
import Mathlib.Tactic.Polyrith
/-!

# Pheno constrained charge spectra

## i. Overview

We define a predicate `IsPhenoConstrained` on `ChargeSpectrum ğ“©` which is true if the charge
spectrum allows any super-potential or KÃ¤hler potential term leading to proton decay or
R-parity violation.

We prove basic properties of this predicate including monotonicity.

We define some variations of this result.

## ii. Key results

- `IsPhenoConstrained`: The predicate defining a pheno-constrained charge spectrum
  as one allowing any term leading to proton decay or R-parity violation.
- `phenoConstrainingChargesSP`: The multiset of charges of terms in the super-potential
  leading to a pheno-constrained charge spectrum.
- `IsPhenoConstrainedQ5`: The predicate defining when a charge spectrum becomes
  pheno-constrained after adding a single charge to the `Q5` set.
- `IsPhenoConstrainedQ10`: The predicate defining when a charge spectrum becomes
  pheno-constrained after adding a single charge to the `Q10` set.

## iii. Table of contents

- A. Phenomenological constrained charge spectra
  - A.1. Decidability of `IsPhenoConstrained`
  - A.2. The empty charge spectrum is not pheno-constrained
  - A.3. Monotonicity of being pheno-constrained
- B. Charges of pheno-constraining terms in the super potential
  - B.1. The empty charge spectrum has an empty set of pheno-constraining term charges
  - B.2. The charges of pheno-constraining terms in the SP is monotone
- C. Phenomenologically constrained charge spectra after adding a single Q5 charge
  - C.2. Reducing the condition `IsPhenoConstrainedQ5`
  - C.3. Decidability of `IsPhenoConstrainedQ5`
  - C.4. Charge spectra with added `Q5` charge is pheno-constrained iff
- D. Phenomenologically constrained charge spectra after adding a single Q10 charge
  - D.2. Reducing the condition `IsPhenoConstrainedQ10`
  - D.3. Decidability of `IsPhenoConstrainedQ10`
  - D.4. Charge spectra with added `Q10` charge is pheno-constrained iff

## iv. References

There are no known references for the material in this file.

-/
namespace SuperSymmetry

namespace SU5

namespace ChargeSpectrum
open SuperSymmetry.SU5
open PotentialTerm

variable {ğ“© : Type} [AddCommGroup ğ“©]

/-!

## A. Phenomenological constrained charge spectra

-/

/-- A charge is pheno-constrained if it leads to the presence of any term causing proton decay
  ` {W1, Î›, W2, K1}` or R-parity violation `{Î², Î›, W2, W4, K1, K2}`. -/
def IsPhenoConstrained (x : ChargeSpectrum ğ“©) : Prop :=
  x.AllowsTerm Î¼ âˆ¨ x.AllowsTerm Î² âˆ¨ x.AllowsTerm Î› âˆ¨ x.AllowsTerm W2 âˆ¨ x.AllowsTerm W4 âˆ¨
  x.AllowsTerm K1 âˆ¨ x.AllowsTerm K2 âˆ¨ x.AllowsTerm W1

/-!

### A.1. Decidability of `IsPhenoConstrained`

-/

instance decidableIsPhenoConstrained [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) :
    Decidable x.IsPhenoConstrained :=
  inferInstanceAs (Decidable (x.AllowsTerm Î¼ âˆ¨ x.AllowsTerm Î² âˆ¨ x.AllowsTerm Î› âˆ¨ x.AllowsTerm W2
    âˆ¨ x.AllowsTerm W4 âˆ¨ x.AllowsTerm K1 âˆ¨ x.AllowsTerm K2 âˆ¨ x.AllowsTerm W1))

/-!

### A.2. The empty charge spectrum is not pheno-constrained

The empty charge spectrum does not allow any terms, and so is not pheno-constrained.

-/

@[simp]
lemma not_isPhenoConstrained_empty :
    Â¬ IsPhenoConstrained (âˆ… : ChargeSpectrum ğ“©) := by
  simp [IsPhenoConstrained, AllowsTerm, ofPotentialTerm_empty]

/-!

### A.3. Monotonicity of being pheno-constrained

If a charge spectrum `x` is pheno-constrained, then any charge spectrum `y` containing `x`
is also pheno-constrained.

-/

lemma isPhenoConstrained_mono {x y : ChargeSpectrum ğ“©} (h : x âŠ† y)
    (hx : x.IsPhenoConstrained) : y.IsPhenoConstrained := by
  simp [IsPhenoConstrained] at *
  rcases hx with hr | hr | hr | hr | hr | hr | hr | hr
  all_goals
    have h' := allowsTerm_mono h hr
    simp_all

/-!

## B. Charges of pheno-constraining terms in the super potential

-/

/-- The collection of charges of super-potential terms leading to a pheno-constrained model. -/
def phenoConstrainingChargesSP (x : ChargeSpectrum ğ“©) : Multiset ğ“© :=
  x.ofPotentialTerm' Î¼ + x.ofPotentialTerm' Î² + x.ofPotentialTerm' Î› +
  x.ofPotentialTerm' W2 + x.ofPotentialTerm' W4 + x.ofPotentialTerm' W1

/-!

### B.1. The empty charge spectrum has an empty set of pheno-constraining term charges

-/

@[simp]
lemma phenoConstrainingChargesSP_empty :
    phenoConstrainingChargesSP (âˆ… : ChargeSpectrum ğ“©) = âˆ… := by
  simp [phenoConstrainingChargesSP]

/-!

### B.2. The charges of pheno-constraining terms in the SP is monotone

-/
lemma phenoConstrainingChargesSP_mono [DecidableEq ğ“©] {x y : ChargeSpectrum ğ“©} (h : x âŠ† y) :
    x.phenoConstrainingChargesSP âŠ† y.phenoConstrainingChargesSP := by
  simp only [phenoConstrainingChargesSP]
  refine Multiset.subset_iff.mpr ?_
  intro z
  simp [or_assoc]
  intro hr
  rcases hr with hr | hr | hr | hr | hr | hr
  all_goals
    have h' := ofPotentialTerm'_mono h _ hr
    simp_all

/-!

## C. Phenomenologically constrained charge spectra after adding a single Q5 charge

We now define `IsPhenoConstrainedQ5` which gives the condition that a charge spectrum becomes
pheno-constrained after adding a single charge to the `Q5` set.

-/

/-- The proposition which is true if the addition of a charge `q5` to a set of charge `x` leads
  `x` to being phenomenologically constrained. -/
def IsPhenoConstrainedQ5 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q5 : ğ“©) : Prop :=
  x.AllowsTermQ5 q5 Î¼ âˆ¨ x.AllowsTermQ5 q5 Î² âˆ¨ x.AllowsTermQ5 q5 Î› âˆ¨ x.AllowsTermQ5 q5 W2 âˆ¨
  x.AllowsTermQ5 q5 W4 âˆ¨
  x.AllowsTermQ5 q5 K1 âˆ¨ x.AllowsTermQ5 q5 K2 âˆ¨ x.AllowsTermQ5 q5 W1

/-!

### C.2. Reducing the condition `IsPhenoConstrainedQ5`

-/

lemma isPhenoConstrainedQ5_iff [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q5 : ğ“©) :
    x.IsPhenoConstrainedQ5 q5 â†”
    x.AllowsTermQ5 q5 Î² âˆ¨ x.AllowsTermQ5 q5 Î› âˆ¨ x.AllowsTermQ5 q5 W4 âˆ¨
    x.AllowsTermQ5 q5 K1 âˆ¨ x.AllowsTermQ5 q5 W1 := by
  simp [IsPhenoConstrainedQ5, AllowsTermQ5]

/-!

### C.3. Decidability of `IsPhenoConstrainedQ5`

-/

instance decidableIsPhenoConstrainedQ5 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q5 : ğ“©) :
    Decidable (x.IsPhenoConstrainedQ5 q5) :=
  decidable_of_iff _ (isPhenoConstrainedQ5_iff x q5).symm

/-!

### C.4. Charge spectra with added `Q5` charge is pheno-constrained iff

-/

lemma isPhenoConstrained_insertQ5_iff_isPhenoConstrainedQ5 [DecidableEq ğ“©] {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q5 : ğ“©} :
    IsPhenoConstrained âŸ¨qHd, qHu, insert q5 Q5, Q10âŸ©â†”
    IsPhenoConstrainedQ5 âŸ¨qHd, qHu, Q5, Q10âŸ© q5 âˆ¨
    IsPhenoConstrained âŸ¨qHd, qHu, Q5, Q10âŸ©:= by
  constructor
  Â· intro hr
    rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
    all_goals
      rw [allowsTerm_insertQ5_iff_allowsTermQ5] at hr
      rcases hr with hr | hr
      all_goals
        simp_all [IsPhenoConstrainedQ5, IsPhenoConstrained]
  Â· intro hr
    rcases hr with hr | hr
    Â· simp [IsPhenoConstrainedQ5] at hr
      rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
      all_goals
        have hr' := allowsTerm_insertQ5_of_allowsTermQ5 _ hr
        simp_all [IsPhenoConstrained]
    Â· apply isPhenoConstrained_mono _ hr
      simp [subset_def]

/-!

## D. Phenomenologically constrained charge spectra after adding a single Q10 charge

We now define `IsPhenoConstrainedQ10` which gives the condition that a charge spectrum becomes
pheno-constrained after adding a single charge to the `Q10` set.

-/

/-- The proposition which is true if the addition of a charge `q10` to a set of charges `x` leads
  `x` to being phenomenologically constrained. -/
def IsPhenoConstrainedQ10 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q10 : ğ“©) : Prop :=
  x.AllowsTermQ10 q10 Î¼ âˆ¨ x.AllowsTermQ10 q10 Î² âˆ¨ x.AllowsTermQ10 q10 Î› âˆ¨ x.AllowsTermQ10 q10 W2 âˆ¨
  x.AllowsTermQ10 q10 W4 âˆ¨
  x.AllowsTermQ10 q10 K1 âˆ¨ x.AllowsTermQ10 q10 K2 âˆ¨ x.AllowsTermQ10 q10 W1

/-!

### D.2. Reducing the condition `IsPhenoConstrainedQ10`

-/

lemma isPhenoConstrainedQ10_iff [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q10 : ğ“©) :
    x.IsPhenoConstrainedQ10 q10 â†” x.AllowsTermQ10 q10 Î› âˆ¨ x.AllowsTermQ10 q10 W2 âˆ¨
    x.AllowsTermQ10 q10 K1 âˆ¨ x.AllowsTermQ10 q10 K2 âˆ¨ x.AllowsTermQ10 q10 W1 := by
  simp [IsPhenoConstrainedQ10, AllowsTermQ10]

/-!

### D.3. Decidability of `IsPhenoConstrainedQ10`

-/

instance decidableIsPhenoConstrainedQ10 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q10 : ğ“©) :
    Decidable (x.IsPhenoConstrainedQ10 q10) :=
  decidable_of_iff _ (isPhenoConstrainedQ10_iff x q10).symm

/-!

### D.4. Charge spectra with added `Q10` charge is pheno-constrained iff

-/

lemma isPhenoConstrained_insertQ10_iff_isPhenoConstrainedQ10 [DecidableEq ğ“©] {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q10 : ğ“©} :
    IsPhenoConstrained âŸ¨qHd, qHu, Q5, insert q10 Q10âŸ© â†”
    IsPhenoConstrainedQ10 âŸ¨qHd, qHu, Q5, Q10âŸ© q10 âˆ¨
    IsPhenoConstrained âŸ¨qHd, qHu, Q5, Q10âŸ© := by
  constructor
  Â· intro hr
    rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
    all_goals
      rw [allowsTerm_insertQ10_iff_allowsTermQ10] at hr
      rcases hr with hr | hr
      all_goals
        simp_all [IsPhenoConstrainedQ10, IsPhenoConstrained]
  Â· intro hr
    rcases hr with hr | hr
    Â· simp [IsPhenoConstrainedQ10] at hr
      rcases hr with hr | hr | hr | hr | hr | hr | hr | hr
      all_goals
        have hr' := allowsTerm_insertQ10_of_allowsTermQ10 _ hr
        simp_all [IsPhenoConstrained]
    Â· apply isPhenoConstrained_mono _ hr
      simp [subset_def]

end ChargeSpectrum

end SU5

end SuperSymmetry
