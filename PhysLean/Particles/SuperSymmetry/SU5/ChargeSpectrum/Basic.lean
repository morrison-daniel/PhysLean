/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Data.Finset.Powerset
import Mathlib.Data.Finset.Prod
import Mathlib.Data.Finset.Sort
import PhysLean.Particles.SuperSymmetry.SU5.Potential
/-!

# Charge Spectrum

## i. Overview

In this module we define the charge spectrum of a `SU(5)` SUSY GUT theory with
additional charges (usually `U(1)`) valued in `ğ“©` satisfying the condition of:
- The optional existence of a `Hd` particle in the `bar 5` representation.
- The optional existence of a `Hu` particle in the `5` representation.
- The optional existence of matter in the `bar 5` representation.
- The optional existence of matter in the `10` representation.

The charge spectrum contains the information of the *unique* charges of each type of particle
present in theory. Importantly, the charge spectrum does not contain information
about the multiplicity of those charges.

With just the charge spectrum of the theory it is possible to put a number of constraints
on the theory, most notably phenomenological constraints.

By keeping the presence of `Hd` and `Hu` optional, we can define a number of useful properties
of the charge spectrum, which can help in searching for viable theories.

## ii. Key results

- `ChargeSpectrum ğ“©` : The type of charge spectra with charges of type `ğ“©`, which is usually
  `â„¤`.

## iii. Table of contents

- A. The definition of the charge spectrum
- B. The subset relation
- C. The empty charge spectrum
- D. The cardinality of a charge spectrum
- E. The power set of a charge spectrum
- F. Finite sets of charge spectra with values

## iv. References

There are no known references for charge spectra in the literature.
They were created specifically for the purpose of PhysLean.

-/

namespace SuperSymmetry

namespace SU5

/-!

## A. The definition of the charge spectrum

-/

/-- The type such that an element corresponds to the collection of
  charges associated with the matter content of the theory.
  The order of charges is implicitly taken to be `qHd`, `qHu`, `Q5`, `Q10`.

  The `Q5` and `Q10` charges are represented by `Finset` rather than
  `Multiset`, so multiplicity is not included.

  This is defined for a general type `ğ“©`, which could be e.g.
- `â„¤` in the case of `U(1)`,
- `â„¤ Ã— â„¤` in the case of `U(1) Ã— U(1)`,
- `Fin 2` in the case of `â„¤â‚‚` etc.
-/
structure ChargeSpectrum (ğ“© : Type := â„¤) where
  /-- The charge of the `Hd` particle. -/
  qHd : Option ğ“©
  /-- The negative of the charge of the `Hu` particle. That is to say,
    the charge of the `Hu` when considered in the 5-bar representation. -/
  qHu : Option ğ“©
  /-- The finite set of charges of the matter fields in the `Q5` representation. -/
  Q5 : Finset ğ“©
  /-- The finite set of charges of the matter fields in the `Q10` representation. -/
  Q10 : Finset ğ“©

namespace ChargeSpectrum

variable {ğ“© : Type}

/-!

### A.1. Extensionality properties

We prove extensionality properties for `ChargeSpectrum ğ“©`, that is
conditions of when two elements of `ChargeSpectrum ğ“©` are equal.
We also show that when `ğ“©` has decidable equality, so does `ChargeSpectrum ğ“©`.

-/

lemma eq_of_parts {x y : ChargeSpectrum ğ“©} (h1 : x.qHd = y.qHd) (h2 : x.qHu = y.qHu)
    (h3 : x.Q5 = y.Q5) (h4 : x.Q10 = y.Q10) : x = y := by
  cases x
  cases y
  simp_all

lemma eq_iff {x y : ChargeSpectrum ğ“©} :
    x = y â†” x.qHd = y.qHd âˆ§ x.qHu = y.qHu âˆ§ x.Q5 = y.Q5 âˆ§ x.Q10 = y.Q10 := by
  constructor
  Â· intro h
    subst h
    simp
  Â· rintro âŸ¨h1, h2, h3, h4âŸ©
    exact eq_of_parts h1 h2 h3 h4

instance [DecidableEq ğ“©] : DecidableEq (ChargeSpectrum ğ“©) := fun _ _ =>
  decidable_of_iff _ eq_iff.symm

/-!

### A.2. Relation to products

We show that `ChargeSpectrum ğ“©` is equivalent to the product
`Option ğ“© Ã— Option ğ“© Ã— Finset ğ“© Ã— Fin ğ“©`.

In an old implementation this was definitionally true, it is not so now.

-/

/-- The explicit casting of a term of type `Charges ğ“©` to a term of
  `Option ğ“© Ã— Option ğ“© Ã— Finset ğ“© Ã— Finset ğ“©`. -/
def toProd : ChargeSpectrum ğ“© â‰ƒ Option ğ“© Ã— Option ğ“© Ã— Finset ğ“© Ã— Finset ğ“© where
  toFun x := (x.qHd, x.qHu, x.Q5, x.Q10)
  invFun x := âŸ¨x.1, x.2.1, x.2.2.1, x.2.2.2âŸ©
  left_inv x := by cases x; rfl
  right_inv x := by cases x; rfl

/-!

### A.3. Rendering

-/

unsafe instance [Repr ğ“©] : Repr (ChargeSpectrum ğ“©) where
  reprPrec x _ := match x with
    | âŸ¨qHd, qHu, Q5, Q10âŸ© =>
      let s1 := reprStr qHd
      let s2 := reprStr qHu
      let s5 := reprStr Q5
      let s10 := reprStr Q10
      s!"âŸ¨{s1}, {s2}, {s5}, {s10}âŸ©"

/-!

## B. The subset relation

We define a `HasSubset` and `HasSSubset` instance on `ChargeSpectrum ğ“©`.

-/

instance hasSubset : HasSubset (ChargeSpectrum ğ“©) where
  Subset x y :=
    x.qHd.toFinset âŠ† y.qHd.toFinset âˆ§
    x.qHu.toFinset âŠ† y.qHu.toFinset âˆ§
    x.Q5 âŠ† y.Q5 âˆ§
    x.Q10 âŠ† y.Q10

instance hasSSubset : HasSSubset (ChargeSpectrum ğ“©) where
  SSubset x y := x âŠ† y âˆ§ x â‰  y

instance subsetDecidable [DecidableEq ğ“©] (x y : ChargeSpectrum ğ“©) : Decidable (x âŠ† y) :=
  instDecidableAnd

lemma subset_def {x y : ChargeSpectrum ğ“©} : x âŠ† y â†” x.qHd.toFinset âŠ† y.qHd.toFinset âˆ§
    x.qHu.toFinset âŠ† y.qHu.toFinset âˆ§ x.Q5 âŠ† y.Q5 âˆ§ x.Q10 âŠ† y.Q10 := by
  rfl

@[simp, refl]
lemma subset_refl (x : ChargeSpectrum ğ“©) : x âŠ† x := âŸ¨by rfl, by rfl, by rfl, by rflâŸ©

lemma _root_.Option.toFinset_inj {x y : Option ğ“©} :
    x = y â†” x.toFinset = y.toFinset := by
  match x, y with
  | none, none => simp [Option.toFinset]
  | none, some a =>
    rw [show (none = some a) â†” False by simp]
    simp only [Option.toFinset_none, Option.toFinset_some, false_iff, ne_eq]
    rw [Finset.eq_singleton_iff_unique_mem]
    simp
  | some _, none => simp [Option.toFinset]
  | some _, some _ => simp [Option.toFinset]

lemma subset_trans {x y z : ChargeSpectrum ğ“©} (hxy : x âŠ† y) (hyz : y âŠ† z) : x âŠ† z := by
  simp_all [Subset]

lemma subset_antisymm {x y : ChargeSpectrum ğ“©} (hxy : x âŠ† y) (hyx : y âŠ† x) : x = y := by
  rw [Subset] at hxy hyx
  dsimp [hasSubset] at hxy hyx
  rcases x with âŸ¨x1, x2, x3, x4âŸ©
  rcases y with âŸ¨y1, y2, y3, y4âŸ©
  have hx1 : x1.toFinset = y1.toFinset := Finset.Subset.antisymm_iff.mpr âŸ¨hxy.1, hyx.1âŸ©
  have hx2 : x2.toFinset = y2.toFinset := Finset.Subset.antisymm_iff.mpr âŸ¨hxy.2.1, hyx.2.1âŸ©
  have hx3 : x3 = y3 := Finset.Subset.antisymm_iff.mpr âŸ¨hxy.2.2.1, hyx.2.2.1âŸ©
  have hx4 : x4 = y4 := Finset.Subset.antisymm_iff.mpr âŸ¨hxy.2.2.2, hyx.2.2.2âŸ©
  rw [â† Option.toFinset_inj] at hx1 hx2
  simp_all

/-!

## C. The empty charge spectrum

-/

instance emptyInst : EmptyCollection (ChargeSpectrum ğ“©) where
  emptyCollection := âŸ¨none, none, {}, {}âŸ©

@[simp]
lemma empty_subset (x : ChargeSpectrum ğ“©) : âˆ… âŠ† x := by
  simp [Subset, emptyInst]

@[simp]
lemma subset_of_empty_iff_empty {x : ChargeSpectrum ğ“©} :
    x âŠ† âˆ… â†” x = âˆ… := by
  constructor
  Â· intro h
    apply subset_antisymm
    Â· exact h
    Â· exact empty_subset x
  Â· intro h
    subst h
    simp

@[simp]
lemma empty_qHd : (âˆ… : ChargeSpectrum ğ“©).qHd = none := by
  simp [emptyInst]

@[simp]
lemma empty_qHu : (âˆ… : ChargeSpectrum ğ“©).qHu = none := by
  simp [emptyInst]

@[simp]
lemma empty_Q5 : (âˆ… : ChargeSpectrum ğ“©).Q5 = âˆ… := by
  simp [emptyInst]

@[simp]
lemma empty_Q10 : (âˆ… : ChargeSpectrum ğ“©).Q10 = âˆ… := by
  simp [emptyInst]

/-!

## D. The cardinality of a charge spectrum

-/

/-- The cardinality of a `Charges` is defined to be the sum of the cardinalities
  of each of the underlying finite sets of charges, with `Option â„¤` turned to finsets. -/
def card (x : ChargeSpectrum ğ“©) : Nat :=
  x.qHu.toFinset.card + x.qHd.toFinset.card + x.Q5.card + x.Q10.card

@[simp]
lemma card_empty : card (âˆ… : ChargeSpectrum ğ“©) = 0 := by
  simp [card, emptyInst]

lemma card_mono {x y : ChargeSpectrum ğ“©} (h : x âŠ† y) : card x â‰¤ card y := by
  simp [hasSubset] at h
  have h1 := Finset.card_le_card h.1
  have h2 := Finset.card_le_card h.2.1
  have h3 := Finset.card_le_card h.2.2.1
  have h4 := Finset.card_le_card h.2.2.2
  simp [card]
  omega

lemma eq_of_subset_card {x y : ChargeSpectrum ğ“©} (h : x âŠ† y) (hcard : card x = card y) : x = y := by
  simp [card] at hcard
  have h1 := Finset.card_le_card h.1
  have h2 := Finset.card_le_card h.2.1
  have h3 := Finset.card_le_card h.2.2.1
  have h4 := Finset.card_le_card h.2.2.2
  have h1 : x.1.toFinset = y.1.toFinset := by
    apply Finset.eq_of_subset_of_card_le h.1
    omega
  have h2 : x.qHu.toFinset = y.qHu.toFinset := by
    apply Finset.eq_of_subset_of_card_le h.2.1
    omega
  have h3 : x.Q5 = y.Q5 := by
    apply Finset.eq_of_subset_of_card_le h.2.2.1
    omega
  have h4 : x.Q10 = y.Q10 := by
    apply Finset.eq_of_subset_of_card_le h.2.2.2
    omega
  match x, y with
  | âŸ¨x1, x2, x3, x4âŸ©, âŸ¨y1, y2, y3, y4âŸ© =>
  rw [â† Option.toFinset_inj] at h1 h2
  simp_all

/-!

## E. The power set of a charge spectrum

-/

variable [DecidableEq ğ“©]

/-- The powerset of `x : Option ğ“©` defined as `{none}` if `x` is `none`
  and `{none, some y}` is `x` is `some y`. -/
def _root_.Option.powerset (x : Option ğ“©) : Finset (Option ğ“©) :=
  match x with
  | none => {none}
  | some x => {none, some x}

@[simp]
lemma _root_.Option.mem_powerset_iff {x : Option ğ“©} (y : Option ğ“©) :
    y âˆˆ x.powerset â†” y.toFinset âŠ† x.toFinset :=
  match x, y with
  | none, none => by
    simp [Option.powerset]
  | none, some _ => by
    simp [Option.powerset]
  | some _, none => by
    simp [Option.powerset]
  | some _, some _ => by
    simp [Option.powerset]

/-- The powerset of a charge . Given a charge `x : Charges`
  it's powerset is the finite set of all `Charges` which are subsets of `x`. -/
def powerset (x : ChargeSpectrum ğ“©) : Finset (ChargeSpectrum ğ“©) :=
  (x.qHd.powerset.product <| x.qHu.powerset.product <| x.Q5.powerset.product <|
    x.Q10.powerset).map toProd.symm.toEmbedding

lemma mem_powerset_iff {x y : ChargeSpectrum ğ“©} :
    x âˆˆ powerset y â†”
    x.qHd âˆˆ y.qHd.powerset âˆ§
    x.qHu âˆˆ y.qHu.powerset âˆ§
    x.Q5 âˆˆ y.Q5.powerset âˆ§
    x.Q10 âˆˆ y.Q10.powerset := by
  simp [powerset, Finset.mem_product, toProd]

@[simp]
lemma mem_powerset_iff_subset {x y : ChargeSpectrum ğ“©} :
    x âˆˆ powerset y â†” x âŠ† y := by
  cases x
  cases y
  simp [mem_powerset_iff, Subset]

lemma self_mem_powerset (x : ChargeSpectrum ğ“©) :
    x âˆˆ powerset x := by simp

lemma empty_mem_powerset (x : ChargeSpectrum ğ“©) :
    âˆ… âˆˆ powerset x := by simp

@[simp]
lemma powerset_of_empty :
    powerset (âˆ… : ChargeSpectrum ğ“©) = {âˆ…} := by
  ext x
  simp

lemma powerset_mono {x y : ChargeSpectrum ğ“©} :
    powerset x âŠ† powerset y â†” x âŠ† y := by
  constructor
  Â· intro h
    rw [â† mem_powerset_iff_subset]
    apply h
    simp
  Â· intro h z
    simp only [mem_powerset_iff_subset]
    intro h1
    exact subset_trans h1 h

lemma min_exists_inductive (S : Finset (ChargeSpectrum ğ“©)) (hS : S â‰  âˆ…) :
    (n : â„•) â†’ (hn : S.card = n) â†’
    âˆƒ y âˆˆ S, powerset y âˆ© S = {y}
  | 0, h => by simp_all
  | 1, h => by
    rw [Finset.card_eq_one] at h
    obtain âŸ¨y, rflâŸ© := h
    use y
    simp
  | n + 1 + 1, h => by
    rw [â† Finset.nonempty_iff_ne_empty] at hS
    obtain âŸ¨y, hyâŸ© := hS
    have hSremo : (S.erase y).card = n + 1 := by
      rw [Finset.card_erase_eq_ite]
      simp_all
    have hSeraseNeEmpty : (S.erase y) â‰  âˆ… := by
        simp only [ne_eq]
        rw [â† Finset.card_eq_zero]
        omega
    obtain âŸ¨x, hx1, hx2âŸ© := min_exists_inductive (S.erase y) hSeraseNeEmpty (n + 1) hSremo
    have hxy : x â‰  y := by
      by_contra hn
      subst hn
      simp at hx1
    by_cases hyPx : y âˆˆ powerset x
    Â· use y
      constructor
      Â· exact hy
      Â· ext z
        constructor
        Â· intro hz
          simp at hz
          simp only [Finset.mem_singleton]
          rw [Finset.inter_erase] at hx2
          by_cases hn : z = y
          Â· exact hn
          apply False.elim
          have hlz : z âˆˆ (x.powerset âˆ© S).erase y := by
            simp [hn, hz.2]
            simp at hyPx
            exact subset_trans hz.1 hyPx
          rw [hx2] at hlz
          simp at hlz
          simp_all
          have hx : y = x := by
            apply subset_antisymm
            Â· exact hyPx
            Â· exact hz
          exact hxy (id (Eq.symm hx))
        Â· intro hz
          simp at hz
          subst hz
          simp_all
    Â· use x
      constructor
      Â· apply Finset.erase_subset y S
        exact hx1
      Â· rw [â† hx2]
        ext z
        simp only [Finset.mem_inter, mem_powerset_iff_subset, Finset.mem_erase, ne_eq,
          and_congr_right_iff, iff_and_self]
        intro hzx hzS hzy
        subst hzy
        simp_all

lemma min_exists (S : Finset (ChargeSpectrum ğ“©)) (hS : S â‰  âˆ…) :
    âˆƒ y âˆˆ S, powerset y âˆ© S = {y} := min_exists_inductive S hS S.card rfl

/-!

## F. Finite sets of charge spectra with values

We define the finite set of `ChargeSpectrum` with 5-bar and 10d representation
charges in a given finite set.

-/

/-- Given `S5 S10 : Finset ğ“©` the finite set of charges associated with
  for which the 5-bar representation charges sit in `S5` and
  the 10d representation charges sit in `S10`. -/
def ofFinset (S5 S10 : Finset ğ“©) : Finset (ChargeSpectrum ğ“©) :=
  let SqHd := {none} âˆª S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ©
  let SqHu := {none} âˆª S5.map âŸ¨Option.some, Option.some_injective ğ“©âŸ©
  let SQ5 := S5.powerset
  let SQ10 := S10.powerset
  (SqHd.product (SqHu.product (SQ5.product SQ10))).map toProd.symm.toEmbedding

lemma mem_ofFinset_iff {S5 S10 : Finset ğ“©} {x : ChargeSpectrum ğ“©} :
    x âˆˆ ofFinset S5 S10 â†” x.qHd.toFinset âŠ† S5 âˆ§ x.qHu.toFinset âŠ† S5 âˆ§
      x.Q5 âŠ† S5 âˆ§ x.Q10 âŠ† S10 := by
  simp only [ofFinset, Finset.singleton_union, Finset.product_eq_sprod, Finset.mem_map,
    Finset.mem_product, Finset.mem_insert, Finset.mem_map, Function.Embedding.coeFn_mk,
    Finset.mem_powerset, Prod.exists]
  have hoption (x : Option ğ“©) (S : Finset ğ“©) :
      x âˆˆ ({none} : Finset (Option ğ“©)) âˆª S.map âŸ¨Option.some, Option.some_injective ğ“©âŸ© â†”
      x.toFinset âŠ† S := by cases x <;> simp
  constructor
  Â· rintro âŸ¨qHd', qHu', Q5', Q10', h, rflâŸ©
    simp_all [toProd]
  Â· intro h
    use x.qHd, x.qHu, x.Q5, x.Q10
    simp_all [toProd]

lemma mem_ofFinset_antitone (S5 S10 : Finset ğ“©)
    {x y : ChargeSpectrum ğ“©} (h : x âŠ† y) (hy : y âˆˆ ofFinset S5 S10) :
    x âˆˆ ofFinset S5 S10 := by
  rw [mem_ofFinset_iff] at hy âŠ¢
  simp [subset_def] at h
  exact âŸ¨h.1.trans hy.1, h.2.1.trans hy.2.1, h.2.2.1.trans hy.2.2.1, h.2.2.2.trans hy.2.2.2âŸ©

lemma ofFinset_subset_of_subset {S5 S5' S10 S10' : Finset ğ“©}
    (h5 : S5 âŠ† S5') (h10 : S10 âŠ† S10') :
    ofFinset S5 S10 âŠ† ofFinset S5' S10' := by
  intro x hx
  rw [mem_ofFinset_iff] at hx âŠ¢
  exact âŸ¨hx.1.trans h5, hx.2.1.trans h5, hx.2.2.1.trans h5, hx.2.2.2.trans h10âŸ©

lemma ofFinset_univ [Fintype ğ“©] (x : ChargeSpectrum ğ“©) :
    x âˆˆ ofFinset (Finset.univ : Finset ğ“©) (Finset.univ : Finset ğ“©) := by
  rw [mem_ofFinset_iff]
  simp

end ChargeSpectrum

end SU5

end SuperSymmetry
