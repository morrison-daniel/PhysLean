/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.Yukawa
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.MinimalSuperSet
import PhysLean.Meta.TODO.Basic
/-!

# Phenomenologically closed sets of charge spectra

## i. Overview

The main goal of this file is to prove the lemma
`completeness_of_isPhenoClosedQ5_isPhenoClosedQ10`, which
allows us to prove that a multiset of charge spectra contains all
phenomenologically viable charge spectra, given a finite set of allowed
`5`-bar and `10`-dimensional.

This lemma relies on the multiset of charge spectra satisfying a number of conditions,
which include three which are defined in this file: `IsPhenoClosedQ5`, `IsPhenoClosedQ10` and
`ContainsPhenoCompletionsOfMinimallyAllows`.

## ii. Key results

- `IsPhenoClosedQ5` : The proposition that a multiset of charges is phenomenologically closed
  under addition of `5`-bar charges from a finite set `S5`.
- `IsPhenoClosedQ10` : The proposition that a multiset of charges is phenomenologically closed
  under addition of `10`-dimensional charges from a finite set `S10`.
- `ContainsPhenoCompletionsOfMinimallyAllows` : The proposition that a multiset of charges
  contains all phenomenologically viable completions of charge spectra which permit the
  top Yukawa.
- `completeMinSubset` : For a given `S5 S10 : Finset ð“©`,
  the minimal multiset of charges which satisfies the condition
  `ContainsPhenoCompletionsOfMinimallyAllows`.
- `completeness_of_isPhenoClosedQ5_isPhenoClosedQ10` : A lemma for simplifying the proof
  that a multiset contains all phenomenologically viable charge spectra.
- `viableChargesMultiset` : A computable multiset containing all phenomenologically viable
  charge spectra for a given `S5 S10 : Finset ð“©`.

## iii. Table of contents

- A. Phenomenologically closed under additions of 5-bar charges
  - A.1. Simplification using pheno-constrained due to additional of 5-bar charge
- B. Phenomenologically closed under additions of 10d charges
  - B.1. Simplification using pheno-constrained due to additional of 10d charge
- C. Prop for multiset containing all pheno-viable completions of charges permitting top Yukawa
  - C.1. Simplification using fast version of completions of charges permitting top Yukawa
  - C.2. Decidability of proposition
  - C.3. Monotonicity of proposition
  - C.4. `completeMinSubset`: Minimal multiset with viable completions of top-permitting charges
    - C.4.1. The multiset `completeMinSubset` has no duplicates
    - C.4.2. The multiset `completeMinSubset` is minimal
    - C.4.3. The multiset `completeMinSubset` contains all completions
- D. Multisets containing all pheno-viable charge spectra
  - D.1. Lemma for simplifying proof that a multiset contains all pheno-viable charge spectra
  - D.2. Computable multiset containing all pheno-viable charge spectra

## iv. References

There are no known references for the material in this module.

-/
namespace SuperSymmetry

namespace SU5

namespace ChargeSpectrum

variable {ð“© : Type} [DecidableEq ð“©] [AddCommGroup ð“©]

/-!

## A. Phenomenologically closed under additions of 5-bar charges

-/

/-- The proposition that for multiset set of charges `charges`,
  adding individual elements of `S5` to the `Q5` charges of elements of `charges` again
  leads to an element in `charges` or a charge which is phenomenologically constrained,
  or regenerates dangerous couplings with one singlet insertion. -/
def IsPhenoClosedQ5 (S5 : Finset ð“©) (charges : Multiset (ChargeSpectrum ð“©)) : Prop :=
  âˆ€ q5 âˆˆ S5, âˆ€ x âˆˆ charges,
    let y : ChargeSpectrum ð“© := âŸ¨x.qHd, x.qHu, insert q5 x.Q5, x.Q10âŸ©
    IsPhenoConstrained y âˆ¨ y âˆˆ charges âˆ¨ YukawaGeneratesDangerousAtLevel y 1

/-!

### A.1. Simplification using pheno-constrained due to additional of 5-bar charge

-/

lemma isPhenClosedQ5_of_isPhenoConstrainedQ5 {S5 : Finset ð“©} {charges : Multiset (ChargeSpectrum ð“©)}
    (h : âˆ€ q5 âˆˆ S5, âˆ€ x âˆˆ charges,
      let y : ChargeSpectrum ð“© := âŸ¨x.qHd, x.qHu, insert q5 x.Q5, x.Q10âŸ©
      IsPhenoConstrainedQ5 x q5 âˆ¨ y âˆˆ charges âˆ¨ YukawaGeneratesDangerousAtLevel y 1) :
    IsPhenoClosedQ5 S5 charges := by
  intro q5 hq5 x hx
  rcases h q5 hq5 x hx with h'| h' | h'
  Â· left
    rw [isPhenoConstrained_insertQ5_iff_isPhenoConstrainedQ5]
    left
    exact h'
  Â· simp_all
  Â· simp_all

/-!

## B. Phenomenologically closed under additions of 10d charges

-/

/-- The proposition that for multiset set of charges `charges`,
  adding individual elements of `S10` to the `Q10` charges of elements of `charges` again
  leads to an element in `charges` or a charge which is phenomenologically constrained,
  or regenerates dangerous couplings with one singlet insertion. -/
def IsPhenoClosedQ10 (S10 : Finset ð“©) (charges : Multiset (ChargeSpectrum ð“©)) : Prop :=
  âˆ€ q10 âˆˆ S10, âˆ€ x âˆˆ charges,
    let y : ChargeSpectrum ð“© := âŸ¨x.qHd, x.qHu, x.Q5, insert q10 x.Q10âŸ©
    IsPhenoConstrained y âˆ¨ y âˆˆ charges âˆ¨ YukawaGeneratesDangerousAtLevel y 1

/-!

### B.1. Simplification using pheno-constrained due to additional of 10d charge

-/

lemma isPhenClosedQ10_of_isPhenoConstrainedQ10 {S10 : Finset ð“©}
    {charges : Multiset (ChargeSpectrum ð“©)}
    (h : âˆ€ q10 âˆˆ S10, âˆ€ x âˆˆ charges,
      let y : ChargeSpectrum ð“© := âŸ¨x.qHd, x.qHu, x.Q5, insert q10 x.Q10âŸ©
      IsPhenoConstrainedQ10 x q10 âˆ¨ y âˆˆ charges âˆ¨ YukawaGeneratesDangerousAtLevel y 1) :
    IsPhenoClosedQ10 S10 charges := by
  intro q10 hq10 x hx
  have h' := h q10 hq10 x hx
  rcases h' with h'| h' | h'
  Â· left
    rw [isPhenoConstrained_insertQ10_iff_isPhenoConstrainedQ10]
    left
    exact h'
  Â· simp_all
  Â· simp_all

/-!

## C. Prop for multiset containing all pheno-viable completions of charges permitting top Yukawa

-/

open PotentialTerm
/-- The proposition that for multiset set of charges `charges` contains all
  viable completions of charges which allow the top Yukawa, given allowed values
  of `5`d and `10`d charges `S5` and `S10`. -/
def ContainsPhenoCompletionsOfMinimallyAllows (S5 S10 : Finset ð“©)
    (charges : Multiset (ChargeSpectrum ð“©)) : Prop :=
  âˆ€ x âˆˆ (minimallyAllowsTermsOfFinset S5 S10 topYukawa),
      Â¬ x.IsPhenoConstrained â†’ âˆ€ y âˆˆ completions S5 S10 x, Â¬ y.IsPhenoConstrained
      âˆ§ Â¬ y.YukawaGeneratesDangerousAtLevel 1 â†’ y âˆˆ charges

/-!

### C.1. Simplification using fast version of completions of charges permitting top Yukawa

-/

lemma containsPhenoCompletionsOfMinimallyAllows_iff_completionsTopYukawa {S5 S10 : Finset ð“©}
    {charges : Multiset (ChargeSpectrum ð“©)} :
    ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges â†”
    âˆ€ x âˆˆ (minimallyAllowsTermsOfFinset S5 S10 topYukawa),
    âˆ€ y âˆˆ completionsTopYukawa S5 x, Â¬ y.IsPhenoConstrained
      âˆ§ Â¬ y.YukawaGeneratesDangerousAtLevel 1 â†’ y âˆˆ charges := by
  rw [ContainsPhenoCompletionsOfMinimallyAllows]
  have h1 (x : ChargeSpectrum ð“©) (hx : x âˆˆ (minimallyAllowsTermsOfFinset S5 S10 topYukawa)) :
    Â¬ x.IsPhenoConstrained â†” True := by
    simp only [iff_true]
    exact not_isPhenoConstrained_of_minimallyAllowsTermsOfFinset_topYukawa hx
  conv_lhs =>
    enter [x, hx]
    rw [completions_eq_completionsTopYukawa_of_mem_minimallyAllowsTermsOfFinset x hx]
    rw [h1 x hx]
  simp

/-!

### C.2. Decidability of proposition

-/

instance [DecidableEq ð“©] {S5 S10 : Finset ð“©} {charges : Multiset (ChargeSpectrum ð“©)} :
    Decidable (ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges) :=
  decidable_of_iff _ (containsPhenoCompletionsOfMinimallyAllows_iff_completionsTopYukawa).symm

/-!

### C.3. Monotonicity of proposition

-/

lemma containsPhenoCompletionsOfMinimallyAllows_of_subset {S5 S10 : Finset ð“©}
    {charges charges' : Multiset (ChargeSpectrum ð“©)}
    (h' : ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges)
    (h : âˆ€ x âˆˆ charges, x âˆˆ charges') :
    ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges' :=
  fun x hx hnot y h3 h4 => h y <| h' x hx hnot y h3 h4

/-!

### C.4. `completeMinSubset`: Minimal multiset with viable completions of top-permitting charges

-/
/-- For a given `S5 S10 : Finset ð“©`, the minimal multiset of charges which satisfies
  the condition `ContainsPhenoCompletionsOfMinimallyAllows`.
  That is to say, every multiset of charges which satisfies
  `ContainsPhenoCompletionsOfMinimallyAllows` has `completeMinSubset` as a subset. -/
def completeMinSubset (S5 S10 : Finset ð“©) : Multiset (ChargeSpectrum ð“©) :=
  ((minimallyAllowsTermsOfFinset S5 S10 topYukawa).bind <|
      completionsTopYukawa S5).dedup.filter
    fun x => Â¬ IsPhenoConstrained x âˆ§ Â¬ YukawaGeneratesDangerousAtLevel x 1

/-!

#### C.4.1. The multiset `completeMinSubset` has no duplicates

-/

lemma completeMinSubset_nodup {S5 S10 : Finset ð“©} :
    (completeMinSubset S5 S10).Nodup := by
  simp [completeMinSubset]
  apply Multiset.Nodup.filter
  exact Multiset.nodup_dedup
      ((minimallyAllowsTermsOfFinset S5 S10 topYukawa).bind (completionsTopYukawa S5))

/-!

#### C.4.2. The multiset `completeMinSubset` is minimal

-/

lemma completeMinSubset_subset_iff_containsPhenoCompletionsOfMinimallyAllows
    (S5 S10 : Finset ð“©) (charges : Multiset (ChargeSpectrum ð“©)) :
    completeMinSubset S5 S10 âŠ† charges â†”
    ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges := by
  constructor
  Â· intro h
    rw [containsPhenoCompletionsOfMinimallyAllows_iff_completionsTopYukawa]
    rw [Multiset.subset_iff] at h
    intro x hx y hy1 hy2
    apply h
    simp [completeMinSubset]
    simp_all
    use x
  Â· intro h y hy
    simp [completeMinSubset] at hy
    obtain âŸ¨âŸ¨x, hx, hyxâŸ©, hy2âŸ© := hy
    rw [containsPhenoCompletionsOfMinimallyAllows_iff_completionsTopYukawa] at h
    exact h x hx y hyx hy2

/-!

#### C.4.3. The multiset `completeMinSubset` contains all completions

-/

lemma completeMinSubset_containsPhenoCompletionsOfMinimallyAllows (S5 S10 : Finset ð“©) :
    ContainsPhenoCompletionsOfMinimallyAllows S5 S10 (completeMinSubset S5 S10) := by
  rw [â† completeMinSubset_subset_iff_containsPhenoCompletionsOfMinimallyAllows]
  simp

/-!

## D. Multisets containing all pheno-viable charge spectra

-/

/-!

### D.1. Lemma for simplifying proof that a multiset contains all pheno-viable charge spectra

-/

/-!
The multiset of charges `charges` contains precisely those charges (given a finite set
of allowed charges) which
- allow the top Yukawa term,
- are not phenomenologically constrained,
- do not generate dangerous couplings with one singlet insertion,
- and are complete,
if the following conditions hold:
- every element of `charges` allows the top Yukawa term,
- every element of `charges` is not phenomenologically constrained,
- every element of `charges` does not generate dangerous couplings with one singlet insertion,
- every element of `charges` is complete,
- `charges` is `IsPhenoClosedQ5` with respect to `S5`,
- `charges` is `IsPhenoClosedQ10` with respect to `S10`
- and satisfies `ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges`.
The importance of this lemma is that it is only regarding properties of finite-set `charges`
not of the whole space of possible charges.
-/
lemma completeness_of_isPhenoClosedQ5_isPhenoClosedQ10
    {S5 S10 : Finset ð“©} {charges : Multiset (ChargeSpectrum ð“©)}
    (charges_topYukawa : âˆ€ x âˆˆ charges, x.AllowsTerm .topYukawa)
    (charges_not_isPhenoConstrained : âˆ€ x âˆˆ charges, Â¬ x.IsPhenoConstrained)
    (charges_yukawa : âˆ€ x âˆˆ charges, Â¬ x.YukawaGeneratesDangerousAtLevel 1)
    (charges_complete : âˆ€ x âˆˆ charges, x.IsComplete)
    (charges_isPhenoClosedQ5 : IsPhenoClosedQ5 S5 charges)
    (charges_isPhenoClosedQ10 : IsPhenoClosedQ10 S10 charges)
    (charges_exist : ContainsPhenoCompletionsOfMinimallyAllows S5 S10 charges)
    {x : ChargeSpectrum ð“©} (hsub : x âˆˆ ofFinset S5 S10) :
    x âˆˆ charges â†” AllowsTerm x .topYukawa âˆ§
    Â¬ IsPhenoConstrained x âˆ§ Â¬ YukawaGeneratesDangerousAtLevel x 1 âˆ§ IsComplete x := by
  constructor
  Â· /- Showing that if `x âˆˆ Charges` it satisfies the conditions. -/
    intro h
    exact âŸ¨charges_topYukawa x h, charges_not_isPhenoConstrained x h, charges_yukawa x h,
      charges_complete x hâŸ©
  Â· intro âŸ¨hTop, hPheno, hY, hCompleteâŸ©
    /- Showing that if `x âˆ‰ charges` and `AllowsTerm x .topYukawa`,
      `Â¬ IsPhenoConstrained x`, ``Â¬ YukawaGeneratesDangerousAtLevel x 1`, `IsComplete x`,
      then `False`. -/
    by_contra hn
    suffices hnot : Â¬ ((Â¬ IsPhenoConstrained x âˆ§ Â¬ YukawaGeneratesDangerousAtLevel x 1) âˆ§
        AllowsTerm x topYukawa) by
      simp_all
    revert hn
    rw [not_and]
    simp only [hTop, not_true_eq_false, imp_false]
    suffices hmem : âˆƒ y âˆˆ charges, y âŠ† x by
      obtain âŸ¨y, y_mem, hyxâŸ© := hmem
      refine subset_insert_filter_card_zero charges S5 S10 (fun x =>
        (Â¬x.IsPhenoConstrained âˆ§ Â¬x.YukawaGeneratesDangerousAtLevel 1))
        ?_ ?_ y ?_ x hyx hsub ?_ ?_
      Â· simpa using fun x y hxy h1 h2 => yukawaGeneratesDangerousAtLevel_of_subset hxy <| h1
          fun hn => h2 <| isPhenoConstrained_mono hxy hn
      Â· intro x
        exact fun a => charges_complete x a
      Â· exact y_mem
      Â· intro q10
        rw [Multiset.empty_eq_zero, Multiset.eq_zero_iff_forall_notMem]
        simp only [Multiset.mem_filter, Multiset.mem_map, not_and, Decidable.not_not,
          forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
        intro z hz hzP h2
        have h1 := charges_isPhenoClosedQ10 q10 q10.2 z hz
        simp_all
      Â· intro q5
        rw [Multiset.empty_eq_zero, Multiset.eq_zero_iff_forall_notMem]
        simp only [Multiset.mem_filter, Multiset.mem_map, not_and, Decidable.not_not,
          forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
        intro z hz hzP h2
        have h1 := charges_isPhenoClosedQ5 q5 q5.2 z hz
        simp_all
    /- Getting the subset of `x` which minimally allows the top Yukawa. -/
    obtain âŸ¨y, hyMem, hysubsetxâŸ© : âˆƒ y âˆˆ (minimallyAllowsTermsOfFinset S5 S10 topYukawa),
        y âŠ† x := by
      rw [allowsTerm_iff_subset_minimallyAllowsTerm] at hTop
      obtain âŸ¨y, hPower, hIrreâŸ© := hTop
      use y
      constructor
      Â· rw [â† minimallyAllowsTerm_iff_mem_minimallyAllowsTermOfFinset]
        Â· exact hIrre
        Â· exact mem_ofFinset_antitone S5 S10 (by simpa using hPower) hsub
      Â· simpa using hPower
    obtain âŸ¨z, hz1, hz2âŸ© := exist_completions_subset_of_complete S5 S10 y x hysubsetx hsub hComplete
    use z
    constructor
    Â· refine charges_exist y hyMem ?_ z hz1 ?_
      Â· by_contra hn
        have := isPhenoConstrained_mono hysubsetx hn
        simp_all
      Â· apply And.intro
        Â· by_contra hn
          have := isPhenoConstrained_mono hz2 hn
          simp_all
        Â· by_contra hn
          have := yukawaGeneratesDangerousAtLevel_of_subset hz2 hn
          simp_all
    Â· simp_all

/-!

### D.2. Computable multiset containing all pheno-viable charge spectra

-/

TODO "JGVOQ" "Make the result `viableChargesMultiset` a safe definition, that is to
  say proof that the recursion terminates."

/-- All charges, for a given `S5 S10 : Finset ð“©`,
  which permit a top Yukawa coupling, are not phenomenologically constrained,
  and do not regenerate dangerous couplings with one insertion of a Yukawa coupling.

  This is the unique multiset without duplicates which satisfies:
  `completeness_of_isPhenoClosedQ5_isPhenoClosedQ10`.

  Note this is fast for evaluation, but to slow with `decide`. -/
unsafe def viableChargesMultiset (S5 S10 : Finset ð“©) :
    Multiset (ChargeSpectrum ð“©) := (aux (completeMinSubset S5 S10) (completeMinSubset S5 S10)).dedup
where
  /-- Auxiliary recursive function to define `viableChargesMultiset`. -/
  aux : Multiset (ChargeSpectrum ð“©) â†’ Multiset (ChargeSpectrum ð“©) â†’ Multiset (ChargeSpectrum ð“©) :=
    fun all add =>
      /- Note that aux terminates since that every iteration the size of `all` increases,
        unless it terminates that round, but `all` is bounded in size by the number
        of allowed charges given `S5` and `S10`. -/
      if add = âˆ… then all else
      let s := add.bind fun x => (minimalSuperSet S5 S10 x).val
      let s2 := s.filter fun y => y âˆ‰ all âˆ§
        Â¬ IsPhenoConstrained y âˆ§ Â¬ YukawaGeneratesDangerousAtLevel y 1
      aux (all + s2) s2

end ChargeSpectrum

end SU5

end SuperSymmetry
