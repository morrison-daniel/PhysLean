/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5.Fluxes.NoExotics.ToList
import PhysLean.Particles.SuperSymmetry.SU5.Charges.MinimallyAllowsTerm.OfFinset
import PhysLean.StringTheory.FTheory.SU5.Charges.OfRationalSection
import PhysLean.Particles.SuperSymmetry.SU5.Charges.Map
/-!

# Quanta of 5-d representations

The 5-bar representations of the `SU(5)Ã—U(1)` carry
the quantum numbers of their U(1) charges and their fluxes.

In this module we define the data structure for these quanta and
properties thereof.

##Â Key definitions

- `FiveQuanta` is the type of quanta of 5-bar representations.
- `FiveQuanta.toFluxesFive` is the underlying `FluxesFive` of a `FiveQuanta`.
- `FiveQuanta.toCharges` is the underlying Multiset charges of a `FiveQuanta`.
- `FiveQuanta.reduce` is the reduction of a `FiveQuanta` which adds together
  all the fluxes corresponding to the same charge (i.e. representation).
- `FiveQuanta.anomalyCoefficent` is the anomaly coefficent associated with a `FiveQuanta`.
- `FiveQuanta.ofChargesExpand` is the `FiveQuanta` with fluxes
  `{(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)}` and finite set of charges equal to a given
  `c`.

## Key theorems

- `mem_ofChargesExpand_map_reduce_iff` states that a `FiveQuanta` is in the
  image of `ofChargesExpand c` under `reduce` if and only if it is a `FiveQuanta` with
  charges equal to `c` and fluxes which have no exotics or zero.
-/
namespace FTheory

namespace SU5
open SU5
variable {I : CodimensionOneConfig}

/-- The quanta of 5-bar representations corresponding to a multiset of
  `(q, M, N)` for each partcile. `(M, N)` are defined in the `FluxesFive` module. -/
abbrev FiveQuanta (ğ“© : Type := â„¤) : Type := Multiset (ğ“© Ã— â„¤ Ã— â„¤)

namespace FiveQuanta

variable {ğ“© : Type}

/-- The underlying `FluxesFive` from a `FiveQuanta`. -/
def toFluxesFive (x : FiveQuanta ğ“©) : FluxesFive := x.map Prod.snd

/-- The underlying Multiset charges from a `FiveQuanta`. -/
def toCharges (x : FiveQuanta ğ“©) : Multiset ğ“© := x.map Prod.fst

/-!

## Reduce

-/

section reduce

variable [DecidableEq ğ“©]

/-- The `reduce` of `FiveQuanta` is a new `FiveQuanta` with all the fluxes
  corresponding to the same charge (i.e. represenation) added together. -/
def reduce (x : FiveQuanta ğ“©) : FiveQuanta ğ“© :=
  x.toCharges.dedup.map fun q5 => (q5, ((x.filter (fun f => f.1 = q5)).map (fun y => y.2)).sum)

lemma reduce_nodup (x : FiveQuanta ğ“©) : x.reduce.Nodup := by
  simp [reduce, toCharges]
  refine Multiset.Nodup.map ?_ ?_
  Â· intro q1 q2 h
    simp at h
    exact h.1
  Â· exact Multiset.nodup_dedup (Multiset.map Prod.fst x)

@[simp]
lemma reduce_dedup (x : FiveQuanta ğ“©) : x.reduce.dedup = x.reduce :=
  Multiset.Nodup.dedup x.reduce_nodup

lemma reduce_toCharges (x : FiveQuanta ğ“©) : x.reduce.toCharges = x.toCharges.dedup := by
  simp [reduce, toCharges]

lemma reduce_eq_val (x : FiveQuanta ğ“©) :
    x.reduce = (x.toCharges.toFinset.image fun q5 =>
      (q5, ((x.filter (fun f => f.1 = q5)).map (fun y => y.2)).sum)).val := by
  simp only [Finset.image_val, Multiset.toFinset_val]
  rw [â† reduce]
  simp

lemma mem_reduce_iff (x : FiveQuanta ğ“©) (p : ğ“© Ã— â„¤ Ã— â„¤) :
    p âˆˆ x.reduce â†” p.1 âˆˆ x.toCharges âˆ§
      p.2 = ((x.filter (fun f => f.1 = p.1)).map (fun y => y.2)).sum := by
  simp [reduce]
  constructor
  Â· intro h
    obtain âŸ¨q, h1, rflâŸ© := h
    simp_all
  Â· simp
    intro h1 h2
    use p.1
    simp_all
    rw [â† h2]

lemma reduce_filter (x : FiveQuanta ğ“©) (q : ğ“©) (h : q âˆˆ x.toCharges) :
    x.reduce.filter (fun f => f.1 = q) =
    {(q, ((x.filter (fun f => f.1 = q)).map (fun y => y.2)).sum)} := by
  simp [reduce]
  rw [Multiset.filter_map]
  simp only [Function.comp_apply]
  have hx : (Multiset.filter (fun x => x = q) x.toCharges.dedup) = {q} := by
    refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
    Â· refine Multiset.Nodup.filter (fun x => x = q) ?_
      exact Multiset.nodup_dedup x.toCharges
    Â· exact Multiset.nodup_singleton q
    intro a
    simp only [Multiset.mem_filter, Multiset.mem_dedup, Multiset.mem_singleton,
      and_iff_right_iff_imp]
    intro h'
    subst h'
    exact h
  rw [hx]
  simp

@[simp]
lemma reduce_reduce (x : FiveQuanta ğ“©) :
    x.reduce.reduce = x.reduce := by
  refine Multiset.Nodup.toFinset_inj ?_ ?_ ?_
  Â· exact reduce_nodup x.reduce
  Â· exact reduce_nodup x
  ext p
  simp only [Multiset.mem_toFinset]
  rw [mem_reduce_iff, reduce_toCharges, mem_reduce_iff]
  simp only [Multiset.mem_dedup, and_congr_right_iff]
  intro hp
  have h1 (a b c : â„¤ Ã— â„¤) (h : b = c) : a = b â†” a = c := by subst h; rfl
  apply h1
  rw [reduce_filter]
  simp only [Multiset.map_singleton, Multiset.sum_singleton]
  exact hp

lemma reduce_sum_eq_sum_toCharges {M} [AddCommMonoid M] (x : FiveQuanta ğ“©) (f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ M) :
    (x.reduce.map fun (q5, x) => f q5 x).sum = (x.map fun (q5, x) => f q5 x).sum := by
  calc _
      _ = âˆ‘ q5 âˆˆ x.toCharges.toFinset,
          f q5 ((x.filter (fun f => f.1 = q5)).map (fun y => y.2)).sum := by
        rw [reduce]
        simp [Finset.sum]
      _ = âˆ‘ q5 âˆˆ x.toCharges.toFinset,
          (((x.filter (fun f => f.1 = q5)).map (fun y => f q5 y.2))).sum := by
        congr
        funext q5
        rw [AddMonoidHom.map_multiset_sum, Multiset.map_map]
        rfl
      _ = (x.toCharges.dedup.bind fun q5 =>
          ((x.filter (fun f => f.1 = q5)).map (fun y => f q5 y.2))).sum := by
        rw [Multiset.sum_bind]
        simp [Finset.sum]
      _ = (((x.toCharges.dedup.bind fun q5 =>
          ((x.filter (fun f => f.1 = q5)))).map (fun y => f y.1 y.2))).sum := by
        congr
        rw [Multiset.map_bind]
        congr
        funext q5
        refine Multiset.map_congr rfl ?_
        intro y hy
        simp at hy
        rw [hy.2]
      _ = ((x.map (fun y => f y.1 y.2))).sum := by
        congr
        apply Multiset.ext.mpr
        intro p
        trans ((x.map Prod.fst).dedup.map (fun y => if p.1 = y then x.count p else 0)).sum
        Â· rw [@Multiset.count_bind]
          congr
          funext q5
          rw [Multiset.count_filter]
        by_cases h_mem : p.1 âˆˆ x.map Prod.fst
        Â· have h_mem_dedup : p.1 âˆˆ (x.map Prod.fst).dedup := by rwa [Multiset.mem_dedup]
          rw [Multiset.sum_map_eq_nsmul_single p.1]
          simp only [Multiset.nodup_dedup, â†“reduceIte, smul_eq_mul]
          have h_count_one : Multiset.count p.1 (Multiset.map Prod.fst x).dedup = 1 := by
            refine Multiset.count_eq_one_of_mem ?_ h_mem_dedup
            exact Multiset.nodup_dedup (Multiset.map Prod.fst x)
          simp [h_count_one]
          intro q5' h h2
          simp_all [eq_comm]
        Â· rw [Multiset.sum_eq_zero]
          refine Eq.symm (Multiset.count_eq_zero_of_notMem ?_)
          intro h
          have h_mem : p.1 âˆˆ Multiset.map Prod.fst x := by
            simp_all [h]
          (expose_names; exact h_mem_1 h_mem)
          intro p' hp
          simp at hp
          obtain âŸ¨q5', âŸ¨f1, f2, hfâŸ©, hp'âŸ© := hp
          by_cases h_eq : p.1 = q5'
          Â· simp_all
          Â· simp_all

lemma reduce_eq_self_of_ofCharges_nodup (x : FiveQuanta ğ“©) (h : x.toCharges.Nodup) :
    x.reduce = x := by
  rw [reduce]
  rw [Multiset.Nodup.dedup h]
  simp [toCharges]
  conv_rhs => rw [â† Multiset.map_id x]
  apply Multiset.map_congr rfl
  intro p hp
  simp only [id_eq]
  have x_noDup : x.Nodup := Multiset.Nodup.of_map Prod.fst h
  suffices (Multiset.filter (fun f => f.1 = p.1) x) = {p} by simp [this]
  refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
  Â· exact Multiset.Nodup.filter (fun f => f.1 = p.1) x_noDup
  Â· exact Multiset.nodup_singleton p
  intro p'
  simp only [Multiset.mem_filter, Multiset.mem_singleton]
  constructor
  Â· rintro âŸ¨h1, h2âŸ©
    simp [toCharges] at h
    rw [propext (Multiset.nodup_map_iff_inj_on x_noDup)] at h
    apply h
    Â· exact h1
    Â· exact hp
    Â· exact h2
  Â· rintro âŸ¨rflâŸ©
    simp_all

end reduce

/-!

## Anomaly cancellation

-/

section ACCs

variable [CommRing ğ“©]

/--
  The anomaly coefficent of a `FiveQuanta` is given by the pair of integers:
  `(âˆ‘áµ¢ qáµ¢ Náµ¢, âˆ‘áµ¢ qáµ¢Â² Náµ¢)`.

  The first components is for the mixed U(1)-MSSM, see equation (22) of arXiv:1401.5084.
  The second component is for the mixed U(1)Y-U(1)-U(1) gauge anomaly,
  see equation (23) of arXiv:1401.5084.
-/
def anomalyCoefficent (F : FiveQuanta ğ“©) : ğ“© Ã— ğ“© :=
  ((F.map fun x => x.2.2 â€¢ x.1).sum, (F.map fun x => x.2.2 â€¢ (x.1 * x.1)).sum)

@[simp]
lemma anomalyCoefficent_of_map {ğ“© ğ“©1 : Type} [CommRing ğ“©] [CommRing ğ“©1]
    (f : ğ“© â†’+* ğ“©1) (F : FiveQuanta ğ“©) :
    FiveQuanta.anomalyCoefficent (F.map fun y => (f y.1, y.2) : FiveQuanta ğ“©1) =
    (f.prodMap f) F.anomalyCoefficent := by
  simp [FiveQuanta.anomalyCoefficent, map_multiset_sum, Multiset.map_map]

lemma anomalyCoefficent_of_reduce (F : FiveQuanta ğ“©) [DecidableEq ğ“©] :
    F.reduce.anomalyCoefficent = F.anomalyCoefficent := by
  simp [anomalyCoefficent]
  constructor
  Â· let f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ ğ“© := fun q5 => {
      toFun := fun x => x.2 â€¢ q5
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul, mul_add] }
    simpa [f] using reduce_sum_eq_sum_toCharges F f
  Â· let f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ ğ“© := fun q5 => {
      toFun := fun x => x.2 â€¢ (q5 * q5)
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul, mul_add] }
    simpa [f] using reduce_sum_eq_sum_toCharges F f

end ACCs
/-!

## ofChargesExpand

-/

section ofChargesExpand

open SuperSymmetry.SU5.Charges

variable [DecidableEq ğ“©]

/-- Given a finite set of charges `c` the `FiveQuanta`
  with fluxes `{(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)}`
  and finite set of charges equal to `c`. -/
def ofChargesExpand (c : Finset ğ“©) : Multiset (FiveQuanta ğ“©) :=
  /- The multisets of cardinality 3 containing 3 elements of `c`. -/
  let S53 : Multiset (Multiset ğ“©) := toMultisetsThree c
  /- Pairs of multisets (s1, s2) such that s1 and s2 are cardinality of `3` containing
    elements of `c` and that all elements of `c` are in `s1 + s2`. -/
  let S5p : Multiset (Multiset ğ“© Ã— Multiset ğ“©) :=
    (S53.product S53).filter fun (s1, s2) => c.val â‰¤ s1 + s2
  let Fp : Multiset (FiveQuanta ğ“©) :=
    S5p.map (fun y => y.1.map (fun z => (z, 1, -1)) + y.2.map (fun z => (z, 0, 1)))
  Fp

lemma toFluxesFive_of_mem_ofChargesExpand (c : Finset ğ“©)
    {x : FiveQuanta ğ“©} (h : x âˆˆ ofChargesExpand c) :
    x.toFluxesFive = {(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)} := by
  simp [ofChargesExpand] at h
  obtain âŸ¨s1, s2, âŸ¨âŸ¨âŸ¨s1_subset, s1_cardâŸ©, âŸ¨s2_subset, s2_cardâŸ©âŸ©, hsumâŸ©, rflâŸ© := h
  simp [toFluxesFive, s1_card, s2_card]
  decide

lemma toCharges_of_mem_ofChargesExpand (c : Finset ğ“©)
    {x : FiveQuanta ğ“©} (h : x âˆˆ ofChargesExpand c) :
    x.toCharges.toFinset = c := by
  simp [ofChargesExpand] at h
  obtain âŸ¨s1, s2, âŸ¨âŸ¨âŸ¨s1_subset, s1_cardâŸ©, âŸ¨s2_subset, s2_cardâŸ©âŸ©, hsumâŸ©, rflâŸ© := h
  simp [toCharges]
  trans (s1 + s2).toFinset
  Â· exact Eq.symm (Multiset.toFinset_add s1 s2)
  ext a
  simp only [Multiset.toFinset_add, Finset.mem_union, Multiset.mem_toFinset]
  constructor
  Â· intro hr
    rcases hr with hr | hr
    Â· apply s1_subset
      simpa using hr
    Â· apply s2_subset
      simpa using hr
  Â· intro hr
    simpa using Multiset.mem_of_le hsum hr

lemma mem_ofChargesExpand_of_toCharges_toFluxesFive (c : Finset ğ“©) {x : FiveQuanta ğ“©}
    (h : x.toCharges.toFinset = c) (h2 : x.toFluxesFive =
      {(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)}) :
    x âˆˆ ofChargesExpand c := by
  simp [ofChargesExpand]
  let s1 := (x.filter (fun y => y.2 = (1, -1))).map Prod.fst
  let s2 := (x.filter (fun y => y.2 = (0, 1))).map Prod.fst
  use s1, s2
  have hx : Multiset.filter (fun y => y.2 = (0, 1)) x
        = Multiset.filter (fun y => Â¬ y.2 = (1, -1)) x := by
    refine Multiset.filter_congr ?_
    intro p hp
    have h1 : p.2 âˆˆ x.toFluxesFive := by simp [toFluxesFive]; use p.1
    rw [h2] at h1
    simp_all
    rcases h1 with hp | hp
    Â· simp [hp]
    Â· simp [hp]
  refine âŸ¨âŸ¨âŸ¨âŸ¨?_, ?_âŸ©, âŸ¨?_, ?_âŸ©âŸ©, ?_âŸ©, ?_âŸ©
  Â· simp [s1, â† h, toCharges]
  Â· simp [s1]
    trans (Multiset.filter (fun y => y = (1, -1)) (x.toFluxesFive)).card
    Â· rw [toFluxesFive, Multiset.filter_map]
      simp
    rw [h2]
    decide
  Â· simp [s2, â† h, toCharges]
  Â· simp [s2]
    trans (Multiset.filter (fun y => y = (0, 1)) (x.toFluxesFive)).card
    Â· rw [toFluxesFive, Multiset.filter_map]
      simp
    rw [h2]
    decide
  Â· rw [â† h]
    simp [s1, s2, toCharges]
    rw [â† Multiset.map_add]
    refine (Multiset.le_iff_subset (Multiset.nodup_dedup (Multiset.map Prod.fst x))).mpr ?_
    simp only [Multiset.dedup_subset']
    refine Multiset.map_subset_map ?_
    rw [hx, Multiset.filter_add_not]
    exact fun â¦ƒaâ¦„ a => a
  Â· simp [s1, s2]
    have h1 : Multiset.map (fun x => (x.1, 1, -1)) (Multiset.filter (fun y => y.2 = (1, -1)) x)
        = (Multiset.filter (fun y => y.2 = (1, -1)) x) := by
      trans Multiset.map (fun x => x) (Multiset.filter (fun y => y.2 = (1, -1)) x)
      Â· apply Multiset.map_congr
        Â· rfl
        Â· intro y hx
          simp at hx
          rw [â† hx.2]
      simp
    have h2 : Multiset.map (fun x => (x.1, 0, 1)) (Multiset.filter (fun y => y.2 = (0, 1)) x)
        = (Multiset.filter (fun y => y.2 = (0, 1)) x) := by
      trans Multiset.map (fun x => x) (Multiset.filter (fun y => y.2 = (0, 1)) x)
      Â· apply Multiset.map_congr
        Â· rfl
        Â· intro y hx
          simp at hx
          rw [â† hx.2]
      simp
    rw [h1, h2, hx]
    exact Multiset.filter_add_not (fun y => y.2 = (1, -1)) x

lemma mem_ofChargesExpand_iff(c : Finset ğ“©) {x : FiveQuanta ğ“©} :
    x âˆˆ ofChargesExpand c â†”
    x.toCharges.toFinset = c âˆ§ x.toFluxesFive =
      {(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)} := by
  constructor
  Â· intro h
    constructor
    exact toCharges_of_mem_ofChargesExpand c h
    exact toFluxesFive_of_mem_ofChargesExpand c h
  Â· intro h
    obtain âŸ¨h1, h2âŸ© := h
    exact mem_ofChargesExpand_of_toCharges_toFluxesFive c h1 h2

lemma eq_sum_filter_of_mem_ofChargesExpand (c : Finset ğ“©) (F : FiveQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F = (F.filter fun x => x.2 = (1, -1)) + (F.filter fun x => x.2 = (0, 1)) := by
  rw [mem_ofChargesExpand_iff] at h
  obtain âŸ¨hc, hâŸ© := h
  have h1 : Multiset.filter (fun x => x.2 = (0, 1)) F
      = Multiset.filter (fun x => Â¬ x.2 = (1, -1)) F := by
    apply Multiset.filter_congr
    intro x f
    have h2 : x.2 âˆˆ F.toFluxesFive := by
      simp [toFluxesFive]
      use x.1
    rw [h] at h2
    simp at h2
    rcases h2 with h | h
    Â· simp [h]
    Â· simp [h]
  rw [h1]
  exact Eq.symm (Multiset.filter_add_not (fun x => x.2 = (1, -1)) F)

lemma mem_ofChargesExpand_of_noExotics_hasNoZero (F : FiveQuanta ğ“©) (c : Finset ğ“©)
    (hc : F.toCharges.toFinset = c)
    (h1 : F.toFluxesFive.NoExotics) (h2 : F.toFluxesFive.HasNoZero) :
    âˆƒ y âˆˆ ofChargesExpand c, y.reduce = F.reduce := by
  have hf : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics := by
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics]
    simp_all
    exact h2
  let Ex : FiveQuanta ğ“© := F.bind fun (q5, M, N) => Multiset.replicate M.natAbs (q5, 1, -1)
      + Multiset.replicate (M + N).natAbs (q5, 0, 1)
  have ex_filter (q5 : ğ“©) : (Ex.filter fun x => x.1 = q5) =
      (F.filter fun x => x.1 = q5).bind fun (q5, M, N) =>
      Multiset.replicate M.natAbs (q5, (1 : â„¤), -1)
      + Multiset.replicate (M + N).natAbs (q5, 0, 1) := by
    dsimp [Ex]
    simp only [Int.reduceNeg, Multiset.bind_add, Multiset.filter_add, Ex]
    congr
    Â· ext p
      by_cases hp : p âˆ‰ (Multiset.filter (fun x => x.1 = q5)
        (Multiset.bind F fun a => Multiset.replicate a.2.1.natAbs (a.1, 1, -1)))
      Â· rw [Multiset.count_eq_zero_of_notMem, Multiset.count_eq_zero_of_notMem]
        Â· simp
          simp at hp
          intro q M N h1 hq
          subst hq
          have hp' := hp q M N h1
          by_contra hn
          have hl := hp' hn
          rw [Multiset.mem_replicate] at hn
          obtain âŸ¨h, rflâŸ© := hn
          simp only [not_true_eq_false, Ex] at hl
        Â· exact hp
      Â· simp at hp
        obtain âŸ¨q, M, âŸ¨N, is_memâŸ©, h2, rflâŸ© := hp
        rw [Multiset.count_filter]
        simp only [â†“reduceIte, Int.reduceNeg, Ex]
        rw [Multiset.count_bind, Multiset.count_bind]
        have hf : F = (Multiset.filter (fun x => x.1 = p.1) F) +
          (Multiset.filter (fun x => Â¬ x.1 = p.1) F) :=
            Eq.symm (Multiset.filter_add_not (fun x => x.1 = p.1) F)
        conv_lhs => rw [hf]
        simp only [Int.reduceNeg, Multiset.map_add, Multiset.sum_add, Nat.add_eq_left, Ex]
        apply Multiset.sum_eq_zero
        intro x hx
        simp only [Int.reduceNeg, Multiset.mem_map, Multiset.mem_filter, Prod.exists,
          exists_and_right, Ex] at hx
        obtain âŸ¨q', M', âŸ¨âŸ¨N', is_memâŸ©, h2âŸ©, hâŸ© := hx
        rw [Multiset.count_eq_zero_of_notMem] at h
        exact h.symm
        rw [Multiset.mem_replicate]
        simp only [ne_eq, Int.natAbs_eq_zero, Int.reduceNeg, not_and, Ex]
        intro hM'
        by_contra hn
        subst hn
        simp at h2
    Â· ext p
      by_cases hp : p âˆ‰ Multiset.filter (fun x => x.1 = q5)
        (Multiset.bind F fun a => Multiset.replicate (a.2.1 + a.2.2).natAbs (a.1, 0, 1))
      Â· rw [Multiset.count_eq_zero_of_notMem, Multiset.count_eq_zero_of_notMem]
        Â· simp only [Multiset.mem_bind, Multiset.mem_filter, Prod.exists, not_exists, not_and,
          and_imp, Ex]
          simp only [Multiset.mem_filter, Multiset.mem_bind, Prod.exists, not_and,
            forall_exists_index, and_imp, Ex] at hp
          intro q M N h1 hq
          subst hq
          have hp' := hp q M N h1
          by_contra hn
          have hl := hp' hn
          rw [Multiset.mem_replicate] at hn
          obtain âŸ¨h, rflâŸ© := hn
          simp at hl
        Â· exact hp
      Â· simp only [Multiset.mem_filter, Multiset.mem_bind, Prod.exists, not_and,
        forall_exists_index, and_imp, not_forall, Classical.not_imp, Decidable.not_not, Ex] at hp
        obtain âŸ¨q, M, N, os_mem, h1, rflâŸ© := hp
        rw [Multiset.count_filter]
        simp only [â†“reduceIte, Ex]
        rw [Multiset.count_bind, Multiset.count_bind]
        have hf : F = (Multiset.filter (fun x => x.1 = p.1) F) +
          (Multiset.filter (fun x => Â¬ x.1 = p.1) F) :=
            Eq.symm (Multiset.filter_add_not (fun x => x.1 = p.1) F)
        conv_lhs => rw [hf]
        simp only [Multiset.map_add, Multiset.sum_add, Nat.add_eq_left, Ex]
        apply Multiset.sum_eq_zero
        intro x hx
        simp at hx
        obtain âŸ¨q', M', N', h2, hâŸ© := hx
        rw [Multiset.count_eq_zero_of_notMem] at h
        exact h.symm
        rw [Multiset.mem_replicate]
        simp only [ne_eq, Int.natAbs_eq_zero, not_and, Ex]
        intro hM'
        by_contra hn
        subst hn
        simp at h2
  have ex_charges : Ex.toCharges.toFinset = c := by
    ext p
    constructor
    Â· intro h
      simp [Ex, toCharges] at h
      rcases h with h | h
      Â· obtain âŸ¨M', N', p', M, âŸ¨N, hâŸ©, h2âŸ© := h
        rw [Multiset.mem_replicate] at h2
        simp at h2
        obtain âŸ¨h2, rfl, rfl, rflâŸ© := h2
        rw [â† hc]
        simp [toCharges]
        use M, N
      Â· obtain âŸ¨M', N', p', M, N, h1, h2âŸ© := h
        simp [Multiset.mem_replicate] at h2
        obtain âŸ¨h2, rfl, rfl, rflâŸ© := h2
        rw [â† hc]
        simp [toCharges]
        use M, N
    Â· intro h
      simp [Ex, toCharges]
      rw [â† hc] at h
      simp [toCharges] at h
      obtain âŸ¨M, N, hâŸ© := h
      have h1 : (M, N) âˆˆ F.toFluxesFive := by
        simp [toFluxesFive]
        use p
      by_cases h : M.natAbs â‰  0
      Â· left
        use 1, -1, p, M
        constructor
        Â· use N
        Â· refine Multiset.mem_replicate.mpr ?_
          simp_all
      Â· right
        use 0, 1, p, M, N
        simp_all
        refine Multiset.mem_replicate.mpr ?_
        simp only [ne_eq, Int.natAbs_eq_zero, and_true, Ex]
        revert h1
        revert hf
        generalize F.toFluxesFive = FF
        intro hFF
        fin_cases hFF
        all_goals simp
        all_goals aesop
  use Ex
  constructor
  /- Ex âˆˆ ofChargesExpand c -/
  Â· rw [mem_ofChargesExpand_iff]
    constructor
    /- Ex.toCharges.toFinset = c -/
    Â· exact ex_charges
    /- Ex.toFluxesFive = {(1, -1), (1, -1), (1, -1), (0, 1), (0, 1), (0, 1)} -/
    Â· simp [Ex, toFluxesFive]
      rw [Multiset.map_bind, Multiset.map_bind]
      simp only [Int.reduceNeg, Multiset.map_replicate, Ex]
      trans ((Multiset.bind F.toFluxesFive fun a => Multiset.replicate a.1.natAbs (1, -1)) +
        Multiset.bind F.toFluxesFive fun a => Multiset.replicate (a.1 + a.2).natAbs (0, 1))
      Â· congr 1
        Â· rw [toFluxesFive, Multiset.bind_map]
        Â· rw [toFluxesFive, Multiset.bind_map]
      Â· generalize F.toFluxesFive = FF at *
        fin_cases hf
        any_goals decide
  /- Ex.reduce = F.reduce -/
  Â· refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
    Â· exact reduce_nodup Ex
    Â· exact reduce_nodup F
    intro a
    rw [mem_reduce_iff, mem_reduce_iff]
    conv_rhs => rw [â† Multiset.mem_toFinset, hc]
    rw [â† Multiset.mem_toFinset, ex_charges]
    rw [and_congr_right_iff]
    intro h1
    have hab (a b c : â„¤ Ã— â„¤) (hcb : c = b) : (a = c â†” a = b) := by
      subst hcb
      rfl
    rw [hab]
    rw [ex_filter]
    dsimp
    rw [Multiset.map_bind]
    simp only [Int.reduceNeg, Multiset.map_add, Multiset.map_replicate, Multiset.bind_add,
      Multiset.sum_add, Multiset.sum_bind, Multiset.sum_replicate, Prod.smul_mk, nsmul_eq_mul,
      Int.natCast_natAbs, mul_one, smul_neg, smul_zero, Ex]
    have h1 : (Multiset.map (fun x => (|x.2.1|, -|x.2.1|))
        (Multiset.filter (fun x => x.1 = a.1) F)).sum
        = (Multiset.map (fun x => (|x.1|, -|x.1|))
        (Multiset.map (fun y => y.2) (Multiset.filter (fun x => x.1 = a.1) F))).sum := by
      rw [Multiset.map_map]
      congr
    have h2 : (Multiset.map (fun x => ((0 : â„¤), |x.2.1 + x.2.2|))
      (Multiset.filter (fun x => x.1 = a.1) F)).sum
      = (Multiset.map (fun x => (0, |x.1 + x.2|))
        (Multiset.map (fun y => y.2) (Multiset.filter (fun x => x.1 = a.1) F))).sum := by
      rw [Multiset.map_map]
      congr
    rw [h1, h2]
    generalize hS : Multiset.map (fun y => y.2) (Multiset.filter (fun x => x.1 = a.1) F) = S
    have hS' : S â‰¤ F.toFluxesFive := by
      rw [â† hS]
      simp [toFluxesFive]
    have hS'' : S âˆˆ F.toFluxesFive.powerset := by
      exact Multiset.mem_powerset.mpr hS'
    clear hS hS'
    generalize F.toFluxesFive = FF at hf hS''
    exact FluxesFive.map_sum_add_of_mem_powerset_elemsNoExotics FF S hf hS''

lemma exists_charges_of_mem_ofChargesExpand (c : Finset ğ“©) (F : FiveQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    âˆƒ q1 q2 q3 q4 q5 q6 : ğ“©,
      F = {(q1, 1, -1), (q2, 1, -1), (q3, 1, -1), (q4, 0, 1), (q5, 0, 1), (q6, 0, 1)} := by
  let Fâ‚ := F.filter (fun x => x.snd = (1, -1))
  let Fâ‚‚ := F.filter (fun x => x.snd = (0, 1))
  have h_F_split : F = Fâ‚ + Fâ‚‚ := by
    dsimp [Fâ‚, Fâ‚‚]
    rw [â† eq_sum_filter_of_mem_ofChargesExpand c F h]
  have h_card_Fâ‚ : Fâ‚.card = 3 := by
    trans (F.toFluxesFive.filter (fun x => x = (1, -1))).card
    Â· simp [toFluxesFive, Fâ‚]
      rw [Multiset.filter_map]
      simp
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  have h_card_Fâ‚‚ : Fâ‚‚.card = 3 := by
    trans (F.toFluxesFive.filter (fun x => x = (0, 1))).card
    Â· simp [toFluxesFive, Fâ‚‚]
      rw [Multiset.filter_map]
      simp
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  have Fâ‚_map_prod_snd : Fâ‚.map Prod.snd = Multiset.replicate 3 (1, -1) := by
    dsimp [Fâ‚]
    trans (Multiset.filter (fun x => x = (1, -1)) F.toFluxesFive)
    Â· simp [toFluxesFive, Fâ‚]
      rw [Multiset.filter_map]
      simp
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  have Fâ‚‚_map_prod_snd : Fâ‚‚.map Prod.snd = Multiset.replicate 3 (0, 1) := by
    dsimp [Fâ‚‚]
    trans (Multiset.filter (fun x => x = (0, 1)) F.toFluxesFive)
    Â· simp [toFluxesFive, Fâ‚‚]
      rw [Multiset.filter_map]
      simp
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  obtain âŸ¨q1, q2, q3, hFâ‚âŸ© : âˆƒ q1 q2 q3, Fâ‚ = {(q1, 1, -1), (q2, 1, -1), (q3, 1, -1)} := by
    rw [Multiset.card_eq_three] at h_card_Fâ‚
    obtain âŸ¨a1, a2, a3, hâŸ© := h_card_Fâ‚
    rw [h]
    rw [h] at Fâ‚_map_prod_snd
    use a1.1, a2.1, a3.1
    congr
    Â· have h1 : a1.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
    Â· have h1 : a2.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
    Â· have h1 : a3.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
  obtain âŸ¨q4, q5, q6, hFâ‚‚âŸ© : âˆƒ q4 q5 q6, Fâ‚‚ = {(q4, 0, 1), (q5, 0, 1), (q6, 0, 1)} := by
    rw [Multiset.card_eq_three] at h_card_Fâ‚‚
    obtain âŸ¨a1, a2, a3, hâŸ© := h_card_Fâ‚‚
    rw [h]
    rw [h] at Fâ‚‚_map_prod_snd
    use a1.1, a2.1, a3.1
    congr
    Â· have h1 : a1.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
    Â· have h1 : a2.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
    Â· have h1 : a3.2 âˆˆ Multiset.map Prod.snd {a1, a2, a3} := by
        simp
      rw [Fâ‚‚_map_prod_snd] at h1
      simp at h1
      rw [â† h1]
  use q1, q2, q3, q4, q5, q6
  rw [h_F_split, hFâ‚, hFâ‚‚]
  rfl
lemma exists_charges_le_of_mem_ofChargesExpand (c : Finset â„¤) (F : FiveQuanta â„¤)
    (h : F âˆˆ ofChargesExpand c) :
    âˆƒ q1 q2 q3 q4 q5 q6 : â„¤,
      F = {(q1, 1, -1), (q2, 1, -1), (q3, 1, -1), (q4, 0, 1), (q5, 0, 1), (q6, 0, 1)} âˆ§
      q1 â‰¤ q2 âˆ§ q2 â‰¤ q3 âˆ§ q4 â‰¤ q5 âˆ§ q5 â‰¤ q6 := by
  obtain âŸ¨q1, q2, q3, q4, q5, q6, hFâŸ© := exists_charges_of_mem_ofChargesExpand c F h
  let Fâ‚ := F.filter (fun x => x.snd = (1, -1))
  let Fâ‚‚ := F.filter (fun x => x.snd = (0, 1))
  have Fâ‚_eq : Fâ‚ = {(q1, 1, -1), (q2, 1, -1), (q3, 1, -1)} := by
    dsimp [Fâ‚]
    subst hF
    simp [@Multiset.filter_singleton]
  have Fâ‚‚_eq : Fâ‚‚ = {(q4, 0, 1), (q5, 0, 1), (q6, 0, 1)} := by
    dsimp [Fâ‚‚]
    subst hF
    simp [@Multiset.filter_singleton]
  have F_eq : F = Fâ‚ + Fâ‚‚ := by
    dsimp [Fâ‚, Fâ‚‚]
    rw [â† eq_sum_filter_of_mem_ofChargesExpand c F h]
  suffices h : (âˆƒ q1 q2 q3 : â„¤, Fâ‚ = {(q1, 1, -1), (q2, 1, -1), (q3, 1, -1)} âˆ§
        q1 â‰¤ q2 âˆ§ q2 â‰¤ q3) âˆ§
        (âˆƒ q4 q5 q6 : â„¤, Fâ‚‚ = {(q4, 0, 1), (q5, 0, 1), (q6, 0, 1)} âˆ§
        q4 â‰¤ q5 âˆ§ q5 â‰¤ q6) by
      obtain âŸ¨âŸ¨q1', q2', q3', hFâ‚, h12âŸ©, âŸ¨q4', q5', q6', hFâ‚‚, h23âŸ©âŸ© := h
      use q1', q2', q3', q4', q5', q6'
      rw [F_eq, hFâ‚, hFâ‚‚]
      constructor
      Â· rfl
      simp_all
  have h1 : (q1 â‰¤ q2 âˆ§ q2 â‰¤ q3) âˆ¨ (q2 â‰¤ q3 âˆ§ q3 â‰¤ q1) âˆ¨ (q3 â‰¤ q1 âˆ§ q1 â‰¤ q2)
      âˆ¨ (q1 â‰¤ q3 âˆ§ q3 â‰¤ q2) âˆ¨ (q3 â‰¤ q2 âˆ§ q2 â‰¤ q1) âˆ¨ (q2 â‰¤ q1 âˆ§ q1 â‰¤ q3) := by
    omega
  have h2 : (q4 â‰¤ q5 âˆ§ q5 â‰¤ q6) âˆ¨ (q5 â‰¤ q6 âˆ§ q6 â‰¤ q4) âˆ¨ (q6 â‰¤ q4 âˆ§ q4 â‰¤ q5)
      âˆ¨ (q4 â‰¤ q6 âˆ§ q6 â‰¤ q5) âˆ¨ (q6 â‰¤ q5 âˆ§ q5 â‰¤ q4) âˆ¨ (q5 â‰¤ q4 âˆ§ q4 â‰¤ q6) := by omega
  constructor
  Â· rw [Fâ‚_eq]
    rcases h1 with h1 | h1 | h1 | h1 | h1 | h1
    Â· use q1, q2, q3
    Â· use q2, q3, q1
      simp_all
      rw [Multiset.cons_swap, Multiset.cons_inj_right,
        â† Multiset.cons_zero, Multiset.cons_swap]
      rfl
    Â· use q3, q1, q2
      simp_all
      rw [â† Multiset.cons_zero]
      conv_lhs =>
        enter [2]
        rw [Multiset.cons_swap]
      rw [Multiset.cons_swap, Multiset.cons_inj_right]
      rfl
    Â· use q1, q3, q2
      simp_all
      rw [â† Multiset.cons_zero, Multiset.cons_swap]
      rfl
    Â· use q3, q2, q1
      simp_all
      rw [â† Multiset.cons_zero]
      conv_lhs =>
        enter [2]
        rw [Multiset.cons_swap]
      rw [Multiset.cons_swap, Multiset.cons_inj_right, Multiset.cons_swap]
      rfl
    Â· use q2, q1, q3
      simp_all
      rw [Multiset.cons_swap]
  Â· rw [Fâ‚‚_eq]
    rcases h2 with h2 | h2 | h2 | h2 | h2 | h2
    Â· use q4, q5, q6
    Â· use q5, q6, q4
      simp_all
      rw [Multiset.cons_swap, Multiset.cons_inj_right,
        â† Multiset.cons_zero, Multiset.cons_swap]
      rfl
    Â· use q6, q4, q5
      simp_all
      rw [â† Multiset.cons_zero]
      conv_lhs =>
        enter [2]
        rw [Multiset.cons_swap]
      rw [Multiset.cons_swap, Multiset.cons_inj_right]
      rfl
    Â· use q4, q6, q5
      simp_all
      rw [â† Multiset.cons_zero, Multiset.cons_swap]
      rfl
    Â· use q6, q5, q4
      simp_all
      rw [â† Multiset.cons_zero]
      conv_lhs =>
        enter [2]
        rw [Multiset.cons_swap]
      rw [Multiset.cons_swap, Multiset.cons_inj_right, Multiset.cons_swap]
      rfl
    Â· use q5, q4, q6
      simp_all
      rw [Multiset.cons_swap]

lemma reduce_hasNoZeros_of_mem_ofChargesExpand (c : Finset ğ“©) (F : FiveQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesFive.HasNoZero := by
  simp [reduce, toFluxesFive, FluxesFive.HasNoZero]
  intro x hx
  have hs : (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F))
      âˆˆ F.toFluxesFive.powerset := by
    simp [toFluxesFive]
  have h1 : Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F) â‰  0 := by
    simp only [ne_eq, Multiset.map_eq_zero]
    rw [@Multiset.filter_eq_nil]
    simp [Prod.forall, not_forall, Classical.not_imp, Decidable.not_not, exists_and_right,
      exists_eq_right]
    simp [toCharges] at hx
    obtain âŸ¨a, b, hâŸ© := hx
    use a, b
  generalize (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = x) F)) = s at *
  rw [mem_ofChargesExpand_iff] at h
  rw [h.2] at hs
  fin_cases hs
  Â· simp at h1
  all_goals
  Â· decide

lemma reduce_noExotics_of_mem_ofChargesExpand (c : Finset ğ“©) (F : FiveQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesFive.NoExotics := by
  simp [FluxesFive.NoExotics]
  have h1 : (Multiset.filter (fun x => x < 0)
      (Multiset.map (fun f => f.1 + f.2) F.reduce.toFluxesFive)) = âˆ… := by
    simp only [Multiset.empty_eq_zero]
    rw [@Multiset.filter_eq_nil]
    intro a ha
    simp at ha
    obtain âŸ¨c, b, h1, h2âŸ© := ha
    obtain âŸ¨s, h'âŸ© : âˆƒ s âˆˆ F.toFluxesFive.powerset, (c, b) = s.sum := by
      simp [reduce, toFluxesFive] at h1
      obtain âŸ¨q, q_mem, h1âŸ© := h1
      use (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = q) F))
      simp_all [toFluxesFive]
    have ha : a = s.sum.1 + s.sum.2 := by
      rw [â† h2, â† h'.2]
    rw [ha]
    have h2 := h'.1
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2] at h2
    fin_cases h2
    all_goals
    Â· decide
  have h2 : (Multiset.filter (fun x => x < 0) (Multiset.map (fun f => f.1) F.reduce.toFluxesFive))
      = âˆ… := by
    simp only [Multiset.empty_eq_zero]
    rw [@Multiset.filter_eq_nil]
    intro a ha
    simp at ha
    obtain âŸ¨c, h1âŸ© := ha
    obtain âŸ¨s, h'âŸ© : âˆƒ s âˆˆ F.toFluxesFive.powerset, (a, c) = s.sum := by
      simp [reduce, toFluxesFive] at h1
      obtain âŸ¨q, q_mem, h1âŸ© := h1
      use (Multiset.map (fun y => y.2) (Multiset.filter (fun f => f.1 = q) F))
      simp_all [toFluxesFive]
    have ha : a = s.sum.1 := by
      rw [â† h'.2]
    rw [ha]
    have h2 := h'.1
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2] at h2
    fin_cases h2
    all_goals
    Â· decide
  refine âŸ¨?_, ?_, ?_, ?_âŸ©
  Â· rw [FluxesFive.numChiralL_eq_sum_sub_numAntiChiralL]
    simp [FluxesFive.numAntiChiralL, FluxesFive.chiralIndicesOfL]
    rw [h1]
    have sum_1 : (Multiset.map Prod.fst F.reduce.toFluxesFive).sum
      = (Multiset.map Prod.fst F.toFluxesFive).sum := by
      rw [toFluxesFive, Multiset.map_map]
      let f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ â„¤ := fun q5 => {
        toFun := fun x => x.1
        map_add' := by simp
        map_zero' := by simp
      }
      change (Multiset.map (fun (q5, x) => f q5 x) F.reduce).sum = _
      rw [reduce_sum_eq_sum_toCharges F]
      simp [f, toFluxesFive]
    have sum_2 : (Multiset.map Prod.snd F.reduce.toFluxesFive).sum
      = (Multiset.map Prod.snd F.toFluxesFive).sum := by
      rw [toFluxesFive, Multiset.map_map]
      let f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ â„¤ := fun q5 => {
        toFun := fun x => x.2
        map_add' := by simp
        map_zero' := by simp
      }
      change (Multiset.map (fun (q5, x) => f q5 x) F.reduce).sum = _
      rw [reduce_sum_eq_sum_toCharges F]
      simp [f, toFluxesFive]
    rw [sum_1, sum_2]
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  Â· simp [FluxesFive.numAntiChiralL, FluxesFive.chiralIndicesOfL]
    rw [h1]
    simp
  Â· rw [FluxesFive.numChiralD_eq_sum_sub_numAntiChiralD]
    simp [FluxesFive.numAntiChiralD, FluxesFive.chiralIndicesOfD]
    rw [h2]
    simp only [Multiset.empty_eq_zero, Multiset.sum_zero, sub_zero]
    let f : ğ“© â†’ â„¤ Ã— â„¤ â†’+ â„¤ := fun q5 => {
      toFun := fun x => x.1
      map_add' := by simp
      map_zero' := by simp
    }
    simp [toFluxesFive]
    change (Multiset.map (fun (q5, x) => f q5 x) F.reduce).sum = _
    rw [reduce_sum_eq_sum_toCharges F]
    trans (Multiset.map (fun x => x.1) F.toFluxesFive).sum
    Â· simp [toFluxesFive]
      rfl
    rw [mem_ofChargesExpand_iff] at h
    rw [h.2]
    decide
  Â· simp [FluxesFive.numAntiChiralD, FluxesFive.chiralIndicesOfD]
    rw [h2]
    simp

lemma reduce_mem_elemsNoExotics_of_mem_ofChargesExpand (c : Finset ğ“©) (F : FiveQuanta ğ“©)
    (h : F âˆˆ ofChargesExpand c) :
    F.reduce.toFluxesFive âˆˆ FluxesFive.elemsNoExotics := by
  rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics]
  constructor
  Â· exact reduce_noExotics_of_mem_ofChargesExpand c F h
  Â· exact reduce_hasNoZeros_of_mem_ofChargesExpand c F h

lemma mem_ofChargesExpand_map_reduce_iff (c : Finset ğ“©) (S : FiveQuanta ğ“©) :
    S âˆˆ (ofChargesExpand c).map reduce â†” S.toFluxesFive âˆˆ FluxesFive.elemsNoExotics
      âˆ§ S.toCharges.toFinset = c âˆ§ S.reduce = S := by
  constructor
  Â· intro h
    simp at h
    obtain âŸ¨F, h1, rflâŸ© := h
    refine âŸ¨?_, ?_, ?_âŸ©
    Â· exact reduce_mem_elemsNoExotics_of_mem_ofChargesExpand c F h1
    Â· rw [mem_ofChargesExpand_iff] at h1
      rw [â† h1.1, reduce_toCharges]
      exact Multiset.toFinset_dedup F.toCharges
    Â· rw [reduce_reduce]
  Â· intro h
    simp only [Multiset.mem_map]
    rw [â† h.2.2]
    have h1 := h.1
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics] at h1
    refine mem_ofChargesExpand_of_noExotics_hasNoZero S c ?_ ?_ ?_
    Â· exact h.2.1
    Â· exact h1.1
    Â· exact h1.2

end ofChargesExpand

end FiveQuanta

end SU5
end FTheory
