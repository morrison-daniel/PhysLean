/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.Quanta.AnomalyCancellation
import PhysLean.StringTheory.FTheory.SU5U1.Charges.PhenoConstrained.Completeness
/-!

# Viable Quanta

We say a term of a type `Quanta` is viable for a given `I : CodimensionOneConfig`,
  if it satisfies the following properties:
- It has a `Hd`, `Hu` and at leat one matter particle in the 5 and 10 representation.
- It has no exotic chiral particles.
- It leads to a top Yukawa coupling.
- It does not lead to a pheno constraining terms.
- It satisfies anomaly cancellation.
- The charges are allowed by the `I` configuration.

This somes with one caveat, the `IsViable` constraint enforces the anomaly cancellation condition:
`âˆ‘áµ¢ qáµ¢Â² Náµ¢ + 3 * âˆ‘â‚ qâ‚Â² Nâ‚ = 0`
to hold, which is not always necessary, see arXiv:1401.5084.

-/
namespace FTheory

namespace SU5U1

variable {I : CodimensionOneConfig}

namespace Quanta
open PotentialTerm ChargeProfile Charges

/-- For a given `I : CodimensionOneConfig` the condition on a `Quanta` for it to be
  phenomenologically viable. -/
def IsViable (I : CodimensionOneConfig) (x : Quanta) : Prop :=
  x.toCharges.IsComplete âˆ§
  Â¬ x.toCharges.IsPhenoConstrained âˆ§
  IsPresent topYukawa (toChargeProfile topYukawa x.toCharges) âˆ§
  x.2.2.1.toFluxesFive.NoExotics âˆ§
  x.2.2.1.toFluxesFive.HasNoZero âˆ§
  x.2.2.2.toFluxesTen.NoExotics âˆ§
  x.2.2.2.toFluxesTen.HasNoZero âˆ§
  AnomalyCancellation x.1 x.2.1 x.2.2.1 x.2.2.2 âˆ§
  x.toCharges âˆˆ ofFinset I.allowedBarFiveCharges I.allowedTenCharges âˆ§
  x.2.2.1.toCharges.Nodup âˆ§
  x.2.2.2.toCharges.Nodup

lemma isViable_iff_expand_ofFinset (I : CodimensionOneConfig) (x : Quanta) :
    IsViable I x â†”
      x.toCharges.IsComplete âˆ§
  Â¬ x.toCharges.IsPhenoConstrained âˆ§
  IsPresent topYukawa (toChargeProfile topYukawa x.toCharges) âˆ§
  x.2.2.1.toFluxesFive.NoExotics âˆ§
  x.2.2.1.toFluxesFive.HasNoZero âˆ§
  x.2.2.2.toFluxesTen.NoExotics âˆ§
  x.2.2.2.toFluxesTen.HasNoZero âˆ§
  AnomalyCancellation x.1 x.2.1 x.2.2.1 x.2.2.2 âˆ§
  (x.1.toFinset âŠ† I.allowedBarFiveCharges âˆ§ x.2.1.toFinset âŠ† I.allowedBarFiveCharges âˆ§
      x.toCharges.2.2.1 âŠ† I.allowedBarFiveCharges âˆ§ x.toCharges.2.2.2 âŠ† I.allowedTenCharges)
      âˆ§
    x.2.2.1.toCharges.Nodup âˆ§
    x.2.2.2.toCharges.Nodup := by
  rw [IsViable, Charges.mem_ofFinset_iff]
  simp [toCharges]

instance (I : CodimensionOneConfig) (x : Quanta) : Decidable (IsViable I x) :=
  decidable_of_iff _ (isViable_iff_expand_ofFinset I x).symm

lemma toCharges_five_nodup_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.1.toCharges.Nodup := by
  exact h.2.2.2.2.2.2.2.2.2.1

lemma toCharges_ten_nodup_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.2.toCharges.Nodup := by
  exact h.2.2.2.2.2.2.2.2.2.2

lemma toCharges_mem_ofFinset_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.toCharges âˆˆ ofFinset I.allowedBarFiveCharges I.allowedTenCharges := by
  exact h.2.2.2.2.2.2.2.2.1

lemma toFluxesFive_noExotics_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.1.toFluxesFive.NoExotics := by
  exact h.2.2.2.1

lemma toFluxesTen_noExotics_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.2.toFluxesTen.NoExotics := by
  exact h.2.2.2.2.2.1

lemma toFluxesFive_hasNoZero_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.1.toFluxesFive.HasNoZero := by
  exact h.2.2.2.2.1

lemma toFluxesTen_hasNoZero_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.2.toFluxesTen.HasNoZero := by
  exact h.2.2.2.2.2.2.1

lemma Q10_charges_mem_allowedBarTenCharges_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    âˆ€ s âˆˆ x.2.2.2.toCharges, s âˆˆ I.allowedTenCharges := by
  have h1 := toCharges_mem_ofFinset_of_isViable I x h
  rw [Charges.mem_ofFinset_iff] at h1
  have h2 := h1.2.2.2
  intro y hy
  apply h2
  simp [toCharges]
  exact hy

lemma Q5_charges_mem_allowedBarFiveCharges_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    âˆ€ s âˆˆ x.2.2.1.toCharges, s âˆˆ I.allowedBarFiveCharges := by
  have h1 := toCharges_mem_ofFinset_of_isViable I x h
  rw [Charges.mem_ofFinset_iff] at h1
  have h2 := h1.2.2.1
  intro y hy
  apply h2
  simp [toCharges]
  exact hy

lemma fiveQuanta_mem_ofCharges_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.1 âˆˆ FiveQuanta.ofCharges I x.2.2.1.toCharges := by
  rw [FiveQuanta.mem_ofCharges_iff]
  Â· simp
    constructor
    Â· exact toFluxesFive_noExotics_of_isViable I x h
    Â· exact toFluxesFive_hasNoZero_of_isViable I x h
  Â· exact fun s a => Q5_charges_mem_allowedBarFiveCharges_of_isViable I x h s a

lemma tenQuanta_mem_ofCharges_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.2.2.2 âˆˆ TenQuanta.ofCharges I x.2.2.2.toCharges := by
  rw [TenQuanta.mem_ofCharges_iff]
  Â· simp
    constructor
    Â· exact toFluxesTen_noExotics_of_isViable I x h
    Â· exact toFluxesTen_hasNoZero_of_isViable I x h
  Â· exact fun s a => Q10_charges_mem_allowedBarTenCharges_of_isViable I x h s a

lemma topYukawa_isPresent_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    IsPresent topYukawa (toChargeProfile topYukawa x.toCharges) := by
  exact h.2.2.1

lemma not_isPhenoConstrained_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    Â¬ x.toCharges.IsPhenoConstrained := by
  exact h.2.1

lemma toCharges_isComplete_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.toCharges.IsComplete := by
  exact h.1

lemma anomalyCancellation_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    AnomalyCancellation x.1 x.2.1 x.2.2.1 x.2.2.2 := by
  exact h.2.2.2.2.2.2.2.1
/-!

## toCharges mem

-/

lemma toCharges_mem_nonPhenoConstrainedCharges_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.toCharges âˆˆ nonPhenoConstrainedCharges I := by
  rw [â† not_isPhenoConstrained_iff_mem_nonPhenoConstrainedCharge]
  Â· constructor
    Â· exact topYukawa_isPresent_of_isViable I x h
    constructor
    Â· exact not_isPhenoConstrained_of_isViable I x h
    Â· exact toCharges_isComplete_of_isViable I x h
  Â· exact toCharges_mem_ofFinset_of_isViable I x h

lemma toCharges_mem_nonPhenoConstrainedCharges_filterAnomalyCancellation_of_isViable
    (I : CodimensionOneConfig) (x : Quanta) (h : IsViable I x) :
    x.toCharges âˆˆ Charges.Tree.filterAnomalyCancellation I (nonPhenoConstrainedCharges I) := by
  rw [Tree.mem_filterAnomalyCancellation_iff]
  constructor
  Â· exact toCharges_mem_nonPhenoConstrainedCharges_of_isViable I x h
  use x.2.2.1, x.2.2.2
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· simp [toCharges]
    have h1 : FiveQuanta.ofCharges I x.2.2.1.toCharges.dedup =
        FiveQuanta.ofCharges I x.2.2.1.toCharges := by
      congr
      refine Multiset.Nodup.dedup ?_
      exact toCharges_five_nodup_of_isViable I x h
    rw [h1]
    exact fiveQuanta_mem_ofCharges_of_isViable I x h
  Â· simp [toCharges]
    have h1 : TenQuanta.ofCharges I x.2.2.2.toCharges.dedup =
        TenQuanta.ofCharges I x.2.2.2.toCharges := by
      congr
      refine Multiset.Nodup.dedup ?_
      exact toCharges_ten_nodup_of_isViable I x h
    rw [h1]
    exact tenQuanta_mem_ofCharges_of_isViable I x h
  Â· exact anomalyCancellation_of_isViable I x h

lemma mem_ofCharges_self_of_isViable (I : CodimensionOneConfig) (ğ“  : Quanta)
    (h : IsViable I ğ“ ) :
    ğ“  âˆˆ ofCharges I ğ“ .toCharges := by
  simp [ofCharges]
  use ğ“ .2.2.1, ğ“ .2.2.2
  refine âŸ¨âŸ¨?_, ?_âŸ©, ?_âŸ©
  Â· simp [toCharges]
    have h1 : FiveQuanta.ofCharges I ğ“ .2.2.1.toCharges.dedup =
        FiveQuanta.ofCharges I ğ“ .2.2.1.toCharges := by
      congr
      refine Multiset.Nodup.dedup ?_
      exact toCharges_five_nodup_of_isViable I ğ“  h
    rw [h1]
    apply FiveQuanta.mem_ofCharges_self
    exact toFluxesFive_noExotics_of_isViable I ğ“  h
    exact toFluxesFive_hasNoZero_of_isViable I ğ“  h
    exact fun s a => Q5_charges_mem_allowedBarFiveCharges_of_isViable I ğ“  h s a
  Â· simp [toCharges]
    have h1 : TenQuanta.ofCharges I ğ“ .2.2.2.toCharges.dedup =
        TenQuanta.ofCharges I ğ“ .2.2.2.toCharges := by
      congr
      refine Multiset.Nodup.dedup ?_
      exact toCharges_ten_nodup_of_isViable I ğ“  h
    rw [h1]
    apply TenQuanta.mem_ofCharges_self
    exact toFluxesTen_noExotics_of_isViable I ğ“  h
    exact toFluxesTen_hasNoZero_of_isViable I ğ“  h
    exact fun s a => Q10_charges_mem_allowedBarTenCharges_of_isViable I ğ“  h s a
  Â· simp [toCharges]

end Quanta

end SU5U1

end FTheory
