/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.NoExotics.Basic
/-!

# No exotics for 10d representations

In this module we give lemmas on the 10d matter content arising
from the conditions of a valid spectrum.

## Important results

- `quantaTen_card_le_three` :
  The number of 10d representations is less than or equal to three.
- `quantaTen_chiralityFlux_mem` :
  The multiset of chirality fluxes of matter content in the 10d representation
  satisfying `ThreeChiralFamiles` and `ThreeLeptonDoublets` must either be
  `{1, 1, 1}`, `{1, 2}`, or `{3}`.

-/
namespace FTheory

namespace SU5U1

namespace MatterContent

variable {I : CodimensionOneConfig} {ğ“œ : MatterContent I}

/-- The chirality flux associated with a 10d representation must be non-negative. -/
lemma quantaTen_chiralityFlux_nonneg' {a : QuantaTen}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaTen) : 0 â‰¤ a.M := by
  exact h3.2.2.2 a h

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaTen_chiralityFlux_nonneg {a : ChiralityFlux}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : 0 â‰¤ a := by
  simp only [Multiset.mem_map] at h
  obtain âŸ¨a', h', rflâŸ© := h
  exact h3.2.2.2 a' h'

/-- Due to the condition of having no exotics in the spectrum, the chirality flux of a
  10d representation must be non-zero.
  This also uses the condition that the hypercharge flux and the chirality flux cannot both
  be zero. -/
lemma quantaTen_chiralityFlux_ne_zero {a : ChiralityFlux}
    (he : ğ“œ.NoExotics) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : Â¬ a = 0 := by
  rw [Multiset.mem_map] at h
  obtain âŸ¨a', h', rflâŸ© := h
  have h1 := he.2.2.1 a' h'
  have h2 := ğ“œ.chirality_charge_not_both_zero_ten a' h'
  rcases a' with âŸ¨m, n, qâŸ©
  simp_all [QuantaTen.N, QuantaTen.M]
  by_contra hn
  subst hn
  simp_all only [neg_zero, forall_const]
  have h0 (n : â„¤) (hn : 0 â‰¤ n âˆ§ n â‰¤ 0) (hn : Â¬ n = 0) : False := by omega
  exact h0 n h1 h2

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaTen_chiralityFlux_pos {a : ChiralityFlux} (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : 0 < a := by
  have h1 := quantaTen_chiralityFlux_nonneg h3 h
  have h2 := quantaTen_chiralityFlux_ne_zero he h
  simp_all only [ChiralityFlux]
  omega

lemma quantaTen_chiralityFlux_filter_ne_zero_eq_self (he : ğ“œ.NoExotics) :
    (ğ“œ.quantaTen.map (QuantaTen.M)).filter (fun x => Â¬ x = 0) =
    ğ“œ.quantaTen.map (QuantaTen.M) := by
  refine Multiset.filter_eq_self.mpr ?_
  intro a ha
  exact quantaTen_chiralityFlux_ne_zero he ha

/-- The chirality flux associated with a 10d representation must be
  less then or equal to three. -/
lemma quantaTen_chiralityFlux_le_three {a : QuantaTen}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaTen) : a.M â‰¤ 3 := by
  rw [â† h3.2.1]
  refine Multiset.single_le_sum (Î± := ChiralityFlux) ?_ _ ?_
  Â· intro a ha
    simp only [Multiset.mem_map] at ha
    obtain âŸ¨a', h1, rflâŸ© := ha
    exact quantaTen_chiralityFlux_nonneg' h3 h1
  Â· simp only [Multiset.mem_map]
    use a

lemma quantaTen_chiralityFlux_card_le_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    (ğ“œ.quantaTen.map (QuantaTen.M)).card â‰¤ 3 := by
  have h1 := h3.2.1
  have hl : (ğ“œ.quantaTen.map (QuantaTen.M)).card â€¢ 1 â‰¤ (ğ“œ.quantaTen.map (QuantaTen.M)).sum := by
    apply Multiset.card_nsmul_le_sum
    intro x hx
    have hx' := quantaTen_chiralityFlux_pos he h3 hx
    simp_all only [ChiralityFlux]
    omega
  simp only [nsmul_eq_mul, mul_one] at hl
  simp_all [ChiralityFlux]

lemma quantaTen_chiralityFlux_card_mem (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    ((ğ“œ.quantaTen.map (QuantaTen.M))).card âˆˆ
    ({1, 2, 3} : Finset â„•) := by
  have hn {n : â„•} (hn : n â‰¤ 3) (hn : n â‰  0) : n âˆˆ ({1, 2, 3} : Finset â„•) := by
    simp only [Finset.mem_insert, Finset.mem_singleton]
    omega
  refine hn (quantaTen_chiralityFlux_card_le_three he h3) ?_
  by_contra hn
  have hl := h3.2
  simp at hn
  rw [hn] at hl
  simp at hl

/-- The number of 10d representations is less than or equal to three. -/
lemma quantaTen_card_le_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) : ğ“œ.quantaTen.card â‰¤ 3 := by
  simpa using quantaTen_chiralityFlux_card_le_three he h3

lemma quantaTen_map_q_powerset_filter_card_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    (ğ“œ.quantaTen.map QuantaTen.q).toFinset âˆˆ
    I.allowedTenCharges.powerset.filter (fun x => x.card â‰¤ 3) := by
  rw [Finset.mem_filter]
  apply And.intro
  Â· exact ğ“œ.quantaTen_map_q_mem_powerset
  Â· apply le_of_eq_of_le _ (ğ“œ.quantaTen_card_le_three he h3)
    trans (ğ“œ.quantaTen.map QuantaTen.q).card
    Â· conv_rhs => rw [ğ“œ.quantaTen_map_q_eq_toFinset]
      simp only [Multiset.toFinset_val]
      rfl
    Â· rw [Multiset.card_map]

/-- The multiset of chirality fluxes of matter content in the 10d representation
  satisfying `NoExotics` and
  `ThreeChiralFamiles` must either be
- `{1, 1, 1}`
- `{1, 2}`
- `{3}`
-/
lemma quantaTen_chiralityFlux_mem (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.quantaTen.map QuantaTen.M
    âˆˆ ({{1, 1, 1}, {1, 2}, {3}} : Finset (Multiset ChiralityFlux)) := by
  have hr := quantaTen_chiralityFlux_card_mem he h3
  simp at hr
  rcases hr with hr | hr | hr
  Â· rw [Multiset.card_eq_one] at hr
    obtain âŸ¨a, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl
    simp at hl
    rw [ha]
    simpa using hl.1
  Â· rw [Multiset.card_eq_two] at hr
    obtain âŸ¨a, b, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl âŠ¢
    simp at hl
    have a_mem_filter : a âˆˆ ğ“œ.quantaTen := by simp [ha]
    have b_mem_filter : b âˆˆ ğ“œ.quantaTen := by simp [ha]
    have a_pos : 0 < a.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use a)
    have b_pos : 0 < b.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use b)
    have hab (a b : â„¤) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 3) :
        (a = 2 âˆ§ b = 1) âˆ¨ (a = 1 âˆ§ b = 2) := by omega
    rcases hab a.M b.M a_pos b_pos hl.1 with hr | hr
    Â· conv_rhs => rw [Multiset.pair_comm]
      simp [hr]
    Â· simp [hr]
  Â· rw [Multiset.card_eq_three] at hr
    obtain âŸ¨a, b, c, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl
    simp [â† add_assoc] at hl
    rw [ha]
    have a_mem_filter : a âˆˆ ğ“œ.quantaTen := by simp [ha]
    have b_mem_filter : b âˆˆ ğ“œ.quantaTen := by simp [ha]
    have c_mem_filter : c âˆˆ ğ“œ.quantaTen := by simp [ha]
    have a_pos : 0 < a.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use a)
    have b_pos : 0 < b.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use b)
    have c_pos : 0 < c.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use c)
    have habc (a b c : â„¤) (ha : 0 < a) (hb : 0 < b) (hc : 0 < c) (habc : a + b + c = 3) :
        (a = 1 âˆ§ b = 1 âˆ§ c = 1) := by omega
    rcases habc a.M b.M c.M a_pos b_pos c_pos hl.1 with hr
    simp [hr]

lemma quantaTen_MN_mem (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.quantaTen.map QuantaTen.MN
    âˆˆ ({{(1, 0), (1, 0), (1, 0)}, {(1, 1), (1, -1), (1, 0)},
      {(1, 0), (2, 0)}, {(1, -1), (2, 1)}, {(1, 1), (2, -1)}, {(3, 0)}} :
      Finset (Multiset (ChiralityFlux Ã— HyperChargeFlux))) := by
  have hr := quantaTen_chiralityFlux_mem he h3
  simp only [Finset.mem_insert, Finset.mem_singleton] at hr
  rcases hr with hr | hr | hr
  Â· have hS (S : Multiset (â„¤ Ã— â„¤)) (hprod : S.map Prod.fst = {1, 1, 1}) :
        âˆƒ n1 n2 n3, S = {(1, n1), (1, n2), (1, n3)} := by
      simp only [Multiset.insert_eq_cons, â† Multiset.map_eq_cons, Multiset.map_eq_singleton,
        Prod.exists, exists_and_right, exists_eq_right] at hprod
      obtain âŸ¨_, n1, h1, rfl, hiâŸ© := hprod
      obtain âŸ¨_, n2, h2, rfl, hiâŸ© := hi
      obtain âŸ¨n3, hiâŸ© := hi
      rw [â† (Multiset.cons_erase h1), â† (Multiset.cons_erase h2), hi]
      use n1, n2
      simp
    obtain âŸ¨n1, n2, n3, hSâŸ© := hS (ğ“œ.quantaTen.map QuantaTen.MN) (by simpa using hr)
    have hx := he.1
    have hx2 := ğ“œ.quantaTen_map_MN_bound_N_of_noExotics he
    rw [show (Multiset.map QuantaTen.N ğ“œ.quantaTen) = (ğ“œ.quantaTen.map QuantaTen.MN).map Prod.snd
      by rw [Multiset.map_map]; rfl] at hx
    rw [hS] at hx hx2 âŠ¢
    simp at hx hx2
    have hl (m : â„¤) (hm : -1 â‰¤ m) (hm1 : m â‰¤ 1) : m âˆˆ ({-1, 0, 1} : Finset â„¤) := by
      simp only [Int.reduceNeg, Finset.mem_insert, Finset.mem_singleton]
      omega
    have hn := hl n1 hx2.1.1 hx2.1.2
    have hn2 := hl n2 hx2.2.1.1 hx2.2.1.2
    have hn3 := hl n3 hx2.2.2.1 hx2.2.2.2
    clear hr hS
    clear h3 he hS hl
    revert hx hx2
    revert n1
    revert n2
    revert n3
    decide
  Â· have hS (S : Multiset (â„¤ Ã— â„¤)) (hprod : S.map Prod.fst = {1, 2}) :
        âˆƒ n1 n2, S = {(1, n1), (2, n2)} := by
      simp only [Multiset.insert_eq_cons, â† Multiset.map_eq_cons, Multiset.map_eq_singleton,
        Prod.exists, exists_and_right, exists_eq_right] at hprod
      obtain âŸ¨_, n1, h1, rfl, hiâŸ© := hprod
      obtain âŸ¨n2, hiâŸ© := hi
      rw [â† (Multiset.cons_erase h1), hi]
      use n1, n2
      simp
    obtain âŸ¨n1, n2, hSâŸ© := hS (ğ“œ.quantaTen.map QuantaTen.MN) (by simpa using hr)
    have hx := he.1
    have hx2 := ğ“œ.quantaTen_map_MN_bound_N_of_noExotics he
    rw [show (Multiset.map QuantaTen.N ğ“œ.quantaTen) = (ğ“œ.quantaTen.map QuantaTen.MN).map Prod.snd
      by rw [Multiset.map_map]; rfl] at hx
    rw [hS] at hx hx2 âŠ¢
    simp at hx hx2
    clear hr hS
    clear h3 he hS
    have hl (m : â„¤) (hm : -1 â‰¤ m) (hm1 : m â‰¤ 1) : m âˆˆ ({-1, 0, 1} : Finset â„¤) := by
      simp only [Int.reduceNeg, Finset.mem_insert, Finset.mem_singleton]
      omega
    have hl2 (m : â„¤) (hm : -2 â‰¤ m) (hm1 : m â‰¤ 2) : m âˆˆ ({-2, -1, 0, 1, 2} : Finset â„¤) := by
      simp only [Int.reduceNeg, Finset.mem_insert, Finset.mem_singleton]
      omega
    have hn := hl n1 hx2.1.1 hx2.1.2
    have hn2 := hl2 n2 hx2.2.1 hx2.2.2
    revert hx hx2
    revert n1
    revert n2
    decide
  Â· have hS (S : Multiset (â„¤ Ã— â„¤)) (hprod : S.map Prod.fst = {3}) :
        âˆƒ n1, S = {(3, n1)} := by
      simp only [Multiset.insert_eq_cons, â† Multiset.map_eq_cons, Multiset.map_eq_singleton,
        Prod.exists, exists_and_right, exists_eq_right] at hprod
      obtain âŸ¨n1, h1, rfl, hiâŸ© := hprod
      use n1
    obtain âŸ¨n1, hSâŸ© := hS (ğ“œ.quantaTen.map QuantaTen.MN) (by simpa using hr)
    have hx := he.1
    have hx2 := ğ“œ.quantaTen_map_MN_bound_N_of_noExotics he
    rw [show (Multiset.map QuantaTen.N ğ“œ.quantaTen) = (ğ“œ.quantaTen.map QuantaTen.MN).map Prod.snd
      by rw [Multiset.map_map]; rfl] at hx
    rw [hS] at hx hx2 âŠ¢
    simp at hx hx2
    clear hr hS
    clear h3 he hS
    have hl (m : â„¤) (hm : -3 â‰¤ m) (hm1 : m â‰¤ 3) : m âˆˆ ({-3, -2, -1, 0, 1, 2, 3} : Finset â„¤) := by
      simp only [Int.reduceNeg, Finset.mem_insert, Finset.mem_singleton]
      omega
    have hn := hl n1 hx2.1 hx2.2
    revert hx hx2
    revert n1
    decide

end MatterContent

end SU5U1

end FTheory
