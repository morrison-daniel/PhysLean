/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.NoExotics.Basic
/-!

# Chirality flux and no exotics

The condition on the matter content for there to be no exotics in the spectrum
leads to constraints on the chirality fluxes of the matter content.

This file contains these constraints for the 5-bar and
10d matter representations.

The constraints present in this file are done without explicit consideration
of the `HyperChargeFlux`.

## Important results

- `quantaBarFiveMatter_chiralityFlux_mem` - Specifies the multisets of `ChiralityFlux`
  carried by the 5-bar matter representations potentially allowed by the no exotics conditions.
- `quantaTen_chiralityFlux_mem` - Specifies the allowed multisets of `ChiralityFlux`
  carried by the 10d matter representations potentially allowed by the no exotics conditions.

-/
namespace FTheory

namespace SU5U1

namespace MatterContent

variable {I : CodimensionOneConfig} {ğ“œ : MatterContent I}

/-!

## 5bar matter

-/

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaBarFiveMatter_chiralityFlux_nonneg' {a : QuantaBarFive}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaBarFiveMatter) : 0 â‰¤ a.M := by
  exact h3.2.2.1 a (by simp [quantaBarFive]; right; right; exact h)

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaBarFiveMatter_chiralityFlux_nonneg {a : ChiralityFlux}
    (h3 : ğ“œ.ThreeChiralFamiles)
    (h : a âˆˆ (ğ“œ.quantaBarFiveMatter).map QuantaBarFive.M) : 0 â‰¤ a := by
  simp only [Multiset.mem_map] at h
  obtain âŸ¨a', h', rflâŸ© := h
  refine h3.2.2.1 a' ?_
  simp [quantaBarFive]
  right
  right
  exact h'

/-- The chirality flux associated with a 5-bar representation must be
  less then or equal to three. -/
lemma quantaBarFiveMatter_chiralityFlux_le_three {a : QuantaBarFive}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaBarFiveMatter) : a.M â‰¤ 3 := by
  rw [â† h3.1]
  refine Multiset.single_le_sum (Î± := ChiralityFlux) ?_ _ ?_
  Â· intro a ha
    simp only [Multiset.mem_map, Prod.exists, Subtype.exists] at ha
    obtain âŸ¨a', b, c, h, rflâŸ© := ha
    rw [quantaBarFive] at h
    simp at h
    rcases h with âŸ¨rfl, rfl, rflâŸ© | âŸ¨rfl, rfl, rflâŸ© | h
    Â· simp
    Â· simp
    Â· refine quantaBarFiveMatter_chiralityFlux_nonneg' h3 h
  Â· simp only [Multiset.mem_map, Prod.exists, Subtype.exists]
    use a.1, a.2.1, a.2.2
    simp [quantaBarFive]
    right
    right
    exact h

lemma quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum (h3 : ğ“œ.ThreeChiralFamiles) :
    ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).sum = 3 := by
  have h2 : ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => x = 0)).sum = 0 := by
    rw [Multiset.sum_eq_zero]
    intro x hx
    rw [Multiset.mem_filter] at hx
    exact hx.2
  trans ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => x = 0)).sum +
      ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).sum
  Â· rw [h2]
    simp
  rw [Multiset.sum_filter_add_sum_filter_not]
  simpa [quantaBarFive, QuantaBarFive.M] using h3.1

/-- The number of 5-bar matter representations with non-zero chirality
  flux is less than or equal to three. -/
lemma quantaBarFiveMatter_chiralityFlux_filter_non_zero_card_le_three (h3 : ğ“œ.ThreeChiralFamiles) :
    ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).card â‰¤ 3 := by
  have h1 := quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum h3
  have hl : ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).card â€¢ 1 â‰¤
      ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).sum := by
    apply Multiset.card_nsmul_le_sum
    intro x hx
    rw [Multiset.mem_filter] at hx
    have hx' := quantaBarFiveMatter_chiralityFlux_nonneg h3 hx.1
    have hx'' := hx.2
    simp_all only [ChiralityFlux]
    omega
  simp only [nsmul_eq_mul, mul_one] at hl
  simp_all only [ChiralityFlux]
  omega

lemma pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero {a : ChiralityFlux}
    (h3 : ğ“œ.ThreeChiralFamiles)
    (h : a âˆˆ ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0))) :
    0 < a := by
  rw [Multiset.mem_filter] at h
  have h' := quantaBarFiveMatter_chiralityFlux_nonneg h3 h.1
  have h'' := h.2
  simp_all only [ChiralityFlux]
  omega

lemma quantaBarFiveMatter_chiralityFlux_filter_non_zero_card_mem (h3 : ğ“œ.ThreeChiralFamiles) :
    ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)).card âˆˆ
    ({1, 2, 3} : Finset â„•) := by
  have hn {n : â„•} (hn : n â‰¤ 3) (hn : n â‰  0) : n âˆˆ ({1, 2, 3} : Finset â„•) := by
    simp only [Finset.mem_insert, Finset.mem_singleton]
    omega
  refine hn (quantaBarFiveMatter_chiralityFlux_filter_non_zero_card_le_three h3) ?_
  by_contra hn
  have hl := quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum h3
  simp at hn
  rw [hn] at hl
  simp at hl

/-- The multiset of non-zero chirality fluxes of matter content in the 5-bar
  representation satisfying
  `ThreeChiralFamiles` must either be
- `{1, 1, 1}`
- `{1, 2}`
- `{3}`
-/
lemma quantaBarFiveMatter_chiralityFlux_filter_non_zero_mem (h3 : ğ“œ.ThreeChiralFamiles) :
    (ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0)
    âˆˆ ({{1, 1, 1}, {1, 2}, {3}} : Finset (Multiset ChiralityFlux)) := by
  have hr := quantaBarFiveMatter_chiralityFlux_filter_non_zero_card_mem h3
  simp at hr
  rcases hr with hr | hr | hr
  Â· rw [Multiset.card_eq_one] at hr
    obtain âŸ¨a, haâŸ© := hr
    have hl := quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum h3
    rw [ha] at hl
    simp at hl
    rw [ha, hl]
    simp
  Â· rw [Multiset.card_eq_two] at hr
    obtain âŸ¨a, b, haâŸ© := hr
    have hl := quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum h3
    rw [ha] at hl âŠ¢
    simp at hl
    have a_mem_filter : a âˆˆ
        (ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0) := by
      simp [ha]
    have b_mem_filter : b âˆˆ
        (ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => Â¬ x = 0) := by
      simp [ha]
    have a_pos : 0 < a :=
      pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero h3 a_mem_filter
    have b_pos : 0 < b :=
      pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero h3 b_mem_filter
    have hab (a b : â„¤) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 3) :
        (a = 2 âˆ§ b = 1) âˆ¨ (a = 1 âˆ§ b = 2) := by omega
    rcases hab a b a_pos b_pos hl with âŸ¨rfl, rflâŸ© | âŸ¨rfl, rflâŸ©
    Â· conv_rhs => rw [Multiset.pair_comm]
      simp
    Â· simp
  Â· rw [Multiset.card_eq_three] at hr
    obtain âŸ¨a, b, c, haâŸ© := hr
    have hl := quantaBarFiveMatter_chiralityFlux_filter_non_zero_sum h3
    rw [ha] at hl
    simp [â† add_assoc] at hl
    rw [ha]
    have a_mem_filter : a âˆˆ (ğ“œ.quantaBarFiveMatter.map
        (QuantaBarFive.M)).filter (fun x => Â¬ x = 0) := by
      simp [ha]
    have b_mem_filter : b âˆˆ (ğ“œ.quantaBarFiveMatter.map
        (QuantaBarFive.M)).filter (fun x => Â¬ x = 0) := by
      simp [ha]
    have c_mem_filter : c âˆˆ (ğ“œ.quantaBarFiveMatter.map
        (QuantaBarFive.M)).filter (fun x => Â¬ x = 0) := by
      simp [ha]
    have a_pos : 0 < a :=
      pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero h3 a_mem_filter
    have b_pos : 0 < b :=
      pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero h3 b_mem_filter
    have c_pos : 0 < c :=
      pos_of_mem_quantaBarFiveMatter_chiralityFlux_filter_non_zero h3 c_mem_filter
    have habc (a b c : â„¤) (ha : 0 < a) (hb : 0 < b) (hc : 0 < c) (habc : a + b + c = 3) :
        (a = 1 âˆ§ b = 1 âˆ§ c = 1) := by omega
    rcases habc a b c a_pos b_pos c_pos hl with âŸ¨rfl, rfl, rflâŸ©
    simp

lemma quantaBarFiveMatter_zero_chiralityFlux_abs_sum_le_three (h3L : ğ“œ.ThreeLeptonDoublets) :
    (Multiset.map (fun a => |a.M + a.N|)
      (ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0))).sum â‰¤ 3 := by
  simp [ThreeLeptonDoublets, quantaBarFive, QuantaBarFive.M, QuantaBarFive.N] at h3L
  have h3L : (Multiset.map (fun x => |x.1 + x.2.1|) ğ“œ.quantaBarFiveMatter).sum = 3 := by linarith
  have h1 : (Multiset.map (fun a => |a.M + a.N|) ğ“œ.quantaBarFiveMatter).sum
      = (Multiset.map (fun a => |a.M + a.N|) (ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0))).sum
      + (Multiset.map (fun a => |a.M + a.N|)
      (ğ“œ.quantaBarFiveMatter.filter (fun x => Â¬ x.M = 0))).sum := by
    conv_lhs => rw [Eq.symm (Multiset.filter_add_not (fun x => x.M = 0) ğ“œ.quantaBarFiveMatter)]
    rw [Multiset.map_add]
    rw [Multiset.sum_add]
  rw [h1] at h3L
  have hz_pos : 0 â‰¤
      (Multiset.map (fun a => |a.M + a.N|)
        (ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0))).sum := by
    apply Multiset.sum_nonneg
    intro x hx
    simp at hx
    obtain âŸ¨a, b, hc, d, ha, rflâŸ© := hx
    exact abs_nonneg _
  have hz_non_zero_pos : 0 â‰¤ (Multiset.map (fun a => |a.M + a.N|)
      (ğ“œ.quantaBarFiveMatter.filter (fun x => Â¬ x.M = 0))).sum := by
    apply Multiset.sum_nonneg
    intro x hx
    simp at hx
    obtain âŸ¨a, b, hc, d, ha, rflâŸ© := hx
    exact abs_nonneg _
  have hab {a b : â„¤} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hc : a + b = 3) : a â‰¤ 3 := by omega
  exact hab hz_pos hz_non_zero_pos h3L

/-- The number of 5d representations with chirality flux equal to zero is
  less than or equal to five. -/
lemma quantaBarFiveMatter_zero_chiralityFlux_card_le_three (h3L : ğ“œ.ThreeLeptonDoublets) :
    ((ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0))).card â‰¤ 3 := by
  have h1' : ((ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0)).map
      (fun a => |a.M + a.N|)).card â€¢ 1 â‰¤
      ((ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0)).map (fun a => |a.M + a.N|)).sum := by
    apply Multiset.card_nsmul_le_sum
    intro x hx
    simp only [Multiset.mem_map, Multiset.mem_filter, Prod.exists, Subtype.exists] at hx
    obtain âŸ¨m, n, q, ha, rflâŸ© := hx
    rw [ha.2]
    have hp := ğ“œ.chirality_charge_not_both_zero_bar_five (m, n, q) (by
      simp [quantaBarFive]
      right; right
      exact ha.1)
    simp [ha.2] at hp
    have ha {a : â„¤} (h : Â¬ a = 0) : 1 â‰¤ |a| := by
      exact Int.one_le_abs h
    apply ha
    simpa using hp
  have h1 := ğ“œ.quantaBarFiveMatter_zero_chiralityFlux_abs_sum_le_three h3L
  simp_all [HyperChargeFlux, ChiralityFlux]
  exact Int.ofNat_le.mp (le_trans h1' h1)

lemma quantaBarFiveMatter_zero_chiralityFlux_card_mem (h3L : ğ“œ.ThreeLeptonDoublets) :
    ((ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => x = 0)).card âˆˆ
    ({0, 1, 2, 3} : Finset â„•) := by
  have hn {n : â„•} (hn : n â‰¤ 3) : n âˆˆ ({0, 1, 2, 3} : Finset â„•) := by
    simp only [Finset.mem_insert, Finset.mem_singleton]
    omega
  apply hn
  simpa [Multiset.filter_map] using ğ“œ.quantaBarFiveMatter_zero_chiralityFlux_card_le_three h3L

open Multiset in
lemma quantaBarFiveMatter_zero_chiralityFlux_mem (h3L : ğ“œ.ThreeLeptonDoublets) :
    (ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => x = 0) âˆˆ
    ({replicate 3 0, replicate 2 0, replicate 1 0, replicate 0 0} :
      Finset (Multiset ChiralityFlux)) := by
  have h1 := quantaBarFiveMatter_zero_chiralityFlux_card_mem h3L
  have hn (n : â„•) (hr :
      (filter (fun x => x = 0) (map QuantaBarFive.M ğ“œ.quantaBarFiveMatter)).card = n) :
      (ğ“œ.quantaBarFiveMatter.map (QuantaBarFive.M)).filter (fun x => x = 0) = replicate n 0 := by
    rw [eq_replicate, hr]
    simp
  simp only [Finset.mem_insert, Finset.mem_singleton] at h1
  rcases h1 with hr | hr | hr | hr
  Â· rw [hn 0 hr]
    simp
  Â· rw [hn 1 hr]
    simp
  Â· rw [hn 2 hr]
    simp
  Â· rw [hn 3 hr]
    simp

/-- The number of 5-bar matter representations is less than or equal to six.
  Note the existences of `quantaBarFive_card_le_seven`. The proof of this weaker result
  does not rely on the assumptions of no-duplicate charges. -/
lemma quantaBarFiveMatter_card_le_six (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    ğ“œ.quantaBarFiveMatter.card â‰¤ 6 := by
  have h1 : ğ“œ.quantaBarFiveMatter.card =
      ((ğ“œ.quantaBarFiveMatter.filter (fun x => x.M = 0))).card +
      ((ğ“œ.quantaBarFiveMatter.filter (fun x => Â¬ x.M = 0))).card := by
    conv_lhs => rw [Eq.symm (Multiset.filter_add_not (fun x => x.M = 0) ğ“œ.quantaBarFiveMatter)]
    exact Multiset.card_add _ _
  rw [h1]
  have h2 : ((ğ“œ.quantaBarFiveMatter.filter (fun x => Â¬ x.M = 0))).card â‰¤ 3 := by
    simpa [Multiset.filter_map] using
      ğ“œ.quantaBarFiveMatter_chiralityFlux_filter_non_zero_card_le_three h3
  have h3 := quantaBarFiveMatter_zero_chiralityFlux_card_le_three h3L
  omega

/-- Without explicit consideration of `HyperChargeFlux`, the condition of no exotics in the
  matter spectrum constrains the allowed value of the `ChiralityFlux` carried by the 5-bar
  representations to be one of the following Multisets:
- `{1, 1, 1, 0, 0}`, `{1, 1, 1, 0}`, `{1, 1, 1}`
- `{1, 2, 0, 0, 0}`, `{1, 2, 0, 0}`, `{1, 2, 0}`, `{1, 2}`
- `{3, 0, 0, 0}`, `{3, 0, 0}`, `{3, 0}`, `{3}`
-/
lemma quantaBarFiveMatter_chiralityFlux_mem
    (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    ğ“œ.quantaBarFiveMatter.map QuantaBarFive.M âˆˆ
    ({{1, 1, 1, 0, 0}, {1, 1, 1, 0}, {1, 1, 1},
      {1, 2, 0, 0, 0}, {1, 2, 0, 0}, {1, 2, 0}, {1, 2},
      {3, 0, 0, 0}, {3, 0, 0}, {3, 0}, {3}} :
      Finset (Multiset ChiralityFlux)) := by
  have hcard : (ğ“œ.quantaBarFive.map QuantaBarFive.M).card â‰¤ 7 := by
    rw [Multiset.card_map]
    exact ğ“œ.quantaBarFive_card_le_seven
  have hcard : (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.M).card â‰¤ 5 := by
    simp [quantaBarFive] at hcard
    simpa using hcard
  rw [â† (Multiset.filter_add_not (fun x => x = 0)
    (Multiset.map QuantaBarFive.M ğ“œ.quantaBarFiveMatter))] at hcard âŠ¢
  have hz := quantaBarFiveMatter_zero_chiralityFlux_mem h3L
  have h0 := quantaBarFiveMatter_chiralityFlux_filter_non_zero_mem h3
  simp only [Finset.mem_insert, Finset.mem_singleton] at hz
  simp only [Finset.mem_insert, Finset.mem_singleton] at h0
  simp only [Multiset.insert_eq_cons]
  rcases hz with hz | hz | hz | hz
    <;> rcases h0 with h0 | h0 | h0
  all_goals
    rw [hz, h0]
    repeat rw [Multiset.replicate_succ]
    try rw [Multiset.replicate_zero]
    repeat rw [Multiset.insert_eq_cons]
    repeat rw [Multiset.add_cons]
    rw [Multiset.add_comm, Multiset.singleton_add]
    simp only [Multiset.cons_zero, Finset.mem_insert, Finset.mem_singleton,
      Multiset.empty_eq_zero, true_or, or_true]
  -- The case of {1, 1, 1, 0, 0, 0, 0, 0}
  rw [hz, h0] at hcard
  simp at hcard

/-- The number of 5-bar representations (including the Higges) is greater then or equal to three. -/
lemma quantaBarFiveMatter_one_le_card (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    1 â‰¤ ğ“œ.quantaBarFiveMatter.card := by
  refine le_of_le_of_eq ?_ (Multiset.card_map QuantaBarFive.M ğ“œ.quantaBarFiveMatter)
  have hr := ğ“œ.quantaBarFiveMatter_chiralityFlux_mem h3 h3L
  generalize (Multiset.map QuantaBarFive.M ğ“œ.quantaBarFiveMatter) = x at *
  fin_cases hr
    <;> simp

/-!

## 10d matter

-/

/-- The chirality flux associated with a 10d representation must be non-negative. -/
lemma quantaTen_chiralityFlux_nonneg' {a : QuantaTen}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaTen) : 0 â‰¤ a.M := by
  exact h3.2.2.2 a h

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaTen_chiralityFlux_nonneg {a : ChiralityFlux}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : 0 â‰¤ a := by
  simp only [Multiset.mem_map] at h
  obtain âŸ¨a', h', rflâŸ© := h
  exact h3.2.2.2 a' h'

/-- Due to the condition of having no exotics in the spectrum, the chirality flux of a
  10d representation must be non-zero.
  This also uses the condition that the hypercharge flux and the chirality flux cannot both
  be zero. -/
lemma quantaTen_chiralityFlux_ne_zero {a : ChiralityFlux}
    (he : ğ“œ.NoExotics) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : Â¬ a = 0 := by
  rw [Multiset.mem_map] at h
  obtain âŸ¨a', h', rflâŸ© := h
  have h1 := he.2.2.1 a' h'
  have h2 := ğ“œ.chirality_charge_not_both_zero_ten a' h'
  rcases a' with âŸ¨m, n, qâŸ©
  simp_all [QuantaTen.N, QuantaTen.M]
  by_contra hn
  subst hn
  simp_all only [neg_zero, forall_const]
  have h0 (n : â„¤) (hn : 0 â‰¤ n âˆ§ n â‰¤ 0) (hn : Â¬ n = 0) : False := by omega
  exact h0 n h1 h2

/-- The chirality flux associated with a 5-bar representation must be non-negative. -/
lemma quantaTen_chiralityFlux_pos {a : ChiralityFlux} (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ (ğ“œ.quantaTen).map QuantaTen.M) : 0 < a := by
  have h1 := quantaTen_chiralityFlux_nonneg h3 h
  have h2 := quantaTen_chiralityFlux_ne_zero he h
  simp_all only [ChiralityFlux]
  omega

lemma quantaTen_chiralityFlux_filter_ne_zero_eq_self (he : ğ“œ.NoExotics) :
    (ğ“œ.quantaTen.map (QuantaTen.M)).filter (fun x => Â¬ x = 0) =
    ğ“œ.quantaTen.map (QuantaTen.M) := by
  refine Multiset.filter_eq_self.mpr ?_
  intro a ha
  exact quantaTen_chiralityFlux_ne_zero he ha

/-- The chirality flux associated with a 10d representation must be
  less then or equal to three. -/
lemma quantaTen_chiralityFlux_le_three {a : QuantaTen}
    (h3 : ğ“œ.ThreeChiralFamiles) (h : a âˆˆ ğ“œ.quantaTen) : a.M â‰¤ 3 := by
  rw [â† h3.2.1]
  refine Multiset.single_le_sum (Î± := ChiralityFlux) ?_ _ ?_
  Â· intro a ha
    simp only [Multiset.mem_map] at ha
    obtain âŸ¨a', h1, rflâŸ© := ha
    exact quantaTen_chiralityFlux_nonneg' h3 h1
  Â· simp only [Multiset.mem_map]
    use a

lemma quantaTen_chiralityFlux_card_le_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    (ğ“œ.quantaTen.map (QuantaTen.M)).card â‰¤ 3 := by
  have h1 := h3.2.1
  have hl : (ğ“œ.quantaTen.map (QuantaTen.M)).card â€¢ 1 â‰¤ (ğ“œ.quantaTen.map (QuantaTen.M)).sum := by
    apply Multiset.card_nsmul_le_sum
    intro x hx
    have hx' := quantaTen_chiralityFlux_pos he h3 hx
    simp_all only [ChiralityFlux]
    omega
  simp only [nsmul_eq_mul, mul_one] at hl
  simp_all [ChiralityFlux]

lemma quantaTen_chiralityFlux_card_mem (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    ((ğ“œ.quantaTen.map (QuantaTen.M))).card âˆˆ
    ({1, 2, 3} : Finset â„•) := by
  have hn {n : â„•} (hn : n â‰¤ 3) (hn : n â‰  0) : n âˆˆ ({1, 2, 3} : Finset â„•) := by
    simp only [Finset.mem_insert, Finset.mem_singleton]
    omega
  refine hn (quantaTen_chiralityFlux_card_le_three he h3) ?_
  by_contra hn
  have hl := h3.2
  simp at hn
  rw [hn] at hl
  simp at hl

/-- The number of 10d representations is less than or equal to three. -/
lemma quantaTen_card_le_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) : ğ“œ.quantaTen.card â‰¤ 3 := by
  simpa using quantaTen_chiralityFlux_card_le_three he h3

lemma Q10_mem_powerset_filter_card_three (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) : ğ“œ.Q10.toFinset âˆˆ
    I.allowedTenCharges.powerset.filter (fun x => x.card â‰¤ 3) := by
  rw [Finset.mem_filter]
  apply And.intro
  Â· exact ğ“œ.Q10_mem_powerset
  Â· apply le_of_eq_of_le _ (ğ“œ.quantaTen_card_le_three he h3)
    trans ğ“œ.Q10.card
    Â· conv_rhs => rw [ğ“œ.Q10_eq_toFinset]
      simp only [Multiset.toFinset_val]
      rfl
    Â· rw [Multiset.card_map]

/-- Without consideration of `HyperChargeFlux`, the condition of no exotics in the matter spectrum
  constrains the allowed value of the `ChiralityFlux` carried by the 10d representations to be one
  of the following Multisets:

  `{1, 1, 1}`, `{1, 2}`, `{3}`
-/
lemma quantaTen_chiralityFlux_mem (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.quantaTen.map QuantaTen.M
    âˆˆ ({{1, 1, 1}, {1, 2}, {3}} : Finset (Multiset ChiralityFlux)) := by
  have hr := quantaTen_chiralityFlux_card_mem he h3
  simp at hr
  rcases hr with hr | hr | hr
  Â· rw [Multiset.card_eq_one] at hr
    obtain âŸ¨a, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl
    simp at hl
    rw [ha]
    simpa using hl.1
  Â· rw [Multiset.card_eq_two] at hr
    obtain âŸ¨a, b, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl âŠ¢
    simp at hl
    have a_mem_filter : a âˆˆ ğ“œ.quantaTen := by simp [ha]
    have b_mem_filter : b âˆˆ ğ“œ.quantaTen := by simp [ha]
    have a_pos : 0 < a.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use a)
    have b_pos : 0 < b.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use b)
    have hab (a b : â„¤) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 3) :
        (a = 2 âˆ§ b = 1) âˆ¨ (a = 1 âˆ§ b = 2) := by omega
    rcases hab a.M b.M a_pos b_pos hl.1 with hr | hr
    Â· conv_rhs => rw [Multiset.pair_comm]
      simp [hr]
    Â· simp [hr]
  Â· rw [Multiset.card_eq_three] at hr
    obtain âŸ¨a, b, c, haâŸ© := hr
    have hl := h3.2
    rw [ha] at hl
    simp [â† add_assoc] at hl
    rw [ha]
    have a_mem_filter : a âˆˆ ğ“œ.quantaTen := by simp [ha]
    have b_mem_filter : b âˆˆ ğ“œ.quantaTen := by simp [ha]
    have c_mem_filter : c âˆˆ ğ“œ.quantaTen := by simp [ha]
    have a_pos : 0 < a.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use a)
    have b_pos : 0 < b.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use b)
    have c_pos : 0 < c.M := quantaTen_chiralityFlux_pos he h3 (by rw [Multiset.mem_map]; use c)
    have habc (a b c : â„¤) (ha : 0 < a) (hb : 0 < b) (hc : 0 < c) (habc : a + b + c = 3) :
        (a = 1 âˆ§ b = 1 âˆ§ c = 1) := by omega
    rcases habc a.M b.M c.M a_pos b_pos c_pos hl.1 with hr
    simp [hr]

end MatterContent

end SU5U1

end FTheory
