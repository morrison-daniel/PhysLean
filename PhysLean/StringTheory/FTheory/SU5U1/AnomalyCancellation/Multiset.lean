/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.AnomalyCancellation.Basic
import PhysLean.StringTheory.FTheory.SU5U1.NoExotics.HyperchargeFlux
/-!

# Multisets of anomaly cancellation coefficents

In this module we construct the multisets of anomaly cancellation coefficents
given possible pairings of hypercharge fluxes and `U(1)` charges.

-/
namespace FTheory

namespace SU5U1
namespace MatterContent

variable {I : CodimensionOneConfig} (ğ“œ : MatterContent I)

/-!

## Auxillary results

Related to zips and projections of multisets.

-/

lemma zip_perm_orderedInsert (l : â„¤) : (r ls : List â„¤) â†’ (h : r.length = (l :: ls).length) â†’
    âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip (l :: ls)).Perm (r.zip (ls.orderedInsert LE.le l))
  | rs, [] => by
    aesop
  | [], l2 :: ls => by
    aesop
  | r :: [], l2 :: ls => by
    aesop
  | r :: r1 :: [], l2 :: ls => by
    simp only [List.length_cons, List.length_nil, zero_add, Nat.reduceAdd, right_eq_add,
      List.length_eq_zero_iff, List.orderedInsert]
    intro h
    subst h
    simp only [List.orderedInsert]
    split_ifs
    Â· use [r, r1]
    Â· use [r1, r]
      constructor
      Â· exact List.Perm.swap r r1 []
      Â· simp
        exact List.Perm.swap (r, l2) (r1, l) []
  | r2 :: r1 :: rs, l2 :: ls => by
    intro h
    simp only [List.orderedInsert]
    split_ifs
    Â· use r2 :: r1 :: rs
    Â· have ih := zip_perm_orderedInsert l (r1 :: rs) ls (by simp_all)
      obtain âŸ¨r', h1, h2âŸ© := ih
      have h' : ((r2 :: r1 :: rs).zip (l2 :: List.orderedInsert LE.le l ls)).Perm
          ((r2, l2) :: (r'.zip (l :: ls))) := by
        simp only [List.zip_cons_cons, List.perm_cons]
        exact id (List.Perm.symm h2)
      have hn : âˆƒ r'' : List â„¤,
          r''.Perm (r2 :: r') âˆ§ (r''.zip (l :: l2 :: ls)).Perm
          ((r2, l2) :: (r'.zip (l :: ls))) := by
        induction r' with
        | nil => simp at h1
        | cons rx r' ih' =>
          use rx :: r2 :: r'
          constructor
          Â· exact List.Perm.swap r2 rx r'
          Â· simp
            exact List.Perm.swap (r2, l2) (rx, l) (r'.zip ls)
      obtain âŸ¨r'', h3, h4âŸ© := hn
      use r''
      constructor
      Â· trans (r2 :: r')
        Â· exact h3
        Â· exact List.Perm.cons r2 h1
      Â· trans ((r2, l2) :: (r'.zip (l :: ls)))
        Â· exact h4
        Â· exact id (List.Perm.symm h')

lemma zip_perm_insertionSort : (r l3 : List â„¤) â†’ (h : r.length = l3.length) â†’
    âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip l3).Perm (r.zip (l3.insertionSort LE.le))
  | [], [] => by
    simp
  | [], l :: ls => by
    simp
  | r :: rs, [] => by
    simp
  | r :: rs, l :: ls => by
    intro h
    simp only [List.insertionSort]
    have h1 := zip_perm_orderedInsert l (r :: rs) (List.insertionSort LE.le ls) (by simp_all)
    obtain âŸ¨r1, h2, h3âŸ© := h1
    have h2 : âˆƒ r2 : List â„¤, r2.Perm r1 âˆ§ (r2.zip (l :: ls)).Perm
        (r1.zip (l :: List.insertionSort LE.le ls)) := by
      induction r1 with
      | nil => simp
      | cons rx r1 ih' =>
        -- r2 perm rx :: r1
        have hlenrsr1 : r1.length = rs.length := by
          simpa using List.Perm.length_eq h2
        obtain âŸ¨r2, h4, h5âŸ© := zip_perm_insertionSort r1 ls (by simp at h; omega)
        use rx :: r2
        constructor
        Â· exact List.Perm.cons rx h4
        Â· simp
          exact h5
    obtain âŸ¨r2, h4, h5âŸ© := h2
    use r2
    constructor
    Â· exact List.Perm.trans h4 h2
    Â· exact List.Perm.trans h5 h3

lemma zip_perm (l1 l2 l3 : List â„¤) (hp : l2.Perm l3) (hl : l2.length = l1.length) :
    âˆƒ (r : List â„¤), r.Perm l1 âˆ§ (l1.zip l2).Perm (r.zip l3) := by
  have hl' : (l2.insertionSort LE.le) = (l3.insertionSort LE.le) := by
    apply List.eq_of_perm_of_sorted (r := LE.le)
    Â· trans l2
      Â· exact List.perm_insertionSort LE.le l2
      trans l3
      Â· exact hp
      Â· exact List.Perm.symm (List.perm_insertionSort LE.le l3)
    Â· exact List.sorted_insertionSort LE.le l2
    Â· exact List.sorted_insertionSort LE.le l3
  have h1 : ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.1.Perm l1 := by
    simp only [List.unzip_fst]
    trans (List.map Prod.fst ((l1.zip l2)))
    Â· refine List.Perm.map Prod.fst ?_
      exact List.perm_insertionSort (fun x y => x.2 â‰¤ y.2) (l1.zip l2)
    Â· rw [â† List.unzip_fst]
      rw [List.unzip_zip]
      (expose_names; exact id (Eq.symm hl))
  have h2 : ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.2 =
      l2.insertionSort LE.le := by
    simp only [List.unzip_snd]
    have h1 := List.map_insertionSort (r := (fun x y => x.2 â‰¤ y.2)) (s := LE.le) Prod.snd
      (l1.zip l2) (by aesop)
    rw [h1]
    rw [â† List.unzip_snd]
    rw [List.unzip_zip]
    (expose_names; exact id (Eq.symm hl))
  have h3 : âˆƒ (r : List â„¤), l1.Perm r âˆ§ (l1.zip l2).Perm (r.zip (l3.insertionSort LE.le)) := by
    use ((l1.zip l2).insertionSort (fun x y => x.2 â‰¤ y.2)).unzip.1
    rw [â† hl', â† h2, List.zip_unzip]
    constructor
    Â· exact id (List.Perm.symm h1)
    Â· exact List.Perm.symm (List.perm_insertionSort (fun x y => x.2 â‰¤ y.2) (l1.zip l2))
  obtain âŸ¨r, h4, h5âŸ© := h3
  have h4 : âˆƒ (r' : List â„¤), r'.Perm r âˆ§ (r'.zip l3).Perm (r.zip (l3.insertionSort LE.le)) := by
    have := List.Perm.length_eq hp
    have := List.Perm.length_eq h4
    exact zip_perm_insertionSort r l3 (by omega)
  obtain âŸ¨r2, h6, h7âŸ© := h4
  use r2
  constructor
  Â· exact List.Perm.trans h6 (id (List.Perm.symm h4))
  Â· exact List.Perm.trans h5 (id (List.Perm.symm h7))

lemma mem_multiSetPairs_of_proj {S T : Multiset â„¤} (hlen : S.card = T.card)
    (X : Multiset (â„¤ Ã— â„¤)) :
    (âˆƒ (l l2 : List â„¤), l.Perm (Multiset.sort LE.le S) âˆ§
    l2.Perm (Multiset.sort LE.le T) âˆ§ X.toList.Perm (l.zip l2)) â†”
    X.map Prod.fst = S âˆ§ X.map Prod.snd = T := by
  constructor
  Â· intro h
    obtain âŸ¨l, l2, h1, h2, h3âŸ© := h
    constructor
    Â· trans Multiset.ofList (List.map Prod.fst X.toList)
      Â· rw [â† Multiset.map_coe]
        simp
      trans Multiset.ofList S.toList
      swap
      Â· simp
      refine Multiset.coe_eq_coe.mpr ?_
      trans (List.map Prod.fst (l.zip l2))
      Â· apply List.Perm.map Prod.fst
        exact h3
      rw [â† List.unzip_fst, List.unzip_zip]
      simp only
      trans (Multiset.sort LE.le S)
      Â· exact h1
      rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq, Multiset.coe_toList]
      have hL1 := List.Perm.length_eq h1
      have hL2 := List.Perm.length_eq h2
      simp at hL1 hL2
      omega
    Â· trans Multiset.ofList (List.map Prod.snd X.toList)
      Â· rw [â† Multiset.map_coe]
        simp
      trans Multiset.ofList T.toList
      swap
      Â· simp
      refine Multiset.coe_eq_coe.mpr ?_
      trans (List.map Prod.snd (l.zip l2))
      Â· apply List.Perm.map Prod.snd
        exact h3
      rw [â† List.unzip_snd, List.unzip_zip]
      simp only
      trans (Multiset.sort LE.le T)
      Â· exact h2
      rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq, Multiset.coe_toList]
      have hL1 := List.Perm.length_eq h1
      have hL2 := List.Perm.length_eq h2
      simp at hL1 hL2
      omega
  Â· intro h
    use (List.map Prod.fst X.toList), (List.map Prod.snd X.toList)
    constructor
    Â· rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq]
      rw [â† Multiset.map_coe]
      simpa using h.1
    constructor
    Â· rw [â† Multiset.coe_eq_coe]
      simp only [Multiset.sort_eq]
      rw [â† Multiset.map_coe]
      simpa using h.2
    Â· rw [â† List.unzip_fst, â† List.unzip_snd, List.zip_unzip]

lemma mem_list_of_prod_fst_snd (S T : Multiset â„¤) (hlen : S.card = T.card)
    (l : List â„¤) (hTl : T = â†‘l) (X : Multiset (â„¤ Ã— â„¤))
    (hS : X.map Prod.fst = S) (hT : X.map Prod.snd = T) :
    X âˆˆ S.lists.dedup.map (fun l2 => l2.zip l) := by
  simp only [Multiset.mem_map, Multiset.mem_dedup, Multiset.mem_lists_iff, Multiset.quot_mk_to_coe]
  have h1 := (mem_multiSetPairs_of_proj hlen X).mpr (by simp_all)
  obtain âŸ¨r1, r2, hr1, hr2, hrPâŸ© := h1
  have hr2' : r2.Perm l := by
    trans (Multiset.sort LE.le T)
    Â· exact hr2
    rw [â† @Multiset.coe_eq_coe, â† hTl]
    simp
  have hr1len : r1.length = S.card := by simp [List.Perm.length_eq hr1]
  have hr2len : r2.length = T.card := by simp [List.Perm.length_eq hr2]
  obtain âŸ¨j1, hjP, hjP2âŸ© := zip_perm r1 r2 l hr2' (by omega)
  use j1
  constructor
  Â· simpa [â† Multiset.coe_eq_coe] using (hjP.trans hr1).symm
  Â· simpa [â† Multiset.coe_eq_coe] using (hrP.trans hjP2).symm

/-!

## Multisets of anomaly coefficents due to pairings.

-/

open CodimensionOneConfig

/-- Given a multiset `N` corresponding to hypercharge fluxes and a multiset `Q`
  corresponding to `U(1)` charges both for five-bar matter, the multiset of possible
  anomaly cancellation coefficents formed by possible pairings of elements of `N`
  with elements of `Q`. -/
def fiveAnomalyFreeSet (I : CodimensionOneConfig) (N Q : Multiset â„¤) :
    Multiset (â„¤ Ã— â„¤) :=
  ((hyperchargeFluxLists N).map
      (fun l => (l.zip (fiveChargeMultisetToList I Q) : Multiset (â„¤ Ã— â„¤)))).map
    fun N => ((N.map fun x => (x.2 * x.1)).sum,
      (N.map fun x => (x.2 * x.2 * x.1)).sum)

lemma quantaBarFiveMatter_NQ_mem (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    ğ“œ.quantaBarFiveMatter.map (fun x => (x.N, x.q)) âˆˆ
    (hyperchargeFluxLists (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N)).map
    (fun l => (l.zip (fiveChargeMultisetToList I ğ“œ.Q5) :
      Multiset (â„¤ Ã— â„¤))) := by
  rw [â† ğ“œ.hyperchargeFlux_lists_eq_hyperchargeFluxLists he h3 h3L]
  refine mem_list_of_prod_fst_snd (Multiset.map QuantaBarFive.N ğ“œ.quantaBarFiveMatter)
    ğ“œ.Q5 (by simp)
      (fiveChargeMultisetToList I (Multiset.map QuantaBarFive.q ğ“œ.quantaBarFiveMatter)) (?_)
      (Multiset.map (fun x => (x.N, x.q)) ğ“œ.quantaBarFiveMatter) (by simp) (by simp)
  symm
  refine coe_fiveChargeMultisetToList_of_all_mem I
    (Multiset.map QuantaBarFive.q ğ“œ.quantaBarFiveMatter) ?_
  intro s hs
  apply ğ“œ.Q5_subset_allowedBarFiveCharges
  exact Multiset.mem_toFinset.mpr hs

lemma fiveAnomalyCoefficient_mem_fiveAnomalyFreeSet
    (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    ğ“œ.fiveAnomalyCoefficient âˆˆ fiveAnomalyFreeSet I (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N)
      ğ“œ.Q5 := by
  rw [fiveAnomalyFreeSet]
  rw [Multiset.mem_map]
  use ğ“œ.quantaBarFiveMatter.map (fun x => (x.N, x.q))
  constructor
  Â· exact ğ“œ.quantaBarFiveMatter_NQ_mem he h3 h3L
  Â· rw [fiveAnomalyCoefficient]
    congr 1
    Â· simp
    Â· simp

/-- Given a multiset `N` corresponding to hypercharge fluxes and a multiset `Q`
  corresponding to `U(1)` charges both for 10d matter, the multiset of possible
  anomaly cancellation coefficents formed by possible pairings of elements of `N`
  with elements of `Q`. -/
def tenAnomalyFreeSet (I : CodimensionOneConfig) (N Q : Multiset â„¤) :
    Multiset (â„¤ Ã— â„¤) :=
  ((hyperchargeFluxListsTen N).map
      (fun l => (l.zip (tenChargeMultisetToList I Q) : Multiset (â„¤ Ã— â„¤)))).map
    fun N => ((N.map fun x => (x.2 * x.1)).sum,
      3 * (N.map fun x => (x.2 * x.2 * x.1)).sum)

lemma quantaTen_NQ_mem (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.quantaTen.map (fun x => (x.N, x.q)) âˆˆ
    (hyperchargeFluxListsTen (ğ“œ.quantaTen.map QuantaTen.N)).map
    (fun l => (l.zip (tenChargeMultisetToList I ğ“œ.Q10) :
      Multiset (â„¤ Ã— â„¤))) := by
  rw [â† ğ“œ.hyperchargeFlux_lists_eq_hyperchargeFluxListsTen he h3]
  refine mem_list_of_prod_fst_snd (Multiset.map QuantaTen.N ğ“œ.quantaTen)
    (ğ“œ.Q10) (by simp)
      (tenChargeMultisetToList I (Multiset.map QuantaTen.q ğ“œ.quantaTen)) (?_)
      (Multiset.map (fun x => (x.N, x.q)) ğ“œ.quantaTen) (by simp) (by simp)
  symm
  refine
    coe_tenChargeMultisetToList_of_all_mem I (Multiset.map QuantaTen.q ğ“œ.quantaTen) ?_
  intro s hs
  apply ğ“œ.Q10_subset_allowedTenCharges
  exact Multiset.mem_toFinset.mpr hs

lemma tenAnomalyCoefficient_mem_tenAnomalyFreeSet
    (he : ğ“œ.NoExotics)
    (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.tenAnomalyCoefficient âˆˆ tenAnomalyFreeSet I (ğ“œ.quantaTen.map QuantaTen.N) ğ“œ.Q10 := by
  rw [tenAnomalyFreeSet]
  rw [Multiset.mem_map]
  use ğ“œ.quantaTen.map (fun x => (x.N, x.q))
  constructor
  Â· exact ğ“œ.quantaTen_NQ_mem he h3
  Â· rw [tenAnomalyCoefficient]
    congr 1
    Â· simp
    Â· simp

/-!

## With no-exotics constraints.

-/

/--
Given a multiset `Q` corresponding to `U(1)` charges for five-bar matter, the multiset of possible
anomaly cancellation coefficents formed by possible pairings of elements of `Q`
with valid multisets of hypercharge fluxes given constraints on there been no exotics in the
spectrum.
-/
def fiveAnomalyFreeSetCharge (I : CodimensionOneConfig) (Q : Multiset â„¤) :
    Multiset (â„¤ Ã— â„¤) :=
  if Q.card = 5 then
    (fiveAnomalyFreeSet I {-1, -1, -1, 1, 2} Q âˆª fiveAnomalyFreeSet I {-1, -1, 0, 1, 1} Q âˆª
      fiveAnomalyFreeSet I {-1, -2, 1, 1, 1} Q).dedup
  else if Q.card = 4 then
    (fiveAnomalyFreeSet I {-3, 1, 1, 1} Q âˆª fiveAnomalyFreeSet I {-2, -1, 1, 2} Q âˆª
      fiveAnomalyFreeSet I {-2, 0, 1, 1} Q âˆª fiveAnomalyFreeSet I {-1, -1, -1, 3} Q âˆª
      fiveAnomalyFreeSet I {-1, -1, 0, 2} Q âˆª fiveAnomalyFreeSet I {-1, -1, 1, 1} Q âˆª
      fiveAnomalyFreeSet I {0, 0, -1, 1} Q).dedup
  else if Q.card = 3 then
    (fiveAnomalyFreeSet I {-3, 1, 2} Q âˆª fiveAnomalyFreeSet I {-2, -1, 3} Q âˆª
      fiveAnomalyFreeSet I {-2, 0, 2} Q âˆª fiveAnomalyFreeSet I {-2, 1, 1} Q âˆª
      fiveAnomalyFreeSet I {-1, -1, 2} Q âˆª fiveAnomalyFreeSet I {-1, 0, 1} Q âˆª
      fiveAnomalyFreeSet I {0, 0, 0} Q).dedup
  else if Q.card = 2 then
    (fiveAnomalyFreeSet I {-3, 3} Q âˆª fiveAnomalyFreeSet I {-2, 2} Q âˆª
      fiveAnomalyFreeSet I {-1, 1} Q âˆª fiveAnomalyFreeSet I {0, 0} Q).dedup
  else if Q.card = 1 then
    (fiveAnomalyFreeSet I {0} Q).dedup
  else âˆ…

lemma fiveAnomalyCoefficient_mem_fiveAnomalyFreeSetCharge
    (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles) (h3L : ğ“œ.ThreeLeptonDoublets) :
    ğ“œ.fiveAnomalyCoefficient âˆˆ fiveAnomalyFreeSetCharge I ğ“œ.Q5 := by
  have hN := ğ“œ.quantaBarFiveMatter_N_mem he h3 h3L
  have hN2 := ğ“œ.fiveAnomalyCoefficient_mem_fiveAnomalyFreeSet he h3 h3L
  rw [fiveAnomalyFreeSetCharge]
  have hcard : (Multiset.map QuantaBarFive.q ğ“œ.quantaBarFiveMatter).card =
      (Multiset.map QuantaBarFive.N ğ“œ.quantaBarFiveMatter).card := by
    simp
  rw [hcard]
  generalize (ğ“œ.quantaBarFiveMatter.map QuantaBarFive.N) = N at *
  fin_cases hN
  all_goals simp_all

/--
Given a multiset `Q` corresponding to `U(1)` charges for 10d matter, the multiset of possible
anomaly cancellation coefficents formed by possible pairings of elements of `Q`
with valid multisets of hypercharge fluxes given constraints on there been no exotics in the
spectrum.
-/
def tenAnomalyFreeSetCharge (I : CodimensionOneConfig) (Q : Multiset â„¤) :
    Multiset (â„¤ Ã— â„¤) :=
  if Q.card = 3 then
    (tenAnomalyFreeSet I {0, 0, 0} Q âˆª tenAnomalyFreeSet I {1, -1, 0} Q).dedup
  else if Q.card = 2 then
    (tenAnomalyFreeSet I {0, 0} Q âˆª tenAnomalyFreeSet I {-1, 1} Q).dedup
  else if Q.card = 1 then
    (tenAnomalyFreeSet I {0} Q).dedup
  else âˆ…

lemma tenAnomalyCoefficient_mem_tenAnomalyFreeSetCharge
    (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles) :
    ğ“œ.tenAnomalyCoefficient âˆˆ tenAnomalyFreeSetCharge I ğ“œ.Q10 := by
  have hN := ğ“œ.quantaTen_N_mem he h3
  have hN2 := ğ“œ.tenAnomalyCoefficient_mem_tenAnomalyFreeSet he h3
  rw [tenAnomalyFreeSetCharge]
  have hcard : (Multiset.map QuantaTen.q ğ“œ.quantaTen).card =
      (Multiset.map QuantaTen.N ğ“œ.quantaTen).card := by
    simp
  rw [hcard]
  generalize (ğ“œ.quantaTen.map QuantaTen.N) = N at *
  fin_cases hN
  all_goals simp_all

/-!

## Anomaly free condition

-/

/--
For a charges `qHd` and `qHu` and a multiset of `U(1)` charges `Q10` and `Q5`, the
  condition that the there exists choices of hypercharge fluxes for the 10d and 5d
  matter which obey the no-exotics conditions and such that the anomaly cancellation
  conditions are satisfied.
-/
def AnomalyFreeCharges (I : CodimensionOneConfig) (qHd qHu : â„¤) (Q10 Q5 : Multiset â„¤) :
    Prop :=
  (0, 0) âˆˆ ((tenAnomalyFreeSetCharge I Q10).product (fiveAnomalyFreeSetCharge I Q5)).map
    (fun x => (x.1 + x.2 - (qHu, qHu * qHu) + (qHd, qHd * qHd)))

instance (I : CodimensionOneConfig) (qHd qHu : â„¤) (Q10 Q5 : Multiset â„¤) :
    Decidable (AnomalyFreeCharges I qHd qHu Q10 Q5) :=
  Multiset.decidableMem _ _

lemma anomalyFreeCharges_of_anomalyFree (he : ğ“œ.NoExotics) (h3 : ğ“œ.ThreeChiralFamiles)
    (h3L : ğ“œ.ThreeLeptonDoublets) (hU1 : ğ“œ.GaugeAnomalyU1MSSM)
    (hU1U1 : ğ“œ.GaugeAnomalyU1YU1U1) :
    AnomalyFreeCharges I ğ“œ.qHd ğ“œ.qHu ğ“œ.Q10 ğ“œ.Q5 := by
  rw [AnomalyFreeCharges]
  simp only [Prod.mk_zero_zero, Multiset.mem_map, Multiset.mem_product,
    Prod.mk_eq_zero]
  rw [Prod.exists]
  use ğ“œ.tenAnomalyCoefficient
  use ğ“œ.fiveAnomalyCoefficient
  constructor
  Â· constructor
    Â· simpa using ğ“œ.tenAnomalyCoefficient_mem_tenAnomalyFreeSetCharge he h3
    Â· simpa using ğ“œ.fiveAnomalyCoefficient_mem_fiveAnomalyFreeSetCharge he h3 h3L
  Â· simp
    change _ = (0, 0)
    rw [â† ğ“œ.anomalyCoefficent_sum_of_gaugeAnomalyU1YU1U1_gaugeAnomalyU1YU1U1 hU1 hU1U1]
    ring

end MatterContent

end SU5U1

end FTheory
