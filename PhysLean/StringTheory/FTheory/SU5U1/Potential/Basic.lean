/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Data.Finset.Powerset
import Mathlib.Data.Finset.Prod
import Mathlib.Tactic.FinCases
import Mathlib.Tactic.DeriveFintype
import PhysLean.StringTheory.FTheory.SU5U1.Charges.OfRationalSection
/-!

# Potential of the SU(5) + U(1) GUT for F-Theory

This file contains properties of the potential terms of the `SU(5)` SUSY GUT with an
additional `U(1)` gauge group in F-theory.

The terms from the superpotential considered are (arXiv:0912.0853) :
`W âŠƒ Î¼ 5Hu 5Ì„Hd + ğ›½áµ¢ 5Ì„Mâ±5Hu + ğœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ + WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡`
`+ WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd + WÂ³áµ¢â±¼ 5Ì„Mâ± 5Ì„MÊ² 5Hu 5Hu + Wâ´áµ¢ 5Ì„Mâ± 5Ì„Hd 5Hu 5Hu`

The terms of the Kahler potential are (arXiv:0912.0853) :
`K âŠƒ KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ + KÂ²áµ¢ 5Ì„Hu 5Ì„Hd 10â±`

## Important results

- `PotentialTerm` : The inductive type indexing the potential terms.
- `violateRParity` : The finite set of terms which violate R-parity.
  `Î²`, `Î»`, `WÂ²`, `Wâ´`, `KÂ¹`, `KÂ²`
- `causeProtonDecay` : The finite set of terms which contribute to proton decay.
  `WÂ¹`, `WÂ²`, `KÂ¹`, `Î»`
- `AllowsTerm`: The condition on the potential terms for them to be present
  based on the `U(1)` charges.

## Previous versions

A previous version of this code was replaced in PR #569.

-/

namespace FTheory

namespace SU5U1

variable {I : CodimensionOneConfig}

/-- Relevant terms part of the superpotential and Kahler potential of the `SU(5)` SUSY GUT. -/
inductive PotentialTerm
  /-- The term `Î¼ 5Hu 5Ì„Hd`. -/
  | Î¼ : PotentialTerm
  /-- The term `ğ›½áµ¢ 5Ì„Mâ±5Hu`. -/
  | Î² : PotentialTerm
  /-- The term `ğœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ`. -/
  | Î› : PotentialTerm
  /-- The term `WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡` -/
  | W1 : PotentialTerm
  /-- The term `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`. -/
  | W2 : PotentialTerm
  /-- The term `WÂ³áµ¢â±¼ 5Ì„Mâ± 5Ì„MÊ² 5Hu 5Hu`. -/
  | W3 : PotentialTerm
  /-- The term `Wâ´áµ¢ 5Ì„Mâ± 5Ì„Hd 5Hu 5Hu`. -/
  | W4 : PotentialTerm
  /-- The term `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ`. -/
  | K1 : PotentialTerm
  /-- The term `KÂ²áµ¢ 5Ì„Hu 5Ì„Hd 10â±` -/
  | K2 : PotentialTerm
  /-- The term `Î»áµ—áµ¢â±¼ 10â± 10Ê² 5Hu`. -/
  | topYukawa : PotentialTerm
  /-- The term `Î»áµ‡áµ¢â±¼ 10â± 5Ì„MÊ² 5Ì„Hd`. -/
  | bottomYukawa : PotentialTerm
deriving DecidableEq, Fintype

/-- The types of field present in SU(5) F-Theory. -/
inductive FieldLabel
  | fiveBarHu
  | fiveHu
  | fiveBarHd
  | fiveHd
  | fiveBarMatter
  | fiveMatter
  | tenMatter
deriving DecidableEq, Fintype

/-- The R-Parity of a field, landding on `1` if it is in the non-trivial representation
  and `0` otherwise. -/
def FieldLabel.RParity : FieldLabel â†’ Fin 2
  | fiveBarHu => 0
  | fiveHu => 0
  | fiveBarHd => 0
  | fiveHd => 0
  | fiveBarMatter => 1
  | fiveMatter => 1
  | tenMatter => 1

namespace PotentialTerm

/-- The fields contained within a given term of the potential. -/
def toFieldLabel : PotentialTerm â†’ List FieldLabel
  | Î¼ => [.fiveBarHd, .fiveHu]
  | Î² => [.fiveHu, .fiveBarMatter]
  | Î› => [.fiveBarMatter, .fiveBarMatter, .tenMatter]
  | W1 => [.tenMatter, .tenMatter, .tenMatter, .fiveBarMatter]
  | W2 => [.tenMatter, .tenMatter, .tenMatter, .fiveBarHd]
  | W3 => [.fiveBarMatter, .fiveBarMatter, .fiveHu, .fiveHu]
  | W4 => [.fiveBarMatter, .fiveBarHd, .fiveHu, .fiveHu]
  | K1 => [.tenMatter, .tenMatter, .fiveMatter]
  | K2 => [.fiveBarHu, .fiveBarHd, .tenMatter]
  | topYukawa => [.tenMatter, .tenMatter, .fiveHu]
  | bottomYukawa => [.tenMatter, .fiveBarMatter, .fiveBarHd]

/-- The degree of a term in the potential. -/
def degree (T : PotentialTerm) : â„• := T.toFieldLabel.length

lemma degree_le_four (T : PotentialTerm) : T.degree â‰¤ 4 := by
  cases T
  all_goals simp [toFieldLabel, degree]

/-- The R-parity of a term in the potential. -/
def RParity (T : PotentialTerm) : Fin 2 :=
  (T.toFieldLabel.map FieldLabel.RParity).foldl (Â· + Â·) 0

/- The terms which violate R-parity are those with an odd-number of matter fields. -/
lemma violates_RParity_iff_mem {T : PotentialTerm} :
    T.RParity = 1 â†” T âˆˆ ({Î², Î›, W2, W4, K1, K2} : Finset PotentialTerm) := by
  revert T
  decide

/-- The finite set of terms in the superpotential and Kahler potential which are involved in
  proton decay.
- `WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡`
- `ğœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ`
- `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`
- `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ`
-/
def causeProtonDecay : Finset PotentialTerm :=
  {W1, Î›, W2, K1}

end PotentialTerm

end SU5U1

end FTheory
