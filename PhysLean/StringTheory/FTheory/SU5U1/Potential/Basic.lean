/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.StringTheory.FTheory.SU5U1.Matter
import Mathlib.Algebra.Order.BigOperators.Group.Multiset
import PhysLean.Relativity.SpaceTime.Basic
import PhysLean.Meta.Informal.SemiFormal
/-!

# Potential of the SU(5) + U(1) GUT for F-Theory

This file contains properties of the potential terms of the `SU(5)` SUSY GUT with an
additional `U(1)` gauge group in F-theory.

The terms from the superpotential considered are (arXiv:0912.0853) :
`W âŠƒ Î¼ 5Hu 5Ì„Hd + ð›½áµ¢ 5Ì„Mâ±5Hu + ðœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ + WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡`
`+ WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd + WÂ³áµ¢â±¼ 5Ì„Mâ± 5Ì„MÊ² 5Hu 5Hu + Wâ´áµ¢ 5Ì„Mâ± 5Ì„Hd 5Hu 5Hu`

The terms of the Kahler potential are (arXiv:0912.0853) :
`K âŠƒ KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ + KÂ²áµ¢ 5Ì„Hu 5Ì„Hd 10â±`

## Important results

- `PotentialTerm` : The inductive type indexing the potential terms.
- `violateRParity` : The finite set of terms which violate R-parity.
  `Î²`, `Î»`, `WÂ²`, `Wâ´`, `KÂ¹`, `KÂ²`
- `causeProtonDecay` : The finite set of terms which contribute to proton decay.
  `WÂ¹`, `WÂ²`, `KÂ¹`, `Î»`
- `IsPresent`: The condition on the potential terms for them to be present
  based on the `U(1)` charges.
-/

namespace FTheory

namespace SU5U1

variable {I : CodimensionOneConfig}

/-- Relevant terms part of the superpotential and Kahler potential of the `SU(5)` SUSY GUT. -/
inductive PotentialTerm
  /-- The term `Î¼ 5Hu 5Ì„Hd`. -/
  | Î¼ : PotentialTerm
  /-- The term `ð›½áµ¢ 5Ì„Mâ±5Hu`. -/
  | Î² : PotentialTerm
  /-- The term `ðœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ`. -/
  | Î› : PotentialTerm
  /-- The term `WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡` -/
  | W1 : PotentialTerm
  /-- The term `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`. -/
  | W2 : PotentialTerm
  /-- The term `WÂ³áµ¢â±¼ 5Ì„Mâ± 5Ì„MÊ² 5Hu 5Hu`. -/
  | W3 : PotentialTerm
  /-- The term `Wâ´áµ¢ 5Ì„Mâ± 5Ì„Hd 5Hu 5Hu`. -/
  | W4 : PotentialTerm
  /-- The term `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ`. -/
  | K1 : PotentialTerm
  /-- The term `KÂ²áµ¢ 5Ì„Hu 5Ì„Hd 10â±` -/
  | K2 : PotentialTerm
  /-- The term `Î»áµ—áµ¢â±¼ 10â± 10Ê² 5Hu`. -/
  | topYukawa : PotentialTerm
  /-- The term `Î»áµ‡áµ¢â±¼ 10â± 5Ì„MÊ² 5Ì„Hd`. -/
  | bottomYukawa : PotentialTerm
deriving DecidableEq

namespace PotentialTerm

/-- The finite set of terms in the superpotential and Kahler potential which violate R-parity.
- `ð›½áµ¢ 5Ì„Mâ±5Hu`
- `ðœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ`
- `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`
- `Wâ´áµ¢ 5Ì„Mâ± 5Ì„Hd 5Hu 5Hu`
- `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ`
- `KÂ²áµ¢ 5Ì„Hu 5Ì„Hd 10â±`
These correspond to the terms with an odd number of matter fields.
-/
def violateRParity : Finset PotentialTerm :=
  {Î², Î›, W2, W4, K1, K2}

/-- The finite set of terms in the superpotential and Kahler potential which are involved in
  proton decay.
- `WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡`
- `ðœ†áµ¢â±¼â‚– 5Ì„Mâ± 5Ì„MÊ² 10áµ`
- `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`
- `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ`
-/
def causeProtonDecay : Finset PotentialTerm :=
  {W1, Î›, W2, K1}

/-- The type of charges associated with the potential terms.
  The implicit order of the charges is: `qHd`, `qHu`, `Q5`, `Q10`.
  The types correspond to:
- `Î¼` : `qHd Ã— qHu`
- `Î²` : `qHu Ã— Q5`
- `Î›` : `Q5 Ã— Q10`
- `W1` : `Q5 Ã— Q10`
- `W2` : `qHd Ã— Q10`
- `W3` : `qHu Ã— Q5`
- `W4` : `qHd Ã— qHu Ã— Q5`
- `K1` : `Q5 Ã— Q10`
- `K2` : `qHd Ã— qHu Ã— Q10`
- `topYukawa` : `qHu Ã— Q10`
- `bottomYukawa` : `qHd Ã— Q5 Ã— Q10`
-/
def ChargeType : PotentialTerm â†’ Type
    /- qHd Ã— qHu -/
  | Î¼ => â„¤ Ã— â„¤
  /- qHu Ã— Q5 -/
  | Î² => â„¤ Ã— Multiset â„¤
  /- Q5 Ã— Q10 -/
  | Î› => Multiset â„¤ Ã— Multiset â„¤
  /- Q5 Ã— Q10 -/
  | W1 => Multiset â„¤ Ã— Multiset â„¤
  /- qHd Ã— Q10 -/
  | W2 => â„¤ Ã— Multiset â„¤
  /- qHu Ã— Q5 -/
  | W3 => â„¤ Ã— Multiset â„¤
  /- qHd Ã— qHu Ã— Q5 -/
  | W4 => â„¤ Ã— â„¤ Ã— Multiset â„¤
  /- Q5 Ã— Q10 -/
  | K1 => Multiset â„¤ Ã— Multiset â„¤
  /- qHd Ã— qHu Ã— Q10 -/
  | K2 => â„¤ Ã— â„¤ Ã— Multiset â„¤
  /- qHu Ã— Q10 -/
  | topYukawa => â„¤ Ã— Multiset â„¤
  /- qHd Ã— Q5 Ã— Q10 -/
  | bottomYukawa => â„¤ Ã— Multiset â„¤ Ã— Multiset â„¤

/-- The U(1) charges of each potential term given an element of the corresponding `ChargeType`.
  For example, for the term `ð›½áµ¢ 5Ì„Mâ±5Hu` and `Q5 = {0, 2}` and `qHu = 3` then
  the charges of this term would be `{-3, -1}`. -/
def charges : (T : PotentialTerm) â†’ T.ChargeType â†’ Multiset â„¤
  | Î¼ => fun (qHd, qHu) => {- qHu + qHd}
  | Î² => fun (qHu, Q5) => Q5.map (fun x => x + (- qHu))
  | Î› => fun (Q5, Q10) => (Q5.product (Q5.product Q10)).map
    (fun x => x.1 + x.2.1 + x.2.2)
  | W1 => fun (Q5, Q10) => (Q10.product (Q10.product (Q10.product Q5))).map
    (fun x => x.1 + x.2.1 + x.2.2.1 + x.2.2.2)
  | W2 => fun (qHd, Q10) => (Q10.product (Q10.product Q10)).map
    (fun x => x.1 + x.2.1 + x.2.2 + qHd)
  | W3 => fun (qHu, Q5) => (Q5.product Q5).map
    (fun x => x.1 + x.2 - qHu - qHu)
  | W4 => fun (qHd, qHu, Q5) => Q5.map (fun x => x + qHd + (- qHu) + (- qHu))
  | K1 => fun (Q5, Q10) => (Q10.product (Q10.product Q5)).map
    (fun x => x.1 + x.2.1 + (- x.2.2))
  | K2 => fun (qHd, qHu, Q10) => Q10.map (fun x => qHu + qHd + x)
  | topYukawa => fun (qHu, Q10) => ((Q10.product Q10)).map (fun x => x.1 + x.2 + (- qHu))
  | bottomYukawa => fun (qHd, Q5, Q10) => (Q10.product Q5).map (fun x => x.1 + x.2 + qHd)

/-!

## Excluded terms based on `U(1)` charges

The terms in the potential can be excluded based on the presences of `U(1)` charges
they carry.

-/

/-- Given a potential term `T` with an element of it's associated `ChargeType`, the proposition
  corresonding to there been no `zero` in the charges of that term. That is to say, it is excluded
  by the `U(1)` charges.

  For example, for the term `ð›½áµ¢ 5Ì„Mâ±5Hu` and `Q5 = {0, 2}` and `qHu = 3`
  the charges of this term are `{-3, -1}`, and this is excluded by the `U(1)` charges.
  Thus `IsExcluded Î² (3, {0, 2})` would be `true`.
  -/
def IsExcluded : (T : PotentialTerm) â†’ T.ChargeType â†’ Prop
  | Î¼ => fun (qHd, qHu) => 0 âˆ‰ charges Î¼ (qHd, qHu)
  | Î² => fun (qHu, Q5) => 0 âˆ‰ charges Î² (qHu, Q5)
  | Î› => fun (Q5, Q10) => 0 âˆ‰ charges Î› (Q5, Q10)
  | W1 => fun (Q5, Q10) => 0 âˆ‰ charges W1 (Q5, Q10)
  | W2 => fun (qHd, Q10) => 0 âˆ‰ charges W2 (qHd, Q10)
  | W3 => fun (qHu, Q5) => 0 âˆ‰ charges W3 (qHu, Q5)
  | W4 => fun (qHd, qHu, Q5) => 0 âˆ‰ charges W4 (qHd, qHu, Q5)
  | K1 => fun (Q5, Q10) => 0 âˆ‰ charges K1 (Q5, Q10)
  | K2 => fun (qHd, qHu, Q10) => 0 âˆ‰ charges K2 (qHd, qHu, Q10)
  | topYukawa => fun (qHu, Q10) => 0 âˆ‰ charges topYukawa (qHu, Q10)
  | bottomYukawa => fun (qHd, Q5, Q10) => 0 âˆ‰ charges bottomYukawa (qHd, Q5, Q10)

/-!

### Equivalent conditions to `IsExcluded`

For different potential terms, the `IsExcluded` condition can be expressed in different ways,
which are faster to check with `decide`.

-/

lemma isExcluded_Î›_iff_Q5_intersect_Q5 (Q5Q10 : Multiset â„¤ Ã— Multiset â„¤) :
    IsExcluded Î› Q5Q10 â†”
    ((Q5Q10.1.product Q5Q10.1).map (fun x => - (x.1 + x.2)) âˆ© Q5Q10.2 = âˆ…) := by
  obtain âŸ¨Q5, Q10âŸ© := Q5Q10
  constructor
  Â· intro h
    simp only [IsExcluded, charges, Multiset.mem_map, Multiset.mem_product, Finset.mem_val,
      Prod.exists, not_exists, not_and, and_imp] at h
    simp only [Finset.product_eq_sprod, Finset.product_val, neg_add_rev, Multiset.empty_eq_zero]
    rw [Multiset.eq_zero_iff_forall_not_mem]
    simp only [Multiset.mem_inter, Multiset.mem_map, Prod.exists, Finset.mem_val, not_and,
      forall_exists_index, and_imp]
    intro q10 q51 q52 hq5 hsum hq10
    simp only [SProd.sprod, Multiset.mem_product, Finset.mem_val] at hq5
    have h1 := h q51 q52 q10 hq5.1 hq5.2 hq10
    omega
  Â· intro h
    simp only [IsExcluded, charges, Multiset.mem_map, Multiset.mem_product, Finset.mem_val,
      Prod.exists, not_exists, not_and, and_imp]
    intro q51 q52 q10 hq51 hq52 hq10 hsum
    simp only [Finset.product_eq_sprod, Finset.product_val, neg_add_rev, Multiset.empty_eq_zero,
      Multiset.eq_zero_iff_forall_not_mem, Multiset.mem_inter, Multiset.mem_map, Prod.exists,
      Finset.mem_val, not_and, forall_exists_index, and_imp] at h
    have h1 := (h q10 q51 q52 (by simpa [SProd.sprod] using âŸ¨hq51, hq52âŸ©)).mt (by simpa using hq10)
    omega

/-- A rewriting of the condition for `0` is not in the charges associated with the term
  `KÂ¹áµ¢â±¼â‚– 10â± 10Ê² 5Máµ` in terms of the intersection of finite sets. -/
lemma isExcluded_K1_iff_Q10_intersect_Q10 (Q5Q10 : Multiset â„¤ Ã— Multiset â„¤) :
    IsExcluded K1 Q5Q10 â†”
    ((Q5Q10.2.product Q5Q10.2).map (fun x => x.1 + x.2) âˆ© Q5Q10.1 = âˆ…) := by
  constructor
  Â· intro h
    simp only [IsExcluded, charges, Multiset.mem_map, Multiset.mem_product, Prod.exists, not_exists,
      not_and, and_imp] at h
    simp only [Finset.product_eq_sprod, Finset.product_val, neg_add_rev, Multiset.empty_eq_zero]
    rw [Multiset.eq_zero_iff_forall_not_mem]
    simp only [Multiset.mem_inter, Multiset.mem_map, Prod.exists, Finset.mem_val, not_and,
      forall_exists_index, and_imp]
    intro q5 q101 q102 hq10 hsum hq5
    simp only [SProd.sprod, Multiset.mem_product, Finset.mem_val] at hq10
    have h1 := h q101 q102 q5 hq10.1 hq10.2 hq5
    omega
  Â· intro h
    simp only [IsExcluded, charges, Multiset.mem_map, Multiset.mem_product, Finset.mem_val,
      Prod.exists, not_exists, not_and, and_imp]
    intro q51 q52 q10 hq51 hq52 hq10 hsum
    simp only [Finset.product_eq_sprod, Finset.product_val, neg_add_rev, Multiset.empty_eq_zero,
      Multiset.eq_zero_iff_forall_not_mem, Multiset.mem_inter, Multiset.mem_map, Prod.exists,
      Finset.mem_val, not_and, forall_exists_index, and_imp] at h
    have h1 := (h q10 q51 q52 (by simpa [SProd.sprod] using âŸ¨hq51, hq52âŸ©)).mt (by simpa using hq10)
    omega
/-!

### Decidability of `IsExcluded`

-/

/-- The decidability of `IsExcluded` for the potential terms. -/
instance : (T : PotentialTerm) â†’ DecidablePred T.IsExcluded
  | Î¼ => fun _ => instDecidableNot
  | Î² => fun _ => instDecidableNot
  | Î› => fun Q5Q10 => decidable_of_decidable_of_iff (isExcluded_Î›_iff_Q5_intersect_Q5 Q5Q10).symm
  | W1 => fun _ => instDecidableNot
  | W2 => fun _ => instDecidableNot
  | W3 => fun _ => instDecidableNot
  | W4 => fun _ => instDecidableNot
  | K1 => fun Q5Q10 =>
    decidable_of_decidable_of_iff (isExcluded_K1_iff_Q10_intersect_Q10 Q5Q10).symm
  | K2 => fun _ => instDecidableNot
  | topYukawa => fun _ => instDecidableNot
  | bottomYukawa => fun _ => instDecidableNot

/-!

## Present terms based on `U(1)` charges

The terms in the potential can be present based on the presences of `U(1)` charges
they carry.

Of particular intrest is the presence of the top Yukawa term.

-/

/-- Given a potential term `T` with an element of it's associated `ChargeType`, the proposition
  corresonding to there been `zero` in the charges of that term. That is to say, it is present
  by the `U(1)` charges.

  For example, for the term `ð›½áµ¢ 5Ì„Mâ±5Hu` and `Q5 = {0, 2}` and `qHu = 2`
  the charges of this term are `{-2, 0}`, and this is excluded by the `U(1)` charges.
  Thus `IsPresent Î² (3, {0, 2})` would be `true`.
  -/
def IsPresent : (T : PotentialTerm) â†’ T.ChargeType â†’ Prop
  | Î¼ => fun (qHd, qHu) => 0 âˆˆ charges Î¼ (qHd, qHu)
  | Î² => fun (qHu, Q5) => 0 âˆˆ charges Î² (qHu, Q5)
  | Î› => fun (Q5, Q10) => 0 âˆˆ charges Î› (Q5, Q10)
  | W1 => fun (Q5, Q10) => 0 âˆˆ charges W1 (Q5, Q10)
  | W2 => fun (qHd, Q10) => 0 âˆˆ charges W2 (qHd, Q10)
  | W3 => fun (qHu, Q5) => 0 âˆˆ charges W3 (qHu, Q5)
  | W4 => fun (qHd, qHu, Q5) => 0 âˆˆ charges W4 (qHd, qHu, Q5)
  | K1 => fun (Q5, Q10) => 0 âˆˆ charges K1 (Q5, Q10)
  | K2 => fun (qHd, qHu, Q10) => 0 âˆˆ charges K2 (qHd, qHu, Q10)
  | topYukawa => fun (qHu, Q10) => 0 âˆˆ charges topYukawa (qHu, Q10)
  | bottomYukawa => fun (qHd, Q5, Q10) => 0 âˆˆ charges bottomYukawa (qHd, Q5, Q10)

lemma not_isExcluded_iff_isPresent : (T : PotentialTerm) â†’ (x : T.ChargeType) â†’
    Â¬ T.IsExcluded x â†” T.IsPresent x
  | Î¼ => fun (qHd, qHu) => by simp [IsExcluded, IsPresent]
  | Î² => fun (qHu, Q5) => by simp [IsExcluded, IsPresent]
  | Î› => fun (Q5, Q10) => by simp [IsExcluded, IsPresent]
  | W1 => fun (Q5, Q10) => by simp [IsExcluded, IsPresent]
  | W2 => fun (qHd, Q10) => by simp [IsExcluded, IsPresent]
  | W3 => fun (qHu, Q5) => by simp [IsExcluded, IsPresent]
  | W4 => fun (qHd, qHu, Q5) => by simp [IsExcluded, IsPresent]
  | K1 => fun (Q5, Q10) => by simp [IsExcluded, IsPresent]
  | K2 => fun (qHd, qHu, Q10) => by simp [IsExcluded, IsPresent]
  | topYukawa => fun (qHu, Q10) => by simp [IsExcluded, IsPresent]
  | bottomYukawa => fun (qHd, Q5, Q10) => by simp [IsExcluded, IsPresent]

lemma isPresent_topYukawa_iff_Q10_product_Q10 (qHuQ10 : â„¤ Ã— Multiset â„¤) :
    IsPresent topYukawa qHuQ10 â†”
      qHuQ10.1 âˆˆ (qHuQ10.2.product qHuQ10.2).map (fun x => x.1 + x.2) := by
  obtain âŸ¨qHu, Q10âŸ© := qHuQ10
  constructor
  Â· simp [IsPresent, charges]
    intro q1 q2 h1 h2 hsum
    use q1, q2
    simp_all
    omega
  Â· simp [IsPresent, charges]
    intro q1 q2 h1 h2 hsum
    use q1, q2
    simp_all

/-- The decidability of `IsPresent` for the potential terms. -/
instance : (T : PotentialTerm) â†’ DecidablePred T.IsPresent
  | Î¼ => fun _ => Multiset.decidableMem _ _
  | Î² => fun _ => Multiset.decidableMem _ _
  | Î› => fun _ => Multiset.decidableMem _ _
  | W1 => fun _ => Multiset.decidableMem _ _
  | W2 => fun _ => Multiset.decidableMem _ _
  | W3 => fun _ => Multiset.decidableMem _ _
  | W4 => fun _ => Multiset.decidableMem _ _
  | K1 => fun _ => Multiset.decidableMem _ _
  | K2 => fun _ => Multiset.decidableMem _ _
  | topYukawa => fun qHuQ10 =>
    decidable_of_decidable_of_iff (isPresent_topYukawa_iff_Q10_product_Q10 qHuQ10).symm
  | bottomYukawa => fun _ => Multiset.decidableMem _ _

end PotentialTerm

end SU5U1

end FTheory
