/-
Copyright (c) 2025 Zhi Kai Pong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith
-/
import PhysLean.Mathematics.FDerivCurry
import PhysLean.SpaceAndTime.Time.Basic
import Mathlib.Analysis.Calculus.Deriv.Prod
import Mathlib.LinearAlgebra.CrossProduct
import Mathlib.Analysis.Calculus.FDeriv.Pi
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import PhysLean.SpaceAndTime.Space.Basic
/-!
# Classical vector calculus properties

Vector calculus properties under classical space and time derivatives.

-/
namespace ClassicalMechanics

open Space
open Time

lemma dt_distrib (f : Time ‚Üí Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) :
    (fderiv ‚Ñù (fun t => (fderiv ‚Ñù (fun x => f t x u) x) dx1) t -
    fderiv ‚Ñù (fun t => (fderiv ‚Ñù (fun x => f t x v) x) dx2) t) 1
    =
    (fderiv ‚Ñù (fun t => (fderiv ‚Ñù (fun x => f t x u) x) dx1) t) 1 -
    (fderiv ‚Ñù (fun t => (fderiv ‚Ñù (fun x => f t x v) x) dx2) t) 1 := by
  rfl

lemma fderiv_coord_dt (f : Time ‚Üí Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (t dt : Time)
    (hf : Differentiable ‚Ñù (‚Üøf)) :
    (fun x => (fderiv ‚Ñù (fun t => f t x i) t) dt)
    =
    (fun x => (fderiv ‚Ñù (fun t => f t x) t) dt i) := by
  ext x
  rw [fderiv_pi]
  rfl
  ¬∑ fun_prop

/-- Derivatives along space coordinates and time commute. -/
lemma fderiv_swap_time_space_coord
    (f : Time ‚Üí Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (t dt : Time) (x dx : Space)
    (hf : ContDiff ‚Ñù 2 ‚Üøf) :
    fderiv ‚Ñù (fun t' => fderiv ‚Ñù (fun x' => f t' x' i) x dx) t dt
    =
    fderiv ‚Ñù (fun x' => fderiv ‚Ñù (fun t' => f t' x' i) t dt) x dx := by
  have h' := fderiv_swap (ùïú := ‚Ñù) f t dt x dx
  change (fderiv ‚Ñù
      (fun t' => (fderiv ‚Ñù ((EuclideanSpace.proj i) ‚àò
      (fun x' => f t' x')) x) dx) t) dt = _
  trans (fderiv ‚Ñù
      (fun t' => ((fderiv ‚Ñù (‚áë(EuclideanSpace.proj i)) (f t' x)).comp
      (fderiv ‚Ñù (fun x' => f t' x') x)) dx) t) dt
  ¬∑ congr
    funext t'
    rw [fderiv_comp]
    ¬∑ fun_prop
    ¬∑ apply function_differentiableAt_snd
      exact hf.two_differentiable
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp',
    Function.comp_apply]
  rw [fderiv_comp']
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp',
    Function.comp_apply]
  rw [h']
  change _ =
      (fderiv ‚Ñù (fun x' => (fderiv ‚Ñù ((EuclideanSpace.proj i) ‚àò
      (fun t' => f t' x')) t) dt) x) dx
  trans (fderiv ‚Ñù
      (fun x' => ((fderiv ‚Ñù (‚áë(EuclideanSpace.proj i)) (f t x')).comp
      (fderiv ‚Ñù (fun t' => f t' x') t)) dt) x) dx
  swap
  ¬∑ congr
    funext x'
    rw [fderiv_comp]
    ¬∑ fun_prop
    ¬∑ apply function_differentiableAt_fst
      exact hf.two_differentiable
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp',
    Function.comp_apply]
  rw [fderiv_comp']
  simp only [PiLp.proj_apply, ContinuousLinearMap.fderiv,
    ContinuousLinearMap.coe_comp', Function.comp_apply]
  /- Start of differentiablity conditions. -/
  ¬∑ fun_prop
  ¬∑ apply fderiv_curry_differentiableAt_fst_comp_snd
    exact hf
  ¬∑ fun_prop
  ¬∑ fun_prop
  ¬∑ apply fderiv_curry_differentiableAt_snd_comp_fst
    exact hf

lemma differentiableAt_fderiv_coord_single
    (ft : Time ‚Üí Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 ‚Üøft) :
    DifferentiableAt ‚Ñù (fun t =>
    (fderiv ‚Ñù (fun x => ft t x u) x) (EuclideanSpace.single v 1)) t := by
  apply Differentiable.clm_apply
  ¬∑ let ftt : Time ‚Üí Space ‚Üí ‚Ñù := fun t x => ft t x u
    have hd : ContDiff ‚Ñù 2 (‚Üøftt) := by
      change ContDiff ‚Ñù 2 (fun x => (‚Üøft) x u)
      change ContDiff ‚Ñù 2 ((EuclideanSpace.proj u) ‚àò (‚Üøft))
      apply ContDiff.comp
      ¬∑ exact ContinuousLinearMap.contDiff (EuclideanSpace.proj u) (ùïú := ‚Ñù)
      ¬∑ exact hf
    have hdd : Differentiable ‚Ñù (‚Üøftt) := hd.two_differentiable
    have h1 (t : Time) : fderiv ‚Ñù (fun x => ftt t x) x
      = fderiv ‚Ñù (‚Üøftt) (t, x) ‚àòL (ContinuousLinearMap.inr ‚Ñù Time Space) := by
      ext w
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, ContinuousLinearMap.inr_apply]
      rw [fderiv_uncurry]
      simp only [map_zero, zero_add]
      fun_prop
    conv =>
      enter [2, y]
      change fderiv ‚Ñù (fun x => ftt y x) x
      rw [h1]
    fun_prop
  ¬∑ fun_prop

/-- Curl and time derivative commute. -/
lemma time_deriv_curl_commute (f‚Çú : Time ‚Üí Space ‚Üí EuclideanSpace ‚Ñù (Fin 3))
    (t : Time) (x : Space) (hf : ContDiff ‚Ñù 2 ‚Üøf‚Çú) :
    ‚àÇ‚Çú (fun t => (‚àá √ó f‚Çú t) x) t = (‚àá √ó fun x => (‚àÇ‚Çú (fun t => f‚Çú t x) t)) x:= by
  funext i
  unfold Time.deriv
  rw [fderiv_pi]
  ¬∑ change (fderiv ‚Ñù (fun t => curl (f‚Çú t) x i) t) 1 = _
    unfold curl Space.deriv Space.coord Space.basis
    fin_cases i <;>
    ¬∑ simp only [Fin.zero_eta, Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
      EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
      Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte]
      rw [fderiv_fun_sub]
      rw [dt_distrib]
      rw [fderiv_swap_time_space_coord, fderiv_swap_time_space_coord]
      rw [fderiv_coord_dt, fderiv_coord_dt]
      repeat exact hf.two_differentiable
      repeat fun_prop
      repeat
        apply differentiableAt_fderiv_coord_single
        exact hf
  ¬∑ intro i
    unfold curl Space.deriv Space.coord Space.basis
    fin_cases i <;>
    ¬∑ simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
        EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
        Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte]
      apply DifferentiableAt.sub
      repeat
        apply differentiableAt_fderiv_coord_single
        exact hf

open Matrix

set_option quotPrecheck false in
/-- Cross product in `EuclideanSpace ‚Ñù (Fin 3)`. Uses `‚®Ø` which is typed using `\X` or
`\vectorproduct` or `\crossproduct`. -/
infixl:70 " ‚®Ø‚Çë‚ÇÉ " => fun a b => (WithLp.equiv 2 (Fin 3 ‚Üí ‚Ñù)).symm
    (WithLp.equiv 2 (Fin 3 ‚Üí ‚Ñù) a ‚®Ø‚ÇÉ WithLp.equiv 2 (Fin 3 ‚Üí ‚Ñù) b)

/-- Cross product and fderiv commute. -/
lemma fderiv_cross_commute {u : Time} {s : Space} {f : Time ‚Üí EuclideanSpace ‚Ñù (Fin 3)}
    (hf : Differentiable ‚Ñù f) :
    s ‚®Ø‚Çë‚ÇÉ (fderiv ‚Ñù (fun u => f u) u) 1
    =
    fderiv ‚Ñù (fun u => s ‚®Ø‚Çë‚ÇÉ (f u)) u 1 := by
  have h (i j : Fin 3) : s i * (fderiv ‚Ñù (fun u => f u) u) 1 j -
      s j * (fderiv ‚Ñù (fun u => f u) u) 1 i
      =
      (fderiv ‚Ñù (fun t => s i * f t j - s j * f t i) u) 1:= by
    rw [fderiv_fun_sub, fderiv_const_mul, fderiv_const_mul]
    rw [fderiv_pi]
    rfl
    intro i
    repeat fun_prop
  rw [crossProduct]
  ext i
  fin_cases i <;>
  ¬∑ simp [Nat.succ_eq_add_one, Nat.reduceAdd, Fin.isValue, WithLp.equiv_apply,
      LinearMap.mk‚ÇÇ_apply, PiLp.ofLp_apply, Fin.reduceFinMk, WithLp.equiv_symm_apply,
      PiLp.toLp_apply, cons_val]
    rw [h]
    simp only [Fin.isValue]
    rw [fderiv_pi]
    simp only [Fin.isValue, PiLp.toLp_apply]
    rfl
    ¬∑ intro i
      fin_cases i <;>
      ¬∑ simp
        fun_prop

/-- Cross product and time derivative commute. -/
lemma time_deriv_cross_commute {s : Space} {f : Time ‚Üí EuclideanSpace ‚Ñù (Fin 3)}
    (hf : Differentiable ‚Ñù f) :
    s ‚®Ø‚Çë‚ÇÉ (‚àÇ‚Çú (fun t => f t) t)
    =
    ‚àÇ‚Çú (fun t => s ‚®Ø‚Çë‚ÇÉ (f t)) t := by
  repeat rw [Time.deriv]
  rw [fderiv_cross_commute]
  fun_prop

lemma inner_cross_self (v w : EuclideanSpace ‚Ñù (Fin 3)) :
    inner ‚Ñù v (w ‚®Ø‚Çë‚ÇÉ v) = 0 := by
  cases v using WithLp.rec with | _ v =>
  cases w using WithLp.rec with | _ w =>
  simp only [WithLp.equiv_apply, WithLp.ofLp_toLp, WithLp.equiv_symm_apply]
  change (crossProduct w) v ‚¨ù·µ• v = _
  rw [dotProduct_comm, dot_cross_self]

lemma inner_self_cross (v w : EuclideanSpace ‚Ñù (Fin 3)) :
    inner ‚Ñù v (v ‚®Ø‚Çë‚ÇÉ w) = 0 := by
  cases v using WithLp.rec with | _ v =>
  cases w using WithLp.rec with | _ w =>
  simp only [WithLp.equiv_apply, WithLp.ofLp_toLp, WithLp.equiv_symm_apply, PiLp.inner_apply,
    PiLp.toLp_apply, RCLike.inner_apply, conj_trivial]
  change (crossProduct v) w ‚¨ù·µ• v = _
  rw [dotProduct_comm, dot_self_cross]

end ClassicalMechanics
