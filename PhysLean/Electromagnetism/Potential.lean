/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Basic
import PhysLean.SpaceAndTime.SpaceTime.TimeSlice
import PhysLean.Relativity.Tensors.RealTensor.CoVector.Basic
import PhysLean.Mathematics.VariationalCalculus.HasVarGradient
/-!

# The Electromagnetic Potential

## i. Overview

The electromagnetic potential `A^Î¼` is the fundamental objects in
electromagnetism. Mathematically it is related to a connection
on a `U(1)`-bundle.

We define the electromagnetic potential as a function from
spacetime to contravariant Lorentz vectors. from this we can define
the field strength tensor, the kinetic energy, and the electric and magnetic fields.

## ii. Key results

- `ElectromagneticPotential`: is the type of electromagnetic potentials.
- `ElectromagneticPotential.deriv`: the derivative tensor `âˆ‚_Î¼ A^Î½`.
- `ElectromagneticPotential.toFieldStrength`: the field strength tensor `F_Î¼^Î½`.
- `ElectromagneticPotential.scalarPotential`: the scalar potential `Ï†`.
- `ElectromagneticPotential.vectorPotential`: the vector potential `A_i`.
- `ElectromagneticPotential.electricField`: the electric field `E`.
- `ElectromagneticPotential.magneticField`: the magnetic field `B`.

## iii. Table of contents

- A. The electromagnetic potential
  - A.1. The action on the space-time derivatives
  - A.2. Differentiability
  - A.3. Varitational adjoint derivative of component
  - A.4. Variational adjoint derivative of derivatives of the potential
- B. The derivative tensor of the electricomagnetic potential
  - B.1. Equivariance of the derivative tensor
  - B.2. The elements of the derivative tensor in terms of the basis
- C. The field strength tensor
  - C.1. Basic equalitites
  - C.2. Elements of the field strength tensor in terms of basis
  - C.3. The field strength matrix
    - C.3.1. Differentiability of the field strength matrix
  - C.4. The antisymmetry of the field strength tensor
  - C.5. Equivariance of the field strength tensor
- E. The electric and magnetic fields
  - E.1. The scalar potential
  - E.2. The vector potential
  - E.3. The electric field
    - E.3.1. Relation between the electric field and the field strength matrix
    - E.3.2. Differentiability of the electric field
  - E.4. The magnetic field
    - E.4.1. Relation between the magnetic field and the field strength matrix
  - E.5. Field strength matrix in terms of the electric and magnetic fields

## iv. References

- https://quantummechanics.ucsd.edu/ph130a/130_notes/node452.html

-/

namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor

/-!

## A. The electromagnetic potential

We define the electromagnetic potential as a function from spacetime to
contravariant Lorentz vectors, and prove some simple results about it.

-/
/-- The electricomagnetic potential is a tensor `A^Î¼`. -/
noncomputable abbrev ElectromagneticPotential :=
  SpaceTime â†’ Lorentz.Vector

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one
/-!

### A.1. The action on the space-time derivatives

Given a ElectromagneticPotential `A^Î¼`, we can consider its derivative `âˆ‚_Î¼ A^Î½`.
Under a Lorentz transformation `Î›`, this transforms as
`âˆ‚_ Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x))`, we write an expression for this in terms of the tenosr.
`âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº`.

-/

lemma spaceTime_deriv_action_eq_sum {Î¼ Î½ : Fin 1 âŠ• Fin 3}
    (Î› : LorentzGroup 3) (A : ElectromagneticPotential) (hA : Differentiable â„ A) :
    âˆ‚_ Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x Î½ =
    âˆ‘ Îº, âˆ‘ Ï, (Î›.1 Î½ Îº * Î›â»Â¹.1 Ï Î¼) * âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº := by
  calc _
    _ = ((Î› â€¢ (âˆ‚_ Î¼ (fun x => A (Î›â»Â¹ â€¢ x)) x)) Î½) := by
      have hdif : âˆ€ i, DifferentiableAt â„ (fun x => A (Î›â»Â¹ â€¢ x) i) x := by
          rw [â† differentiableAt_pi]
          conv =>
            enter [2, x]; rw [â† Lorentz.Vector.actionCLM_apply]
          apply Differentiable.differentiableAt
          apply Differentiable.comp hA
          exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›â»Â¹)
      trans âˆ‚_ Î¼ (fun x => (Î› â€¢ A (Î›â»Â¹ â€¢ x)) Î½) x
      Â· rw [SpaceTime.deriv_eq, SpaceTime.deriv_eq]
        rw [fderiv_pi]
        rfl
        rw [â† differentiableAt_pi]
        conv =>
          enter [2, x]; rw [â† Lorentz.Vector.actionCLM_apply, â† Lorentz.Vector.actionCLM_apply]
        apply Differentiable.differentiableAt
        apply Differentiable.comp
        Â· exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›)
        Â· apply Differentiable.comp
          Â· exact hA
          Â· exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›â»Â¹)
      conv_lhs =>
        enter [2, x]
        rw [Lorentz.Vector.smul_eq_sum]
      rw [SpaceTime.deriv_eq]
      rw [fderiv_fun_sum (ğ•œ := â„)]
      conv_lhs =>
        enter [1, 2, i]
        rw [fderiv_const_mul (hdif i)]
      simp only [Nat.reduceSucc, ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_smul',
        Finset.sum_apply, Pi.smul_apply, smul_eq_mul]
      rw [Lorentz.Vector.smul_eq_sum]
      congr
      funext Îº
      congr
      rw [SpaceTime.deriv_eq, fderiv_pi]
      rfl
      Â· exact fun i => hdif i
      Â· intro i _
        apply DifferentiableAt.const_mul
        exact hdif i
    _ = (((Î› â€¢ (âˆ‘ Ï, Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x)))) Î½) := by
      rw [SpaceTime.deriv_comp_lorentz_action]
      Â· exact hA
    _ = (âˆ‘ Îº, Î›.1 Î½ Îº * (âˆ‘ Ï, Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº)) := by
      rw [Lorentz.Vector.smul_eq_sum]
      rfl
  apply Finset.sum_congr rfl (fun Îº _ => ?_)
  rw [Finset.mul_sum]
  apply Finset.sum_congr rfl (fun Ï _ => ?_)
  simp only [Nat.reduceSucc, smul_eq_mul]
  ring

/-!

### A.2. Differentiability

We show that the components of field strength tensor are differentiable if the potential is.
-/

lemma differentiable_component
    (A : ElectromagneticPotential) (hA : Differentiable â„ A) (Î¼ : Fin 1 âŠ• Fin 3) :
    Differentiable â„ (fun x => A x Î¼) := by
  revert Î¼
  rw [â† differentiable_pi]
  exact hA

/-!

### A.3. Varitational adjoint derivative of component

We find the variational adjoint derivative of the components of the potential.
This will be used to find e.g. the variational derivative of the kinetic term,
and derive the equations of motion.

-/

/-- A local instance of the inner product structure on `SpaceTime`. -/
noncomputable local instance : InnerProductSpace â„ SpaceTime := SpaceTime.innerProductSpace 3

open ContDiff
lemma hasVarAdjDerivAt_component (Î¼ : Fin 1 âŠ• Fin 3) (A : SpaceTime â†’ Lorentz.Vector)
    (hA : ContDiff â„ âˆ A) :
        HasVarAdjDerivAt (fun (A' : SpaceTime â†’ Lorentz.Vector) x => A' x Î¼)
          (fun (A' : SpaceTime â†’ â„) x => A' x â€¢ Lorentz.Vector.basis Î¼) A := by
  let f : SpaceTime â†’ Lorentz.Vector â†’ â„ := fun x v => v Î¼
  let f' : SpaceTime â†’ Lorentz.Vector â†’ â„ â†’ Lorentz.Vector := fun x _ c =>
    c â€¢ Lorentz.Vector.basis Î¼
  change HasVarAdjDerivAt (fun A' x => f x (A' x)) (fun Ïˆ x => f' x (A x) (Ïˆ x)) A
  apply HasVarAdjDerivAt.fmap
  Â· fun_prop
  Â· fun_prop
  intro x A
  refine { differentiableAt := ?_, hasAdjoint_fderiv := ?_ }
  Â· fun_prop
  refine { adjoint_inner_left := ?_ }
  intro u v
  simp [f,f']
  rw [PiLp.inner_apply]
  simp only [Lorentz.Vector.apply_smul, Lorentz.Vector.basis_apply, mul_ite, mul_one, mul_zero,
    RCLike.inner_apply, conj_trivial, Finset.sum_ite_eq, Finset.mem_univ, â†“reduceIte,
    mul_eq_mul_right_iff]
  left
  let fCLM : Lorentz.Vector â†’L[â„] â„ := {
    toFun := fun v => v Î¼,
    map_add' := by intros; simp,
    map_smul' := by intros; simp
    cont := by fun_prop
  }
  change _ = (fderiv â„ fCLM A) u
  simp only [ContinuousLinearMap.fderiv]
  rfl

/-!

### A.4. Variational adjoint derivative of derivatives of the potential

We find the variational adjoint derivative of the derivatives of the components of the potential.
This will again be used to find the variational derivative of the kinetic term,
and derive the equations of motion (Maxwell's equations).

-/

lemma deriv_hasVarAdjDerivAt (Î¼ Î½ : Fin 1 âŠ• Fin 3) (A : SpaceTime â†’ Lorentz.Vector)
    (hA : ContDiff â„ âˆ A) :
    HasVarAdjDerivAt (fun (A : SpaceTime â†’ Lorentz.Vector) x => âˆ‚_ Î¼ A x Î½)
      (fun Ïˆ x => - (fderiv â„ Ïˆ x) (Lorentz.Vector.basis Î¼) â€¢ Lorentz.Vector.basis Î½) A := by
  have h0' := HasVarAdjDerivAt.fderiv' _ _
        (hF := hasVarAdjDerivAt_component Î½ A hA) A (Lorentz.Vector.basis Î¼)
  refine HasVarAdjDerivAt.congr (G := (fun (A : SpaceTime â†’ Lorentz.Vector) x => âˆ‚_ Î¼ A x Î½)) h0' ?_
  intro Ï† hÏ†
  funext x
  simp only
  rw [deriv_apply_eq Î¼ Î½ Ï†]
  exact hÏ†.differentiable (by simp)

/-!

## B. The derivative tensor of the electricomagnetic potential

We define the derivative as a tensor in `Lorentz.CoVector âŠ—[â„] Lorentz.Vector` for the
electromagnetic potential `A^Î¼`. We then prove that this tensor transforms correctly
under Lorentz transformations.

-/

/-- The derivative of the electric potential, `âˆ‚_Î¼ A^Î½`. -/
noncomputable def deriv (A : ElectromagneticPotential) :
    SpaceTime â†’ Lorentz.CoVector âŠ—[â„] Lorentz.Vector := fun x =>
  âˆ‘ Î¼, âˆ‘ Î½, (âˆ‚_ Î¼ A x Î½) â€¢ Lorentz.CoVector.basis Î¼ âŠ—â‚œ[â„] Lorentz.Vector.basis Î½

/-!

### B.1. Equivariance of the derivative tensor

We show that the derivative tensor is equivariant under the action of the Lorentz group.
That is, `âˆ‚_Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) = Î› â€¢ (âˆ‚_Î¼ A (Î›â»Â¹ â€¢ x))`, or in words:
applying the Lorentz transformation to the potential and then taking the derivative is the same
as taking the derivative and then applying the Lorentz transformation to the resulting tensor.

-/
lemma deriv_equivariant {x : SpaceTime} (A : ElectromagneticPotential) (Î› : LorentzGroup 3)
    (hf : Differentiable â„ A) : deriv (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x = Î› â€¢ (deriv A (Î›â»Â¹ â€¢ x)) := by
    calc _
      _ = âˆ‘ Î¼, âˆ‘ Î½, âˆ‘ Îº, âˆ‘ Ï, (Î›.1 Î½ Îº * (Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº)) â€¢
          (Lorentz.CoVector.basis Î¼) âŠ—â‚œ[â„]
          Lorentz.Vector.basis Î½ := by
        refine Finset.sum_congr rfl (fun Î¼ _ => ?_)
        refine Finset.sum_congr rfl (fun Î½ _ => ?_)
        rw [spaceTime_deriv_action_eq_sum Î› A hf]
        rw [Finset.sum_smul]
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        rw [Finset.sum_smul]
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        congr 1
        simp only [Nat.succ_eq_add_one, Nat.reduceAdd, smul_eq_mul]
        ring
      _ = âˆ‘ Î¼, âˆ‘ Î½, âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢
          (Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        refine Finset.sum_congr rfl (fun Î¼ _ => ?_)
        refine Finset.sum_congr rfl (fun Î½ _ => ?_)
        refine Finset.sum_congr rfl (fun Îº _ => ?_)
        refine Finset.sum_congr rfl (fun Ï _ => ?_)
        rw [smul_tmul, tmul_smul, tmul_smul, smul_smul, smul_smul]
        congr 1
        simp only [Nat.reduceSucc, smul_eq_mul]
        ring
      _ = âˆ‘ Îº, âˆ‘ Ï, âˆ‘ Î¼, âˆ‘ Î½, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢
          (Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        conv_lhs => enter [2, Î¼]; rw [Finset.sum_comm]
        conv_lhs => rw [Finset.sum_comm]
        conv_lhs => enter [2, Îº, 2, Î¼]; rw [Finset.sum_comm]
        conv_lhs => enter [2, Îº]; rw [Finset.sum_comm]
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ (âˆ‘ Î¼, Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (âˆ‘ Î½, Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        conv_rhs =>
          enter [2, Îº,2, Ï]; rw [sum_tmul, Finset.smul_sum]
          enter [2, Î¼]; rw [tmul_sum, Finset.smul_sum];
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ (Î› â€¢ (Lorentz.CoVector.basis Ï)) âŠ—â‚œ[â„]
          (Î› â€¢ Lorentz.Vector.basis Îº) := by
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        congr 2
        Â· rw [Lorentz.CoVector.smul_basis]
        Â· rw [Lorentz.Vector.smul_basis]
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ Î› â€¢ ((Lorentz.CoVector.basis Ï) âŠ—â‚œ[â„]
        Lorentz.Vector.basis Îº) := by
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        rw [Tensorial.smul_prod]
    rw [deriv]
    conv_rhs => rw [â† Tensorial.smulLinearMap_apply]
    rw [Finset.sum_comm]
    simp
    rfl

/-!

### B.2. The elements of the derivative tensor in terms of the basis

We show that in the standard basis, the elements of the derivative tensor
are just equal to `âˆ‚_ Î¼ A x Î½`.

-/

@[simp]
lemma deriv_basis_repr_apply {Î¼Î½ : (Fin 1 âŠ• Fin 3) Ã— (Fin 1 âŠ• Fin 3)} (A : ElectromagneticPotential)
    (x : SpaceTime) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (deriv A x) Î¼Î½ =
    âˆ‚_ Î¼Î½.1 A x Î¼Î½.2 := by
  match Î¼Î½ with
  | (Î¼, Î½) =>
  rw [deriv]
  simp only [map_sum, map_smul, Finsupp.coe_finset_sum, Finsupp.coe_smul, Finset.sum_apply,
    Pi.smul_apply, Basis.tensorProduct_repr_tmul_apply, Basis.repr_self, smul_eq_mul]
  rw [Finset.sum_eq_single Î¼, Finset.sum_eq_single Î½]
  Â· simp
  Â· intro Î¼' _ h
    simp [h]
  Â· simp
  Â· intro Î½' _ h
    simp [h]
  Â· simp

lemma toTensor_deriv_basis_repr_apply (A : ElectromagneticPotential)
    (x : SpaceTime) (b : ComponentIdx (S := realLorentzTensor)
      (Sum.elim ![Color.down] ![Color.up] âˆ˜ â‡‘finSumFinEquiv.symm)) :
    (Tensor.basis _).repr (Tensorial.toTensor (deriv A x)) b =
    âˆ‚_ (finSumFinEquiv.symm (b 0)) A x (finSumFinEquiv.symm (b 1)) := by
  rw [Tensorial.basis_toTensor_apply]
  rw [Tensorial.basis_map_prod]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply,
    Equiv.symm_symm, Fin.isValue]
  rw [Lorentz.Vector.tensor_basis_map_eq_basis_reindex,
    Lorentz.CoVector.tensor_basis_map_eq_basis_reindex]
  have hb : (((Lorentz.CoVector.basis (d := 3)).reindex
      Lorentz.CoVector.indexEquiv.symm).tensorProduct
      (Lorentz.Vector.basis.reindex Lorentz.Vector.indexEquiv.symm)) =
      ((Lorentz.CoVector.basis (d := 3)).tensorProduct (Lorentz.Vector.basis (d := 3))).reindex
      (Lorentz.CoVector.indexEquiv.symm.prodCongr Lorentz.Vector.indexEquiv.symm) := by
    ext b
    match b with
    | âŸ¨i, jâŸ© =>
    simp
  rw [hb]
  rw [Module.Basis.repr_reindex_apply, deriv_basis_repr_apply]
  rfl

/-!

## C. The field strength tensor

We define the field strength tensor `F_Î¼^Î½` in terms of the derivative of the
electromagnetic potential `A^Î¼`. We then prove that this tensor transforms correctly
under Lorentz transformations.

-/
/-- The field strength from an electromagnetic potential, as a tensor `F_Î¼^Î½`. -/
noncomputable def toFieldStrength (A : ElectromagneticPotential) :
    SpaceTime â†’ Lorentz.Vector âŠ—[â„] Lorentz.Vector := fun x =>
  Tensorial.toTensor.symm
  (permT id (PermCond.auto) {(Î· 3 | Î¼ Î¼' âŠ— A.deriv x | Î¼' Î½) + - (Î· 3 | Î½ Î½' âŠ— A.deriv x | Î½' Î¼)}áµ€)

/-!

### C.1. Basic equalitites

-/

lemma toFieldStrength_eq_add (A : ElectromagneticPotential) (x : SpaceTime) :
    toFieldStrength A x =
    Tensorial.toTensor.symm (permT id (PermCond.auto) {(Î· 3 | Î¼ Î¼' âŠ— A.deriv x | Î¼' Î½)}áµ€)
    - Tensorial.toTensor.symm (permT ![1, 0] (PermCond.auto)
      {(Î· 3 | Î¼ Î¼' âŠ— A.deriv x | Î¼' Î½)}áµ€) := by
  rw [toFieldStrength]
  simp only [map_add, map_neg]
  rw [sub_eq_add_neg]
  apply congrArgâ‚‚
  Â· rfl
  Â· rw [permT_permT]
    rfl

lemma toTensor_toFieldStrength (A : ElectromagneticPotential) (x : SpaceTime) :
    Tensorial.toTensor (toFieldStrength A x) =
    (permT id (PermCond.auto) {(Î· 3 | Î¼ Î¼' âŠ— A.deriv x | Î¼' Î½)}áµ€)
    - (permT ![1, 0] (PermCond.auto) {(Î· 3 | Î¼ Î¼' âŠ— A.deriv x | Î¼' Î½)}áµ€) := by
  rw [toFieldStrength_eq_add]
  simp

/-!

### C.2. Elements of the field strength tensor in terms of basis

-/

lemma toTensor_toFieldStrength_basis_repr (A : ElectromagneticPotential) (x : SpaceTime)
    (b : ComponentIdx (S := realLorentzTensor)
      (Sum.elim ![Color.up] ![Color.up] âˆ˜ â‡‘finSumFinEquiv.symm)) :
    (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x)) b =
    âˆ‘ Îº, (Î· (finSumFinEquiv.symm (b 0)) Îº * âˆ‚_ Îº A x (finSumFinEquiv.symm (b 1)) -
      Î· (finSumFinEquiv.symm (b 1)) Îº * âˆ‚_ Îº A x (finSumFinEquiv.symm (b 0))) := by
  rw [toTensor_toFieldStrength]
  simp only [Tensorial.self_toTensor_apply, map_sub,
    Finsupp.coe_sub, Pi.sub_apply]
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [1, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Î· (finSumFinEquiv.symm (b 0)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [1, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change âˆ‚_ (finSumFinEquiv.symm n) A x (finSumFinEquiv.symm (b 1))
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [2, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Î· (finSumFinEquiv.symm (b 1)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [2, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change âˆ‚_ (finSumFinEquiv.symm n) A x (finSumFinEquiv.symm (b 0))
  rw [â† Finset.sum_sub_distrib]
  rw [â† finSumFinEquiv.sum_comp]
  simp only [Nat.reduceAdd, Fin.isValue, Equiv.symm_apply_apply]

lemma toFieldStrength_tensor_basis_eq_basis (A : ElectromagneticPotential) (x : SpaceTime)
    (b : ComponentIdx (S := realLorentzTensor)
      (Sum.elim ![Color.up] ![Color.up] âˆ˜ â‡‘finSumFinEquiv.symm)) :
    (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x)) b =
    (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr (toFieldStrength A x)
      (finSumFinEquiv.symm (b 0), finSumFinEquiv.symm (b 1)) := by
  rw [Tensorial.basis_toTensor_apply]
  rw [Tensorial.basis_map_prod]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply,
    Equiv.symm_symm, Fin.isValue]
  rw [Lorentz.Vector.tensor_basis_map_eq_basis_reindex]
  have hb : (((Lorentz.Vector.basis (d := 3)).reindex Lorentz.Vector.indexEquiv.symm).tensorProduct
          (Lorentz.Vector.basis.reindex Lorentz.Vector.indexEquiv.symm)) =
          ((Lorentz.Vector.basis (d := 3)).tensorProduct (Lorentz.Vector.basis (d := 3))).reindex
          (Lorentz.Vector.indexEquiv.symm.prodCongr Lorentz.Vector.indexEquiv.symm) := by
        ext b
        match b with
        | âŸ¨i, jâŸ© =>
        simp
  rw [hb]
  rw [Module.Basis.repr_reindex_apply]
  congr 1

lemma toFieldStrength_basis_repr_apply {Î¼Î½ : (Fin 1 âŠ• Fin 3) Ã— (Fin 1 âŠ• Fin 3)}
    (A : ElectromagneticPotential) (x : SpaceTime) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x) Î¼Î½ =
    âˆ‘ Îº, ((Î· Î¼Î½.1 Îº * âˆ‚_ Îº A x Î¼Î½.2) - Î· Î¼Î½.2 Îº * âˆ‚_ Îº A x Î¼Î½.1) := by
  match Î¼Î½ with
  | (Î¼, Î½) =>
  trans (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x))
    (fun | 0 => finSumFinEquiv Î¼ | 1 => finSumFinEquiv Î½); swap
  Â· rw [toTensor_toFieldStrength_basis_repr]
    simp
  rw [toFieldStrength_tensor_basis_eq_basis]
  congr 1
  change _ = (finSumFinEquiv.symm (finSumFinEquiv Î¼), finSumFinEquiv.symm (finSumFinEquiv Î½))
  simp

lemma toFieldStrength_basis_repr_apply_eq_single {Î¼Î½ : (Fin 1 âŠ• Fin 3) Ã— (Fin 1 âŠ• Fin 3)}
    (A : ElectromagneticPotential) (x : SpaceTime) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x) Î¼Î½ =
    ((Î· Î¼Î½.1 Î¼Î½.1 * âˆ‚_ Î¼Î½.1 A x Î¼Î½.2) - Î· Î¼Î½.2 Î¼Î½.2 * âˆ‚_ Î¼Î½.2 A x Î¼Î½.1) := by
  rw [toFieldStrength_basis_repr_apply]
  simp only [Finset.sum_sub_distrib]
  rw [Finset.sum_eq_single Î¼Î½.1, Finset.sum_eq_single Î¼Î½.2]
  Â· intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  Â· simp
  Â· intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  Â· simp

/-!

### C.3. The field strength matrix

We define the field strength matrix to be the matrix representation of the field strength tensor
in the standard basis.

This is currently not used as much as it could be.
-/
open ContDiff

/-- The matrix corresponding to the field strength in the standard basis. -/
noncomputable abbrev fieldStrengthMatrix (A : ElectromagneticPotential) (x : SpaceTime) :=
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x)

/-!

#### C.3.1. Differentiability of the field strength matrix

-/

lemma fieldStrengthMatrix_differentiable {A : ElectromagneticPotential}
    {Î¼Î½} (hA : ContDiff â„ 2 A) :
    Differentiable â„ (A.fieldStrengthMatrix Â· Î¼Î½) := by
  have diff_partial (Î¼) :
      âˆ€ Î½, Differentiable â„ fun x => (fderiv â„ A x) (Lorentz.Vector.basis Î¼) Î½ := by
    rw [â† differentiable_pi]
    refine Differentiable.clm_apply ?_ ?_
    Â· exact differentiable_fderiv _ (ContDiff.of_le hA (by simp))
    Â· fun_prop
  conv => enter [2, x]; rw [toFieldStrength_basis_repr_apply_eq_single,
    SpaceTime.deriv_eq, SpaceTime.deriv_eq]
  apply Differentiable.sub
  apply Differentiable.const_mul
  Â· exact diff_partial _ _
  apply Differentiable.const_mul
  Â· exact diff_partial _ _

/-!

### C.4. The antisymmetry of the field strength tensor

We show that the field strength tensor is antisymmetric.

-/

lemma toFieldStrength_antisymmetric (A : ElectromagneticPotential) (x : SpaceTime) :
    {A.toFieldStrength x | Î¼ Î½ = - (A.toFieldStrength x | Î½ Î¼)}áµ€ := by
  apply (Tensor.basis _).repr.injective
  ext b
  rw [toTensor_toFieldStrength_basis_repr]
  rw [permT_basis_repr_symm_apply, map_neg]
  simp only [Nat.reduceAdd, Fin.isValue, Nat.reduceSucc, Function.comp_apply, Finsupp.coe_neg,
    Pi.neg_apply]
  rw [toTensor_toFieldStrength_basis_repr]
  rw [â† Finset.sum_neg_distrib]
  apply Finset.sum_congr rfl (fun Îº _ => ?_)
  simp only [Fin.isValue, Nat.reduceAdd, Fin.cast_eq_self, neg_sub]
  rfl

lemma fieldStrengthMatrix_antisymm (A : ElectromagneticPotential) (x : SpaceTime)
    (Î¼ Î½ : Fin 1 âŠ• Fin 3) :
    A.fieldStrengthMatrix x (Î¼, Î½) = - A.fieldStrengthMatrix x (Î½, Î¼) := by
  rw [toFieldStrength_basis_repr_apply, toFieldStrength_basis_repr_apply]
  rw [â† Finset.sum_neg_distrib]
  apply Finset.sum_congr rfl (fun Îº _ => ?_)
  simp

@[simp]
lemma fieldStrengthMatrix_diag_eq_zero (A : ElectromagneticPotential) (x : SpaceTime)
    (Î¼ : Fin 1 âŠ• Fin 3) :
    A.fieldStrengthMatrix x (Î¼, Î¼) = 0 := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp

/-!

### C.5. Equivariance of the field strength tensor

We show that the field strength tensor is equivariant under the action of the Lorentz group.
That is transforming the potential and then taking the field strength is the same
as taking the field strength and then transforming the resulting tensor.

-/

lemma toFieldStrength_equivariant (A : ElectromagneticPotential) (Î› : LorentzGroup 3)
    (hf : Differentiable â„ A) (x : SpaceTime) :
    toFieldStrength (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x =
      Î› â€¢ toFieldStrength A (Î›â»Â¹ â€¢ x) := by
  rw [toFieldStrength, deriv_equivariant A Î› hf, â† actionT_contrMetric Î›, toFieldStrength]
  simp only [Tensorial.toTensor_smul, prodT_equivariant, contrT_equivariant, map_neg,
    permT_equivariant, map_add, â† Tensorial.smul_toTensor_symm, Tensorial.smul_add,
    Tensorial.smul_neg]

/-!

## E. The electric and magnetic fields

We now define the scalar and vector potentials, and the electric and magnetic fields
in terms of the electromagnetic potential.
This breaks the manifest Lorentz covariance.

-/

/-

### E.1. The scalar potential

This assumes that the spee of light is set to `1`.
-/

/-- The scalar potential from the electromagnetic potential. -/
noncomputable def scalarPotential (A : ElectromagneticPotential) : Time â†’ Space â†’ â„ := timeSlice <|
  fun x => A x (Sum.inl 0)

/-!

### E.2. The vector potential

-/

/-- The vector potential from the electromagnetic potential. -/
noncomputable def vectorPotential (A : ElectromagneticPotential) :
    Time â†’ Space â†’ EuclideanSpace â„ (Fin 3) := timeSlice <|
  fun x i => A x (Sum.inr i)

/-!

### E.3. The electric field

-/

open Space Time

/-- The electric field from the electromagnetic potential. -/
noncomputable def electricField (A : ElectromagneticPotential) : ElectricField :=
  fun t x => - âˆ‡ (A.scalarPotential t) x - âˆ‚â‚œ (fun t => A.vectorPotential t x) t

/-!

#### E.3.1. Relation between the electric field and the field strength matrix

-/

lemma electricField_eq_fieldStrengthMatrix (A : ElectromagneticPotential) (t : Time)
    (x : Space) (i : Fin 3) (hA : Differentiable â„ A) :
    A.electricField t x i = -
    A.fieldStrengthMatrix (SpaceTime.toTimeAndSpace.symm (t, x)) (Sum.inl 0, Sum.inr i) := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp only [Fin.isValue, inl_0_inl_0, one_mul, inr_i_inr_i, neg_mul, sub_neg_eq_add, neg_add_rev]
  rw [electricField]
  simp only [PiLp.sub_apply, PiLp.neg_apply, Fin.isValue]
  congr
  Â· rw [Space.grad_apply]
    trans âˆ‚_ (Sum.inr i) (fun x => A x (Sum.inl 0)) (toTimeAndSpace.symm (t, x)); swap
    Â· rw [SpaceTime.deriv_eq, SpaceTime.deriv_eq]
      rw [fderiv_pi]
      rfl
      Â· exact fun Î¼ => (differentiable_component A hA Î¼).differentiableAt
    Â· rw [SpaceTime.deriv_sum_inr]
      simp
      rfl
      Â· exact fun Î¼ => (differentiable_component A hA _).differentiableAt
  Â· rw [SpaceTime.deriv_sum_inl]
    simp only [ContinuousLinearEquiv.apply_symm_apply]
    rw [Time.deriv_eq, Time.deriv_eq]
    rw [vectorPotential]
    simp [timeSlice]
    rw [fderiv_pi, fderiv_pi]
    rfl
    Â· intro Î¼
      apply Differentiable.differentiableAt
      have h1 := (differentiable_component A hA Î¼)
      apply Differentiable.comp h1
      refine Differentiable.fun_comp ?_ ?_
      Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm
      Â· fun_prop
    Â· intro Î¼
      apply Differentiable.differentiableAt
      have h1 := (differentiable_component A hA (Sum.inr Î¼))
      apply Differentiable.comp h1
      refine Differentiable.fun_comp ?_ ?_
      Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm
      Â· fun_prop
    Â· exact hA

/-!

#### E.3.2. Differentiability of the electric field

-/

lemma electricField_differentiable {A : ElectromagneticPotential}
    (hA : ContDiff â„ 2 A) : Differentiable â„ (â†¿A.electricField) := by
  rw [differentiable_pi]
  intro i
  conv =>
    enter [2, x];
    change A.electricField x.1 x.2 i
    rw [electricField_eq_fieldStrengthMatrix (A) x.1 x.2 i (hA.differentiable (by simp))]
    change - A.fieldStrengthMatrix (toTimeAndSpace.symm (x.1, x.2)) (Sum.inl 0, Sum.inr i)
  apply Differentiable.neg
  change Differentiable â„ ((fun x => A.fieldStrengthMatrix x (Sum.inl 0, Sum.inr i))
    âˆ˜ (toTimeAndSpace (d := 3)).symm)
  refine Differentiable.comp ?_ ?_
  Â· exact fieldStrengthMatrix_differentiable (hA)
  Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm

/-!

### E.4. The magnetic field

-/

/-- The magnetic field from the electromagnetic potential. -/
noncomputable def magneticField (A : ElectromagneticPotential) : MagneticField :=
  fun t x => (âˆ‡ Ã— (A.vectorPotential t)) x

/-!

#### E.4.1. Relation between the magnetic field and the field strength matrix

-/

lemma magneticField_fst_eq_fieldStrengthMatrix (A : ElectromagneticPotential) (t : Time)
    (x : Space) (hA : Differentiable â„ A) :
    A.magneticField t x 0 =
    - A.fieldStrengthMatrix (SpaceTime.toTimeAndSpace.symm (t, x)) (Sum.inr 1, Sum.inr 2) := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp only [Fin.isValue, inr_i_inr_i, neg_mul, one_mul, sub_neg_eq_add, neg_add_rev, neg_neg]
  rw [magneticField]
  simp [Space.curl, Space.coord]
  rw [neg_add_eq_sub]
  congr
  all_goals
  Â· rw [SpaceTime.deriv_sum_inr _ hA]
    simp only [Fin.isValue, ContinuousLinearEquiv.apply_symm_apply]
    rw [Space.deriv_eq, Space.deriv_eq, fderiv_pi]
    rfl
    Â· intro Î¼
      apply Differentiable.differentiableAt
      have h1 := (differentiable_component A hA Î¼)
      apply Differentiable.comp h1
      refine Differentiable.fun_comp ?_ ?_
      Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm
      Â· fun_prop

lemma magneticField_snd_eq_fieldStrengthMatrix (A : ElectromagneticPotential) (t : Time)
    (x : Space) (hA : Differentiable â„ A) :
    A.magneticField t x 1 = A.fieldStrengthMatrix (SpaceTime.toTimeAndSpace.symm (t, x))
      (Sum.inr 0, Sum.inr 2) := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp only [Fin.isValue, inr_i_inr_i, neg_mul, one_mul, sub_neg_eq_add]
  rw [magneticField]
  simp [Space.curl, Space.coord]
  rw [neg_add_eq_sub]
  congr
  all_goals
  Â· rw [SpaceTime.deriv_sum_inr _ hA]
    simp only [Fin.isValue, ContinuousLinearEquiv.apply_symm_apply]
    rw [Space.deriv_eq, Space.deriv_eq, fderiv_pi]
    rfl
    Â· intro Î¼
      apply Differentiable.differentiableAt
      have h1 := (differentiable_component A hA Î¼)
      apply Differentiable.comp h1
      refine Differentiable.fun_comp ?_ ?_
      Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm
      Â· fun_prop

lemma magneticField_thd_eq_fieldStrengthMatrix (A : ElectromagneticPotential) (t : Time)
    (x : Space) (hA : Differentiable â„ A) :
    A.magneticField t x 2 =
    - A.fieldStrengthMatrix (SpaceTime.toTimeAndSpace.symm (t, x)) (Sum.inr 0, Sum.inr 1) := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp only [Fin.isValue, inr_i_inr_i, neg_mul, one_mul, sub_neg_eq_add, neg_add_rev, neg_neg]
  rw [magneticField]
  simp [Space.curl, Space.coord]
  rw [neg_add_eq_sub]
  congr
  all_goals
  Â· rw [SpaceTime.deriv_sum_inr _ hA]
    simp only [Fin.isValue, ContinuousLinearEquiv.apply_symm_apply]
    rw [Space.deriv_eq, Space.deriv_eq, fderiv_pi]
    rfl
    Â· intro Î¼
      apply Differentiable.differentiableAt
      have h1 := (differentiable_component A hA Î¼)
      apply Differentiable.comp h1
      refine Differentiable.fun_comp ?_ ?_
      Â· exact ContinuousLinearEquiv.differentiable toTimeAndSpace.symm
      Â· fun_prop

/-!

### E.5. Field strength matrix in terms of the electric and magnetic fields

-/

lemma fieldStrengthMatrix_eq_electric_magnetic (A : ElectromagneticPotential) (t : Time)
    (x : Space) (hA : Differentiable â„ A) (Î¼ Î½ : Fin 1 âŠ• Fin 3) :
    A.fieldStrengthMatrix (SpaceTime.toTimeAndSpace.symm (t, x)) (Î¼, Î½) =
    match Î¼, Î½ with
    | Sum.inl 0, Sum.inl 0 => 0
    | Sum.inl 0, Sum.inr i => - A.electricField t x i
    | Sum.inr i, Sum.inl 0 => A.electricField t x i
    | Sum.inr i, Sum.inr j =>
    match i, j with
    | 0, 0 => 0
    | 0, 1 => - A.magneticField t x 2
    | 0, 2 => A.magneticField t x 1
    | 1, 0 => A.magneticField t x 2
    | 1, 1 => 0
    | 1, 2 => - A.magneticField t x 0
    | 2, 0 => - A.magneticField t x 1
    | 2, 1 => A.magneticField t x 0
    | 2, 2 => 0 := by
  match Î¼, Î½ with
  | Sum.inl 0, Sum.inl 0 => simp
  | Sum.inl 0, Sum.inr i => simp [electricField_eq_fieldStrengthMatrix A t x i hA]
  | Sum.inr i, Sum.inl 0 =>
    simp [electricField_eq_fieldStrengthMatrix A t x i hA]
    exact fieldStrengthMatrix_antisymm A (toTimeAndSpace.symm (t, x)) (Sum.inr i) (Sum.inl 0)
  | Sum.inr i, Sum.inr j =>
    match i, j with
    | 0, 0 => simp
    | 0, 1 =>
      simp [magneticField_thd_eq_fieldStrengthMatrix A t x hA]
    | 0, 2 =>
      simp [magneticField_snd_eq_fieldStrengthMatrix A t x hA]
    | 1, 0 =>
      simp [magneticField_thd_eq_fieldStrengthMatrix A t x hA]
      rw [fieldStrengthMatrix_antisymm]
    | 1, 1 => simp
    | 1, 2 =>
      simp [magneticField_fst_eq_fieldStrengthMatrix A t x hA]
    | 2, 0 =>
      simp [magneticField_snd_eq_fieldStrengthMatrix A t x hA]
      rw [fieldStrengthMatrix_antisymm]
    | 2, 1 =>
      simp [magneticField_fst_eq_fieldStrengthMatrix A t x hA]
      rw [fieldStrengthMatrix_antisymm]
    | 2, 2 => simp

end ElectromagneticPotential

end Electromagnetism
