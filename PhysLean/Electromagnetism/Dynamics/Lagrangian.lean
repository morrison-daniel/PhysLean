/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Dynamics.CurrentDensity
import PhysLean.Electromagnetism.Dynamics.KineticTerm
/-!

# The Lagrangian in electromagnetism

## i. Overview

In this module we define the Lagrangian density for the electromagnetic field in
presence of a current density. We prove properties of this lagrangian density,
and find it's variational gradient.

The lagrangian density is given by
`L = -1/(4 Î¼â‚€) F_{Î¼Î½} F^{Î¼Î½} - A_Î¼ J^Î¼`

In this implementation we set `Î¼â‚€ = 1`. It is a TODO to introduce this constant.

## ii. Key results

- `freeCurrentPotential` : The potential energy from the interaction of the electromagnetic
  potential with a free Lorentz current density.
- `gradFreeCurrentPotential` : The variational gradient of the free current potential.
- `lagrangian` : The lagrangian density for the electromagnetic field in presence of a
  Lorentz current density.
- `gradLagrangian` : The variational gradient of the lagrangian density.
- `gradLagrangian_eq_electricField_magneticField` : The variational gradient of the lagrangian
  density expressed in Gauss's and AmpÃ¨re laws.

## iii. Table of contents

- A. Free current potential
  - A.1. Shifts in the free current potential under shifts in the potential
  - A.2. The free current potential has a variational gradient
  - A.3. The free current potential in terms of the scalar and vector potentials
  - A.4. The variational gradient of the free current potential
- B. The Lagrangian density
  - B.1. Shifts in the lagrangian under shifts in the potential
  - B.2. Lagrangian in terms of electric and magnetic fields
- C. The variational gradient of the lagrangian density
  - C.1. The lagrangian density has a variational gradient
  - C.2. The definition of, `gradLagrangian`, the variational gradient of the lagrangian density
  - C.3. The variational gradient in terms of the gradient of the kinetic term
  - C.4. The lagrangian density has the variational gradient equal to `gradLagrangian`
  - C.5. The variational gradient in terms of the field strength tensor
  - C.6. The lagrangian gradient recovering Gauss's and AmpÃ¨re laws
- D. The gradient of the lagrangian density for distributions
  - D.1. The gradient of the free current potential
  - D.2. The gradient of the lagrangian density

## iv. References

- https://quantummechanics.ucsd.edu/ph130a/130_notes/node452.html
- https://ph.qmul.ac.uk/sites/default/files/EMT10new.pdf

-/

namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor ContDiff

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace
open Lorentz.Vector
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. Free current potential

-/

/-- The potential energy from the interaction of the electromagnetic potential
  with the free current `J`. -/
noncomputable def freeCurrentPotential (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d)
    (x : SpaceTime d) : â„ := âŸªA x, J xâŸ«â‚˜

/-!

### A.1. Shifts in the free current potential under shifts in the potential

-/

lemma freeCurrentPotential_add_const (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) (c : Lorentz.Vector d) (x : SpaceTime d) :
    freeCurrentPotential (fun x => A x + c) J x = freeCurrentPotential A J x + âŸªc, J xâŸ«â‚˜ := by
  rw [freeCurrentPotential, freeCurrentPotential]
  simp

/-!

### A.2. The free current potential has a variational gradient

-/

lemma freeCurrentPotential_hasVarGradientAt (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) :
    HasVarGradientAt (fun A => freeCurrentPotential A J)
    (((âˆ‘ Î¼, fun x => (Î· Î¼ Î¼ * J x Î¼) â€¢ Lorentz.Vector.basis Î¼))) A := by
  conv =>
    enter [1, q', x]
    rw [freeCurrentPotential, minkowskiProduct_toCoord_minkowskiMatrix]
  apply HasVarGradientAt.sum _ hA
  intro Î¼
  have h1 := hasVarAdjDerivAt_component Î¼ A hA
  have h2' : ContDiff â„ âˆ fun x => Î· Î¼ Î¼ * J x Î¼ :=
    ContDiff.mul (by fun_prop) ((Lorentz.Vector.contDiff_apply _).mpr hJ Î¼)
  have h2 := HasVarAdjDerivAt.fun_mul h2' _ _ A h1
  have h3' : (fun (Ï† : SpaceTime d â†’ Lorentz.Vector d) x => Î· Î¼ Î¼ * J x Î¼ * Ï† x Î¼) =
    (fun (Ï† : SpaceTime d â†’ Lorentz.Vector d) x => Î· Î¼ Î¼ * Ï† x Î¼ * J x Î¼) := by
    funext Ï† x
    ring
  rw [h3'] at h2
  apply HasVarGradientAt.intro _ h2
  simp

/-!

### A.3. The free current potential in terms of the scalar and vector potentials

-/

lemma freeCurrentPotential_eq_sum_scalarPotential_vectorPotential
    (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) :
    A.freeCurrentPotential J x =
    A.scalarPotential ğ“•.c (x.time ğ“•.c) x.space * J.chargeDensity ğ“•.c (x.time ğ“•.c) x.space
    - âˆ‘ i, A.vectorPotential ğ“•.c (x.time ğ“•.c) x.space i *
        J.currentDensity ğ“•.c (x.time ğ“•.c) x.space i := by
  rw [freeCurrentPotential, minkowskiProduct_toCoord_minkowskiMatrix]
  simp [Fintype.sum_sum_type, scalarPotential, vectorPotential, LorentzCurrentDensity.chargeDensity,
    LorentzCurrentDensity.currentDensity, timeSlice]
  field_simp
  ring

/-!

### A.4. The variational gradient of the free current potential

-/

/-- The variational gradient of the free current potential. -/
noncomputable def gradFreeCurrentPotential {d} (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) : SpaceTime d â†’ Lorentz.Vector d :=
  (Î´ (q':=A), âˆ« x, freeCurrentPotential q' J x)

lemma gradFreeCurrentPotential_eq_sum_basis {d} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) :
    A.gradFreeCurrentPotential J = (âˆ‘ Î¼, fun x => (Î· Î¼ Î¼ * J x Î¼) â€¢ Lorentz.Vector.basis Î¼) := by
  apply HasVarGradientAt.varGradient
  apply freeCurrentPotential_hasVarGradientAt A hA J hJ

lemma gradFreeCurrentPotential_eq_chargeDensity_currentDensity {d}
    (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (x : SpaceTime d) :
    A.gradFreeCurrentPotential J x =
      (ğ“•.c * J.chargeDensity ğ“•.c (x.time ğ“•.c) x.space) â€¢ Lorentz.Vector.basis (Sum.inl 0) +
      (âˆ‘ i, - J.currentDensity ğ“•.c (x.time ğ“•.c) x.space i â€¢ Lorentz.Vector.basis (Sum.inr i)) := by
  rw [gradFreeCurrentPotential_eq_sum_basis A hA J hJ]
  rw [Fintype.sum_sum_type]
  simp only [Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Finset.sum_singleton,
    inl_0_inl_0, one_mul, inr_i_inr_i, neg_mul, _root_.neg_smul, Pi.add_apply, Finset.sum_apply,
    Finset.sum_neg_distrib]
  congr
  Â· simp [LorentzCurrentDensity.chargeDensity]
  Â· simp [LorentzCurrentDensity.currentDensity]

lemma gradFreeCurrentPotential_eq_tensor {d} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (x : SpaceTime d) (Î½ : Fin 1 âŠ• Fin d) :
    A.gradFreeCurrentPotential J x Î½ = Î· Î½ Î½ * ((Tensorial.toTensor (M := Lorentz.Vector d)).symm
    (permT id (PermCond.auto) {J x | Î½'}áµ€)) Î½ := by
  trans Î· Î½ Î½ * (Lorentz.Vector.basis.repr ((Tensorial.toTensor (M := Lorentz.Vector d)).symm
    (permT id (PermCond.auto) {J x | Î½'}áµ€))) Î½
  swap
  Â· simp [Lorentz.Vector.basis_repr_apply]
  simp [Lorentz.Vector.basis_repr_apply]
  rw [gradFreeCurrentPotential_eq_sum_basis A hA J hJ]
  simp [Lorentz.Vector.apply_sum]

/-!

## B. The Lagrangian density

The lagrangian density for the electromagnetic field in presence of a current density `J` is
`L = -1/(4 Î¼â‚€) F_{Î¼Î½} F^{Î¼Î½} - A_Î¼ J^Î¼`

-/

/-- The lagrangian density associated with a electromagnetic potential and a Lorentz
  current density. -/
noncomputable def lagrangian (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) : â„ :=
  A.kineticTerm ğ“• x - A.freeCurrentPotential J x

/-!

### B.1. Shifts in the lagrangian under shifts in the potential

-/

lemma lagrangian_add_const {d} {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) (c : Lorentz.Vector d) (x : SpaceTime d) :
    lagrangian ğ“• (fun x => A x + c) J x = lagrangian ğ“• A J x - âŸªc, J xâŸ«â‚˜ := by
  rw [lagrangian, lagrangian, kineticTerm_add_const, freeCurrentPotential_add_const]
  ring

/-!

### B.2. Lagrangian in terms of electric and magnetic fields

-/

/-- The Lagrangian is equal to `1/2 * (Îµâ‚€ E^2 - 1/Î¼â‚€ B^2) - Ï†Ï + A Â· j`-/
lemma lagrangian_eq_electric_magnetic {d} {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d) (hA : ContDiff â„ 2 A)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) :
    A.lagrangian ğ“• J x = 1 / 2 * (ğ“•.Îµâ‚€ * â€–A.electricField ğ“•.c (x.time ğ“•.c) x.spaceâ€– ^ 2 -
    (1 / (2 * ğ“•.Î¼â‚€)) * âˆ‘ i, âˆ‘ j, â€–A.magneticFieldMatrix ğ“•.c (x.time ğ“•.c) x.space (i, j)â€– ^ 2)
    - A.scalarPotential ğ“•.c (x.time ğ“•.c) x.space * J.chargeDensity ğ“•.c (x.time ğ“•.c) x.space
    + âˆ‘ i, A.vectorPotential ğ“•.c (x.time ğ“•.c) x.space i *
        J.currentDensity ğ“•.c (x.time ğ“•.c) x.space i := by
  rw [lagrangian]
  rw[kineticTerm_eq_electricMatrix_magneticFieldMatrix _ _ (hA.differentiable (by simp))]
  rw [freeCurrentPotential_eq_sum_scalarPotential_vectorPotential ğ“• A J x]
  ring

/-!

## C. The variational gradient of the lagrangian density
-/

/-!

### C.1. The lagrangian density has a variational gradient

-/

lemma lagrangian_hasVarGradientAt_eq_add_gradKineticTerm {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d) (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) :
    HasVarGradientAt (fun A => lagrangian ğ“• A J)
    (A.gradKineticTerm ğ“• - A.gradFreeCurrentPotential J) A := by
  conv =>
    enter [1, q', x]
    rw [lagrangian]
  apply HasVarGradientAt.add
  Â· exact A.kineticTerm_hasVarGradientAt hA
  apply HasVarGradientAt.neg
  convert freeCurrentPotential_hasVarGradientAt A hA J hJ
  rw [â† gradFreeCurrentPotential_eq_sum_basis A hA J hJ]

/-!

### C.2. The definition of, `gradLagrangian`, the variational gradient of the lagrangian density

-/

/-- The variational gradient of the lagrangian of electromagnetic field. -/
noncomputable def gradLagrangian {d} (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) : SpaceTime d â†’ Lorentz.Vector d :=
  (Î´ (q':=A), âˆ« x, lagrangian ğ“• q' J x)

/-!

### C.3. The variational gradient in terms of the gradient of the kinetic term

-/

lemma gradLagrangian_eq_kineticTerm_sub {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) :
    A.gradLagrangian ğ“• J = A.gradKineticTerm ğ“• - A.gradFreeCurrentPotential J := by
  apply HasVarGradientAt.varGradient
  apply lagrangian_hasVarGradientAt_eq_add_gradKineticTerm A hA J hJ

/-!

### C.4. The lagrangian density has the variational gradient equal to `gradLagrangian`

-/
lemma lagrangian_hasVarGradientAt_gradLagrangian {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J) :
    HasVarGradientAt (fun A => lagrangian ğ“• A J) (A.gradLagrangian ğ“• J) A := by
  rw [gradLagrangian_eq_kineticTerm_sub A hA J hJ]
  apply lagrangian_hasVarGradientAt_eq_add_gradKineticTerm A hA J hJ

/-!

### C.5. The variational gradient in terms of the field strength tensor

-/

lemma gradLagrangian_eq_sum_fieldStrengthMatrix {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J) :
    A.gradLagrangian ğ“• J = fun x => âˆ‘ Î½,
      (Î· Î½ Î½ â€¢ (1 / ğ“•.Î¼â‚€ * âˆ‘ Î¼, âˆ‚_ Î¼ (fun x => (A.fieldStrengthMatrix x) (Î¼, Î½)) x - J x Î½)
      â€¢ Lorentz.Vector.basis Î½) := by
  rw [gradLagrangian_eq_kineticTerm_sub A hA J hJ]
  funext x
  simp only [Pi.sub_apply]
  rw [gradKineticTerm_eq_fieldStrength, gradFreeCurrentPotential_eq_sum_basis A hA J hJ]
  simp only [one_div, Finset.sum_apply]
  rw [â† Finset.sum_sub_distrib]
  refine Finset.sum_congr rfl (fun Î½ _ => ?_)
  rw [smul_smul, smul_smul, â† sub_smul]
  ring_nf
  exact hA

/-!

### C.6. The lagrangian gradient recovering Gauss's and AmpÃ¨re laws

-/

open Time LorentzCurrentDensity
lemma gradLagrangian_eq_electricField_magneticField {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (x : SpaceTime d) :
    A.gradLagrangian ğ“• J x = (1 / (ğ“•.Î¼â‚€ * ğ“•.c.val) *
        Space.div (electricField ğ“•.c A ((time ğ“•.c) x)) (space x) +
        - ğ“•.c * J.chargeDensity ğ“•.c (x.time ğ“•.c) x.space) â€¢
      Lorentz.Vector.basis (Sum.inl 0) +
    âˆ‘ i, (ğ“•.Î¼â‚€â»Â¹ * (ğ“•.Îµâ‚€ * ğ“•.Î¼â‚€ * âˆ‚â‚œ (electricField ğ“•.c A Â· x.space) ((time ğ“•.c) x) i -
      âˆ‘ j, âˆ‚[j] (magneticFieldMatrix ğ“•.c A (x.time ğ“•.c) Â· (j, i)) x.space) +
      J.currentDensity ğ“•.c (x.time ğ“•.c) x.space i) â€¢
        Lorentz.Vector.basis (Sum.inr i) := by
  rw [gradLagrangian_eq_kineticTerm_sub A hA J hJ]
  simp only [Pi.sub_apply, one_div, mul_inv_rev, neg_mul, Fin.isValue]
  rw [gradKineticTerm_eq_electric_magnetic _ _ hA]
  rw [gradFreeCurrentPotential_eq_chargeDensity_currentDensity ğ“• A hA J hJ x]
  conv_lhs =>
    enter [2]
    rw [add_comm]
  rw [add_sub_assoc]
  conv_lhs =>
    enter [2]
    rw [sub_add_eq_sub_sub]
    rw [â† Finset.sum_sub_distrib]
    rw [â† neg_add_eq_sub]
  rw [â† add_assoc]
  conv_lhs =>
    enter [1, 2]
    rw [â† _root_.neg_smul]
  rw [â† add_smul]
  conv_lhs =>
    enter [2, 2, i]
    rw [â† sub_smul]
    simp [FreeSpace.c_sq]
  ring_nf

/-!

### C.7. The lagrangian gradient in tensor notation

-/

lemma gradLagrangian_eq_tensor {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (x : SpaceTime d) (Î½ : Fin 1 âŠ• Fin d) :
    A.gradLagrangian ğ“• J x Î½ =
    Î· Î½ Î½ * ((Tensorial.toTensor (M := Lorentz.Vector d)).symm
    (permT id (PermCond.auto) {((1/ ğ“•.Î¼â‚€ : â„) â€¢ tensorDeriv A.toFieldStrength x | Îº Îº Î½') +
    - (J x | Î½')}áµ€)) Î½ := by
  rw [gradLagrangian_eq_kineticTerm_sub _ hA _ hJ]
  simp only [Pi.sub_apply, apply_sub, Nat.reduceSucc, Nat.reduceAdd, Fin.isValue, one_div, map_smul,
    map_neg, map_add, permT_permT, CompTriple.comp_eq, apply_add, apply_smul, neg_apply]
  rw [gradKineticTerm_eq_tensorDeriv A x hA]
  rw [gradFreeCurrentPotential_eq_tensor A hA J hJ x Î½]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Fin.isValue, one_div, map_smul, apply_smul,
    permT_id_self, LinearEquiv.symm_apply_apply]
  ring_nf
  congr
  rw [permT_congr_eq_id]
  simp only [LinearEquiv.symm_apply_apply]
  funext i
  fin_cases i
  simp

end ElectromagneticPotential

/-!

## D. The gradient of the lagrangian density for distributions

-/

namespace DistElectromagneticPotential
open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace
open Lorentz.Vector SchwartzMap
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one
/-!

### D.1. The gradient of the free current potential

We define this through the lemma `gradFreeCurrentPotential_eq_sum_basis`
-/

/-- The variational gradient of the free current potential for distributional potentials. -/
noncomputable def gradFreeCurrentPotential {d} :
    DistLorentzCurrentDensity d â†’â‚—[â„] ((SpaceTime d) â†’d[â„] Lorentz.Vector d) where
  toFun J := {
    toFun Îµ := âˆ‘ Î¼, (Î· Î¼ Î¼ â€¢ (J Îµ Î¼) â€¢ Lorentz.Vector.basis Î¼)
    map_add' Îµâ‚ Îµâ‚‚ := by
      simp [Finset.sum_add_distrib, add_smul]
    map_smul' r Îµ := by
      simp only [map_smul, apply_smul, smul_smul, Real.ringHom_apply, Finset.smul_sum]
      congr
      funext i
      ring_nf
    cont := by fun_prop
  }
  map_add' Jâ‚ Jâ‚‚ := by
    ext Îµ
    simp [Finset.sum_add_distrib, add_smul]
  map_smul' r J := by
    ext Îµ
    simp [Finset.smul_sum, smul_smul]
    congr
    funext i
    ring_nf

lemma gradFreeCurrentPotential_sum_inl_0 (ğ“• : FreeSpace) {d}
    (J : DistLorentzCurrentDensity d) (Îµ : ğ“¢(SpaceTime d, â„)) :
    (gradFreeCurrentPotential J) Îµ (Sum.inl 0) =
    ğ“•.c * (distTimeSlice ğ“•.c).symm (J.chargeDensity ğ“•.c) Îµ := by
  simp only [gradFreeCurrentPotential, LinearMap.coe_mk, AddHom.coe_mk, Fin.isValue,
    ContinuousLinearMap.coe_mk', apply_sum, apply_smul, Lorentz.Vector.basis_apply, mul_ite,
    mul_one, mul_zero, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, inl_0_inl_0, one_mul,
    DistLorentzCurrentDensity.chargeDensity, one_div, temporalCLM, map_smul,
    ContinuousLinearMap.coe_smul', Pi.smul_apply, distTimeSlice_symm_apply,
    ContinuousLinearMap.coe_comp', LinearMap.coe_toContinuousLinearMap', Function.comp_apply,
    smul_eq_mul, ne_eq, SpeedOfLight.val_ne_zero, not_false_eq_true, mul_inv_cancel_leftâ‚€]
  rw [â† distTimeSlice_symm_apply]
  simp

lemma gradFreeCurrentPotential_sum_inr_i (ğ“• : FreeSpace) {d}
    (J : DistLorentzCurrentDensity d) (Îµ : ğ“¢(SpaceTime d, â„)) (i : Fin d) :
    (gradFreeCurrentPotential J) Îµ (Sum.inr i) =
    - (distTimeSlice ğ“•.c).symm (J.currentDensity ğ“•.c) Îµ i := by
  simp only [gradFreeCurrentPotential, LinearMap.coe_mk, AddHom.coe_mk, ContinuousLinearMap.coe_mk',
    apply_sum, apply_smul, Lorentz.Vector.basis_apply, mul_ite, mul_one, mul_zero,
    Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, inr_i_inr_i,
    DistLorentzCurrentDensity.currentDensity, spatialCLM, distTimeSlice_symm_apply,
    ContinuousLinearMap.coe_comp', Function.comp_apply]
  rw [â† distTimeSlice_symm_apply]
  simp
/-!

### D.2. The gradient of the lagrangian density

Defined through `gradLagrangian_eq_kineticTerm_sub`.

-/

/-- The variational gradient of lagrangian for an electromagnetic potential which is
  a distribution. -/
noncomputable def gradLagrangian {d} (ğ“• : FreeSpace) (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) : ((SpaceTime d) â†’d[â„] Lorentz.Vector d) :=
  A.gradKineticTerm ğ“• - gradFreeCurrentPotential J

lemma gradLagrangian_sum_inl_0 {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d) (J : DistLorentzCurrentDensity d)
    (Îµ : ğ“¢(SpaceTime d, â„)) :
    A.gradLagrangian ğ“• J Îµ (Sum.inl 0) =
    (1/(ğ“•.Î¼â‚€ * ğ“•.c) * (distTimeSlice ğ“•.c).symm (Space.distSpaceDiv (A.electricField ğ“•.c)) Îµ)
    - ğ“•.c * (distTimeSlice ğ“•.c).symm (J.chargeDensity ğ“•.c) Îµ := by
  simp [gradLagrangian, gradKineticTerm_sum_inl_eq, gradFreeCurrentPotential_sum_inl_0 ğ“•]

lemma gradLagrangian_sum_inr_i {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d) (J : DistLorentzCurrentDensity d)
    (Îµ : ğ“¢(SpaceTime d, â„)) (i : Fin d) :
    A.gradLagrangian ğ“• J Îµ (Sum.inr i) =
    ğ“•.Î¼â‚€â»Â¹ * (1 / ğ“•.c ^ 2 *
      (distTimeSlice ğ“•.c).symm (Space.distTimeDeriv (A.electricField ğ“•.c)) Îµ i -
      âˆ‘ j, ((PiLp.basisFun 2 â„ (Fin d)).tensorProduct (PiLp.basisFun 2 â„ (Fin d))).repr
        ((distTimeSlice ğ“•.c).symm (Space.distSpaceDeriv j (A.magneticFieldMatrix ğ“•.c)) Îµ) (j, i)) +
    (distTimeSlice ğ“•.c).symm (J.currentDensity ğ“•.c) Îµ i := by
  simp [gradLagrangian, gradKineticTerm_sum_inr_eq, gradFreeCurrentPotential_sum_inr_i ğ“•]

end DistElectromagneticPotential
end Electromagnetism
