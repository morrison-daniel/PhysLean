/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Dynamics.Lagrangian
/-!

# Extrema of the Lagrangian density

## i. Overview

In this module we define what it means for an electromagnetic potential
to be an extremum of the Lagrangian density in presence of a Lorentz current density.

This is equivalent to the electromagnetic potential satisfying
Maxwell's equations with sources, i.e. Gauss's law and AmpÃ¨re's law.

## ii. Key results

- `IsExtrema` : The condition on an electromagnetic potential to be an extrema of the lagrangian.
- `isExtrema_iff_gauss_ampere_magneticFieldMatrix` : The electromagnetic potential is an extrema
  of the lagrangian if and only if Gauss's law and AmpÃ¨re's law hold
  (in terms of the magnetic field matrix).
- `time_deriv_time_deriv_magneticFieldMatrix_of_isExtrema` : A wave-like equation for the
  magnetic field matrix from the extrema condition.
- `time_deriv_time_deriv_electricField_of_isExtrema` : A wave-like equation for the
  electric field from the extrema condition.

## iii. Table of contents

- A. The condition for an extrema of the Lagrangian density
  - A.1. Extrema condition in terms of the field strength matrix
  - A.2. Extrema condition in terms of tensors
  - A.3. Equivariance of the extrema condition
- B. Gauss's law and AmpÃ¨re's law and the extrema condition
- C. Time derivatives from the extrema condition
- D. Second time derivatives from the extrema condition
  - D.1. Second time derivatives of the magnetic field from the extrema condition
  - D.2. Second time derivatives of the electric field from the extrema condition
- E. Is Extema condition in the distributional case
  - E.1. IsExtrema and Gauss's law and AmpÃ¨re's law
  - E.2. IsExtrema in terms of Vector Potentials
  - E.3. The exterma condition in terms of tensors
  - E.4. The invariance of the exterma condition under Lorentz transformations

## iv. References

-/
namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor ContDiff

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace
open Lorentz.Vector
open Time Space
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. The condition for an extrema of the Lagrangian density

-/

/-- The condition on an electromagnetic potential to be an extrema of the lagrangian. -/
def IsExtrema {d} (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) : Prop :=
  gradLagrangian ğ“• A J = 0

lemma isExtrema_iff_gradLagrangian {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†” A.gradLagrangian ğ“• J = 0 := by rfl

/-!

### A.1. Extrema condition in terms of the field strength matrix

-/

lemma isExtrema_iff_fieldStrengthMatrix {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J) :
    IsExtrema ğ“• A J â†”
    âˆ€ x, âˆ€ Î½, âˆ‘ Î¼, âˆ‚_ Î¼ (A.fieldStrengthMatrix Â· (Î¼, Î½)) x = ğ“•.Î¼â‚€ * J x Î½ := by
  rw [isExtrema_iff_gradLagrangian, gradLagrangian_eq_sum_fieldStrengthMatrix A hA J hJ, funext_iff]
  conv_lhs =>
    enter [x, 1, 2, Î½]
    rw [smul_smul]
  conv_lhs =>
    enter [x]
    simp only [one_div, Pi.zero_apply]
    rw [Lorentz.Vector.sum_basis_eq_zero_iff]
  apply Iff.intro
  Â· intro h x Î½
    specialize h x Î½
    simp at h
    have h' : Î· Î½ Î½ â‰  0 := Î·_diag_ne_zero
    simp_all
    linear_combination (norm := field_simp) ğ“•.Î¼â‚€ * h
    ring
  Â· intro h x Î½
    specialize h x Î½
    simp only [mul_eq_zero]
    right
    linear_combination (norm := field_simp) ğ“•.Î¼â‚€â»Â¹ * h
    ring

/-!

### A.2. Extrema condition in terms of tensors

The electromagnetic potential is an exterma of the lagrangian if and only if

$$\frac{1}{\mu_0} \partial_\mu F^{\mu \nu} - J^{\nu} = 0.$$

-/

lemma isExtrema_iff_tensors {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J) :
    IsExtrema ğ“• A J â†” âˆ€ x,
    {((1/ ğ“•.Î¼â‚€ : â„) â€¢ tensorDeriv A.toFieldStrength x | Îº Îº Î½') + - (J x | Î½')}áµ€ = 0 := by
  apply Iff.intro
  Â· intro h
    simp only [IsExtrema] at h
    intro x
    have h1 : ((Tensorial.toTensor (M := Lorentz.Vector d)).symm
        (permT id (PermCond.auto) {((1/ ğ“•.Î¼â‚€ : â„) â€¢ tensorDeriv A.toFieldStrength x | Îº Îº Î½') +
        - (J x | Î½')}áµ€)) = 0 := by
      funext Î½
      have h2 : gradLagrangian ğ“• A J x Î½ = 0 := by simp [h]
      rw [gradLagrangian_eq_tensor A hA J hJ] at h2
      simp only [Nat.reduceSucc, Nat.reduceAdd, Fin.isValue, one_div, map_smul, map_neg, map_add,
        permT_permT, CompTriple.comp_eq, apply_add, apply_smul, Lorentz.Vector.neg_apply,
        mul_eq_zero] at h2
      have hn : Î· Î½ Î½ â‰  0 := Î·_diag_ne_zero
      simp_all only [Fin.isValue, false_or, ne_eq, Nat.reduceSucc, Nat.reduceAdd, one_div, map_smul,
        map_neg, map_add, permT_permT, CompTriple.comp_eq, apply_add, apply_smul,
        Lorentz.Vector.neg_apply, Lorentz.Vector.zero_apply]
    generalize {((1/ ğ“•.Î¼â‚€ : â„) â€¢ tensorDeriv A.toFieldStrength x | Îº Îº Î½') +
        - (J x | Î½')}áµ€ = V at *
    simp only [Nat.reduceSucc, Nat.reduceAdd, Fin.isValue, EmbeddingLike.map_eq_zero_iff] at h1
    rw [permT_eq_zero_iff] at h1
    exact h1
  Â· intro h
    simp only [IsExtrema]
    funext x
    funext Î½
    rw [gradLagrangian_eq_tensor A hA J hJ, h]
    simp only [map_zero, Lorentz.Vector.zero_apply, mul_zero, Pi.zero_apply]

/-!

### A.3. Equivariance of the extrema condition

If `A` is an extrema of the lagrangian with current density `J`, then the Lorentz transformation
`Î› â€¢ A (Î›â»Â¹ â€¢ x)` is an extrema of the lagrangian with current density `Î› â€¢ J (Î›â»Â¹ â€¢ x)`.

Combined with `time_deriv_time_deriv_electricField_of_isExtrema`, this shows that
the speed with which an electromagnetic wave propagates is invariant under Lorentz transformations.

-/

lemma isExtrema_lorentzGroup_apply_iff {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d)
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J)
    (Î› : LorentzGroup d) :
    IsExtrema ğ“• (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) (fun x => Î› â€¢ J (Î›â»Â¹ â€¢ x)) â†”
    IsExtrema ğ“• A J := by
  rw [isExtrema_iff_tensors]
  conv_lhs =>
    enter [x, 1, 1, 2, 2, 2]
    change tensorDeriv (fun x => toFieldStrength (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x) x
    enter [1,x]
    rw [toFieldStrength_equivariant _ _ (hA.differentiable (by simp))]
  conv_lhs =>
    enter [x]
    rw [tensorDeriv_equivariant _ _ _ (by
      apply toFieldStrength_differentiable
      exact hA.of_le ENat.LEInfty.out)]
    rw [smul_comm]
    rw [Tensorial.toTensor_smul, Tensorial.toTensor_smul]
    simp only [Nat.reduceAdd, Nat.reduceSucc, Fin.isValue, one_div, map_smul, actionT_smul,
      contrT_equivariant, map_neg, permT_equivariant]
    rw [smul_comm, â† Tensor.actionT_neg, â† Tensor.actionT_add]
  apply Iff.intro
  Â· intro h
    rw [isExtrema_iff_tensors A hA J hJ]
    intro x
    apply MulAction.injective Î›
    simp only [Nat.reduceAdd, Nat.reduceSucc, Fin.isValue, one_div, map_smul, map_neg,
      _root_.smul_add, actionT_smul, _root_.smul_neg, _root_.smul_zero]
    simpa using h (Î› â€¢ x)
  Â· intro h x
    rw [isExtrema_iff_tensors A hA J hJ] at h
    specialize h (Î›â»Â¹ â€¢ x)
    simp at h
    rw [h]
    simp
  Â· change ContDiff â„ âˆ (actionCLM Î› âˆ˜ A âˆ˜ actionCLM Î›â»Â¹)
    apply ContDiff.comp
    Â· exact ContinuousLinearMap.contDiff (actionCLM Î›)
    Â· apply ContDiff.comp
      Â· exact hA
      Â· exact ContinuousLinearMap.contDiff (actionCLM Î›â»Â¹)
  Â· change ContDiff â„ âˆ (actionCLM Î› âˆ˜ J âˆ˜ actionCLM Î›â»Â¹)
    apply ContDiff.comp
    Â· exact ContinuousLinearMap.contDiff (actionCLM Î›)
    Â· apply ContDiff.comp
      Â· exact hJ
      Â· exact ContinuousLinearMap.contDiff (actionCLM Î›â»Â¹)

/-!

## B. Gauss's law and AmpÃ¨re's law and the extrema condition

-/

lemma isExtrema_iff_gauss_ampere_magneticFieldMatrix {d} {ğ“• : FreeSpace}
    {A : ElectromagneticPotential d}
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) :
    IsExtrema ğ“• A J â†” âˆ€ t, âˆ€ x, (âˆ‡ â¬ (A.electricField ğ“•.c t)) x = J.chargeDensity ğ“•.c t x / ğ“•.Îµâ‚€
    âˆ§ âˆ€ i, ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€ * âˆ‚â‚œ (fun t => A.electricField ğ“•.c t x) t i =
    âˆ‘ j, âˆ‚[j] (A.magneticFieldMatrix ğ“•.c t Â· (j, i)) x - ğ“•.Î¼â‚€ * J.currentDensity ğ“•.c t x i := by
  rw [isExtrema_iff_gradLagrangian]
  rw [funext_iff]
  conv_lhs =>
    enter [x]
    rw [gradLagrangian_eq_electricField_magneticField (ğ“• := ğ“•) A hA J hJ]
    simp only [Pi.zero_apply]
    rw [Lorentz.Vector.sum_inl_inr_basis_eq_zero_iff]
  simp only [forall_and]
  apply and_congr
  Â· apply Iff.intro
    Â· intro h t x
      specialize h ((toTimeAndSpace ğ“•.c).symm (t, x))
      simp at h
      linear_combination (norm := simp) (ğ“•.Î¼â‚€ * ğ“•.c) * h
      field_simp
      simp only [FreeSpace.c_sq, one_div, mul_inv_rev, mul_zero]
      field_simp
      ring
    Â· intro h x
      specialize h (x.time ğ“•.c) x.space
      linear_combination (norm := simp) (ğ“•.Î¼â‚€â»Â¹ * ğ“•.câ»Â¹) * h
      field_simp
      simp only [FreeSpace.c_sq, one_div, mul_inv_rev, mul_zero]
      field_simp
      ring
  Â· apply Iff.intro
    Â· intro h t x i
      specialize h ((toTimeAndSpace ğ“•.c).symm (t, x)) i
      simp at h
      linear_combination (norm := simp) (ğ“•.Î¼â‚€) * h
      field_simp
      simp
    Â· intro h x i
      specialize h (x.time ğ“•.c) x.space i
      linear_combination (norm := simp) (ğ“•.Î¼â‚€â»Â¹) * h
      field_simp
      simp

/-!

## C. Time derivatives from the extrema condition

-/

lemma time_deriv_electricField_of_isExtrema {A : ElectromagneticPotential d}
    {ğ“• : FreeSpace}
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d) (hJ : ContDiff â„ âˆ J)
    (h : IsExtrema ğ“• A J) (t : Time) (x : Space d) (i : Fin d) :
    âˆ‚â‚œ (A.electricField ğ“•.c Â· x) t i =
      1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, âˆ‚[j] (A.magneticFieldMatrix ğ“•.c t Â· (j, i)) x -
      (1/ ğ“•.Îµâ‚€) * J.currentDensity ğ“•.c t x i := by
  rw [isExtrema_iff_gauss_ampere_magneticFieldMatrix hA J hJ] at h
  linear_combination (norm := simp) (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€)â»Â¹ * ((h t x).2 i)
  field_simp
  ring

/-!

## D. Second time derivatives from the extrema condition

-/

/-!

### D.1. Second time derivatives of the magnetic field from the extrema condition

We show that the magnetic field matrix $B_{ij}$ satisfies the following wave-like equation

$$\frac{\partial^2 B_{ij}}{\partial t^2} = c^2 \sum_k \frac{\partial^2 B_{ij}}{\partial x_k^2}
  + \frac{1}{\epsilon_0} \left( \frac{\partial J_i}{\partial x_j} -
  \frac{\partial J_j}{\partial x_i} \right).$$

When the free current density is zero, this reduces to the wave equation.
-/

lemma time_deriv_time_deriv_magneticFieldMatrix_of_isExtrema {A : ElectromagneticPotential d}
    {ğ“• : FreeSpace}
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (h : IsExtrema ğ“• A J)
    (t : Time) (x : Space d) (i j : Fin d) :
    âˆ‚â‚œ (âˆ‚â‚œ (A.magneticFieldMatrix ğ“•.c Â· x (i, j))) t =
    ğ“•.c ^ 2 * âˆ‘ k, âˆ‚[k] (âˆ‚[k] (A.magneticFieldMatrix ğ“•.c t Â· (i, j))) x +
    ğ“•.Îµâ‚€â»Â¹ * (âˆ‚[j] (J.currentDensity ğ“•.c t Â· i) x - âˆ‚[i] (J.currentDensity ğ“•.c t Â· j) x) := by
  rw [time_deriv_time_deriv_magneticFieldMatrix A (hA.of_le (ENat.LEInfty.out))]
  conv_lhs =>
    enter [2, 2, x]
    rw [time_deriv_electricField_of_isExtrema hA J hJ h]
  conv_lhs =>
    enter [1, 2, x]
    rw [time_deriv_electricField_of_isExtrema hA J hJ h]
  rw [Space.deriv_eq_fderiv_basis]
  rw [fderiv_fun_sub (by
        apply Differentiable.const_mul
        apply Differentiable.fun_sum
        intro i _
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))
          ((LorentzCurrentDensity.currentDensity_apply_differentiable_space
          (hJ.differentiable (ENat.LEInfty.out)) _ _).const_mul _).differentiableAt,
    fderiv_const_mul (by
        apply Differentiable.fun_sum
        intro i _
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl))),
    fderiv_const_mul (by
        apply (LorentzCurrentDensity.currentDensity_apply_differentiable_space
        (hJ.differentiable (ENat.LEInfty.out)) _ _).differentiableAt),
    fderiv_fun_sum fun i _ => by
        apply Differentiable.differentiableAt
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl))]
  conv_lhs =>
    enter [2]
    rw [Space.deriv_eq_fderiv_basis]
    rw [fderiv_fun_sub (by
        apply Differentiable.const_mul
        apply Differentiable.fun_sum
        intro i _
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))
          ((LorentzCurrentDensity.currentDensity_apply_differentiable_space
          (hJ.differentiable (ENat.LEInfty.out)) _ _).const_mul _).differentiableAt,
    fderiv_const_mul (by
        apply Differentiable.fun_sum
        intro i _
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl))),
    fderiv_const_mul (by
        apply (LorentzCurrentDensity.currentDensity_apply_differentiable_space
        (hJ.differentiable (ENat.LEInfty.out)) _ _).differentiableAt),
    fderiv_fun_sum fun i _ => by
        apply Differentiable.differentiableAt
        apply Space.deriv_differentiable
        apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl))]
  simp [â† Space.deriv_eq_fderiv_basis, FreeSpace.c_sq]
  field_simp
  conv_rhs =>
    enter [1, 2, k, 2, x]
    rw [magneticFieldMatrix_space_deriv_eq A (hA.of_le (right_eq_inf.mp rfl))]
  conv_rhs =>
    enter [1, 2, k]
    rw [Space.deriv_eq_fderiv_basis]
    rw [fderiv_fun_sub (by
      apply Space.deriv_differentiable
      apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))
      (by
      apply Space.deriv_differentiable
      apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))]
    simp [â† Space.deriv_eq_fderiv_basis]
    rw [Space.deriv_commute _ (by
      apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))]
    enter [2]
    rw [Space.deriv_commute _ (by
      apply magneticFieldMatrix_space_contDiff _ (hA.of_le (right_eq_inf.mp rfl)))]
  simp only [Finset.sum_sub_distrib]
  ring

/-!

### D.2. Second time derivatives of the electric field from the extrema condition

We show that the electric field $E_i$ satisfies the following wave-like equation:

$$\frac{\partial^2 E_{i}}{\partial t^2} = c^2 \sum_k \frac{\partial^2 E_{i}}{\partial x_k^2}
  - \frac{c ^ 2}{\epsilon_0} \frac{\partial \rho}{\partial x_i}
  - c ^ 2 Î¼_0 \frac{\partial J_i}{\partial t}.$$

When the free current density and charge density are zero, this reduces to the wave equation.

-/

lemma time_deriv_time_deriv_electricField_of_isExtrema {A : ElectromagneticPotential d}
    {ğ“• : FreeSpace}
    (hA : ContDiff â„ âˆ A) (J : LorentzCurrentDensity d)
    (hJ : ContDiff â„ âˆ J) (h : IsExtrema ğ“• A J)
    (t : Time) (x : Space d) (i : Fin d) :
    âˆ‚â‚œ (âˆ‚â‚œ (A.electricField ğ“•.c Â· x i)) t =
      ğ“•.c ^ 2 * âˆ‘ j, (âˆ‚[j] (âˆ‚[j] (A.electricField ğ“•.c t Â· i)) x) -
      ğ“•.c ^ 2 / ğ“•.Îµâ‚€ * âˆ‚[i] (J.chargeDensity ğ“•.c t Â·) x -
      ğ“•.c ^ 2 * ğ“•.Î¼â‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
  calc _
    _= âˆ‚â‚œ (fun t =>
      1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, Space.deriv j (fun x => magneticFieldMatrix ğ“•.c A t x (j, i)) x -
      1 / ğ“•.Îµâ‚€ * LorentzCurrentDensity.currentDensity ğ“•.c J t x i) t := by
        conv_lhs =>
          enter [1]
          change fun t => âˆ‚â‚œ (A.electricField ğ“•.c Â· x i) t
          enter [t]
          rw [Time.deriv_euclid (electricField_differentiable_time
            (hA.of_le (right_eq_inf.mp rfl)) _),
            time_deriv_electricField_of_isExtrema hA J hJ h]
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‚â‚œ (fun t => âˆ‘ j, âˆ‚[j] (A.magneticFieldMatrix ğ“•.c t Â· (j, i)) x) t -
      1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
      rw [Time.deriv_eq]
      rw [fderiv_fun_sub]
      simp only [one_div, mul_inv_rev, ContinuousLinearMap.coe_sub', Pi.sub_apply]
      rw [fderiv_const_mul (by
        apply Differentiable.fun_sum
        intro j _
        apply Space.space_deriv_differentiable_time
        apply magneticFieldMatrix_contDiff
        apply hA.of_le (right_eq_inf.mp rfl))]
      rw [fderiv_const_mul (by
        apply Differentiable.differentiableAt
        apply LorentzCurrentDensity.currentDensity_apply_differentiable_time
        exact hJ.differentiable (by simp))]
      simp [Time.deriv_eq]
      Â· apply Differentiable.const_mul
        apply Differentiable.fun_sum
        intro j _
        apply Space.space_deriv_differentiable_time
        apply magneticFieldMatrix_contDiff
        apply hA.of_le (right_eq_inf.mp rfl)
      Â· apply DifferentiableAt.const_mul
        apply Differentiable.differentiableAt
        apply LorentzCurrentDensity.currentDensity_apply_differentiable_time
        exact hJ.differentiable (by simp)
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * ((âˆ‘ j, âˆ‚â‚œ (fun t => âˆ‚[j] (A.magneticFieldMatrix ğ“•.c t Â· (j, i)) x)) t) -
      1 / ğ“•.Îµâ‚€ * (âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t) := by
      congr
      rw [Time.deriv_eq]
      rw [fderiv_fun_sum]
      simp only [ContinuousLinearMap.coe_sum', Finset.sum_apply]
      rfl
      intro i _
      apply Differentiable.differentiableAt
      apply Space.space_deriv_differentiable_time
      apply magneticFieldMatrix_contDiff
      apply hA.of_le (right_eq_inf.mp rfl)
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * (âˆ‘ j, âˆ‚[j] (fun x => âˆ‚â‚œ (A.magneticFieldMatrix ğ“•.c Â· x (j, i)) t)) x -
        1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
      congr
      simp only [Finset.sum_apply]
      congr
      funext k
      rw [Space.time_deriv_comm_space_deriv]
      apply magneticFieldMatrix_contDiff
      apply hA.of_le (right_eq_inf.mp rfl)
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) *(âˆ‘ j, âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x -
        âˆ‚[i] (A.electricField ğ“•.c t Â· j) x)) x -
        1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        congr
        simp only [Finset.sum_apply]
        congr
        funext k
        congr
        funext x
        rw [time_deriv_magneticFieldMatrix _ (hA.of_le (ENat.LEInfty.out))]
    _ = (1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x) x -
          âˆ‚[j] (fun x => âˆ‚[i] (A.electricField ğ“•.c t Â· j) x) x)) -
          1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        congr
        simp only [Finset.sum_apply]
        congr
        funext j
        rw [Space.deriv_eq_fderiv_basis]
        rw [fderiv_fun_sub]
        simp [â† Space.deriv_eq_fderiv_basis]
        all_goals
        Â· apply Differentiable.differentiableAt
          apply Space.deriv_differentiable
          apply electricField_apply_contDiff_space
          apply hA.of_le
          exact right_eq_inf.mp rfl
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x) x) -
          1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[i] (A.electricField ğ“•.c t Â· j) x) x) -
          1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by simp [mul_sub]
    _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x) x) -
        1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[i] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· j) x) x) -
        1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        congr
        funext j
        rw [Space.deriv_commute _ (by
          apply electricField_apply_contDiff_space
          apply hA.of_le
          exact right_eq_inf.mp rfl), Space.deriv_eq_fderiv_basis]
      _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x) x) -
          1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * (âˆ‚[i] (fun x => âˆ‘ j, âˆ‚[j] (A.electricField ğ“•.c t Â· j) x) x) -
          1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        congr
        rw [Space.deriv_eq_fderiv_basis]
        rw [fderiv_fun_sum]
        simp [â† Space.deriv_eq_fderiv_basis]
        intro i _
        apply Differentiable.differentiableAt
        apply Space.deriv_differentiable
        apply electricField_apply_contDiff_space
        apply hA.of_le
        exact right_eq_inf.mp rfl
      _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (fun x => âˆ‚[j] (A.electricField ğ“•.c t Â· i) x) x) -
          1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * (âˆ‚[i] (fun x => (âˆ‡ â¬ (A.electricField ğ“•.c t)) x) x) -
          1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        rfl
      _ = 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * âˆ‘ j, (âˆ‚[j] (âˆ‚[j] (A.electricField ğ“•.c t Â· i)) x) -
          1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€ ^ 2) * âˆ‚[i] (J.chargeDensity ğ“•.c t Â·) x -
          1 / ğ“•.Îµâ‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
        congr 2
        rw [isExtrema_iff_gauss_ampere_magneticFieldMatrix] at h

        conv_lhs =>
          enter [2, 2, x]
          rw [(h t x).1]
        trans 1 / (ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€) * Space.deriv i
            (fun x => (1/ ğ“•.Îµâ‚€) * LorentzCurrentDensity.chargeDensity ğ“•.c J t x) x
        Â· congr
          funext x
          ring
        Â· rw [Space.deriv_eq_fderiv_basis]
          rw [fderiv_const_mul]
          simp [â† Space.deriv_eq_fderiv_basis]
          field_simp
          apply Differentiable.differentiableAt
          apply LorentzCurrentDensity.chargeDensity_differentiable_space
          exact hJ.differentiable (by simp)
        Â· exact hA
        Â· exact hJ
      _ = ğ“•.c ^ 2 * âˆ‘ j, (âˆ‚[j] (âˆ‚[j] (A.electricField ğ“•.c t Â· i)) x) -
            ğ“•.c ^ 2 / ğ“•.Îµâ‚€ * âˆ‚[i] (J.chargeDensity ğ“•.c t Â·) x -
            ğ“•.c ^ 2 * ğ“•.Î¼â‚€ * âˆ‚â‚œ (J.currentDensity ğ“•.c Â· x i) t := by
          simp [FreeSpace.c_sq]
          field_simp

end ElectromagneticPotential

/-!

## E. Is Extema condition in the distributional case

The above results looked at the extrema condition for electromagnetic potentials that are
functions. We now look at the case where the electromagnetic potential is a distribution.

-/

namespace DistElectromagneticPotential

/-- The proposition on an electromagnetic potential, corresponding to the statement that
  it is an extrema of the lagrangian. -/
def IsExtrema {d} (ğ“• : FreeSpace)
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) : Prop := A.gradLagrangian ğ“• J = 0

lemma isExtrema_iff_gradLagrangian {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†” A.gradLagrangian ğ“• J = 0 := by rfl

lemma isExtrema_iff_components {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†” (âˆ€ Îµ, A.gradLagrangian ğ“• J Îµ (Sum.inl 0) = 0)
    âˆ§ (âˆ€ Îµ i, A.gradLagrangian ğ“• J Îµ (Sum.inr i) = 0) := by
  apply Iff.intro
  Â· intro h
    rw [isExtrema_iff_gradLagrangian] at h
    simp [h]
  Â· intro h
    rw [isExtrema_iff_gradLagrangian]
    ext Îµ
    funext i
    match i with
    | Sum.inl 0 => exact h.1 Îµ
    | Sum.inr j => exact h.2 Îµ j
/-!

### E.1. IsExtrema and Gauss's law and AmpÃ¨re's law

We show that `A` is an extrema of the lagrangian if and only if Gauss's law and AmpÃ¨re's law hold.
In other words,

$$\nabla \cdot \mathbf{E} = \frac{\rho}{\varepsilon_0}$$
and
$$\mu_0 \varepsilon_0 \frac{\partial \mathbf{E}_i}{\partial t} -
  âˆ‘ j, \partial_j \mathbf{B}_{j i} + \mu_0 \mathbf{J}_i = 0.$$
Here $\mathbf{B}$ is the magnetic field matrix.

-/
open Space
lemma isExtrema_iff_space_time {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†”
      (âˆ€ Îµ, distSpaceDiv (A.electricField ğ“•.c) Îµ = (1/ğ“•.Îµâ‚€) * (J.chargeDensity ğ“•.c) Îµ) âˆ§
      (âˆ€ Îµ i, ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€ * (Space.distTimeDeriv (A.electricField ğ“•.c)) Îµ i -
      âˆ‘ j, ((PiLp.basisFun 2 â„ (Fin d)).tensorProduct (PiLp.basisFun 2 â„ (Fin d))).repr
        ((Space.distSpaceDeriv j (A.magneticFieldMatrix ğ“•.c)) Îµ) (j, i) +
      ğ“•.Î¼â‚€ * J.currentDensity ğ“•.c Îµ i = 0) := by
  rw [isExtrema_iff_components]
  refine and_congr ?_ ?_
  Â· simp [gradLagrangian_sum_inl_0]
    field_simp
    simp [ğ“•.c_sq]
    field_simp
    simp [sub_eq_zero]
    apply Iff.intro
    Â· intro h Îµ
      convert h (SchwartzMap.compCLMOfContinuousLinearEquiv (F := â„) â„
        (SpaceTime.toTimeAndSpace ğ“•.c (d := d)) Îµ) using 1
      Â· simp [SpaceTime.distTimeSlice_symm_apply]
        ring_nf
        congr
        ext x
        simp
      Â· simp [SpaceTime.distTimeSlice_symm_apply]
        congr
        ext x
        simp
    Â· intro h Îµ
      convert h (SchwartzMap.compCLMOfContinuousLinearEquiv (F := â„) â„
        (SpaceTime.toTimeAndSpace ğ“•.c (d := d)).symm Îµ) using 1
      Â· simp [SpaceTime.distTimeSlice_symm_apply]
        ring_nf
  Â· apply Iff.intro
    Â· intro h Îµ i
      specialize h (SchwartzMap.compCLMOfContinuousLinearEquiv (F := â„) â„
        (SpaceTime.toTimeAndSpace ğ“•.c (d := d)) Îµ) i
      linear_combination (norm := field_simp) (ğ“•.Î¼â‚€) * h
      simp [gradLagrangian_sum_inr_i, SpaceTime.distTimeSlice_symm_apply]
      have hx : (SchwartzMap.compCLMOfContinuousLinearEquiv â„ (SpaceTime.toTimeAndSpace ğ“•.c).symm)
          ((SchwartzMap.compCLMOfContinuousLinearEquiv â„ (SpaceTime.toTimeAndSpace ğ“•.c)) Îµ)
          = Îµ := by
        ext i
        simp
      simp [hx, ğ“•.c_sq]
      field_simp
      ring
    Â· intro h Îµ i
      specialize h (SchwartzMap.compCLMOfContinuousLinearEquiv (F := â„) â„
        (SpaceTime.toTimeAndSpace ğ“•.c (d := d)).symm Îµ) i
      linear_combination (norm := field_simp) (ğ“•.Î¼â‚€â»Â¹) * h
      simp [gradLagrangian_sum_inr_i, SpaceTime.distTimeSlice_symm_apply, ğ“•.c_sq]
      field_simp
      ring

/-!

### E.2. IsExtrema in terms of Vector Potentials

We show that `A` is an extrema of the lagrangian if and only if Gauss's law and AmpÃ¨re's law hold.
In other words,

$$\nabla \cdot \mathbf{E} = \frac{\rho}{\varepsilon_0}$$
and
$$\mu_0 \varepsilon_0 \frac{\partial \mathbf{E}_i}{\partial t} -
  âˆ‘ j, -(\partial_j \partial_j \vec A_i - \partial_j \partial_i \vec A_j)
  + \mu_0 \mathbf{J}_i = 0.$$

-/

lemma isExtrema_iff_vectorPotential {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†”
      (âˆ€ Îµ, distSpaceDiv (A.electricField ğ“•.c) Îµ = (1/ğ“•.Îµâ‚€) * (J.chargeDensity ğ“•.c) Îµ) âˆ§
      (âˆ€ Îµ i, ğ“•.Î¼â‚€ * ğ“•.Îµâ‚€ * distTimeDeriv (A.electricField ğ“•.c) Îµ i -
      (âˆ‘ x, -(distSpaceDeriv x (distSpaceDeriv x (A.vectorPotential ğ“•.c)) Îµ i
        - distSpaceDeriv x (distSpaceDeriv i (A.vectorPotential ğ“•.c)) Îµ x)) +
      ğ“•.Î¼â‚€ * J.currentDensity ğ“•.c Îµ i = 0) := by
  rw [isExtrema_iff_space_time]
  refine and_congr (by rfl) ?_
  suffices âˆ€ Îµ i, âˆ‘ x, -(distSpaceDeriv x (distSpaceDeriv x (A.vectorPotential ğ“•.c)) Îµ i
        - distSpaceDeriv x (distSpaceDeriv i (A.vectorPotential ğ“•.c)) Îµ x) =
        âˆ‘ j, ((PiLp.basisFun 2 â„ (Fin d)).tensorProduct (PiLp.basisFun 2 â„ (Fin d))).repr
          ((Space.distSpaceDeriv j (A.magneticFieldMatrix ğ“•.c)) Îµ) (j, i) by
    conv_lhs => enter [2, 2]; rw [â† this]
  intro Îµ i
  congr
  funext j
  rw [magneticFieldMatrix_distSpaceDeriv_basis_repr_eq_vector_potential]
  ring

/-!

### E.3. The exterma condition in terms of tensors

We show that `A` is an extrema of the lagrangian if and only if the equation
$$(\frac{1}{\mu_0} \partial_\kappa F^{\kappa \nu'} - J^{\nu'}) = 0,$$
holds.

-/
open SpaceTime minkowskiMatrix
lemma isExterma_iff_tensor {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) :
    IsExtrema ğ“• A J â†” âˆ€ Îµ,
    {((1/ ğ“•.Î¼â‚€ : â„) â€¢ distTensorDeriv A.fieldStrength Îµ | Îº Îº Î½') + - (J Îµ | Î½')}áµ€ = 0 := by
  apply Iff.intro
  Â· intro h
    simp only [IsExtrema] at h
    intro x
    have h1 : ((Tensorial.toTensor (M := Lorentz.Vector d)).symm
        (permT id (PermCond.auto) {((1/ ğ“•.Î¼â‚€ : â„) â€¢ distTensorDeriv A.fieldStrength x | Îº Îº Î½') +
        - (J x | Î½')}áµ€)) = 0 := by
      funext Î½
      have h2 : gradLagrangian ğ“• A J x Î½ = 0 := by simp [h]
      rw [gradLagrangian_eq_tensor A J] at h2
      simp at h2
      have hn : minkowskiMatrix Î½ Î½ â‰  0 := minkowskiMatrix.Î·_diag_ne_zero
      simp_all
    rw [EmbeddingLike.map_eq_zero_iff, permT_eq_zero_iff] at h1
    exact h1
  Â· intro h
    simp only [IsExtrema]
    ext x
    funext Î½
    rw [gradLagrangian_eq_tensor A J, h]
    simp

/-!

### E.4. The invariance of the exterma condition under Lorentz transformations

We show that the Exterma condition is invariant under Lorentz transformations.
This implies that if an electromagnetic potential is an extrema in one inertial frame,
it is also an extrema in any other inertial frame.
In otherwords that the Maxwell's equations are Lorentz invariant.
A natural consequence of this is that the speed of light is the same in all inertial frames.

-/

lemma isExterma_equivariant {ğ“• : FreeSpace}
    (A : DistElectromagneticPotential d)
    (J : DistLorentzCurrentDensity d) (Î› : LorentzGroup d) :
    IsExtrema ğ“• (Î› â€¢ A) (Î› â€¢ J) â†” IsExtrema ğ“• A J := by
  rw [isExterma_iff_tensor]
  conv_lhs =>
    enter [x, 1, 1, 2, 2, 2]
    rw [fieldStrength_equivariant, distTensorDeriv_equivariant]
    rw [lorentzGroup_smul_dist_apply]
  conv_lhs =>
    enter [x]
    rw [smul_comm]
    rw [Tensorial.toTensor_smul, lorentzGroup_smul_dist_apply, Tensorial.toTensor_smul]
    simp only [Nat.reduceAdd, Nat.reduceSucc, Fin.isValue, one_div, map_smul, actionT_smul,
      contrT_equivariant, map_neg, permT_equivariant]
    rw [smul_comm, â† Tensor.actionT_neg, â† Tensor.actionT_add]
  apply Iff.intro
  Â· intro h
    rw [isExterma_iff_tensor A J]
    intro x
    apply MulAction.injective Î›
    simp only [Nat.reduceAdd, Nat.reduceSucc, Fin.isValue, one_div, map_smul, map_neg,
      _root_.smul_add, actionT_smul, _root_.smul_neg, _root_.smul_zero]
    simpa [schwartzAction_mul_apply] using h (schwartzAction Î› x)
  Â· intro h x
    rw [isExterma_iff_tensor A J] at h
    specialize h (schwartzAction Î›â»Â¹ x)
    simp at h
    rw [h]
    simp

end DistElectromagneticPotential
end Electromagnetism
