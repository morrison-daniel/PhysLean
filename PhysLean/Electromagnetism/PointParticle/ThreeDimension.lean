/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Dynamics.IsExtrema
import PhysLean.SpaceAndTime.Space.Norm
import PhysLean.SpaceAndTime.Space.Translations
import PhysLean.SpaceAndTime.TimeAndSpace.ConstantTimeDist
/-!

# Electrostatics of a stationary point particle in 3d

## i. Overview

In this module we give the electromagnetic properties of a point particle
sitting at the origin in 3d space.

## ii. Key results

- `threeDimPointParticle` : The electromagnetic potential of a point particle
  stationary at a point in 3d space.
- `threeDimPointParticle_isExterma` : The electric field of a point
  particle stationary at a point of 3d space satisfies Maxwell's equations

## iii. Table of contents

- A. The current density
  - A.1. The charge density
  - A.2. The 3-current density
- B. The Potentials
  - B.1. The electromagnetic potential
  - B.2. The scalar potential
  - B.3. The vector potential is zero
- C. The electric field
  - C.1. the time derivative of the electric field
- D. The magnetic field
- E. Maxwell's equations

## iv. References

-/

namespace Electromagnetism
open Distribution SchwartzMap
open Space MeasureTheory
namespace DistElectromagneticPotential

/-!

## A. The current density

The current density of a point particle in 3d space is given by:

$$J(r) = (c q \delta(r - râ‚€), 0, 0, 0) $$

where $c$ is the speed light, $q$ is the charge of the particle and $râ‚€$ is the position of the
particle in 3d space.

-/

/-- The current density of a point particle stationary at a point `râ‚€`
  of 3d space. -/
noncomputable def threeDimPointParticleCurrentDensity (c : SpeedOfLight) (q : â„) (râ‚€ : Space 3) :
    DistLorentzCurrentDensity 3 := (SpaceTime.distTimeSlice c).symm <|
  constantTime ((c * q) â€¢ diracDelta' â„ râ‚€ (Lorentz.Vector.basis (Sum.inl 0)))

lemma threeDimPointParticleCurrentDensity_eq_distTranslate (c : SpeedOfLight) (q : â„)
    (râ‚€ : Space 3) :
    threeDimPointParticleCurrentDensity c q râ‚€ = ((SpaceTime.distTimeSlice c).symm <|
    constantTime <|
    distTranslate (basis.repr râ‚€) <|
    ((c * q) â€¢ diracDelta' â„ 0 (Lorentz.Vector.basis (Sum.inl 0)))) := by
  rw [threeDimPointParticleCurrentDensity]
  congr
  ext Î·
  simp [distTranslate_apply]

/-!

### A.1. The charge density

The charge density of a point particle in 3d space is given by:
$$Ï(r) = q \delta(r - râ‚€) $$

where $q$ is the charge of the particle and $râ‚€$ is the position of the particle in 3d space.

-/

@[simp]
lemma threeDimPointParticleCurrentDensity_chargeDensity (c : SpeedOfLight) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticleCurrentDensity c q râ‚€).chargeDensity c =
    constantTime (q â€¢ diracDelta â„ râ‚€) := by
  ext Îµ
  simp only [DistLorentzCurrentDensity.chargeDensity, one_div, Lorentz.Vector.temporalCLM,
    Fin.isValue, threeDimPointParticleCurrentDensity, map_smul, LinearMap.coe_mk, AddHom.coe_mk,
    ContinuousLinearEquiv.apply_symm_apply, ContinuousLinearMap.coe_smul',
    ContinuousLinearMap.coe_comp', LinearMap.coe_toContinuousLinearMap', Pi.smul_apply,
    Function.comp_apply, constantTime_apply, diracDelta'_apply, Lorentz.Vector.apply_smul,
    Lorentz.Vector.basis_apply, â†“reduceIte, mul_one, smul_eq_mul, diracDelta_apply]
  field_simp

/-!

### A.2. The 3-current density

The 3-current density of a point particle in 3d space is given by:
$$\vec J(r) = 0.$$

In other words, there is no current flow for a point particle at rest.

-/

@[simp]
lemma threeDimPointParticleCurrentDensity_currentDensity (c : SpeedOfLight) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticleCurrentDensity c q râ‚€).currentDensity c = 0 := by
  ext Îµ i
  simp [threeDimPointParticleCurrentDensity, DistLorentzCurrentDensity.currentDensity,
    Lorentz.Vector.spatialCLM, constantTime_apply]

/-!

## B. The Potentials

-/

/-!

### B.1. The electromagnetic potential

The 4-potential of a point particle in 3d space is given by:

$$A(r) = \frac{q Î¼â‚€ c}{4 Ï€ |r - râ‚€|} (1, 0, 0, 0) $$

where $Î¼â‚€$ is the permeability of free space, $c$ is the speed of light,
$q$ is the charge of the particle and $râ‚€$ is the position of the particle in 3d space.

-/
open Real

/-- The electromagnetic potential of a point particle stationary at `râ‚€`
  of 3d space. -/
noncomputable def threeDimPointParticle (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    DistElectromagneticPotential 3 := (SpaceTime.distTimeSlice ğ“•.c).symm <| Space.constantTime <|
  distOfFunction (fun x => (((q * ğ“•.Î¼â‚€ * ğ“•.c)/ (4 * Ï€)) * â€–x - râ‚€â€–â»Â¹) â€¢
    Lorentz.Vector.basis (Sum.inl 0))
    (((IsDistBounded.inv_shift _).const_mul_fun _).smul_const _)

lemma threeDimPointParticle_eq_distTranslate (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    threeDimPointParticle ğ“• q râ‚€ = ((SpaceTime.distTimeSlice ğ“•.c).symm <|
    constantTime <|
    distTranslate (basis.repr râ‚€) <|
    distOfFunction (fun x => (((q * ğ“•.Î¼â‚€ * ğ“•.c)/ (4 * Ï€))* â€–xâ€–â»Â¹) â€¢
      Lorentz.Vector.basis (Sum.inl 0))
      ((IsDistBounded.inv.const_mul_fun _).smul_const _)) := by
  rw [threeDimPointParticle]
  congr
  ext Î·
  simp [distTranslate_ofFunction]

/-!

### B.2. The scalar potential

The first component of the 4-potential is the scalar potential, once
one has taken account of factors of the speed of light. It is given by:

$$V(r) = \frac{q}{4 Ï€ \epsilon_0 |r - r_0|}.$$

-/

lemma threeDimPointParticle_scalarPotential (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticle ğ“• q râ‚€).scalarPotential ğ“•.c =
    Space.constantTime (distOfFunction (fun x => (q/ (4 * Ï€ * ğ“•.Îµâ‚€))â€¢ â€–x - râ‚€â€–â»Â¹)
      (((IsDistBounded.inv_shift _).const_mul_fun _))) := by
  ext Îµ
  simp only [scalarPotential, Lorentz.Vector.temporalCLM, Fin.isValue, map_smul,
    ContinuousLinearMap.comp_smulâ‚›â‚—, ringHom_apply, threeDimPointParticle, LinearMap.coe_mk,
    AddHom.coe_mk, ContinuousLinearEquiv.apply_symm_apply, ContinuousLinearMap.coe_smul',
    ContinuousLinearMap.coe_comp', LinearMap.coe_toContinuousLinearMap', Pi.smul_apply,
    Function.comp_apply, constantTime_apply, distOfFunction_vector_eval, Lorentz.Vector.apply_smul,
    Lorentz.Vector.basis_apply, â†“reduceIte, mul_one, smul_eq_mul]
  rw [distOfFunction_mul_fun _ (IsDistBounded.inv_shift _),
    distOfFunction_mul_fun _ (IsDistBounded.inv_shift _)]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, ContinuousLinearMap.coe_smul', Pi.smul_apply,
    smul_eq_mul]
  ring_nf
  simp only [ğ“•.c_sq, one_div, mul_inv_rev, mul_eq_mul_right_iff, inv_eq_zero, OfNat.ofNat_ne_zero,
    or_false]
  field_simp

/-!

### B.3. The vector potential is zero

The spatial components of the 4-potential give the vector potential, which is zero
for a stationary point particle.

$$\vec A(r) = 0.$$

-/

@[simp]
lemma threeDimPointParticle_vectorPotential (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticle ğ“• q râ‚€).vectorPotential ğ“•.c = 0 := by
  ext Îµ i
  simp [vectorPotential, Lorentz.Vector.spatialCLM,
    threeDimPointParticle, constantTime_apply, distOfFunction_vector_eval]

/-!

## C. The electric field

The electric field of a point particle in 3d space is given by:
$$\vec E(r) = \frac{q}{4 Ï€ \epsilon_0} \frac{\vec r - \vec râ‚€}{|\vec r - \vec râ‚€|^3}.$$

-/

lemma threeDimPointParticle_electricField (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticle ğ“• q râ‚€).electricField ğ“•.c =
    (q/ (4 * Ï€ * ğ“•.Îµâ‚€)) â€¢ constantTime (distOfFunction (fun x : Space 3 =>
      â€–x - râ‚€â€– ^ (- 3 : â„¤) â€¢ basis.repr (x - râ‚€))
      ((IsDistBounded.zpow_smul_repr_self (- 3 : â„¤) (by omega)).comp_sub_right râ‚€)) := by
  simp only [electricField, LinearMap.coe_mk, AddHom.coe_mk, threeDimPointParticle_scalarPotential,
    smul_eq_mul, threeDimPointParticle_vectorPotential, map_zero, sub_zero, Int.reduceNeg, zpow_neg]
  rw [constantTime_distSpaceGrad, distOfFunction_mul_fun _
    (IsDistBounded.inv_shift râ‚€)]
  simp only [map_smul]
  trans -(q / (4 * Ï€ * ğ“•.Îµâ‚€)) â€¢ (constantTime <| distGrad <| distTranslate (basis.repr râ‚€) <|
    (distOfFunction (fun x => â€–xâ€–â»Â¹) (IsDistBounded.inv)))
  Â· simp [distTranslate_ofFunction]
  rw [Space.distTranslate_distGrad]
  have := Space.distGrad_distOfFunction_norm_zpow (d := 2) (-1) (by grind)
  simp_all [distOfFunction_neg, distTranslate_ofFunction]

/-!

### C.1. the time derivative of the electric field

-/

@[simp]
lemma threeDimPointParticle_electricField_timeDeriv (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    Space.distTimeDeriv ((threeDimPointParticle ğ“• q râ‚€).electricField ğ“•.c) = 0 := by
  rw [threeDimPointParticle_electricField]
  simp

/-!

## D. The magnetic field

Given that the vector potential is zero, the magnetic field is also zero.

-/

@[simp]
lemma threeDimPointParticle_magneticFieldMatrix (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticle ğ“• q râ‚€).magneticFieldMatrix ğ“•.c = 0 := by
  ext Î·
  simp [magneticFieldMatrix_eq_vectorPotential]

/-!

## E. Maxwell's equations

The divergence of the electric field of a point particle in 3d space is given by:
$$âˆ‡ Â· \vec E(r) = \frac{1}{\epsilon_0} q \delta(r - râ‚€).$$

From this, it follows that the electromagnetic potential of a point particle in 3d space
satisfies Maxwell's equations for a point particle at rest.

-/

lemma threeDimPointParticle_div_electricField {ğ“•} (q : â„) (râ‚€ : Space 3) :
    distSpaceDiv ((threeDimPointParticle ğ“• q râ‚€).electricField ğ“•.c) =
    (1/ğ“•.Îµâ‚€) â€¢ constantTime (q â€¢ diracDelta â„ râ‚€) := by
  rw [threeDimPointParticle_electricField]
  simp only [Int.reduceNeg, zpow_neg, map_smul, smul_smul]
  have h1 := Space.distDiv_inv_pow_eq_dim (d := 2)
  simp at h1
  trans (q / (4 * Ï€ * ğ“•.Îµâ‚€)) â€¢
    distSpaceDiv (constantTime <|
    distTranslate (basis.repr râ‚€) <|
    (distOfFunction (fun x => â€–xâ€– ^ (-3 : â„¤) â€¢ basis.repr x)
      (IsDistBounded.zpow_smul_repr_self (- 3 : â„¤) (by omega))))
  Â· ext Î·
    simp [distTranslate_ofFunction]
  simp only [Int.reduceNeg, zpow_neg, one_div]
  rw [constantTime_distSpaceDiv, distDiv_distTranslate, h1]
  simp only [map_smul]
  suffices h : volume.real (Metric.ball (0 : Space 3) 1) = (4/3 * Real.pi) by
    rw [h]
    simp [smul_smul]
    ext Î·
    simp [constantTime_apply, diracDelta_apply, distTranslate_apply]
    left
    ring_nf
    field_simp
  simp [MeasureTheory.Measure.real]
  exact pi_nonneg

lemma threeDimPointParticle_isExterma (ğ“• : FreeSpace) (q : â„) (râ‚€ : Space 3) :
    (threeDimPointParticle ğ“• q râ‚€).IsExtrema ğ“• (threeDimPointParticleCurrentDensity ğ“•.c q râ‚€) := by
  rw [isExtrema_iff_components]
  apply And.intro
  Â· intro Îµ
    rw [gradLagrangian_sum_inl_0]
    simp only [one_div, mul_inv_rev, threeDimPointParticleCurrentDensity_chargeDensity, map_smul,
      ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
    rw [threeDimPointParticle_div_electricField]
    simp only [one_div, map_smul, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
    field_simp
    simp [ğ“•.c_sq]
    right
    field_simp
    simp
  Â· intro Îµ i
    rw [gradLagrangian_sum_inr_i]
    simp

end DistElectromagneticPotential
end Electromagnetism
