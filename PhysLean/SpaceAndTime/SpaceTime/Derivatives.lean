/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.SpaceTime.LorentzAction
import PhysLean.Relativity.Tensors.RealTensor.CoVector.Basic
import Mathlib.Analysis.InnerProductSpace.TensorProduct
/-!

# Derivatives on SpaceTime

## i. Overview

In this module we define and prove basic lemmas about derivatives of functions and
distributions on `SpaceTime d`.

## ii. Key results

- `deriv` : The derivative of a function `SpaceTime d â†’ M` along the `Î¼` coordinate.
- `deriv_sum_inr` : The derivative along a spatial coordinate in terms of the
  derivative on `Space d`.
- `deriv_sum_inl` : The derivative along the temporal coordinate in terms of the
  derivative on `Time`.
- `distDeriv` : The derivative of a distribution on `SpaceTime d` along the `Î¼` coordinate.
- `distDeriv_commute` : Derivatives of distributions on `SpaceTime d` commute.

## iii. Table of contents

- A. Derivatives of functions on `SpaceTime d`
  - A.1. The definition of the derivative
  - A.2. Basic equality lemmas
  - A.3. Derivative of the zero function
  - A.4. The derivative of a function composed with a Lorentz transformation
  - A.5. Spacetime derivatives in terms of time and space derivatives
- B. Derivatives of distributions
  - B.1. Commutation of derivatives of distributions
  - B.2. Lorentz group action on derivatives of distributions
- C. Derivatives of tensors
  - C.1. Derivatives of tensors for distributions

## iv. References

-/
noncomputable section

namespace SpaceTime

open Manifold
open Matrix
open Complex
open ComplexConjugate
open TensorSpecies

/-!

## A. Derivatives of functions on `SpaceTime d`

-/

/-!

### A.1. The definition of the derivative

-/

/-- The derivative of a function `SpaceTime d â†’ â„` along the `Î¼` coordinate. -/
noncomputable def deriv {M : Type} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    {d : â„•} (Î¼ : Fin 1 âŠ• Fin d) (f : SpaceTime d â†’ M) : SpaceTime d â†’ M :=
  fun y => fderiv â„ f y (Lorentz.Vector.basis Î¼)

@[inherit_doc deriv]
scoped notation "âˆ‚_" => deriv

/-!

### A.2. Basic equality lemmas

-/

variable {M : Type} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
lemma deriv_eq {d : â„•} (Î¼ : Fin 1 âŠ• Fin d) (f : SpaceTime d â†’ M) (y : SpaceTime d) :
    âˆ‚_ Î¼ f y =
    fderiv â„ f y (Lorentz.Vector.basis Î¼) := by
  rfl

lemma differentiable_vector {d : â„•} (f : SpaceTime d â†’ Lorentz.Vector d) :
    (âˆ€ Î½, Differentiable â„ (fun x => f x Î½)) â†” Differentiable â„ f := by
  apply Iff.intro
  Â· intro h
    rw [â† (Lorentz.Vector.equivPi d).comp_differentiable_iff]
    exact differentiable_pi'' h
  Â· intro h Î½
    change Differentiable â„ (Lorentz.Vector.coordCLM Î½ âˆ˜ f)
    apply Differentiable.comp
    Â· fun_prop
    Â· exact h

lemma contDiff_vector {d : â„•} (f : SpaceTime d â†’ Lorentz.Vector d) :
    (âˆ€ Î½, ContDiff â„ n (fun x => f x Î½)) â†” ContDiff â„ n f := by
  apply Iff.intro
  Â· intro h
    rw [â† (Lorentz.Vector.equivPi d).comp_contDiff_iff]
    apply contDiff_pi'
    intro Î½
    exact h Î½
  Â· intro h Î½
    change ContDiff â„ n (Lorentz.Vector.coordCLM Î½ âˆ˜ f)
    apply ContDiff.comp
    Â· fun_prop
    Â· exact h

lemma deriv_apply_eq {d : â„•} (Î¼ Î½ : Fin 1 âŠ• Fin d) (f : SpaceTime d â†’ Lorentz.Vector d)
    (hf : Differentiable â„ f)
    (y : SpaceTime d) :
    âˆ‚_ Î¼ f y Î½ = fderiv â„ (fun x => f x Î½) y (Lorentz.Vector.basis Î¼) := by
  rw [deriv_eq]
  change _ = (fderiv â„ (Lorentz.Vector.coordCLM Î½ âˆ˜ f) y) (Lorentz.Vector.basis Î¼)
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp', Function.comp_apply]
  rfl

lemma fderiv_vector {d : â„•} (f : SpaceTime d â†’ Lorentz.Vector d)
    (hf : Differentiable â„ f) (y dt : SpaceTime d) (Î½ : Fin 1 âŠ• Fin d) :
    fderiv â„ f y dt Î½ = fderiv â„ (fun x => f x Î½) y dt := by
  change _ = (fderiv â„ (Lorentz.Vector.coordCLM Î½ âˆ˜ f) y) dt
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp', Function.comp_apply]
  rfl

@[simp]
lemma deriv_coord {d : â„•} (Î¼ Î½ : Fin 1 âŠ• Fin d) :
    âˆ‚_ Î¼ (fun x => x Î½) = if Î¼ = Î½ then 1 else 0 := by
  change âˆ‚_ Î¼ (coordCLM Î½) = _
  funext x
  rw [deriv_eq]
  simp only [ContinuousLinearMap.fderiv]
  simp [coordCLM]
  split_ifs
  rfl
  rfl

/-!

### A.3. Derivative of the zero function

-/

@[simp]
lemma deriv_zero {d : â„•} (Î¼ : Fin 1 âŠ• Fin d) : SpaceTime.deriv Î¼ (fun _ => (0 : â„)) = 0 := by
  ext y
  rw [SpaceTime.deriv_eq]
  simp

attribute [-simp] Fintype.sum_sum_type

/-!

### A.4. The derivative of a function composed with a Lorentz transformation

-/

lemma deriv_comp_lorentz_action {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M] {d : â„•}
    (Î¼ : Fin 1 âŠ• Fin d)
    (f : SpaceTime d â†’ M) (hf : Differentiable â„ f) (Î› : LorentzGroup d)
    (x : SpaceTime d) :
    âˆ‚_ Î¼ (fun x => f (Î› â€¢ x)) x = âˆ‘ Î½, Î›.1 Î½ Î¼ â€¢ âˆ‚_ Î½ f (Î› â€¢ x) := by
  change fderiv â„ (f âˆ˜ Lorentz.Vector.actionCLM Î›) x (Lorentz.Vector.basis Î¼) = _
  rw [fderiv_comp]
  simp only [Lorentz.Vector.actionCLM_apply, Nat.succ_eq_add_one, Nat.reduceAdd,
    ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_comp', Function.comp_apply]
    -- Fintype.sum_sum_type
  rw [Lorentz.Vector.smul_basis]
  simp
  rfl
  Â· fun_prop
  Â· fun_prop

variable
    {c : Fin n â†’ realLorentzTensor.Color} {M : Type} [NormedAddCommGroup M]
      [NormedSpace â„ M] [Tensorial (realLorentzTensor d) c M] [T2Space M]
lemma deriv_equivariant (f : SpaceTime d â†’ M) (Î› : LorentzGroup d) (x : SpaceTime d)
    (hf : Differentiable â„ f) (Î¼ : Fin 1 âŠ• Fin d) :
    âˆ‚_ Î¼ (fun x => Î› â€¢ f (Î›â»Â¹ â€¢ x)) x =
    âˆ‘ Î½, Î›â»Â¹.1 Î½ Î¼ â€¢ Î› â€¢ âˆ‚_ Î½ f (Î›â»Â¹ â€¢ x) := by
  have h1 (Î¼ : Fin 1 âŠ• Fin d) (x : SpaceTime d) :
      âˆ‚_ Î¼ (fun x => Î› â€¢ f (Î›â»Â¹ â€¢ x)) x =
      Î› â€¢ âˆ‚_ Î¼ (fun x => f (Î›â»Â¹ â€¢ x)) x := by
    change âˆ‚_ Î¼ (TensorSpecies.Tensorial.actionCLM _ Î› âˆ˜ fun x => f (Î›â»Â¹ â€¢ x)) x = _
    rw [deriv_eq]
    rw [fderiv_comp]
    simp [Tensorial.actionCLM_apply, â† deriv_eq]
    Â· fun_prop
    Â· apply Differentiable.differentiableAt
      have hx : Differentiable â„ (f âˆ˜ (Lorentz.Vector.actionCLM Î›â»Â¹)) := by fun_prop
      exact hx
  rw [h1 Î¼ x, deriv_comp_lorentz_action]
  change (TensorSpecies.Tensorial.actionCLM _ Î›) (âˆ‘ Î½, (Î›â»Â¹).1 Î½ Î¼ â€¢ âˆ‚_ Î½ f (Î›â»Â¹ â€¢ x)) = _
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, map_sum, map_smul]
  simp [TensorSpecies.Tensorial.actionCLM_apply]
  Â· fun_prop

/-!

### A.5. Spacetime derivatives in terms of time and space derivatives

-/

lemma deriv_sum_inr {d : â„•} {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M]
    (c : SpeedOfLight) (f : SpaceTime d â†’ M)
    (hf : Differentiable â„ f) (x : SpaceTime d) (i : Fin d) :
    âˆ‚_ (Sum.inr i) f x
    = Space.deriv i (fun y => f ((toTimeAndSpace c).symm ((toTimeAndSpace c x).1, y)))
      (toTimeAndSpace c x).2 := by
  rw [deriv_eq, Space.deriv_eq]
  conv_rhs => rw [fderiv_comp' _ (by fun_prop) (by fun_prop)]
  simp only [Prod.mk.eta, ContinuousLinearEquiv.symm_apply_apply, ContinuousLinearMap.coe_comp',
    Function.comp_apply]
  congr 1
  rw [fderiv_comp']
  simp only [Prod.mk.eta, toTimeAndSpace_symm_fderiv, ContinuousLinearMap.coe_comp',
    ContinuousLinearEquiv.coe_coe, Function.comp_apply]
  change _ = (toTimeAndSpace c).symm ((fderiv â„ ((toTimeAndSpace c x).1, Â·) (toTimeAndSpace c x).2)
    (Space.basis i))
  rw [DifferentiableAt.fderiv_prodMk]
  simp only [fderiv_fun_const, Pi.zero_apply, fderiv_id', ContinuousLinearMap.prod_apply,
    ContinuousLinearMap.zero_apply, ContinuousLinearMap.coe_id', id_eq]
  trans (toTimeAndSpace c).symm (0, Space.basis i)
  Â· rw [â† toTimeAndSpace_basis_inr (c := c)]
    simp
  Â· rfl
  repeat' fun_prop

lemma deriv_sum_inl {d : â„•} {M : Type} [NormedAddCommGroup M]
    [NormedSpace â„ M] (c : SpeedOfLight) (f : SpaceTime d â†’ M)
    (hf : Differentiable â„ f) (x : SpaceTime d) :
    âˆ‚_ (Sum.inl 0) f x
    = (1/(c : â„)) â€¢ Time.deriv (fun t => f ((toTimeAndSpace c).symm (t, (toTimeAndSpace c x).2)))
      (toTimeAndSpace c x).1 := by
  rw [deriv_eq, Time.deriv_eq]
  conv_rhs => rw [fderiv_comp' _ (by fun_prop) (by fun_prop)]
  simp only [Fin.isValue, Prod.mk.eta, ContinuousLinearEquiv.symm_apply_apply,
    ContinuousLinearMap.coe_comp', Function.comp_apply]
  trans
    (fderiv â„ f x)
      ((1 / c.val) â€¢ (fderiv â„ (fun t => (toTimeAndSpace c).symm (t, ((toTimeAndSpace c) x).2))
      ((toTimeAndSpace c) x).1) 1)
  swap
  Â· exact ContinuousLinearMap.map_smul_of_tower (fderiv â„ f x) (1 / c.val) _
  congr 1

  rw [fderiv_comp']
  simp only [Fin.isValue, Prod.mk.eta, toTimeAndSpace_symm_fderiv, ContinuousLinearMap.coe_comp',
    ContinuousLinearEquiv.coe_coe, Function.comp_apply]
  rw [DifferentiableAt.fderiv_prodMk]
  simp only [Fin.isValue, fderiv_id', fderiv_fun_const, Pi.zero_apply,
    ContinuousLinearMap.prod_apply, ContinuousLinearMap.coe_id', id_eq,
    ContinuousLinearMap.zero_apply]
  rw [â† map_smul]
  rw [â† toTimeAndSpace_basis_inl' (c := c)]
  simp only [Fin.isValue, ContinuousLinearEquiv.symm_apply_apply]
  repeat' fun_prop

/-!

## B. Derivatives of distributions

-/

open Distribution SchwartzMap
/-- Given a distribution (function) `f : Space d â†’d[â„] M` the derivative
  of `f` in direction `Î¼`. -/
noncomputable def distDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin 1 âŠ• Fin d) : ((SpaceTime d) â†’d[â„] M) â†’â‚—[â„] (SpaceTime d) â†’d[â„] M where
  toFun f :=
    let ev : (SpaceTime d â†’L[â„] M) â†’L[â„] M := {
      toFun v := v (Lorentz.Vector.basis Î¼)
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply]
      map_smul' a v := by
        simp
    }
    ev.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    simp
  map_smul' a f := by simp

lemma distDeriv_apply {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin 1 âŠ• Fin d) (f : (SpaceTime d) â†’d[â„] M) (Îµ : ğ“¢(SpaceTime d, â„)) :
    distDeriv Î¼ f Îµ = fderivD â„ f Îµ (Lorentz.Vector.basis Î¼) := by
  simp [distDeriv, Distribution.fderivD]

lemma distDeriv_apply' {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin 1 âŠ• Fin d) (f : (SpaceTime d) â†’d[â„] M) (Îµ : ğ“¢(SpaceTime d, â„)) :
    distDeriv Î¼ f Îµ =
    - f ((SchwartzMap.evalCLM (ğ•œ := â„) (Lorentz.Vector.basis Î¼)) ((fderivCLM â„) Îµ)) := by
  simp [distDeriv_apply, Distribution.fderivD]

lemma apply_fderiv_eq_distDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin 1 âŠ• Fin d) (f : (SpaceTime d) â†’d[â„] M) (Îµ : ğ“¢(SpaceTime d, â„)) :
    f ((SchwartzMap.evalCLM (ğ•œ := â„) (Lorentz.Vector.basis Î¼)) ((fderivCLM â„) Îµ)) =
    - distDeriv Î¼ f Îµ := by
  rw [distDeriv_apply']
  simp

/-!

### B.1. Commutation of derivatives of distributions

-/

open ContDiff
lemma distDeriv_commute {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ Î½ : Fin 1 âŠ• Fin d) (f : (SpaceTime d) â†’d[â„] M) :
    distDeriv Î¼ (distDeriv Î½ f) = distDeriv Î½ (distDeriv Î¼ f) := by
  ext Îº
  rw [distDeriv_apply, distDeriv_apply, fderivD_apply, fderivD_apply]
  rw [distDeriv_apply, distDeriv_apply, fderivD_apply, fderivD_apply]
  simp only [neg_neg]
  congr 1
  ext x
  change fderiv â„ (fun x => fderiv â„ Îº x (Lorentz.Vector.basis Î¼)) x (Lorentz.Vector.basis Î½) =
    fderiv â„ (fun x => fderiv â„ Îº x (Lorentz.Vector.basis Î½)) x (Lorentz.Vector.basis Î¼)
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  Â· apply ContDiffAt.isSymmSndFDerivAt (n := âˆ)
    apply ContDiff.contDiffAt
    exact smooth Îº âŠ¤
    simp only [minSmoothness_of_isRCLikeNormedField]
    exact ENat.LEInfty.out
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop

/-!

### B.2. Lorentz group action on derivatives of distributions

We now show how the Lorentz group action on distributions interacts with derivatives.

-/

lemma _root_.SchwartzMap.sum_apply {Î± : Type} [NormedAddCommGroup Î±]
    [NormedSpace â„ Î±]
    {Î¹ : Type} [Fintype Î¹]
    (f : Î¹ â†’ ğ“¢(Î±, â„)) (x : Î±) :
    (âˆ‘ i, f i) x = âˆ‘ i, f i x := by
  let P (Î¹ : Type) [Fintype Î¹] := âˆ€ (f : Î¹ â†’ ğ“¢(Î±, â„)),
    (âˆ‘ i, f i) x = âˆ‘ i, f i x
  revert f
  change P Î¹
  apply Fintype.induction_empty_option
  Â· intro Î¹1 Î¹2 _ e h1 f
    rw [â† @e.sum_comp, â† @e.sum_comp, h1]
  Â· simp [P]
  Â· intro a _ ih f
    simp [Fintype.sum_option]
    rw [ih]

lemma distDeriv_comp_lorentz_action {Î¼ : Fin 1 âŠ• Fin d} (Î› : LorentzGroup d)
    (f : (SpaceTime d) â†’d[â„] M) :
    distDeriv Î¼ (Î› â€¢ f) = âˆ‘ Î½, Î›â»Â¹.1 Î½ Î¼ â€¢ (Î› â€¢ distDeriv Î½ f) := by
  symm
  trans (âˆ‘ Î½, Î› â€¢ Î›â»Â¹.1 Î½ Î¼ â€¢ (distDeriv Î½) f)
  Â· congr
    funext i
    rw [SMulCommClass.smul_comm]
  trans Î› â€¢ (âˆ‘ Î½, Î›â»Â¹.1 Î½ Î¼ â€¢ (distDeriv Î½) f)
  Â· exact Eq.symm Finset.smul_sum
  ext Î·
  rw [lorentzGroup_smul_dist_apply, distDeriv_apply, fderivD_apply,
    lorentzGroup_smul_dist_apply]
  rw [â† smul_neg]
  congr
  rw [ContinuousLinearMap.sum_apply]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply]
  conv_lhs =>
    enter [2, x]
    rw [distDeriv_apply, fderivD_apply]
    simp only [smul_neg]
    rw [â† map_smul]
  rw [Finset.sum_neg_distrib]
  congr
  rw [â† map_sum]
  congr
  /- Reduced to Schwartz maps -/
  ext x
  rw [SchwartzMap.sum_apply]
  symm
  simp [schwartzAction_apply]
  change âˆ‚_ Î¼ Î· (Î› â€¢ x) = âˆ‘ Î½, Î›â»Â¹.1 Î½ Î¼ â€¢ âˆ‚_ Î½ (schwartzAction Î›â»Â¹ Î·) (x)
  obtain âŸ¨Î·, rflâŸ© := schwartzAction_surjective Î› Î·
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, smul_eq_mul]
  rw [schwartzAction_mul_apply]
  simp only [inv_mul_cancel, map_one, ContinuousLinearMap.one_apply]
  change âˆ‚_ Î¼ (fun x => Î· (Î›â»Â¹ â€¢ x)) (Î› â€¢ x) = _
  rw [deriv_comp_lorentz_action]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, inv_smul_smul, smul_eq_mul]
  exact SchwartzMap.differentiable Î·

/-!

## C. Derivatives of tensors

Given a function `f : SpaceTime d â†’ M` where `M` is a tensor space, we can define the
derivative of `f` as a tensor. In particular this is `âˆ‚_Î¼ f` viewed as a tensor in
`Lorentz.CoVector d âŠ—[â„] M`.

-/
open TensorProduct

/-- The derivative of a tensor, as a tensor. -/
def tensorDeriv (f : SpaceTime d â†’ M) :
    SpaceTime d â†’ Lorentz.CoVector d âŠ—[â„] M := fun x =>
  âˆ‘ Î¼, (Lorentz.CoVector.basis Î¼) âŠ—â‚œ (âˆ‚_ Î¼ f x)

lemma tensorDeriv_equivariant (f : SpaceTime d â†’ M) (Î› : LorentzGroup d) (x : SpaceTime d)
    (hf : Differentiable â„ f) :
    tensorDeriv (fun x => Î› â€¢ f (Î›â»Â¹ â€¢ x)) x =
    Î› â€¢ tensorDeriv f (Î›â»Â¹ â€¢ x) := by
  simp [tensorDeriv]
  conv_lhs =>
    enter [2, Î¼]
    rw [deriv_equivariant f Î› x hf Î¼, tmul_sum]
    enter [2, Î½]
    rw [â† smul_tmul]
  rw [Finset.sum_comm]
  conv_lhs =>
    enter [2, Î½]
    rw [â† sum_tmul, â† Lorentz.CoVector.smul_basis, â† Tensorial.smul_prod]
  change _ = (TensorSpecies.Tensorial.smulLinearMap Î›) _
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, map_sum]
  simp [TensorSpecies.Tensorial.smulLinearMap_apply]

lemma tensorDeriv_toTensor_basis_repr
    {f : SpaceTime d â†’ M}
    (hf : Differentiable â„ f) (x : SpaceTime d)
    (b : Tensor.ComponentIdx (Fin.append ![realLorentzTensor.Color.down] c)) :
    (Tensor.basis _).repr (Tensorial.toTensor (tensorDeriv f x)) b =
    âˆ‚_ (Lorentz.CoVector.indexEquiv (Tensor.ComponentIdx.prodEquiv b).1)
      (fun x => (Tensor.basis _).repr (Tensorial.toTensor (f x))
        (Tensor.ComponentIdx.prodEquiv b).2) x := by
  simp [tensorDeriv]
  conv_lhs =>
    enter [2, Î¼]
    rw [Tensorial.toTensor_tprod, Tensor.prodT_basis_repr_apply]
    simp [Lorentz.CoVector.toTensor_basis_eq_tensor_basis, Finsupp.single_apply]
  rw [Finset.sum_eq_single (Lorentz.CoVector.indexEquiv (Tensor.ComponentIdx.prodEquiv b).1)]
  Â· simp
    generalize (Lorentz.CoVector.indexEquiv (Tensor.ComponentIdx.prodEquiv b).1) = Î¼ at *
    generalize (Tensor.ComponentIdx.prodEquiv b).2 = Î½ at *
    have h1 (x : SpaceTime d) : ((Tensor.basis c).repr (Tensorial.toTensor (f x))) Î½ =
        (ContinuousLinearMap.proj Î½ âˆ˜L ((Tensor.basis c).map
        (Tensorial.toTensor).symm).equivFunL.toContinuousLinearMap) (f x) := by
      simp
    conv_rhs =>
      enter [2, x]
      rw [h1 x]
    conv_rhs =>
      rw [deriv_eq]
      rw [fderiv_comp' _ (by fun_prop) (by fun_prop)]
    rw [ContinuousLinearMap.fderiv]
    simp [deriv_eq]
  Â· intro b' _ hb
    simp only [ite_eq_right_iff]
    intro hx
    grind
  Â· simp

/-!

### C.1. Derivatives of tensors for distributions

-/
open InnerProductSpace
/-- The derivative of a tensor, as a tensor for distributions. -/
def distTensorDeriv {M d} [NormedAddCommGroup M]
    [InnerProductSpace â„ M] [FiniteDimensional â„ M] :
    ((SpaceTime d) â†’d[â„] M) â†’â‚—[â„] ((SpaceTime d) â†’d[â„] Lorentz.CoVector d âŠ—[â„] M) where
  toFun f := {
    toFun Îµ := âˆ‘ Î¼, (Lorentz.CoVector.basis Î¼) âŠ—â‚œ distDeriv Î¼ f Îµ
    map_add' Îµ1 Îµ2 := by
      simp [â† Finset.sum_add_distrib, tmul_add]
    map_smul' a Îµ := by
      simp [â† Finset.smul_sum, tmul_smul]
    cont := by
      refine continuous_finset_sum Finset.univ (fun Î¼ _ => ?_)
      refine Continuous.comp' ?_ ?_
      Â· change Continuous (fun y => (Lorentz.CoVector.basis Î¼) âŠ—â‚œ y)
        obtain âŸ¨w,b,hb1âŸ© := exists_orthonormalBasis â„ M
        have h1 : âˆ€ (y : M), (Lorentz.CoVector.basis Î¼) âŠ—â‚œ y =
            âˆ‘ i, âŸªb i, yâŸ«_â„ â€¢ ((Lorentz.CoVector.basis Î¼) âŠ—â‚œ[â„] (b i)) := by
          intro y
          conv_lhs => rw [â† OrthonormalBasis.sum_repr' b y]
          simp [tmul_sum]
        conv => enter [1, y]; rw [h1]
        fun_prop
      Â· fun_prop
  }
  map_add' f1 f2 := by
    ext Îµ
    simp [tmul_add, Finset.sum_add_distrib]
  map_smul' a f := by
    ext Îµ
    simp [tmul_smul, Finset.smul_sum]

lemma distTensorDeriv_apply {M d} [NormedAddCommGroup M]
    [InnerProductSpace â„ M] [FiniteDimensional â„ M] (f : (SpaceTime d) â†’d[â„] M)
    (Îµ : ğ“¢(SpaceTime d, â„)) :
    distTensorDeriv f Îµ = âˆ‘ Î¼, (Lorentz.CoVector.basis Î¼) âŠ—â‚œ distDeriv Î¼ f Îµ := by
  simp [distTensorDeriv]

lemma distTensorDeriv_equivariant {M : Type} [NormedAddCommGroup M]
    [InnerProductSpace â„ M] [FiniteDimensional â„ M] [(realLorentzTensor d).Tensorial c M]
    (f : (SpaceTime d) â†’d[â„] M) (Î› : LorentzGroup d) :
    distTensorDeriv (Î› â€¢ f) = Î› â€¢ distTensorDeriv f := by
  ext Îµ
  rw [distTensorDeriv_apply]
  conv_lhs =>
    enter [2, Î¼]
    rw [distDeriv_comp_lorentz_action]
    simp only [ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_smul', Finset.sum_apply,
      Pi.smul_apply]
    rw [tmul_sum]
    enter [2, Î½]
    rw [â† smul_tmul, lorentzGroup_smul_dist_apply]
  rw [Finset.sum_comm]
  conv_lhs =>
    enter [2, Î½]
    rw [â† sum_tmul, â† Lorentz.CoVector.smul_basis, â† Tensorial.smul_prod]
  change _ = (TensorSpecies.Tensorial.smulLinearMap Î›) _
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, ContinuousLinearMap.coe_comp, LinearMap.coe_comp,
    ContinuousLinearMap.coe_coe, Function.comp_apply]
  rw [distTensorDeriv_apply]
  simp only [map_sum]
  simp [TensorSpecies.Tensorial.smulLinearMap_apply]

end SpaceTime

end
