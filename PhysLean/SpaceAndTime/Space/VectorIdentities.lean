/-
Copyright (c) 2025 Zhi Kai Pong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Basic
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
import Mathlib.Analysis.Calculus.Gradient.Basic
/-!

# VectorIdentities

In this file we define common vector calculus identities based on `Space`.

-/

/-!

## General auxiliary lemmas

-/

section

variable
  {ğ•œ} [NontriviallyNormedField ğ•œ]
  {X} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
  {Y} [NormedAddCommGroup Y] [NormedSpace ğ•œ Y]
  {Î¹ : Type*} [Fintype Î¹] {Y' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (Y' i)]
  [âˆ€ i, NormedSpace ğ•œ (Y' i)] {Î¦ : X â†’ âˆ€ i, Y' i} {x : X}

lemma fderiv_pi' (h : DifferentiableAt ğ•œ Î¦ x) :
    fderiv ğ•œ Î¦ x = ContinuousLinearMap.pi fun i => (fderiv ğ•œ fun x => Î¦ x i) x:= by
  apply HasFDerivAt.fderiv
  apply hasFDerivAt_pi''
  intro i
  rw [differentiableAt_pi] at h
  exact (h i).hasFDerivAt

lemma ContDiff.differentiable_fderiv (f : X â†’ Y) (hf : ContDiff ğ•œ 2 f) :
    Differentiable ğ•œ (fun x => fderiv ğ•œ f x) := by
  have hf' : ContDiff ğ•œ (1+1) f := hf
  rw [contDiff_succ_iff_fderiv] at hf'
  apply hf'.right.right.differentiable
  decide

lemma fderiv_coord_eq_proj_comp (h : DifferentiableAt ğ•œ Î¦ x) :
    (fderiv ğ•œ fun x => Î¦ x i) x = (ContinuousLinearMap.proj i).comp (fderiv ğ•œ Î¦ x) := by
  rw [fderiv_pi', ContinuousLinearMap.proj_pi]
  exact h

end

/-!

## Space.deriv lemmas

-/

namespace Space

/-- Derivatives on space distribute over addition. -/
lemma deriv_add [NormedAddCommGroup M] [NormedSpace â„ M]
    (f1 f2 : Space d â†’ M) (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‚[u] (f1 + f2) = âˆ‚[u] f1 + âˆ‚[u] f2 := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_add]
  rfl
  repeat fun_prop

/-- Derivatives on space distribute coordinate-wise over addition. -/
lemma deriv_coord_add (f1 f2 : Space d â†’ EuclideanSpace â„ (Fin d))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    (âˆ‚[u] (fun x => f1 x i + f2 x i)) =
      (âˆ‚[u] (fun x => f1 x i)) + (âˆ‚[u] (fun x => f2 x i)) := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  repeat fun_prop

/-- Scalar multiplication on space derivatives. -/
lemma deriv_smul [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Space d â†’ M) (k : â„) (hf : Differentiable â„ f) :
    âˆ‚[u] (k â€¢ f) = (k â€¢ âˆ‚[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_const_smul]
  rfl
  fun_prop

/-- Coordinate-wise scalar multiplication on space derivatives. -/
lemma deriv_coord_smul (f : Space d â†’ EuclideanSpace â„ (Fin d)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‚[u] (fun x => k * f x i) x= (k â€¢ âˆ‚[u] (fun x => f x i)) x:= by
  unfold deriv
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  fun_prop

/-- Derivatives on space commute with one another. -/
lemma deriv_commute [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Space d â†’ M) (hf : ContDiff â„ 2 f) : âˆ‚[u] (âˆ‚[v] f) = âˆ‚[v] (âˆ‚[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  apply ContDiffAt.isSymmSndFDerivAt
  exact ContDiff.contDiffAt hf
  simp only [minSmoothness_of_isRCLikeNormedField, le_refl]
  repeat
  Â· apply Differentiable.differentiableAt
    apply ContDiff.differentiable_fderiv
    exact hf
  Â· fun_prop

/-- Coordiate functions of fderiv is differentiable. -/
lemma differentiable_fderiv_coord (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    Differentiable â„ (fderiv â„ fun x => f x i) := by
  have eq : (fderiv â„ (fun x â†¦ f x i))
      = (fun x => (ContinuousLinearMap.proj i).comp (fderiv â„ f x)) := by
    ext x
    rw [fderiv_coord_eq_proj_comp]
    apply hf.differentiable
    decide
  rw [eq]
  apply Differentiable.clm_comp
  Â· fun_prop
  Â· apply ContDiff.differentiable_fderiv
    exact hf

/-- Second derivatives distribute coordinate-wise over addition (all three components for div). -/
lemma deriv_coord_2nd_add (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‚[i] (fun x => âˆ‚[u] (fun x => f x u) x + (âˆ‚[v] (fun x => f x v) x + âˆ‚[w] (fun x => f x w) x)) =
    (âˆ‚[i] (âˆ‚[u] (fun x => f x u))) + (âˆ‚[i] (âˆ‚[v] (fun x => f x v))) +
    (âˆ‚[i] (âˆ‚[w] (fun x => f x w))) := by
  unfold deriv
  ext x
  rw [fderiv_fun_add, fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  ring
  repeat
    try apply DifferentiableAt.add
    apply Differentiable.differentiableAt
    apply Differentiable.clm_apply
    Â· apply differentiable_fderiv_coord
      exact hf
    Â· fun_prop

/-- Second derivatives distribute coordinate-wise over subtraction (two components for curl). -/
lemma deriv_coord_2nd_sub (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‚[u] (fun x => âˆ‚[v] (fun x => f x w) x - âˆ‚[w] (fun x => f x v) x) =
    (âˆ‚[u] (âˆ‚[v] (fun x => f x w))) - (âˆ‚[u] (âˆ‚[w] (fun x => f x v))) := by
  unfold deriv
  ext x
  simp only [Pi.sub_apply]
  rw [fderiv_fun_sub]
  simp only [ContinuousLinearMap.coe_sub', Pi.sub_apply]
  repeat
    apply Differentiable.differentiableAt
    apply Differentiable.clm_apply
    Â· apply differentiable_fderiv_coord
      exact hf
    Â· fun_prop

@[simp]
lemma deriv_component_same (Î¼ : Fin d) (x : Space d) :
    (deriv Î¼ (fun x => x Î¼) x) = 1 := by
  simp [deriv, fderiv_coord_eq_proj_comp, ContinuousLinearMap.proj]
  erw [LinearMap.proj_apply]
  simp

lemma deriv_component_diff (Î¼ Î½ : Fin d) (x : Space d) (h : Î¼ â‰  Î½) :
    (deriv Î¼ (fun x => x Î½) x) = 0 := by
  simp [deriv, fderiv_coord_eq_proj_comp, ContinuousLinearMap.proj]
  erw [LinearMap.proj_apply]
  simp only [EuclideanSpace.single_apply, ite_eq_right_iff, one_ne_zero, imp_false]
  omega

lemma deriv_component (Î¼ Î½ : Fin d) (x : Space d) :
    (deriv Î½ (fun x => x Î¼) x) = if Î½ = Î¼ then 1 else 0 := by
  by_cases h' : Î½ = Î¼
  Â· subst h'
    simp
  Â· rw [deriv_component_diff Î½ Î¼]
    simp only [right_eq_ite_iff, zero_ne_one, imp_false]
    simpa using h'
    simpa using h'

lemma deriv_component_sq {d : â„•} {Î½ Î¼ : Fin d} (x : Space d) :
    (deriv Î½ (fun x => (x Î¼) ^ 2) x) = if Î½ = Î¼ then 2 * x Î¼ else 0:= by
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_pow]
  simp only [Nat.add_one_sub_one, pow_one, nsmul_eq_mul, Nat.cast_ofNat,
    ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [â† deriv_eq_fderiv_basis, deriv_component]
  simp only [mul_ite, mul_one, mul_zero]
  fun_prop

@[fun_prop]
lemma norm_sq_differentiable : Differentiable â„ (fun x : Space d => â€–xâ€– ^ 2) := by
  simp [@PiLp.norm_sq_eq_of_L2]
  fun_prop

lemma deriv_norm_sq (x : Space d) (i : Fin d) :
    deriv i (fun x => â€–xâ€– ^ 2) x = 2 * x i := by
  simp [@PiLp.norm_sq_eq_of_L2]
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_fun_sum]
  simp only [ContinuousLinearMap.coe_sum', Finset.sum_apply]
  conv_lhs =>
    enter [2, j]
    rw [â† deriv_eq_fderiv_basis]
    simp
  simp [deriv_component_sq]
  intro i hi
  fun_prop

open InnerProductSpace

lemma deriv_eq_inner_self (x : Space d) (i : Fin d) :
    deriv i (fun x => âŸªx, xâŸ«_â„) x = 2 * x i := by
  convert deriv_norm_sq x i
  exact real_inner_self_eq_norm_sq _

/-!

## Some properties of grad

-/

lemma grad_eq_sum {d} (f : Space d â†’ â„) (x : Space d) :
    âˆ‡ f x = âˆ‘ i, deriv i f x â€¢ basis i := by
  funext i
  rw [grad, deriv_eq]
  simp only
  rw [Fintype.sum_apply]
  simp only [PiLp.smul_apply, smul_eq_mul]
  rw [Finset.sum_eq_single i]
  Â· simp [basis]
    rfl
  Â· intro j hj
    simp [basis]
    exact fun a a_1 => False.elim (a (id (Eq.symm a_1)))
  Â· simp

open InnerProductSpace

lemma grad_inner_eq {d} (f : Space d â†’ â„) (x : Space d) (y : Space d) :
    âŸªâˆ‡ f x, yâŸ«_â„ = (fderiv â„ f x) y:= by
  rw [grad_eq_sum]
  have hy : y = âˆ‘ i, y i â€¢ basis i := by
      conv_lhs => rw [â† OrthonormalBasis.sum_repr basis y]
      dsimp [basis]
  rw [hy]
  simp only [PiLp.inner_apply, RCLike.inner_apply, conj_trivial, map_sum, map_smul, smul_eq_mul]
  conv_lhs =>
    enter [2, x]
    rw [Fintype.sum_apply, Fintype.sum_apply]
  simp [basis_apply]
  congr
  funext x
  rw [deriv_eq_fderiv_basis]

lemma inner_grad_eq {d} (f : Space d â†’ â„) (x : Space d) (y : Space d) :
    âŸªx, âˆ‡ f yâŸ«_â„ = (fderiv â„ f y) x := by
  rw [â† grad_inner_eq]
  exact real_inner_comm (âˆ‡ f y) x

lemma grad_eq_gradiant {d} (f : Space d â†’ â„) :
    âˆ‡ f = gradient f := by
  funext x
  have hx (y : Space d) : âŸªgradient f x, yâŸ«_â„ =
      âŸªâˆ‡ f x, yâŸ«_â„ := by
    rw [gradient, toDual_symm_apply]
    exact Eq.symm (grad_inner_eq f x y)
  have h1 : âˆ€ y, âŸªgradient f x - âˆ‡ f x, yâŸ«_â„ = 0 := by
    intro y
    rw [inner_sub_left, hx y]
    simp
  have h2 := h1 (gradient f x - âˆ‡ f x)
  rw [inner_self_eq_zero, sub_eq_zero] at h2
  rw [h2]

/-- The gradient in the direction of the space position. -/
lemma grad_inner_space_unit_vector {d} (x : Space d) (f : Space d â†’ â„) (hd : Differentiable â„ f) :
    âŸªâˆ‡ f x, â€–xâ€–â»Â¹ â€¢ xâŸ«_â„ = _root_.deriv (fun r => f (r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– := by
  by_cases hx : x = 0
  Â· subst hx
    simp
  symm
  calc _
    _ = fderiv â„ (f âˆ˜ (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– 1 := by rfl
    _ = (fderiv â„ f (â€–xâ€– â€¢ â€–xâ€–â»Â¹ â€¢ x)) (_root_.deriv (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x) â€–xâ€–) := by
      rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
      simp
    _ = (fderiv â„ f x) (_root_.deriv (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x) â€–xâ€–) := by
      have hx : â€–xâ€– â‰  0 := norm_ne_zero_iff.mpr hx
      field_simp [smul_smul]
  rw [grad_inner_eq f x (â€–xâ€–â»Â¹ â€¢ x)]
  congr
  rw [deriv_smul_const (by fun_prop)]
  simp

lemma grad_inner_space {d} (x : Space d) (f : Space d â†’ â„) (hd : Differentiable â„ f) :
    âŸªâˆ‡ f x, xâŸ«_â„ = â€–xâ€– * _root_.deriv (fun r => f (r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– := by
  rw [â† grad_inner_space_unit_vector _ _ hd, inner_smul_right]
  by_cases hx : x = 0
  Â· subst hx
    simp
  have hx : â€–xâ€– â‰  0 := norm_ne_zero_iff.mpr hx
  field_simp

lemma grad_norm_sq (x : Space d) :
    âˆ‡ (fun x => â€–xâ€– ^ 2) x = (2 : â„) â€¢ x := by
  funext i
  rw [grad_eq_sum]
  simp [deriv_norm_sq, basis_apply]

/-!

## Vector calculus identities

-/

lemma laplacian_eq_div_of_grad (f : Space â†’ â„) :
    Î” f = âˆ‡ â¬ âˆ‡ f := by
  unfold laplacian div grad Finset.sum coord basis
  simp only [Fin.univ_val_map, List.ofFn_succ, Fin.isValue, Fin.succ_zero_eq_one,
    Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons, List.sum_nil, add_zero,
    EuclideanSpace.basisFun_apply, PiLp.inner_apply, EuclideanSpace.single_apply,
    RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul, Finset.sum_ite_eq',
    Finset.mem_univ, â†“reduceIte]

lemma div_of_curl_eq_zero (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‡ â¬ (âˆ‡ Ã— f) = 0 := by
  unfold div curl Finset.sum coord basis
  ext x
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero, Pi.zero_apply]
  rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub, deriv_coord_2nd_sub]
  simp only [Fin.isValue, Pi.sub_apply]
  rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
    deriv_commute fun x => f x 2]
  simp only [Fin.isValue, sub_add_sub_cancel', sub_self]
  repeat
    try apply contDiff_euclidean.mp
    exact hf

lemma curl_of_curl (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‡ Ã— (âˆ‡ Ã— f) = âˆ‡ (âˆ‡ â¬ f) - Î” f := by
  unfold laplacianVec laplacian div grad curl Finset.sum coord basis
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero]
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Fin.reduceFinMk, Pi.sub_apply]
    rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub, deriv_coord_2nd_add]
    rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
      deriv_commute fun x => f x 2]
    simp only [Fin.isValue, Pi.sub_apply, Pi.add_apply]
    ring
    repeat
      try apply contDiff_euclidean.mp
      exact hf

section Const

@[simp]
lemma grad_zero : âˆ‡ (0 : Space d â†’ â„) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_zero, Pi.zero_apply, ContinuousLinearMap.zero_apply]
  rfl

@[simp]
lemma div_zero : âˆ‡ â¬ (0 : Space d â†’ EuclideanSpace â„ (Fin d)) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

@[simp]
lemma curl_zero : âˆ‡ Ã— (0 : Space â†’ EuclideanSpace â„ (Fin 3)) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

variable (c : â„) (v : EuclideanSpace â„ (Fin d)) (vâ‚ƒ : EuclideanSpace â„ (Fin 3))

@[simp]
lemma grad_const : âˆ‡ (fun _ : Space d => c) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply]
  rfl

@[simp]
lemma div_const : âˆ‡ â¬ (fun _ : Space d => v) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

@[simp]
lemma curl_const : âˆ‡ Ã— (fun _ : Space => vâ‚ƒ) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

end Const

/-!

## Addition of âˆ‡ operations

-/

lemma grad_add (f1 f2 : Space d â†’ â„)
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ (f1 + f2) = âˆ‡ f1 + âˆ‡ f2 := by
  unfold grad
  ext x i
  simp only [Pi.add_apply]
  rw [deriv_add]
  rfl
  exact hf1
  exact hf2

lemma div_add (f1 f2 : Space d â†’ EuclideanSpace â„ (Fin d))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ â¬ (f1 + f2) = âˆ‡ â¬ f1 + âˆ‡ â¬ f2 := by
  unfold div
  simp only [Pi.add_apply]
  funext x
  simp only [Pi.add_apply]
  rw [â† Finset.sum_add_distrib]
  congr
  funext i
  simp [coord_apply, Space.deriv]
  rw [fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply]
  Â· fun_prop
  Â· fun_prop

lemma curl_add (f1 f2 : Space â†’ EuclideanSpace â„ (Fin 3))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ Ã— (f1 + f2) = âˆ‡ Ã— f1 + âˆ‡ Ã— f2 := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Pi.add_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.add_apply, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul,
    zero_mul, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.zero_eta]
    repeat rw [deriv_coord_add]
    simp only [Fin.isValue, Pi.add_apply]
    ring
    repeat assumption

/-!

## Scalar multiplication of âˆ‡ operations

-/

lemma grad_smul (f : Space d â†’ â„) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ (k â€¢ f) = k â€¢ âˆ‡ f := by
  unfold grad
  ext x i
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [deriv_smul]
  rfl
  exact hf

lemma div_smul (f : Space d â†’ EuclideanSpace â„ (Fin d)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ â¬ (k â€¢ f) = k â€¢ âˆ‡ â¬ f := by
  unfold div
  simp only [Pi.smul_apply]
  funext x
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [Finset.mul_sum]
  congr
  funext i
  simp [coord_apply]
  simp [Space.deriv]
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  Â· fun_prop

lemma curl_smul (f : Space â†’ EuclideanSpace â„ (Fin 3)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ Ã— (k â€¢ f) = k â€¢ âˆ‡ Ã— f := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Pi.smul_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.smul_apply, smul_eq_mul, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial,
    ite_mul, one_mul, zero_mul, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.zero_eta]
    rw [deriv_coord_smul, deriv_coord_smul, mul_sub]
    simp only [Fin.isValue, Pi.smul_apply, smul_eq_mul]
    repeat fun_prop

/-!

## Linearity of div and curl

-/

variable {W} [NormedAddCommGroup W] [NormedSpace â„ W]

lemma div_linear_map (f : W â†’ Space 3 â†’ EuclideanSpace â„ (Fin 3))
    (hf : âˆ€ w, Differentiable â„ (f w))
    (hf' : IsLinearMap â„ f) :
    IsLinearMap â„ (fun w => âˆ‡ â¬ (f w)) := by
  constructor
  Â· intro w w'
    rw [hf'.map_add]
    rw [div_add]
    repeat fun_prop
  Â· intros k w
    rw [hf'.map_smul]
    rw [div_smul]
    fun_prop

lemma curl_linear_map (f : W â†’ Space 3 â†’ EuclideanSpace â„ (Fin 3))
    (hf : âˆ€ w, Differentiable â„ (f w))
    (hf' : IsLinearMap â„ f) :
    IsLinearMap â„ (fun w => âˆ‡ Ã— (f w)) := by
  constructor
  Â· intro w w'
    rw [hf'.map_add]
    rw [curl_add]
    repeat fun_prop
  Â· intros k w
    rw [hf'.map_smul]
    rw [curl_smul]
    fun_prop

/-!

## Inner product space identities

-/

open InnerProductSpace

/-- The inner product is differentiable. -/
lemma inner_differentiable {d : â„•} :
    Differentiable â„ (fun y : Space d => âŸªy, yâŸ«_â„) := by
  simp only [PiLp.inner_apply, RCLike.inner_apply, conj_trivial]
  fun_prop

/-- The gradient of the inner product is given by `2 â€¢ x`. -/
lemma grad_inner {d : â„•} :
    âˆ‡ (fun y : Space d => âŸªy, yâŸ«_â„) = fun z => (2:â„) â€¢ z := by
  ext z i
  simp [Space.grad]
  rw [deriv]
  erw [fderiv_fun_sum]
  Â· simp
    rw [Finset.sum_eq_single i]
    Â· trans (fderiv â„ (fun y => y i ^ 2) z) (EuclideanSpace.single i 1)
      Â· congr
        funext y
        ring
      trans deriv i ((fun x => x^ 2) âˆ˜ fun y => y i) z
      Â· rfl
      rw [deriv, fderiv_comp]
      Â· simp
        rw [â† deriv_eq]
        simp
      Â· fun_prop
      Â· fun_prop
    Â· intro b _ hb
      trans (fderiv â„ (fun y => y b ^ 2) z) (EuclideanSpace.single i 1)
      Â· congr
        funext y
        ring
      trans deriv i ((fun x => x^ 2) âˆ˜ fun y => y b) z
      Â· rfl
      rw [deriv, fderiv_comp]
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv,
        smul_eq_mul, mul_eq_zero]
      Â· left
        rw [â† deriv_eq]
        rw [deriv_component_diff]
        omega
      Â· fun_prop
      Â· fun_prop
    Â· simp
  Â· intro i _
    refine DifferentiableAt.inner â„ ?_ ?_
    Â· fun_prop
    Â· fun_prop

end Space
