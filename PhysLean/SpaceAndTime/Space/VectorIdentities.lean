/-
Copyright (c) 2025 Zhi Kai Pong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong
-/
import PhysLean.SpaceAndTime.Space.Basic
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Calculus.Gradient.Basic
/-!

# VectorIdentities

In this file we define common vector calculus identities based on `Space`.

-/

/-!

## General auxiliary lemmas

-/

section

variable
  {ùïú} [NontriviallyNormedField ùïú]
  {X} [NormedAddCommGroup X] [NormedSpace ùïú X]
  {Y} [NormedAddCommGroup Y] [NormedSpace ùïú Y]
  {Œπ : Type*} [Fintype Œπ] {Y' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (Y' i)]
  [‚àÄ i, NormedSpace ùïú (Y' i)] {Œ¶ : X ‚Üí ‚àÄ i, Y' i} {x : X}

lemma fderiv_pi' (h : DifferentiableAt ùïú Œ¶ x) :
    fderiv ùïú Œ¶ x = ContinuousLinearMap.pi fun i => (fderiv ùïú fun x => Œ¶ x i) x:= by
  apply HasFDerivAt.fderiv
  apply hasFDerivAt_pi''
  intro i
  rw [differentiableAt_pi] at h
  exact (h i).hasFDerivAt

lemma ContDiff.differentiable_fderiv (f : X ‚Üí Y) (hf : ContDiff ùïú 2 f) :
    Differentiable ùïú (fun x => fderiv ùïú f x) := by
  have hf' : ContDiff ùïú (1+1) f := hf
  rw [contDiff_succ_iff_fderiv] at hf'
  apply hf'.right.right.differentiable
  decide

lemma fderiv_coord_eq_proj_comp (h : DifferentiableAt ùïú Œ¶ x) :
    (fderiv ùïú fun x => Œ¶ x i) x = (ContinuousLinearMap.proj i).comp (fderiv ùïú Œ¶ x) := by
  rw [fderiv_pi', ContinuousLinearMap.proj_pi]
  exact h

end

/-!

## Space.deriv lemmas

-/

namespace Space

/-- Derivatives on space distiribute over addition. -/
lemma deriv_add [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (f1 f2 : Space d ‚Üí M) (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    ‚àÇ[u] (f1 + f2) = ‚àÇ[u] f1 + ‚àÇ[u] f2 := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_add']
  rfl
  repeat fun_prop

/-- Derivatives on space distiribute coordinate-wise over addition. -/
lemma deriv_coord_add (f1 f2 : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d))
    (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    (‚àÇ[u] (fun x => f1 x i + f2 x i)) =
      (‚àÇ[u] (fun x => f1 x i)) + (‚àÇ[u] (fun x => f2 x i)) := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  repeat fun_prop

/-- Scalar multiplication on space derivatives. -/
lemma deriv_smul [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (f : Space d ‚Üí M) (k : ‚Ñù) (hf : Differentiable ‚Ñù f) :
    ‚àÇ[u] (k ‚Ä¢ f) = (k ‚Ä¢ ‚àÇ[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_const_smul']
  rfl
  fun_prop

/-- Coordinate-wise scalar multiplication on space derivatives. -/
lemma deriv_coord_smul (f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) (k : ‚Ñù)
    (hf : Differentiable ‚Ñù f) :
    ‚àÇ[u] (fun x => k * f x i) x= (k ‚Ä¢ ‚àÇ[u] (fun x => f x i)) x:= by
  unfold deriv
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  fun_prop

/-- Derivatives on space commute with one another. -/
lemma deriv_commute [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (f : Space d ‚Üí M) (hf : ContDiff ‚Ñù 2 f) : ‚àÇ[u] (‚àÇ[v] f) = ‚àÇ[v] (‚àÇ[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  apply ContDiffAt.isSymmSndFDerivAt
  exact ContDiff.contDiffAt hf
  simp only [minSmoothness_of_isRCLikeNormedField, le_refl]
  repeat
  ¬∑ apply Differentiable.differentiableAt
    apply ContDiff.differentiable_fderiv
    exact hf
  ¬∑ fun_prop

/-- Coordiate functions of fderiv is differentiable. -/
lemma differentiable_fderiv_coord (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 f) :
    Differentiable ‚Ñù (fderiv ‚Ñù fun x => f x i) := by
  have eq : (fderiv ‚Ñù (fun x ‚Ü¶ f x i))
      = (fun x => (ContinuousLinearMap.proj i).comp (fderiv ‚Ñù f x)) := by
    ext x
    rw [fderiv_coord_eq_proj_comp]
    apply hf.differentiable
    decide
  rw [eq]
  apply Differentiable.clm_comp
  ¬∑ fun_prop
  ¬∑ apply ContDiff.differentiable_fderiv
    exact hf

/-- Second derivatives distiribute coordinate-wise over addition (all three components for div). -/
lemma deriv_coord_2nd_add (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 f) :
    ‚àÇ[i] (fun x => ‚àÇ[u] (fun x => f x u) x + (‚àÇ[v] (fun x => f x v) x + ‚àÇ[w] (fun x => f x w) x)) =
    (‚àÇ[i] (‚àÇ[u] (fun x => f x u))) + (‚àÇ[i] (‚àÇ[v] (fun x => f x v))) +
    (‚àÇ[i] (‚àÇ[w] (fun x => f x w))) := by
  unfold deriv
  ext x
  rw [fderiv_add, fderiv_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  ring
  repeat
    try apply DifferentiableAt.add
    apply Differentiable.differentiableAt
    apply Differentiable.clm_apply
    ¬∑ apply differentiable_fderiv_coord
      exact hf
    ¬∑ fun_prop

/-- Second derivatives distiribute coordinate-wise over subtraction (two components for curl). -/
lemma deriv_coord_2nd_sub (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 f) :
    ‚àÇ[u] (fun x => ‚àÇ[v] (fun x => f x w) x - ‚àÇ[w] (fun x => f x v) x) =
    (‚àÇ[u] (‚àÇ[v] (fun x => f x w))) - (‚àÇ[u] (‚àÇ[w] (fun x => f x v))) := by
  unfold deriv
  ext x
  simp only [Pi.sub_apply]
  rw [fderiv_sub]
  simp only [ContinuousLinearMap.coe_sub', Pi.sub_apply]
  repeat
    apply Differentiable.differentiableAt
    apply Differentiable.clm_apply
    ¬∑ apply differentiable_fderiv_coord
      exact hf
    ¬∑ fun_prop

/-!

## Some properties of grad

-/

lemma grad_eq_sum {d} (f : Space d ‚Üí ‚Ñù) (x : Space d) :
    ‚àá f x = ‚àë i, deriv i f x ‚Ä¢ basis i := by
  funext i
  rw [grad, deriv_eq]
  simp only
  rw [Fintype.sum_apply]
  simp only [PiLp.smul_apply, smul_eq_mul]
  rw [Finset.sum_eq_single i]
  ¬∑ simp [basis]
    rfl
  ¬∑ intro j hj
    simp [basis]
    exact fun a a_1 => False.elim (a (id (Eq.symm a_1)))
  ¬∑ simp

open InnerProductSpace in
lemma grad_eq_gradiant {d} (f : Space d ‚Üí ‚Ñù) :
    ‚àá f = gradient f := by
  funext x
  simp [grad, gradient, deriv_eq]
  have hx (y : Space d) : ‚ü™(InnerProductSpace.toDual ‚Ñù (Space d)).symm (fderiv ‚Ñù f x), y‚ü´_‚Ñù =
      ‚ü™‚àá f x, y‚ü´_‚Ñù := by
    rw [toDual_symm_apply]
    have hy : y = ‚àë i, y i ‚Ä¢ basis i := by
      conv_lhs => rw [‚Üê OrthonormalBasis.sum_repr basis y]
      dsimp [basis]
    rw [hy]
    conv_lhs => simp
    conv_rhs => rw [inner_sum]
    congr
    funext i
    rw [inner_smul_right]
    congr
    rw [grad_eq_sum]
    rw [‚Üê inner_conj_symm]
    simp only [conj_trivial, inner_sum]
    simp only [inner_smul_right]
    rw [Finset.sum_eq_single i]
    ¬∑ have h1 : ‚ü™basis i, basis i‚ü´_‚Ñù = 1 := by
        rw [real_inner_self_eq_norm_sq, basis.norm_eq_one]
        simp
      rw [h1]
      simp only [mul_one]
      rw [deriv_eq]
      simp [basis]
    ¬∑ intro b hb h
      rw [basis.inner_eq_zero]
      simp only [mul_zero]
      exact id (Ne.symm h)
    ¬∑ simp
  have h1 : ‚àÄ y, ‚ü™(toDual ‚Ñù (Space d)).symm (fderiv ‚Ñù f x) - ‚àá f x, y‚ü´_‚Ñù = 0 := by
    intro y
    rw [inner_sub_left, hx y]
    simp
  have h2 := h1 ((toDual ‚Ñù (Space d)).symm (fderiv ‚Ñù f x) - ‚àá f x)
  rw [inner_self_eq_zero, sub_eq_zero] at h2
  rw [h2]

/-!

## Vector calculus identities

-/

lemma laplacian_eq_div_of_grad (f : Space ‚Üí ‚Ñù) :
    Œî f = ‚àá ‚¨ù ‚àá f := by
  unfold laplacian div grad Finset.sum coord basis
  simp only [Fin.univ_val_map, List.ofFn_succ, Fin.isValue, Fin.succ_zero_eq_one,
    Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons, List.sum_nil, add_zero,
    EuclideanSpace.basisFun_apply, PiLp.inner_apply, EuclideanSpace.single_apply,
    RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul, Finset.sum_ite_eq',
    Finset.mem_univ, ‚ÜìreduceIte]

lemma div_of_curl_eq_zero (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 f) :
    ‚àá ‚¨ù (‚àá √ó f) = 0 := by
  unfold div curl Finset.sum coord basis
  ext x
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero, Pi.zero_apply]
  rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub, deriv_coord_2nd_sub]
  simp only [Fin.isValue, Pi.sub_apply]
  rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
    deriv_commute fun x => f x 2]
  simp only [Fin.isValue, sub_add_sub_cancel', sub_add_sub_cancel, sub_self]
  repeat
    try apply contDiff_euclidean.mp
    exact hf

lemma curl_of_curl (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (hf : ContDiff ‚Ñù 2 f) :
    ‚àá √ó (‚àá √ó f) = ‚àá (‚àá ‚¨ù f) - Œî f := by
  unfold laplacian_vec laplacian div grad curl Finset.sum coord basis
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero]
  ext x i
  fin_cases i <;>
  ¬∑ simp only [Fin.isValue, Fin.reduceFinMk, Pi.sub_apply]
    rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub, deriv_coord_2nd_add]
    rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
      deriv_commute fun x => f x 2]
    simp only [Fin.isValue, Pi.sub_apply, Pi.add_apply]
    ring
    repeat
      try apply contDiff_euclidean.mp
      exact hf

section Const

@[simp]
lemma grad_zero : ‚àá (0 : Space d ‚Üí ‚Ñù) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_zero, Pi.zero_apply, ContinuousLinearMap.zero_apply]
  rfl

@[simp]
lemma div_zero : ‚àá ‚¨ù (0 : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

@[simp]
lemma curl_zero : ‚àá √ó (0 : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

variable (c : ‚Ñù) (v : EuclideanSpace ‚Ñù (Fin d)) (v‚ÇÉ : EuclideanSpace ‚Ñù (Fin 3))

@[simp]
lemma grad_const : ‚àá (fun _ : Space d => c) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply]
  rfl

@[simp]
lemma div_const : ‚àá ‚¨ù (fun _ : Space d => v) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

@[simp]
lemma curl_const : ‚àá √ó (fun _ : Space => v‚ÇÉ) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

end Const

/-!

## Addition of ‚àá operations

-/

lemma grad_add (f1 f2 : Space d ‚Üí ‚Ñù)
    (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    ‚àá (f1 + f2) = ‚àá f1 + ‚àá f2 := by
  unfold grad
  ext x i
  simp only [Pi.add_apply]
  rw [deriv_add]
  rfl
  exact hf1
  exact hf2

lemma div_add (f1 f2 : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d))
    (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    ‚àá ‚¨ù (f1 + f2) = ‚àá ‚¨ù f1 + ‚àá ‚¨ù f2 := by
  unfold div
  simp only [Pi.add_apply]
  funext x
  simp only [Pi.add_apply]
  rw [‚Üê Finset.sum_add_distrib]
  congr
  funext i
  simp [coord_apply, Space.deriv]
  rw [fderiv_add]
  simp only [ContinuousLinearMap.add_apply]
  ¬∑ fun_prop
  ¬∑ fun_prop

lemma curl_add (f1 f2 : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3))
    (hf1 : Differentiable ‚Ñù f1) (hf2 : Differentiable ‚Ñù f2) :
    ‚àá √ó (f1 + f2) = ‚àá √ó f1 + ‚àá √ó f2 := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  ¬∑ simp only [Fin.isValue, Pi.add_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.add_apply, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul,
    zero_mul, Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte, Fin.zero_eta]
    repeat rw [deriv_coord_add]
    simp only [Fin.isValue, Pi.add_apply]
    ring
    repeat assumption

/-!

## Scalar multiplication of ‚àá operations

-/

lemma grad_smul (f : Space d ‚Üí ‚Ñù) (k : ‚Ñù)
    (hf : Differentiable ‚Ñù f) :
    ‚àá (k ‚Ä¢ f) = k ‚Ä¢ ‚àá f := by
  unfold grad
  ext x i
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [deriv_smul]
  rfl
  exact hf

lemma div_smul (f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin d)) (k : ‚Ñù)
    (hf : Differentiable ‚Ñù f) :
    ‚àá ‚¨ù (k ‚Ä¢ f) = k ‚Ä¢ ‚àá ‚¨ù f := by
  unfold div
  simp only [Pi.smul_apply]
  funext x
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [Finset.mul_sum]
  congr
  funext i
  simp [coord_apply]
  simp [Space.deriv]
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  ¬∑ fun_prop

lemma curl_smul (f : Space ‚Üí EuclideanSpace ‚Ñù (Fin 3)) (k : ‚Ñù)
    (hf : Differentiable ‚Ñù f) :
    ‚àá √ó (k ‚Ä¢ f) = k ‚Ä¢ ‚àá √ó f := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  ¬∑ simp only [Fin.isValue, Pi.smul_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.smul_apply, smul_eq_mul, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial,
    ite_mul, one_mul, zero_mul, Finset.sum_ite_eq', Finset.mem_univ, ‚ÜìreduceIte, Fin.zero_eta]
    rw [deriv_coord_smul, deriv_coord_smul, mul_sub]
    simp only [Fin.isValue, Pi.smul_apply, smul_eq_mul]
    repeat fun_prop

/-!

## Linearity of div and curl

-/

variable {W} [NormedAddCommGroup W] [NormedSpace ‚Ñù W]

lemma div_linear_map (f : W ‚Üí Space 3 ‚Üí EuclideanSpace ‚Ñù (Fin 3))
    (hf : ‚àÄ w, Differentiable ‚Ñù (f w))
    (hf' : IsLinearMap ‚Ñù f) :
    IsLinearMap ‚Ñù (fun w => ‚àá ‚¨ù (f w)) := by
  constructor
  ¬∑ intro w w'
    rw [hf'.map_add]
    rw [div_add]
    repeat fun_prop
  ¬∑ intros k w
    rw [hf'.map_smul]
    rw [div_smul]
    fun_prop

lemma curl_linear_map (f : W ‚Üí Space 3 ‚Üí EuclideanSpace ‚Ñù (Fin 3))
    (hf : ‚àÄ w, Differentiable ‚Ñù (f w))
    (hf' : IsLinearMap ‚Ñù f) :
    IsLinearMap ‚Ñù (fun w => ‚àá √ó (f w)) := by
  constructor
  ¬∑ intro w w'
    rw [hf'.map_add]
    rw [curl_add]
    repeat fun_prop
  ¬∑ intros k w
    rw [hf'.map_smul]
    rw [curl_smul]
    fun_prop

end Space
