/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.DistOfFunction
import PhysLean.SpaceAndTime.Space.Derivatives.Grad
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.Analysis.InnerProductSpace.NormPow
import Mathlib.Analysis.Calculus.FDeriv.Norm
/-!

# The norm on space

## i. Overview

The main content of this file is defining `Space.normPowerSeries`, a power series which is
differentiable everywhere, and which tends to the norm in the limit as `n ‚Üí ‚àû`.

## ii. Key results

- `normPowerSeries` : A power series which is differentiable everywhere, and in the limit
  as `n ‚Üí ‚àû` tends to `‚Äñx‚Äñ`.
- `normPowerSeries_differentiable` : The power series is differentiable everywhere.
- `normPowerSeries_tendsto` : The power series tends to the norm in the limit as `n ‚Üí ‚àû`.

## iii. Table of contents

- A. The norm as a power series
  - A.1. Differentiability of the norm power series
  - A.2. The limit of the norm power series
  - A.3. The derivative of the norm power series
  - A.4. Limits of the derivative of the power series
  - A.5. The power series is AEStronglyMeasurable
  - A.6. Bounds on the norm power series
  - A.7. The `IsDistBounded` property of the norm power series
  - A.8. Differentiability of functions
  - A.9. Derivatives of functions
  - A.10. Gradients of distributions
- B. Distributions involving norms

## iv. References

-/
open SchwartzMap NNReal
noncomputable section

variable (ùïú : Type) {E F F' : Type} [RCLike ùïú] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F]

namespace Space

open MeasureTheory
/-!

## A. The norm as a power series

-/

/-- A power series which is differentiable everywhere, and in the limit
  as `n ‚Üí ‚àû` tends to `‚Äñx‚Äñ`. -/
def normPowerSeries {d} : ‚Ñï ‚Üí Space d ‚Üí ‚Ñù := fun n x =>
  ‚àö(‚Äñx‚Äñ ^ 2 + 1/(n + 1))

lemma normPowerSeries_eq (n : ‚Ñï) :
    normPowerSeries (d := d) n = fun x => ‚àö(‚Äñx‚Äñ ^ 2 + 1/(n + 1)) := rfl

lemma normPowerSeries_eq_rpow {d} (n : ‚Ñï) :
    normPowerSeries (d := d) n = fun x => ((‚Äñx‚Äñ ^ 2 + 1/(n + 1))) ^ (1/2 : ‚Ñù) := by
  rw [normPowerSeries_eq]
  funext x
  rw [‚Üê Real.sqrt_eq_rpow]

/-!

### A.1. Differentiability of the norm power series

-/

@[fun_prop]
lemma normPowerSeries_differentiable {d} (n : ‚Ñï) :
    Differentiable ‚Ñù (fun (x : Space d) => normPowerSeries n x) := by
  rw [normPowerSeries_eq_rpow]
  refine Differentiable.rpow_const ?_ ?_
  ¬∑ refine (Differentiable.fun_add_iff_right ?_).mpr ?_
    ¬∑ apply Differentiable.norm_sq ‚Ñù
      fun_prop
    ¬∑ fun_prop
  ¬∑ intro x
    have h1 : 0 < ‚Äñx‚Äñ ^ 2 + 1 / (‚Üën + 1) := by positivity
    grind

/-!

### A.2. The limit of the norm power series

-/
open InnerProductSpace

open scoped Topology BigOperators FourierTransform

lemma normPowerSeries_tendsto {d} (x : Space d) (hx : x ‚â† 0) :
    Filter.Tendsto (fun n => normPowerSeries n x) Filter.atTop (ùìù (‚Äñx‚Äñ)) := by
  conv => enter [1, n]; rw [normPowerSeries_eq_rpow]
  simp only [one_div]
  have hx_norm : ‚Äñx‚Äñ = (‚Äñx‚Äñ ^ 2 + 0) ^ (1 / 2 : ‚Ñù) := by
    rw [‚Üê Real.sqrt_eq_rpow]
    simp
  conv_rhs => rw [hx_norm]
  refine Filter.Tendsto.rpow ?_ ?_ ?_
  ¬∑ apply Filter.Tendsto.add
    ¬∑ exact tendsto_const_nhds
    ¬∑ simpa using tendsto_one_div_add_atTop_nhds_zero_nat
  ¬∑ simp
  ¬∑ left
    simpa using hx

lemma normPowerSeries_inv_tendsto {d} (x : Space d) (hx : x ‚â† 0) :
    Filter.Tendsto (fun n => (normPowerSeries n x)‚Åª¬π) Filter.atTop (ùìù (‚Äñx‚Äñ‚Åª¬π)) := by
  apply Filter.Tendsto.inv‚ÇÄ
  ¬∑ exact normPowerSeries_tendsto x hx
  ¬∑ simpa using hx

/-!

### A.3. The derivative of the norm power series

-/
open Space

lemma deriv_normPowerSeries {d} (n : ‚Ñï) (x : Space d) (i : Fin d) :
    ‚àÇ[i] (normPowerSeries n) x = x i * (normPowerSeries n x)‚Åª¬π := by
  rw [deriv_eq_fderiv_basis]
  rw [normPowerSeries_eq]
  rw [fderiv_sqrt]
  simp only [one_div, mul_inv_rev, fderiv_add_const, ContinuousLinearMap.coe_smul', Pi.smul_apply,
    smul_eq_mul]
  rw [‚Üê deriv_eq_fderiv_basis]
  rw [deriv_norm_sq]
  ring
  ¬∑ simp
    apply DifferentiableAt.norm_sq ‚Ñù
    fun_prop
  ¬∑ positivity

lemma fderiv_normPowerSeries {d} (n : ‚Ñï) (x y : Space d) :
    fderiv ‚Ñù (fun (x : Space d) => normPowerSeries n x) x y =
      ‚ü™y, x‚ü´_‚Ñù * (normPowerSeries n x)‚Åª¬π := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_normPowerSeries]
  ring

/-!

### A.4. Limits of the derivative of the power series

-/

lemma deriv_normPowerSeries_tendsto {d} (x : Space d) (hx : x ‚â† 0) (i : Fin d) :
    Filter.Tendsto (fun n => ‚àÇ[i] (normPowerSeries n) x) Filter.atTop (ùìù (x i * (‚Äñx‚Äñ)‚Åª¬π)) := by
  conv => enter [1, n]; rw [deriv_normPowerSeries]
  refine Filter.Tendsto.mul ?_ ?_
  ¬∑ exact tendsto_const_nhds
  ¬∑ exact normPowerSeries_inv_tendsto x hx

lemma fderiv_normPowerSeries_tendsto {d} (x y : Space d) (hx : x ‚â† 0) :
    Filter.Tendsto (fun n => fderiv ‚Ñù (fun (x : Space d) => normPowerSeries n x) x y)
      Filter.atTop (ùìù (‚ü™y, x‚ü´_‚Ñù * (‚Äñx‚Äñ)‚Åª¬π)) := by
  conv => enter [1, n]; rw [fderiv_normPowerSeries]
  refine Filter.Tendsto.mul ?_ ?_
  ¬∑ exact tendsto_const_nhds
  ¬∑ exact normPowerSeries_inv_tendsto x hx

/-!

### A.5. The power series is AEStronglyMeasurable

-/

@[fun_prop]
lemma normPowerSeries_aestronglyMeasurable {d} (n : ‚Ñï) :
    AEStronglyMeasurable (normPowerSeries n : Space d ‚Üí ‚Ñù) volume := by
  rw [normPowerSeries_eq_rpow]
  refine StronglyMeasurable.aestronglyMeasurable ?_
  refine stronglyMeasurable_iff_measurable.mpr ?_
  fun_prop

/-!

### A.6. Bounds on the norm power series

-/

@[simp]
lemma normPowerSeries_nonneg {d} (n : ‚Ñï) (x : Space d) :
    0 ‚â§ normPowerSeries n x := by
  rw [normPowerSeries_eq]
  simp

@[simp]
lemma normPowerSeries_pos {d} (n : ‚Ñï) (x : Space d) :
    0 < normPowerSeries n x := by
  rw [normPowerSeries_eq]
  simp only [one_div, Real.sqrt_pos]
  positivity

@[simp]
lemma normPowerSeries_ne_zero {d} (n : ‚Ñï) (x : Space d) :
    normPowerSeries n x ‚â† 0 := by
  rw [normPowerSeries_eq]
  simp only [one_div, ne_eq]
  positivity

lemma normPowerSeries_le_norm_sq_add_one {d} (n : ‚Ñï) (x : Space d) :
    normPowerSeries n x ‚â§ ‚Äñx‚Äñ + 1 := by
  trans ‚àö(‚Äñx‚Äñ ^ 2 + 1)
  ¬∑ rw [normPowerSeries_eq]
    apply Real.sqrt_le_sqrt
    simp only [one_div, add_le_add_iff_left]
    refine inv_le_one_iff‚ÇÄ.mpr ?_
    right
    simp
  ¬∑ refine (Real.sqrt_le_left (by positivity)).mpr ?_
    trans (‚Äñx‚Äñ ^ 2 + 1) + (2 * ‚Äñx‚Äñ)
    ¬∑ simp
    ¬∑ ring_nf
      rfl

@[simp]
lemma norm_lt_normPowerSeries {d} (n : ‚Ñï) (x : Space d) :
    ‚Äñx‚Äñ < normPowerSeries n x := by
  rw [normPowerSeries_eq]
  apply Real.lt_sqrt_of_sq_lt
  simp only [one_div, lt_add_iff_pos_right, inv_pos]
  positivity

lemma norm_le_normPowerSeries {d} (n : ‚Ñï) (x : Space d) :
    ‚Äñx‚Äñ ‚â§ normPowerSeries n x := by
  rw [normPowerSeries_eq]
  apply Real.le_sqrt_of_sq_le
  simp only [one_div, le_add_iff_nonneg_right, inv_nonneg]
  positivity

lemma normPowerSeries_zpow_le_norm_sq_add_one {d} (n : ‚Ñï) (m : ‚Ñ§) (x : Space d)
    (hx : x ‚â† 0) :
    (normPowerSeries n x) ^ m ‚â§ (‚Äñx‚Äñ + 1) ^ m + ‚Äñx‚Äñ ^ m := by
  match m with
  | .ofNat m =>
    trans (‚Äñx‚Äñ + 1) ^ m
    ¬∑ simp
      refine pow_le_pow_left‚ÇÄ (by simp) ?_ m
      exact normPowerSeries_le_norm_sq_add_one n x
    ¬∑ simp
  | .negSucc m =>
    trans (‚Äñx‚Äñ ^ (m + 1))‚Åª¬π; swap
    ¬∑ simp
      positivity
    simp only [zpow_negSucc]
    refine inv_anti‚ÇÄ ?_ ?_
    ¬∑ positivity
    refine pow_le_pow_left‚ÇÄ (by simp) ?_ (m + 1)
    exact norm_le_normPowerSeries n x

/-!

### A.7. The `IsDistBounded` property of the norm power series

-/

@[fun_prop]
lemma IsDistBounded.normPowerSeries_zpow {d : ‚Ñï} {n : ‚Ñï} (m : ‚Ñ§) :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x) ^ m) := by
  match m with
  | .ofNat m =>
    simp only [Int.ofNat_eq_coe, zpow_natCast]
    apply IsDistBounded.mono (f := fun (x : Space d) => (‚Äñx‚Äñ + 1) ^ m)
    ¬∑ fun_prop
    ¬∑ fun_prop
    intro x
    simp only [norm_pow, Real.norm_eq_abs]
    refine pow_le_pow_left‚ÇÄ (by positivity) ?_ m
    rw [abs_of_nonneg (by simp),abs_of_nonneg (by positivity)]
    exact normPowerSeries_le_norm_sq_add_one n x
  | .negSucc m =>
    simp only [zpow_negSucc]
    apply IsDistBounded.mono (f := fun (x : Space d) => ((‚àö(1/(n + 1)) : ‚Ñù) ^ (m + 1))‚Åª¬π)
    ¬∑ fun_prop
    ¬∑ rw [normPowerSeries_eq_rpow]
      refine StronglyMeasurable.aestronglyMeasurable ?_
      refine stronglyMeasurable_iff_measurable.mpr ?_
      fun_prop
    ¬∑ intro x
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, one_div]
      refine inv_anti‚ÇÄ (by positivity) ?_
      refine (pow_le_pow_iff_left‚ÇÄ (by positivity) (by positivity) (by simp)).mpr ?_
      rw [abs_of_nonneg (by positivity), abs_of_nonneg (by simp)]
      rw [normPowerSeries_eq]
      simp only [Real.sqrt_inv, one_div]
      rw [‚Üê Real.sqrt_inv]
      apply Real.sqrt_le_sqrt
      simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_single {d : ‚Ñï} {n : ‚Ñï} :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x)) := by
  convert IsDistBounded.normPowerSeries_zpow (n := n) (m := 1) using 1
  simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_inv {d : ‚Ñï} {n : ‚Ñï} :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x)‚Åª¬π) := by
  convert normPowerSeries_zpow (n := n) (-1) using 1
  simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_deriv {d : ‚Ñï} (n : ‚Ñï) (i : Fin d) :
    IsDistBounded (d := d) (fun x => ‚àÇ[i] (normPowerSeries n) x) := by
  conv =>
    enter [1, x];
    rw [deriv_normPowerSeries]
  fun_prop

@[fun_prop]
lemma IsDistBounded.normPowerSeries_fderiv {d : ‚Ñï} (n : ‚Ñï) (y : Space d) :
    IsDistBounded (d := d) (fun x => fderiv ‚Ñù (fun (x : Space d) => normPowerSeries n x) x y) := by
  conv =>
    enter [1, x];
    rw [fderiv_eq_sum_deriv]
  apply IsDistBounded.sum_fun
  fun_prop

@[fun_prop]
lemma IsDistBounded.normPowerSeries_log {d : ‚Ñï} (n : ‚Ñï) :
    IsDistBounded (d := d) (fun x => Real.log (normPowerSeries n x)) := by
  apply IsDistBounded.mono (f := fun x => (normPowerSeries n x)‚Åª¬π + (normPowerSeries n x))
  ¬∑ fun_prop
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ intro x
    simp only [Real.norm_eq_abs]
    conv_rhs => rw [abs_of_nonneg (by
      apply add_nonneg
      ¬∑ simp
      ¬∑ simp)]
    have h1 := Real.neg_inv_le_log (x := (normPowerSeries n x)) (by simp)
    have h2 := Real.log_le_rpow_div (x := (normPowerSeries n x)) (by simp) (Œµ := 1) (by positivity)
    simp_all
    rw [abs_le']
    generalize Real.log ‚Äñx‚Äñ = r at *
    apply And.intro
    ¬∑ apply h2.trans
      simp
    ¬∑ rw [neg_le]
      apply le_trans _ h1
      simp

/-!

### A.8. Differentiability of functions

-/

@[fun_prop]
lemma differentiable_normPowerSeries_zpow {d : ‚Ñï} {n : ‚Ñï} (m : ‚Ñ§) :
    Differentiable ‚Ñù (fun x : Space d => (normPowerSeries n x) ^ m) := by
  refine Differentiable.zpow ?_ ?_
  ¬∑ fun_prop
  ¬∑ left
    exact normPowerSeries_ne_zero n

@[fun_prop]
lemma differentiable_normPowerSeries_inv {d : ‚Ñï} {n : ‚Ñï} :
    Differentiable ‚Ñù (fun x : Space d => (normPowerSeries n x)‚Åª¬π) := by
  convert differentiable_normPowerSeries_zpow (n := n) (m := -1) using 1
  funext x
  simp

@[fun_prop]
lemma differentiable_log_normPowerSeries {d : ‚Ñï} {n : ‚Ñï} :
    Differentiable ‚Ñù (fun x : Space d => Real.log (normPowerSeries n x)) := by
  refine Differentiable.log ?_ ?_
  ¬∑ fun_prop
  ¬∑ intro x
    exact normPowerSeries_ne_zero n x
/-!

### A.9. Derivatives of functions

-/

lemma deriv_normPowerSeries_zpow {d : ‚Ñï} {n : ‚Ñï} (m : ‚Ñ§) (x : Space d) (i : Fin d) :
    ‚àÇ[i] (fun x : Space d => (normPowerSeries n x) ^ m) x =
      m * x i * (normPowerSeries n x) ^ (m - 2) := by
  rw [deriv_eq_fderiv_basis]
  change (fderiv ‚Ñù ((fun x => x ^ m) ‚àò normPowerSeries n) x) (basis i) = _
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv, deriv_zpow',
    smul_eq_mul]
  rw [fderiv_normPowerSeries]
  simp only [basis_inner]
  field_simp
  ring_nf
  have h1 : normPowerSeries n x ^ (-1 + m) = normPowerSeries n x ^ ((-2 + m) + 1) := by
    ring_nf
  rw [h1, zpow_add‚ÇÄ]
  simp only [Int.reduceNeg, zpow_one]
  ring
  ¬∑ simp
  ¬∑ refine DifferentiableAt.zpow ?_ ?_
    ¬∑ fun_prop
    ¬∑ left
      exact normPowerSeries_ne_zero n x
  ¬∑ fun_prop

lemma fderiv_normPowerSeries_zpow {d : ‚Ñï} {n : ‚Ñï} (m : ‚Ñ§) (x y : Space d) :
    fderiv ‚Ñù (fun x : Space d => (normPowerSeries n x) ^ m) x y =
      m * ‚ü™y, x‚ü´_‚Ñù * (normPowerSeries n x) ^ (m - 2) := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.mul_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_normPowerSeries_zpow]
  ring

lemma deriv_log_normPowerSeries {d : ‚Ñï} {n : ‚Ñï} (x : Space d) (i : Fin d) :
    ‚àÇ[i] (fun x : Space d => Real.log (normPowerSeries n x)) x =
      x i * (normPowerSeries n x) ^ (-2 : ‚Ñ§) := by
  rw [deriv_eq_fderiv_basis]
  change (fderiv ‚Ñù (Real.log ‚àò normPowerSeries n) x) (basis i) = _
  rw [fderiv_comp,]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv,
    Real.deriv_log', smul_eq_mul, Int.reduceNeg, zpow_neg]
  rw [fderiv_normPowerSeries]
  simp [zpow_ofNat, sq]
  ring
  ¬∑ apply DifferentiableAt.log ?_ ?_
    ¬∑ fun_prop
    exact normPowerSeries_ne_zero n x
  ¬∑ fun_prop

lemma fderiv_log_normPowerSeries {d : ‚Ñï} {n : ‚Ñï} (x y : Space d) :
    fderiv ‚Ñù (fun x : Space d => Real.log (normPowerSeries n x)) x y =
      ‚ü™y, x‚ü´_‚Ñù * (normPowerSeries n x) ^ (-2 : ‚Ñ§) := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_log_normPowerSeries]
  ring

/-!

### A.10. Gradients of distributions

-/

lemma gradient_dist_normPowerSeries_zpow {d : ‚Ñï} {n : ‚Ñï} (m : ‚Ñ§) :
    distGrad (distOfFunction (fun x : Space d => (normPowerSeries n x) ^ m) (by fun_prop)) =
    distOfFunction (fun x : Space d => (m * (normPowerSeries n x) ^ (m - 2)) ‚Ä¢ x)
    (by fun_prop) := by
  ext1 Œ∑
  apply ext_inner_right ‚Ñù
  intro y
  simp [distGrad_inner_eq]
  rw [Distribution.fderivD_apply, distOfFunction_apply, distOfFunction_inner]
  calc _
    _ = - ‚à´ (x : Space d), fderiv ‚Ñù Œ∑ x y * normPowerSeries n x ^ m := by
      rfl
    _ = ‚à´ (x : Space d), Œ∑ x * fderiv ‚Ñù (normPowerSeries n ¬∑ ^ m) x y := by
      rw [integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable]
      ¬∑ fun_prop
      ¬∑ refine IsDistBounded.integrable_space_mul ?_ Œ∑
        conv => enter [1, x]; rw [fderiv_normPowerSeries_zpow]
        simp [mul_assoc]
        fun_prop
      ¬∑ fun_prop
      ¬∑ exact Œ∑.differentiable
      ¬∑ fun_prop
    _ = ‚à´ (x : Space d), Œ∑ x * (m * ‚ü™y, x‚ü´_‚Ñù * (normPowerSeries n x) ^ (m - 2)) := by
      congr
      funext x
      rw [fderiv_normPowerSeries_zpow]
  congr
  funext x
  simp [inner_smul_left_eq_smul]
  left
  rw [real_inner_comm]
  ring

lemma gradient_dist_normPowerSeries_log {d : ‚Ñï} {n : ‚Ñï} :
    distGrad (distOfFunction (fun x : Space d => Real.log (normPowerSeries n x)) (by fun_prop)) =
    distOfFunction (fun x : Space d => ((normPowerSeries n x) ^ (- 2 : ‚Ñ§)) ‚Ä¢ x)
    (by fun_prop) := by
  ext1 Œ∑
  apply ext_inner_right ‚Ñù
  intro y
  simp [distGrad_inner_eq]
  rw [Distribution.fderivD_apply, distOfFunction_apply, distOfFunction_inner]
  calc _
    _ = - ‚à´ (x : Space d), fderiv ‚Ñù Œ∑ x y * Real.log (normPowerSeries n x) := by
      rfl
    _ = ‚à´ (x : Space d), Œ∑ x * fderiv ‚Ñù (fun x => Real.log (normPowerSeries n x)) x y := by
      rw [integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable]
      ¬∑ fun_prop
      ¬∑ refine IsDistBounded.integrable_space_mul ?_ Œ∑
        conv => enter [1, x]; rw [fderiv_log_normPowerSeries]
        fun_prop
      ¬∑ fun_prop
      ¬∑ exact Œ∑.differentiable
      ¬∑ fun_prop
    _ = ‚à´ (x : Space d), Œ∑ x * (‚ü™y, x‚ü´_‚Ñù * (normPowerSeries n x) ^ (- 2 : ‚Ñ§)) := by
      congr
      funext x
      rw [fderiv_log_normPowerSeries]
  congr
  funext x
  simp [inner_smul_left_eq_smul]
  left
  rw [real_inner_comm]
  ring

lemma gradient_dist_normPowerSeries_zpow_tendsTo_distGrad_norm {d : ‚Ñï} (m : ‚Ñ§)
    (hm : - (d.succ - 1 : ‚Ñï) ‚â§ m) (Œ∑ : ùì¢(Space d.succ, ‚Ñù))
    (y : EuclideanSpace ‚Ñù (Fin d.succ)) :
    Filter.Tendsto (fun n =>
    ‚ü™(distGrad (distOfFunction
    (fun x : Space d.succ => (normPowerSeries n x) ^ m) (by fun_prop))) Œ∑, y‚ü´_‚Ñù)
    Filter.atTop
    (ùìù (‚ü™distGrad (distOfFunction (fun x : Space d.succ => ‚Äñx‚Äñ ^ m)
    (IsDistBounded.pow m hm)) Œ∑, y‚ü´_‚Ñù)) := by
  simp [distGrad_inner_eq, Distribution.fderivD_apply, distOfFunction_apply]
  change Filter.Tendsto (fun n => - ‚à´ (x : Space d.succ), fderiv ‚Ñù Œ∑ x y * normPowerSeries n x ^ m)
    Filter.atTop (ùìù (- ‚à´ (x : Space d.succ), fderiv ‚Ñù Œ∑ x y * ‚Äñx‚Äñ ^ m))
  apply Filter.Tendsto.neg
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |fderiv ‚Ñù Œ∑ x y| * ((‚Äñx‚Äñ + 1) ^ m + ‚Äñx‚Äñ ^ m))
  ¬∑ intro n
    apply IsDistBounded.aeStronglyMeasurable_fderiv_schwartzMap_smul (F := ‚Ñù) ?_
    fun_prop
  ¬∑ have h1 : Integrable (fun x => (fderiv ‚Ñù (‚áëŒ∑) x) y * ((‚Äñx‚Äñ + 1) ^ m + ‚Äñx‚Äñ ^ m)) volume := by
      apply IsDistBounded.integrable_space_fderiv ?_
      apply IsDistBounded.add
      ¬∑ refine IsDistBounded.norm_add_pos_nat_zpow m 1 ?_
        simp
      ¬∑ exact IsDistBounded.pow m hm
    rw [‚Üê integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp only [Nat.succ_eq_add_one, norm_mul, Real.norm_eq_abs, mul_eq_mul_left_iff, abs_eq_zero]
    left
    rw [abs_of_nonneg (by positivity)]
    fun_prop
  ¬∑ intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}·∂ú
    constructor
    ¬∑ rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    rw [abs_of_nonneg (by simp)]
    exact normPowerSeries_zpow_le_norm_sq_add_one n m x hx
  ¬∑ rw [Filter.eventually_iff_exists_mem]
    use {0}·∂ú
    constructor
    ¬∑ rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    ¬∑ exact tendsto_const_nhds
    have h1 : Filter.Tendsto (fun x_1 => normPowerSeries x_1 x ^ (m : ‚Ñù))
      Filter.atTop (ùìù (‚Äñx‚Äñ ^ (m : ‚Ñù))) := by
      refine Filter.Tendsto.rpow ?_ ?_ ?_
      ¬∑ apply normPowerSeries_tendsto x hx
      ¬∑ simp
      ¬∑ left
        simpa using hx
    simpa using h1

lemma gradient_dist_normPowerSeries_zpow_tendsTo {d : ‚Ñï} (m : ‚Ñ§) (hm : - (d.succ - 1 : ‚Ñï) + 2 ‚â§ m)
    (Œ∑ : ùì¢(Space d.succ, ‚Ñù)) (y : EuclideanSpace ‚Ñù (Fin d.succ)) :
    Filter.Tendsto (fun n =>
    ‚ü™(distGrad (distOfFunction (fun x : Space d.succ => (normPowerSeries n x) ^ m)
    (by fun_prop))) Œ∑, y‚ü´_‚Ñù)
    Filter.atTop
    (ùìù (‚ü™distOfFunction (fun x : Space d.succ => (m * ‚Äñx‚Äñ ^ (m - 2)) ‚Ä¢ x) (by
    simp [‚Üê smul_smul]
    refine IsDistBounded.const_fun_smul ?_ ‚Üëm
    apply IsDistBounded.zpow_smul_self
    simp_all
    grind) Œ∑, y‚ü´_‚Ñù)) := by
  conv =>
    enter [1, n];
    rw [gradient_dist_normPowerSeries_zpow]
  simp [distOfFunction_inner]
  have h1 (n : ‚Ñï) (x : Space d.succ) :
    Œ∑ x * ‚ü™(‚Üëm * normPowerSeries n x ^ (m - 2)) ‚Ä¢ x, y‚ü´_‚Ñù =
    Œ∑ x * (m * (‚ü™x, y‚ü´_‚Ñù * (normPowerSeries n x) ^ (m - 2))) := by
    simp [inner_smul_left]
    ring_nf
    left
    trivial
  conv =>
    enter [1, n, 2, x];
    rw [h1 n x]
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |Œ∑ x| * |m| * |‚ü™x, y‚ü´_‚Ñù| * ((‚Äñx‚Äñ + 1) ^ (m - 2) + ‚Äñx‚Äñ ^ (m - 2)))
  ¬∑ intro n
    apply IsDistBounded.aeStronglyMeasurable_schwartzMap_smul (F := ‚Ñù) ?_ Œ∑
    apply IsDistBounded.const_mul_fun
    apply IsDistBounded.isDistBounded_mul_inner'
    fun_prop
  ¬∑ have h1 : Integrable (fun x =>
        Œ∑ x * (m * (‚ü™x, y‚ü´_‚Ñù * ((‚Äñx‚Äñ + 1) ^ (m - 2) + ‚Äñx‚Äñ ^ (m - 2))))) volume := by
      apply IsDistBounded.integrable_space_mul ?_ Œ∑
      apply IsDistBounded.const_mul_fun
      apply IsDistBounded.isDistBounded_mul_inner'
      apply IsDistBounded.add
      ¬∑ refine IsDistBounded.norm_add_pos_nat_zpow (m - 2) 1 ?_
        simp
      ¬∑ apply IsDistBounded.pow (m - 2)
        simp_all
        grind
    rw [‚Üê integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp [mul_assoc]
    rw [abs_of_nonneg (by positivity)]
    simp only [true_or]
    fun_prop
  ¬∑ intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}·∂ú
    constructor
    ¬∑ rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp [mul_assoc]
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    rw [abs_of_nonneg (by simp)]
    exact normPowerSeries_zpow_le_norm_sq_add_one n (m - 2) x hx
  ¬∑ rw [Filter.eventually_iff_exists_mem]
    use {0}·∂ú
    constructor
    ¬∑ rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    ¬∑ exact tendsto_const_nhds
    simp [inner_smul_left, mul_assoc]
    apply Filter.Tendsto.mul
    ¬∑ exact tendsto_const_nhds
    ring_nf
    apply Filter.Tendsto.mul
    ¬∑ exact tendsto_const_nhds
    have h1 : Filter.Tendsto (fun x_1 => normPowerSeries x_1 x ^ ((m - 2 : ‚Ñ§) : ‚Ñù))
      Filter.atTop (ùìù (‚Äñx‚Äñ ^ ((m - 2 : ‚Ñ§) : ‚Ñù))) := by
      refine Filter.Tendsto.rpow ?_ ?_ ?_
      ¬∑ apply normPowerSeries_tendsto x hx
      ¬∑ simp
      ¬∑ left
        simpa using hx
    simp [-Int.cast_sub, Real.rpow_intCast] at h1
    convert h1 using 3
    ¬∑ ring
    ¬∑ ring
/-!

## B. Distributions involving norms

-/

lemma distGrad_distOfFunction_norm_zpow {d : ‚Ñï} (m : ‚Ñ§) (hm : - (d.succ - 1 : ‚Ñï) + 2 ‚â§ m) :
    distGrad (distOfFunction (fun x : Space d.succ => ‚Äñx‚Äñ ^ m)
      (IsDistBounded.pow m (by simp_all; omega)))
    = distOfFunction (fun x : Space d.succ => (m * ‚Äñx‚Äñ ^ (m - 2)) ‚Ä¢ x) (by
      simp [‚Üê smul_smul]
      refine IsDistBounded.const_fun_smul ?_ ‚Üëm
      apply IsDistBounded.zpow_smul_self
      simp_all
      omega) := by
  ext1 Œ∑
  apply ext_inner_right ‚Ñù
  intro y
  apply tendsto_nhds_unique
    (gradient_dist_normPowerSeries_zpow_tendsTo_distGrad_norm m (by simp_all; omega) Œ∑ y)
    (gradient_dist_normPowerSeries_zpow_tendsTo m hm Œ∑ y)

end Space
