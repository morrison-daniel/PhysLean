/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.QuantumMechanics.OneDimension.HilbertSpace.PositionStates
import PhysLean.QuantumMechanics.OneDimension.Operators.Unbounded
/-!

# Position operator

In this module we define:
- The position operator on functions `â„ â†’ â„‚`
- The position operator on Schwartz maps as an unbounded operator on the Hilbert space.

We show that position wavefunctions are generalized eigenvectors of the position operator.

-/

namespace QuantumMechanics

namespace OneDimension
noncomputable section
open Constants
open HilbertSpace

/-!

## The position operator on functions `â„ â†’ â„‚`

-/

/-- The position operator is defined as the linear map from `â„ â†’ â„‚` to `â„ â†’ â„‚` taking
  `Ïˆ` to `x * Ïˆ`. -/
def positionOperator : (â„ â†’ â„‚) â†’â‚—[â„‚] â„ â†’ â„‚ where
  toFun Ïˆ := fun x â†¦ x * Ïˆ x
  map_add' Ïˆ1 Ïˆ2 := by
    funext x
    simp only [Pi.add_apply]
    ring
  map_smul' a Ïˆ1 := by
    funext x
    simp only [Pi.smul_apply, smul_eq_mul, RingHom.id_apply]
    ring
/-!

## The position operator on Schwartz maps

-/

private lemma norm_iteratedFDeriv_ofRealCLM {x} (i : â„•) :
    â€–iteratedFDeriv â„ i Complex.ofRealCLM xâ€– = if i = 0 then |x| else if i = 1 then 1 else 0 := by
  match i with
  | 0 =>
    simp [iteratedFDeriv_zero_eq_comp]
  | .succ i =>
    induction i with
    | zero =>
      simp [iteratedFDeriv_succ_eq_comp_right]
      rw [@ContinuousLinearMap.norm_def]
      apply ContinuousLinearMap.opNorm_eq_of_bounds
      Â· simp
      Â· intro x
        simp
      Â· intro N hN h
        simpa using h 1
    | succ i ih =>
      rw [iteratedFDeriv_succ_eq_comp_right]
      simp only [Nat.succ_eq_add_one, ContinuousLinearMap.fderiv, Function.comp_apply,
        LinearIsometryEquiv.norm_map, Nat.add_eq_zero, one_ne_zero, and_false, and_self, â†“reduceIte,
        Nat.add_eq_right]
      rw [iteratedFDeriv_succ_eq_comp_right]
      conv_lhs =>
        enter [1, 2, 3, y]
        rw [fderiv_const_apply Complex.ofRealCLM]
      conv_lhs =>
        enter [1, 2]
        change iteratedFDeriv â„ i 0
      simp only [Nat.succ_eq_add_one, Function.comp_apply, LinearIsometryEquiv.norm_map]
      have h1 : iteratedFDeriv â„ i (0 : â„ â†’ â„ â†’L[â„] â„ â†’L[â„] â„‚) x = 0 := by
        change iteratedFDeriv â„ i (fun x => 0) x = 0
        rw [iteratedFDeriv_zero_fun]
        rfl
      rw [h1]
      exact ContinuousMultilinearMap.opNorm_zero

open ContDiff

open SchwartzMap

/-- The parity operator on the Schwartz maps is defined as the linear map from
  `ğ“¢(â„, â„‚)` to itself, such that `Ïˆ` is taken to `fun x => x * Ïˆ x`. -/
def positionOperatorSchwartz : ğ“¢(â„, â„‚) â†’L[â„‚] ğ“¢(â„, â„‚) := by
  refine mkCLM (fun Ïˆ â†¦ fun x => x * Ïˆ x) ?_ ?_ ?_ ?_
  Â· intro Ïˆ1 Ïˆ2 x
    simp [mul_add]
  Â· intro c Ïˆ x
    simp only [Pi.smul_apply, smul_eq_mul, RingHom.id_apply]
    ring
  Â· intro Ïˆ
    simp only
    apply ContDiff.mul
    Â· change ContDiff â„ _ Complex.ofRealCLM
      fun_prop
    Â· exact SchwartzMap.smooth Ïˆ âŠ¤
  Â· intro (k, n)
    use {(k, n - 1), (k + 1, n)}
    simp only [Real.norm_eq_abs, Finset.sup_insert, schwartzSeminormFamily_apply,
      Finset.sup_singleton, Seminorm.coe_sup, Pi.sup_apply]
    use n + 1
    refine âŸ¨by linarith, ?_âŸ©
    intro Ïˆ x
    trans â€–xâ€– ^ k * âˆ‘ i âˆˆ Finset.range (n + 1), â†‘(n.choose i) *
      â€–iteratedFDeriv â„ i (fun (x : â„) => (x : â„‚)) xâ€– *
      â€–iteratedFDeriv â„ (n - i) (fun x => Ïˆ x) xâ€–
    Â· apply mul_le_mul_of_nonneg'
      Â· exact Preorder.le_refl (â€–xâ€– ^ k)
      Â· apply norm_iteratedFDeriv_mul_le (N := âˆ)
        Â· change ContDiff â„ âˆ Complex.ofRealCLM
          fun_prop
        Â· exact SchwartzMap.smooth (Ïˆ) âŠ¤
        Â· exact right_eq_inf.mp rfl
      Â· exact ContinuousMultilinearMap.opNorm_nonneg _
      Â· refine pow_nonneg ?_ k
        exact norm_nonneg x
    conv_lhs =>
      enter [2, 2, i, 1, 2]
      change â€–iteratedFDeriv â„ i Complex.ofRealCLM xâ€–
      rw [norm_iteratedFDeriv_ofRealCLM i]
    match n with
    | 0 =>
      simp only [Real.norm_eq_abs, zero_add, Finset.range_one, mul_ite, mul_one, mul_zero, ite_mul,
        zero_mul, Finset.sum_singleton, â†“reduceIte, Nat.choose_self, Nat.cast_one, one_mul,
        Nat.sub_zero, norm_iteratedFDeriv_zero, CharP.cast_eq_zero, ge_iff_le]
      trans (SchwartzMap.seminorm â„‚ (k + 1) 0) Ïˆ
      Â· apply le_trans ?_ (Ïˆ.le_seminorm â„ _ _ x)
        simp only [Real.norm_eq_abs, norm_iteratedFDeriv_zero]
        ring_nf
        rfl
      exact le_max_right ((SchwartzMap.seminorm â„‚ k (0 - 1)) Ïˆ)
        ((SchwartzMap.seminorm â„‚ (k + 1) 0) Ïˆ)
    | .succ n =>
      rw [Finset.sum_range_succ', Finset.sum_range_succ']
      simp only [Real.norm_eq_abs, Nat.succ_eq_add_one, Nat.add_eq_zero, one_ne_zero, and_false,
        and_self, â†“reduceIte, Nat.add_eq_right, mul_zero, zero_mul, Finset.sum_const_zero,
        zero_add, Nat.choose_one_right, Nat.cast_add, Nat.cast_one, mul_one, Nat.reduceAdd,
        Nat.add_one_sub_one, Nat.choose_zero_right, one_mul, Nat.sub_zero, ge_iff_le]
      trans (â†‘n + 1) * (|x| ^ k * â€–iteratedFDeriv â„ n (fun x => (Ïˆ) x) xâ€–)
            + (|x| ^ (k + 1) * â€–iteratedFDeriv â„ (n + 1) (fun x => (Ïˆ) x) xâ€–)
      Â· apply le_of_eq
        ring
      trans (â†‘n + 1) * (SchwartzMap.seminorm â„‚ k (n) Ïˆ)
            + (SchwartzMap.seminorm â„‚ (k + 1) (n + 1) Ïˆ)
      Â· apply add_le_add _ _
        apply mul_le_mul_of_nonneg_left _
        refine Left.add_nonneg ?_ ?_
        Â· exact Nat.cast_nonneg' n
        Â· exact zero_le_one' â„
        Â· exact Ïˆ.le_seminorm â„ k n x
        Â· exact Ïˆ.le_seminorm â„ (k + 1) (n + 1) x
      Â· by_cases h1 :((SchwartzMap.seminorm â„‚ (k + 1) (n + 1)) Ïˆ) <
          ((SchwartzMap.seminorm â„‚ k n) Ïˆ)
        Â· rw [max_eq_left_of_lt h1]
          trans (â†‘n + 1) * (SchwartzMap.seminorm â„‚ k n) Ïˆ + (SchwartzMap.seminorm â„‚ k n) Ïˆ
          apply add_le_add
          Â· simp
          Â· exact le_of_lt h1
          apply le_of_eq
          ring
        Â· simp at h1
          rw [max_eq_right h1]
          trans (â†‘n + 1) * (SchwartzMap.seminorm â„‚ (k + 1) (n + 1)) Ïˆ +
            (SchwartzMap.seminorm â„‚ (k + 1) (n + 1)) Ïˆ
          Â· apply add_le_add
            Â· apply mul_le_mul_of_nonneg_left _
              Â· linarith
              Â· exact h1
            Â· simp
          Â· apply le_of_eq
            ring

lemma positionOperatorSchwartz_apply_fun (Ïˆ : ğ“¢(â„, â„‚)) :
    (positionOperatorSchwartz Ïˆ) = fun x => x * Ïˆ x := by
  simp [positionOperatorSchwartz]
  rfl

@[simp]
lemma positionOperatorSchwartz_apply (Ïˆ : ğ“¢(â„, â„‚)) (x : â„) :
    (positionOperatorSchwartz Ïˆ) x = x * Ïˆ x := by
  simp [positionOperatorSchwartz]
  rfl

/-- The unbounded position operator, whose domain is Schwartz maps. -/
def positionOperatorUnbounded : UnboundedOperator schwartzIncl schwartzIncl_injective :=
  UnboundedOperator.ofSelfCLM positionOperatorSchwartz

/-!

## Generalized eigenvectors of the momentum operator

-/

lemma positionStates_generalized_eigenvector_positionOperatorUnbounded (x : â„) :
    positionOperatorUnbounded.IsGeneralizedEigenvector (positionState x) x := by
  dsimp [positionOperatorUnbounded]
  rw [UnboundedOperator.isGeneralizedEigenvector_ofSelfCLM_iff]
  intro Ïˆ
  simp [positionState_apply]

/-!

## Position operator is self adjoint

-/

lemma positionOperatorUnbounded_isSelfAdjoint :
    positionOperatorUnbounded.IsSelfAdjoint := by
  intro Ïˆ1 Ïˆ2
  dsimp [positionOperatorUnbounded]
  rw [schwartzIncl_inner, schwartzIncl_inner]
  congr
  funext x
  simp only [positionOperatorSchwartz_apply, map_mul, Complex.conj_ofReal]
  ring

end
end OneDimension
end QuantumMechanics
