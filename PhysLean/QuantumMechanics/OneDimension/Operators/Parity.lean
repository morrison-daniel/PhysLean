/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.QuantumMechanics.OneDimension.HilbertSpace.PositionStates
import PhysLean.QuantumMechanics.OneDimension.Operators.Unbounded
/-!

# Parity operator

-/

namespace QuantumMechanics

namespace OneDimension
noncomputable section

namespace HilbertSpace
open MeasureTheory SchwartzMap

/-!

## The parity operator on functions

-/

/-- The parity operator is defined as linear map from `â„ â†’ â„‚` to itself, such that
  `Ïˆ` is taken to `fun x => Ïˆ (-x)`. -/
def parityOperator : (â„ â†’ â„‚) â†’â‚—[â„‚] (â„ â†’ â„‚) where
  toFun Ïˆ := fun x => Ïˆ (-x)
  map_add' Ïˆ1 Ïˆ2 := by
    funext x
    simp
  map_smul' a Ïˆ1 := by
    funext x
    simp

/-!

## The parity operator on Schwartz maps

-/

/-- The parity operator on the Schwartz maps is defined as the linear map from
  `ğ“¢(â„, â„‚)` to itself, such that `Ïˆ` is taken to `fun x => Ïˆ (-x)`. -/
def parityOperatorSchwartz : ğ“¢(â„, â„‚) â†’L[â„‚] ğ“¢(â„, â„‚) := by
  refine (SchwartzMap.compCLM â„‚ (g := (fun x => - x : â„ â†’ â„)) âŸ¨?_, ?_âŸ© ?_)
  Â· fun_prop
  Â· intro n
    simp only [Real.norm_eq_abs]
    use 1, 1
    intro x
    simp only [pow_one, one_mul]
    erw [iteratedFDeriv_neg_apply]
    simp only [norm_neg]
    match n with
    | 0 => simp
    | 1 =>
      rw [iteratedFDeriv_succ_eq_comp_right]
      simp
    | .succ (.succ n) =>
      rw [iteratedFDeriv_succ_eq_comp_right]
      simp only [Nat.succ_eq_add_one, fderiv_id', Function.comp_apply, LinearIsometryEquiv.norm_map,
        ge_iff_le]
      rw [iteratedFDeriv_const_of_ne]
      simp only [Pi.zero_apply, norm_zero]
      apply add_nonneg
      Â· exact zero_le_one' â„
      Â· exact abs_nonneg x
      simp
  Â· simp
    use 1, 1
    intro x
    simp

/-- The unbounded parity operator, whose domain is Schwartz maps. -/
def parityOperatorUnbounded : UnboundedOperator schwartzIncl schwartzIncl_injective :=
  UnboundedOperator.ofSelfCLM parityOperatorSchwartz

@[simp]
lemma parityOperatorSchwartz_parityOperatorSchwartz (Ïˆ : ğ“¢(â„, â„‚)) :
    parityOperatorSchwartz (parityOperatorSchwartz Ïˆ) = Ïˆ := by
  ext x
  simp [parityOperatorSchwartz]

/-!

## Parity operator is self adjoint

-/

open InnerProductSpace

lemma parityOperatorUnbounded_isSelfAdjoint :
    parityOperatorUnbounded.IsSelfAdjoint := by
  intro Ïˆ1 Ïˆ2
  dsimp [parityOperatorUnbounded]
  rw [schwartzIncl_inner, schwartzIncl_inner]
  let f (x : â„) :=
    (starRingEnd â„‚) ((Ïˆ1) (-x)) * (Ïˆ2) x
  change âˆ« (x : â„), f x = _
  trans âˆ« (x : â„), f (- x)
  Â· exact Eq.symm (integral_neg_eq_self f volume)
  Â· simp only [neg_neg, f]
    rfl

open InnerProductSpace

end HilbertSpace
end
end OneDimension
end QuantumMechanics
