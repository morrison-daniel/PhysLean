/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.SL2C.SelfAdjoint
import Mathlib.Analysis.Complex.Polynomial.Basic
import PhysLean.Relativity.LorentzGroup.Restricted.Basic
/-!
# The group SL(2, â„‚) and it's relation to the Lorentz group

The aim of this file is to give the relationship between `SL(2, â„‚)` and the Lorentz group.

-/
namespace Lorentz

open Module Matrix
open MatrixGroups
open Complex

namespace SL2C

noncomputable section

/-!

## Some basic properties about SL(2, â„‚)

Possibly to be moved to mathlib at some point.
-/

lemma inverse_coe (M : SL(2, â„‚)) : M.1â»Â¹ = (Mâ»Â¹).1 := by
  apply Matrix.inv_inj
  simp only [SpecialLinearGroup.det_coe, isUnit_iff_ne_zero, ne_eq, one_ne_zero, not_false_eq_true,
    nonsing_inv_nonsing_inv, SpecialLinearGroup.coe_inv]
  have h1 : IsUnit M.1.det := by
    simp
  rw [Matrix.inv_adjugate M.1 h1]
  Â· simp
  Â· simp

lemma transpose_coe (M : SL(2, â„‚)) : M.1áµ€ = (M.transpose).1 := rfl
/-!

## Representation of SL(2, â„‚) on spacetime

Through the correspondence between spacetime and self-adjoint matrices,
we can define a representation a representation of `SL(2, â„‚)` on spacetime.

-/

/-- Given an element `M âˆˆ SL(2, â„‚)` the linear map from `selfAdjoint (Matrix (Fin 2) (Fin 2) â„‚)` to
  itself defined by `A â†¦ M * A * Má´´`. -/
@[simps!]
def toSelfAdjointMap (M : SL(2, â„‚)) :
    selfAdjoint (Matrix (Fin 2) (Fin 2) â„‚) â†’â‚—[â„] selfAdjoint (Matrix (Fin 2) (Fin 2) â„‚) where
  toFun A := âŸ¨M.1 * A.1 * Matrix.conjTranspose M,
    by
      noncomm_ring [selfAdjoint.mem_iff, star_eq_conjTranspose,
        conjTranspose_mul, conjTranspose_conjTranspose,
        (star_eq_conjTranspose A.1).symm.trans $ selfAdjoint.mem_iff.mp A.2]âŸ©
  map_add' A B := by
    simp only [AddSubgroup.coe_add, AddMemClass.mk_add_mk, Subtype.mk.injEq]
    noncomm_ring [AddSubmonoid.coe_add, AddSubgroup.coe_toAddSubmonoid, AddSubmonoid.mk_add_mk,
      Subtype.mk.injEq]
  map_smul' r A := by
    noncomm_ring [selfAdjoint.val_smul, Algebra.mul_smul_comm, Algebra.smul_mul_assoc,
      RingHom.id_apply]

lemma toSelfAdjointMap_apply_det (M : SL(2, â„‚)) (A : selfAdjoint (Matrix (Fin 2) (Fin 2) â„‚)) :
    det ((toSelfAdjointMap M) A).1 = det A.1 := by
  simp only [toSelfAdjointMap, LinearMap.coe_mk, AddHom.coe_mk, det_mul, det_conjTranspose]
  simp only [SpecialLinearGroup.det_coe, one_mul, star_one, mul_one]

lemma toSelfAdjointMap_apply_pauliBasis'_inl (M : SL(2, â„‚)) :
    toSelfAdjointMap M (PauliMatrix.pauliBasis' (Sum.inl 0)) =
    ((â€–M.1 0 0â€– ^ 2 + â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2) â€¢
      PauliMatrix.pauliBasis' (Sum.inl 0) +
    (- ((M.1 0 1).re * (M.1 1 1).re + (M.1 0 1).im * (M.1 1 1).im +
      (M.1 0 0).im * (M.1 1 0).im + (M.1 0 0).re * (M.1 1 0).re)) â€¢
        PauliMatrix.pauliBasis' (Sum.inr 0)
    + ((- (M.1 0 0).re * (M.1 1 0).im + â†‘(M.1 1 0).re * (M.1 0 0).im
      - (M.1 0 1).re * (M.1 1 1).im + (M.1 0 1).im * (M.1 1 1).re)) â€¢
        PauliMatrix.pauliBasis' (Sum.inr 1)
    + ((- â€–M.1 0 0â€– ^ 2 - â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2) â€¢
      PauliMatrix.pauliBasis' (Sum.inr 2) := by
  simp only [toSelfAdjointMap, PauliMatrix.pauliBasis', Fin.isValue, Basis.coe_mk,
    PauliMatrix.pauliSelfAdjoint', PauliMatrix.pauliMatrix, one_fin_two, LinearMap.coe_mk,
    AddHom.coe_mk, neg_add_rev, neg_of, neg_cons, neg_zero, neg_empty, neg_mul, neg_neg]
  ext1
  simp only [Fin.isValue, AddSubgroup.coe_add, selfAdjoint.val_smul, smul_of, smul_cons, real_smul,
    ofReal_div, ofReal_add, ofReal_pow, ofReal_ofNat, mul_one, smul_zero, smul_empty, smul_neg,
    ofReal_neg, ofReal_mul, neg_add_rev, neg_neg, of_add_of, add_cons, head_cons, add_zero,
    tail_cons, zero_add, empty_add_empty, ofReal_sub]
  conv => lhs; erw [â† eta_fin_two 1, mul_one]
  conv => lhs; lhs; rw [eta_fin_two M.1]
  conv => lhs; rhs; rw [eta_fin_two M.1á´´]
  simp only [Fin.isValue, conjTranspose_apply, RCLike.star_def, cons_mul, Nat.succ_eq_add_one,
    Nat.reduceAdd, vecMul_cons, head_cons, smul_cons, smul_eq_mul, smul_empty, tail_cons,
    empty_vecMul, add_zero, add_cons, empty_add_empty, empty_mul, Equiv.symm_apply_apply,
    EmbeddingLike.apply_eq_iff_eq]
  rw [mul_conj', mul_conj', mul_conj', mul_conj']
  ext x y
  match x, y with
  | 0, 0 =>
    simp only [Fin.isValue, cons_val', cons_val_zero, empty_val', cons_val_fin_one]
    ring_nf
  | 0, 1 =>
    simp only [Fin.isValue, cons_val', cons_val_one, empty_val',
      cons_val_fin_one, cons_val_zero]
    ring_nf
    rw [â† re_add_im (M.1 0 0), â† re_add_im (M.1 0 1), â† re_add_im (M.1 1 0), â† re_add_im (M.1 1 1)]
    simp only [Fin.isValue, map_add, conj_ofReal, _root_.map_mul, conj_I, mul_neg, add_re,
      ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self, add_zero, add_im,
      mul_im, zero_add]
    ring_nf
    simp only [Fin.isValue, I_sq, mul_neg, mul_one, neg_mul, neg_neg, one_mul, sub_neg_eq_add]
    ring
  | 1, 0 =>
    simp only [Fin.isValue, cons_val', cons_val_zero, empty_val', cons_val_fin_one,
      cons_val_one]
    ring_nf
    rw [â† re_add_im (M.1 0 0), â† re_add_im (M.1 0 1), â† re_add_im (M.1 1 0), â† re_add_im (M.1 1 1)]
    simp only [Fin.isValue, map_add, conj_ofReal, _root_.map_mul, conj_I, mul_neg, add_re,
      ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self, add_zero, add_im,
      mul_im, zero_add]
    ring_nf
    simp only [Fin.isValue, I_sq, mul_neg, mul_one, neg_mul, neg_neg, one_mul, sub_neg_eq_add]
    ring
  | 1, 1 =>
    simp only [Fin.isValue, cons_val', cons_val_one, cons_val_fin_one, empty_val']
    ring_nf

/-- The monoid homomorphisms from `SL(2, â„‚)` to matrices indexed by `Fin 1 âŠ• Fin 3`
  formed by the action `M A Má´´`. -/
def toMatrix : SL(2, â„‚) â†’* Matrix (Fin 1 âŠ• Fin 3) (Fin 1 âŠ• Fin 3) â„ where
  toFun M := LinearMap.toMatrix PauliMatrix.pauliBasis' PauliMatrix.pauliBasis' (toSelfAdjointMap M)
  map_one' := by
    simp only [toSelfAdjointMap, SpecialLinearGroup.coe_one, one_mul, conjTranspose_one,
      mul_one, Subtype.coe_eta]
    erw [LinearMap.toMatrix_one]
  map_mul' M N := by
    rw [â† LinearMap.toMatrix_mul]
    apply congrArg
    ext1 x
    simp only [toSelfAdjointMap, SpecialLinearGroup.coe_mul, conjTranspose_mul,
      LinearMap.coe_mk, AddHom.coe_mk, Module.End.mul_apply, Subtype.mk.injEq]
    noncomm_ring

open Lorentz in
lemma toMatrix_apply_contrMod (M : SL(2, â„‚)) (v : ContrMod 3) :
    (toMatrix M) *áµ¥ v = ContrMod.toSelfAdjoint.symm
    ((toSelfAdjointMap M) (ContrMod.toSelfAdjoint v)) := by
  simp only [ContrMod.mulVec, toMatrix, MonoidHom.coe_mk, OneHom.coe_mk]
  obtain âŸ¨a, haâŸ© := ContrMod.toSelfAdjoint.symm.surjective v
  subst ha
  rw [LinearEquiv.apply_symm_apply]
  simp only [ContrMod.toSelfAdjoint, LinearEquiv.trans_symm, LinearEquiv.symm_symm,
    LinearEquiv.trans_apply]
  change ContrMod.toFin1dâ„Equiv.symm
    ((((LinearMap.toMatrix PauliMatrix.pauliBasis' PauliMatrix.pauliBasis') (toSelfAdjointMap M)))
    *áµ¥ (((Finsupp.linearEquivFunOnFinite â„ â„ (Fin 1 âŠ• Fin 3))
    (PauliMatrix.pauliBasis'.repr a)))) = _
  apply congrArg
  erw [LinearMap.toMatrix_mulVec_repr]
  rfl

lemma toMatrix_mem_lorentzGroup (M : SL(2, â„‚)) : toMatrix M âˆˆ LorentzGroup 3 := by
  rw [LorentzGroup.mem_iff_norm]
  intro x
  apply ofReal_injective
  rw [Lorentz.contrContrContractField.same_eq_det_toSelfAdjoint]
  rw [toMatrix_apply_contrMod]
  rw [LinearEquiv.apply_symm_apply]
  rw [toSelfAdjointMap_apply_det]
  rw [Lorentz.contrContrContractField.same_eq_det_toSelfAdjoint]

/-- The group homomorphism from `SL(2, â„‚)` to the Lorentz group `ð“›`. -/
@[simps!]
def toLorentzGroup : SL(2, â„‚) â†’* LorentzGroup 3 where
  toFun M := âŸ¨toMatrix M, toMatrix_mem_lorentzGroup MâŸ©
  map_one' := by
    simp only [_root_.map_one]
    rfl
  map_mul' M N := by
    ext1
    simp only [_root_.map_mul, lorentzGroupIsGroup_mul_coe]

lemma toLorentzGroup_eq_pauliBasis' (M : SL(2, â„‚)) :
    toLorentzGroup M = LinearMap.toMatrix
    PauliMatrix.pauliBasis' PauliMatrix.pauliBasis' (toSelfAdjointMap M) := by
  rfl

lemma toSelfAdjointMap_basis (i : Fin 1 âŠ• Fin 3) :
    toSelfAdjointMap M (PauliMatrix.pauliBasis' i) =
    âˆ‘ j, (toLorentzGroup M).1 j i â€¢ PauliMatrix.pauliBasis' j := by
  rw [toLorentzGroup_eq_pauliBasis']
  simp only [LinearMap.toMatrix_apply]
  nth_rewrite 1 [â† (Basis.sum_repr PauliMatrix.pauliBasis'
    ((toSelfAdjointMap M) (PauliMatrix.pauliBasis' i)))]
  rfl

lemma toSelfAdjointMap_pauliBasis (i : Fin 1 âŠ• Fin 3) :
    toSelfAdjointMap M (PauliMatrix.pauliBasis i) =
    âˆ‘ j, (toLorentzGroup Mâ»Â¹).1 i j â€¢ PauliMatrix.pauliBasis j := by
  have h1 : (toLorentzGroup Mâ»Â¹).1 = minkowskiMatrix.dual (toLorentzGroup M).1 := by
    simp [LorentzGroup.inv_eq_dual]
  simp only [h1]
  rw [PauliMatrix.pauliBasis_minkowskiMetric_pauliBasis', _root_.map_smul]
  rw [toSelfAdjointMap_basis]
  rw [Finset.smul_sum]
  apply congrArg
  funext j
  rw [smul_smul, PauliMatrix.pauliBasis_minkowskiMetric_pauliBasis', smul_smul]
  apply congrFun
  apply congrArg
  exact Eq.symm (minkowskiMatrix.dual_apply_minkowskiMatrix ((toLorentzGroup M).1) i j)

/-- The first column of the Lorentz matrix formed from an element of `SL(2, â„‚)`. -/
lemma toLorentzGroup_fst_col (M : SL(2, â„‚)) :
    (fun Î¼ => (toLorentzGroup M).1 Î¼ (Sum.inl 0)) = fun Î¼ =>
      match Î¼ with
      | Sum.inl 0 => ((â€–M.1 0 0â€– ^ 2 + â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2)
      | Sum.inr 0 => (- ((M.1 0 1).re * (M.1 1 1).re + (M.1 0 1).im * (M.1 1 1).im +
        (M.1 0 0).im * (M.1 1 0).im + (M.1 0 0).re * (M.1 1 0).re))
      | Sum.inr 1 => ((- (M.1 0 0).re * (M.1 1 0).im + â†‘(M.1 1 0).re * (M.1 0 0).im
        - (M.1 0 1).re * (M.1 1 1).im + (M.1 0 1).im * (M.1 1 1).re))
      | Sum.inr 2 => ((- â€–M.1 0 0â€– ^ 2 - â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2) := by
  let k : Fin 1 âŠ• Fin 3 â†’ â„ := fun Î¼ =>
    match Î¼ with
    | Sum.inl 0 => ((â€–M.1 0 0â€– ^ 2 + â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2)
    | Sum.inr 0 => (- ((M.1 0 1).re * (M.1 1 1).re + (M.1 0 1).im * (M.1 1 1).im +
      (M.1 0 0).im * (M.1 1 0).im + (M.1 0 0).re * (M.1 1 0).re))
    | Sum.inr 1 => ((- (M.1 0 0).re * (M.1 1 0).im + â†‘(M.1 1 0).re * (M.1 0 0).im
      - (M.1 0 1).re * (M.1 1 1).im + (M.1 0 1).im * (M.1 1 1).re))
    | Sum.inr 2 => ((- â€–M.1 0 0â€– ^ 2 - â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2)
  change (fun Î¼ => (toLorentzGroup M).1 Î¼ (Sum.inl 0)) = k
  have h1 : toSelfAdjointMap M (PauliMatrix.pauliBasis' (Sum.inl 0)) =
      âˆ‘ Î¼, k Î¼ â€¢ PauliMatrix.pauliBasis' Î¼ := by
    simp only [Fin.isValue, Fintype.sum_sum_type, Finset.univ_unique, Fin.default_eq_zero,
      Finset.sum_singleton, Fin.sum_univ_three]
    rw [toSelfAdjointMap_apply_pauliBasis'_inl]
    abel
  rw [toSelfAdjointMap_basis] at h1
  have h1x := sub_eq_zero_of_eq h1
  rw [â† Finset.sum_sub_distrib] at h1x
  funext Î¼
  refine sub_eq_zero.mp ?_
  refine Fintype.linearIndependent_iff.mp PauliMatrix.pauliBasis'.linearIndependent
    (fun x => ((toLorentzGroup M).1 x (Sum.inl 0) - k x)) ?_ Î¼
  rw [â† h1x]
  congr
  funext x
  exact sub_smul ((toLorentzGroup M).1 x (Sum.inl 0)) (k x) (PauliMatrix.pauliBasis' x)

/-- The first element of the image of `SL(2, â„‚)` in the Lorentz group. -/
lemma toLorentzGroup_inl_inl (M : SL(2, â„‚)) :
    (toLorentzGroup M).1 (Sum.inl 0) (Sum.inl 0) =
    ((â€–M.1 0 0â€– ^ 2 + â€–M.1 0 1â€– ^ 2 + â€–M.1 1 0â€– ^ 2 + â€–M.1 1 1â€– ^ 2) / 2) := by
  change (fun Î¼ => (toLorentzGroup M).1 Î¼ (Sum.inl 0)) (Sum.inl 0) = _
  rw [toLorentzGroup_fst_col]

/-- The image of `SL(2, â„‚)` in the Lorentz group is orthochronous. -/
lemma toLorentzGroup_isOrthochronous (M : SL(2, â„‚)) :
    LorentzGroup.IsOrthochronous (toLorentzGroup M) := by
  rw [LorentzGroup.IsOrthochronous]
  rw [toLorentzGroup_inl_inl]
  apply div_nonneg
  Â· apply add_nonneg
    Â· apply add_nonneg
      Â· apply add_nonneg
        Â· exact sq_nonneg _
        Â· exact sq_nonneg _
      Â· exact sq_nonneg _
    Â· exact sq_nonneg _
  Â· exact zero_le_two

/-!

## Homomorphism to the restricted Lorentz group

The homomorphism `toLorentzGroup` restricts to a homomorphism to the restricted Lorentz group.
In this section we will define this homomorphism.

-/

/-- The determinant of the image of `SL(2, â„‚)` in the Lorentz group is one. -/
lemma toLorentzGroup_det_one (M : SL(2, â„‚)) : det (toLorentzGroup M).val = 1 :=
  let U := M.val.schurTriangulationUnitary
  let N := M.val.schurTriangulation.val
  have h : M.val = U * N * star U := M.val.schur_triangulation
  haveI : Invertible U.val := âŸ¨star U.val, U.property.left, U.property.rightâŸ©
  calc det (toLorentzGroup M).val
    _ = LinearMap.det (toSelfAdjointMap' M) := LinearMap.det_toMatrix ..
    _ = LinearMap.det (toSelfAdjointMap' (U * N * U.valâ»Â¹)) :=
      suffices star U = U.valâ»Â¹ by rw [h, this]
      calc star U.val
        _ = star U.val * (U.val * U.valâ»Â¹) := by simp
        _ = star U.val * U.val * U.valâ»Â¹ := by noncomm_ring
        _ = U.valâ»Â¹ := by simp
    _ = LinearMap.det (toSelfAdjointMap' N) := toSelfAdjointMap_similar_det U N
    _ = 1 :=
      suffices N.det = 1 from toSelfAdjointMap_det_one' M.val.schurTriangulation.property this
      calc N.det
        _ = det ((U * star U).val * N) := by simp
        _ = det (U.val * N * star U.val) := det_mul_right_comm ..
        _ = M.val.det := congrArg det h.symm
        _ = 1 := M.property

/-- The homomorphism from `SL(2, â„‚)` to the restricted Lorentz group. -/
@[simps!]
def toRestrictedLorentzGroup : SL(2, â„‚) â†’* LorentzGroup.restricted 3 :=
  toLorentzGroup.codRestrict (LorentzGroup.restricted 3)
    (fun M => And.intro (toLorentzGroup_det_one M) (toLorentzGroup_isOrthochronous M))
end
end SL2C

end Lorentz
