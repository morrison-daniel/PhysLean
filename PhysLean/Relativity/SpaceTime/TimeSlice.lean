/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.SpaceTime.Basic
import PhysLean.Meta.Informal.SemiFormal
/-!
# Time slice

Time slicing functions on spacetime, turning them into a function
`Time â†’ Space d â†’ M`.

This is useful when going from relativistic physics (defined using `SpaceTime`)
to non-relativistic physics (defined using `Space` and `Time`).

-/

noncomputable section

namespace SpaceTime

open Time
open Space

/-- The timeslice of a function `SpaceTime d â†’ M` forming a function
  `Time â†’ Space d â†’ M`. -/
def timeSlice {d : â„•} {M : Type} : (SpaceTime d â†’ M) â‰ƒ (Time â†’ Space d â†’ M) where
  toFun f := Function.curry (f âˆ˜ toTimeAndSpace.symm)
  invFun f := Function.uncurry f âˆ˜ toTimeAndSpace
  left_inv f := by
    funext x
    simp
  right_inv f := by
    funext x t
    simp

/-- The timeslice of a function `SpaceTime d â†’ M` forming a function
  `Time â†’ Space d â†’ M`, as a linear equivalence. -/
def timeSliceLinearEquiv {d : â„•} {M : Type} [AddCommGroup M] [Module â„ M] :
    (SpaceTime d â†’ M) â‰ƒâ‚—[â„] (Time â†’ Space d â†’ M) where
  toFun := timeSlice
  invFun := timeSlice.symm
  map_add' f g := by
    ext t x
    simp [timeSlice]
  map_smul' := by
    intros c f
    ext t x
    simp [timeSlice]
  left_inv f := by simp
  right_inv f := by simp

lemma timeSliceLinearEquiv_apply {d : â„•} {M : Type} [AddCommGroup M] [Module â„ M]
    (f : SpaceTime d â†’ M) : timeSliceLinearEquiv f = timeSlice f := by
  simp [timeSliceLinearEquiv, timeSlice]

lemma timeSliceLinearEquiv_symm_apply {d : â„•} {M : Type} [AddCommGroup M] [Module â„ M]
    (f : Time â†’ Space d â†’ M) : timeSliceLinearEquiv.symm f = timeSlice.symm f := by
  simp [timeSliceLinearEquiv, timeSlice]


variable {k : Type} [NontriviallyNormedField k]
    {N W M : Type} [NormedAddCommGroup M] [NormedSpace k M]
    [NormedAddCommGroup N] [NormedSpace k N]
    [NormedAddCommGroup W] [NormedSpace k W]

private lemma fderiv_uncurry  (f : N â†’ W â†’ M) (y : N Ã— W) (w : W)
    (h : DifferentiableAt k (Function.uncurry f) y)  :
    fderiv k (Function.uncurry f) y (0, w) =
    fderiv k (fun x => f y.1 x) y.2 w := by
  rw [show (fun x => f y.1 x) =
    (Function.uncurry f) âˆ˜ (fun x => (y.1, x)) by {ext w; rfl}]
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  rw [(hasFDerivAt_prodMk_right (ð•œ := k) y.1 y.2).fderiv]
  rfl

private lemma fderiv_uncurry'  (f : N â†’ W â†’ M) (y : N Ã— W) (n : N)
    (h : DifferentiableAt k (Function.uncurry f) y)  :
    fderiv k (Function.uncurry f) y (n, 0) =
    fderiv k (fun x => f x y.2) y.1 n := by
  rw [show (fun x => f x y.2) =
    (Function.uncurry f) âˆ˜ (fun x => (x, y.2)) by {ext w; rfl}]
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  rw [(hasFDerivAt_prodMk_left (ð•œ := k) y.1 y.2).fderiv]
  rfl

private lemma fderiv_curry (f : N Ã— W â†’ M) (n : N) (w : W)
    (h : DifferentiableAt k f (n, w)) (dw : W):
    fderiv k (Function.curry f n) w dw = fderiv k (f) (n, w) (0, dw) := by
  have h1 : f = Function.uncurry (Function.curry f) := by
    ext x
    simp
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry]
  rw [Function.uncurry_curry]
  exact h

private lemma fderiv_curry' (f : N Ã— W â†’ M) (n : N) (w : W)
    (h : DifferentiableAt k f (n, w)) (dn : N):
    fderiv k (fun n => Function.curry f n w) n dn = fderiv k f (n, w) (dn, 0) := by
  have h1 : f = Function.uncurry (Function.curry f) := by
    ext x
    simp
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry']
  rw [Function.uncurry_curry]
  exact h

/-- The derivative on space commutes with time-slicing. -/
lemma timeSlice_spatial_deriv {M : Type}
    [NormedAddCommGroup M] [NormedSpace â„ M] {d : â„•} {f : SpaceTime d â†’ M}
    {t : Time} {x : Space d}
    (hdiff : DifferentiableAt â„ f (toTimeAndSpace.symm (t, x))) (i : Fin d) :
    timeSlice (âˆ‚_ (Fin.natAdd 1 i) f) t x = âˆ‚[i] (timeSlice f t) x := by
  have hf : f = (f âˆ˜ toTimeAndSpace.symm) âˆ˜ toTimeAndSpace := by
    ext x
    simp
  conv_lhs =>
    rw [hf]
    simp only [timeSlice, realLorentzTensor.C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd,
      Equiv.coe_fn_mk, Function.curry_apply, Function.comp_apply]
    rw [deriv_comp_toTimeAndSpace_natAdd i (f âˆ˜ â‡‘toTimeAndSpace.symm)]
  conv_rhs =>
    rw [timeSlice]
    simp [Space.deriv]
  simp only [realLorentzTensor.C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd,
    ContinuousLinearEquiv.apply_symm_apply]
  rw [fderiv_curry]
  Â· simp [basis]
  Â· fun_prop

/-- The derivative on time commutes with time-slicing. -/
lemma timeSlice_time_deriv {M : Type}
    [NormedAddCommGroup M] [NormedSpace â„ M] {d : â„•} (f : SpaceTime d â†’ M)
    {t : Time} {x : Space d}
    (hdiff : DifferentiableAt â„ f (toTimeAndSpace.symm (t, x))):
    timeSlice (âˆ‚_ (finSumFinEquiv (Sum.inl 0)) f) t x = âˆ‚â‚œ (fun t => timeSlice f t x) t := by
  have hf : f = (f âˆ˜ toTimeAndSpace.symm) âˆ˜ toTimeAndSpace := by
    ext x
    simp
  conv_lhs =>
    rw [hf]
    simp only [timeSlice, realLorentzTensor.C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd,
      Fin.isValue, finSumFinEquiv_apply_left, Equiv.coe_fn_mk, Function.curry_apply,
      Function.comp_apply]
    rw [deriv_comp_toTimeAndSpace_castAdd (f âˆ˜ â‡‘toTimeAndSpace.symm)]
  conv_rhs =>
    rw [timeSlice]
    simp only [Time.deriv, realLorentzTensor.C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd,
      Equiv.coe_fn_mk,  Function.comp_apply]
  simp only [realLorentzTensor.C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd,
    ContinuousLinearEquiv.apply_symm_apply]
  rw [fderiv_curry']
  fun_prop

end SpaceTime

end
