/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.Tensors.RealTensor.Vector.Basic
import PhysLean.ClassicalMechanics.Space.Basic
import PhysLean.ClassicalMechanics.Time.Basic
/-!
# Space time

This file introduce 4d Minkowski spacetime.

-/

noncomputable section

TODO "6V2DR" "SpaceTime should be refactored into a structure, or similar, to prevent casting."

/-- The space-time -/
abbrev SpaceTime (d : â„• := 3) := Lorentz.Vector d

namespace SpaceTime

open Manifold
open Matrix
open Complex
open ComplexConjugate
open TensorSpecies

/-!

## To space and time

-/

/-- The space part of spacetime. -/
def space {d : â„•} : SpaceTime d â†’â‚—[â„] Space d where
  toFun x := Lorentz.Vector.spatialPart x
  map_add' x1 x2 := by
    ext i
    simp [Lorentz.Vector.spatialPart]
  map_smul' c x := by
    ext i
    simp [Lorentz.Vector.spatialPart]

@[simp]
lemma space_toCoord_symm {d : â„•} (f : Fin 1 âŠ• Fin d â†’ â„) :
    space (Lorentz.Vector.toCoord.symm f) = fun i => f (Sum.inr i) := by
  funext i
  simp [space, Lorentz.Vector.spatialPart]

open realLorentzTensor
open Tensor

/-- The function `space` is equivariant with respect to rotations. -/
informal_lemma space_equivariant where
  deps := [``space]
  tag := "7MTYX"

/-- The time part of spacetime. -/
def time {d : â„•} : SpaceTime d â†’â‚—[â„] Time where
  toFun x := Lorentz.Vector.timeComponent x
  map_add' x1 x2 := by
    simp [Lorentz.Vector.timeComponent]
  map_smul' c x := by
    simp [Lorentz.Vector.timeComponent]

@[simp]
lemma time_toCoord_symm {d : â„•} (f : Fin 1 âŠ• Fin d â†’ â„) :
    time (Lorentz.Vector.toCoord.symm f) =f (Sum.inl 0) := by
  simp [time, Lorentz.Vector.timeComponent]

/-- A continuous linear equivalence between `SpaceTime d` and
  `Time Ã— Space d`. -/
def toTimeAndSpace {d : â„•} : SpaceTime d â‰ƒL[â„] Time Ã— Space d :=
  LinearEquiv.toContinuousLinearEquiv {
    toFun x := (x.time, x.space)
    invFun tx := Lorentz.Vector.toCoord.symm (fun i =>
      match i with
      | Sum.inl _ => tx.1
      | Sum.inr i => tx.2 i)
    left_inv x := by
      obtain âŸ¨x, rflâŸ© := Lorentz.Vector.toCoord.symm.surjective x
      simp only [C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd, time, LinearMap.coe_mk,
        AddHom.coe_mk, space, EmbeddingLike.apply_eq_iff_eq]
      funext i
      match i with
      | Sum.inl 0 => simp [Lorentz.Vector.timeComponent]
      | Sum.inr i => simp [Lorentz.Vector.spatialPart]
    right_inv tx := by
      simp only [C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd, time, Lorentz.Vector.timeComponent,
        Fin.isValue, LinearMap.coe_mk, AddHom.coe_mk, LinearEquiv.apply_symm_apply, space]
      obtain âŸ¨fst, sndâŸ© := tx
      simp only [Prod.mk.injEq, true_and]
      funext i
      simp [Lorentz.Vector.spatialPart]
    map_add' x y := by
      simp
    map_smul' := by
      simp
  }

lemma toTimeAndSpace_basis_natAdd {d : â„•} (i : Fin d) :
    toTimeAndSpace ((Tensor.basis (S := realLorentzTensor d) ![Color.up])
      fun x => Fin.cast (by simp) (Fin.natAdd 1 i))
    = (0, Space.basis i) := by
  simp only [C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd, toTimeAndSpace, time, LinearMap.coe_mk,
    AddHom.coe_mk, LinearEquiv.coe_toContinuousLinearEquiv', LinearEquiv.coe_mk, Prod.mk.injEq]
  rw [Lorentz.Vector.timeComponent_basis_natAdd]
  simp only [true_and]
  funext j
  simp [space]
  rw [Lorentz.Vector.spatialPart_basis_natAdd]
  simp [Space.basis]
  rw [Finsupp.single_apply]
  simp only [Sum.inr.injEq]
  congr 1
  exact Lean.Grind.eq_congr' rfl rfl

/-!

## Coordinates

-/

/-- For a given `Î¼ : Fin (1 + d)` `coord Î¼ p` is the coordinate of
  `p` in the direction `Î¼`.

  This is denoted `ð” Î¼ p`, where `ð”` is typed with `\MCx`. -/
def coord {d : â„•} (Î¼ : Fin (1 + d)) : SpaceTime d â†’â‚—[â„] â„ where
  toFun := flip (Lorentz.Vector.toCoord (d := d)) (finSumFinEquiv.symm Î¼)
  map_add' x1 x2 := by
    simp [flip]
  map_smul' c x := by
    simp [flip]

@[inherit_doc coord]
scoped notation "ð”" => coord

lemma coord_apply {d : â„•} (Î¼ : Fin (1 + d)) (y : SpaceTime d) :
    ð” Î¼ y = y (finSumFinEquiv.symm Î¼) := by
  rfl

lemma coord_on_repr {d : â„•} (Î¼ : Fin (1 + d))
    (y : ComponentIdx (S := realLorentzTensor d) ![Color.up] â†’ â„) :
    ð” Î¼ ((Tensor.basis (S := realLorentzTensor d)
      ![Color.up]).repr.symm (Finsupp.equivFunOnFinite.symm y)) =
    y (fun _ => Fin.cast (by simp) Î¼) := by
  change ð” Î¼ (Lorentz.Vector.toCoordFull.symm y) = _
  rw [coord_apply]
  rw [Lorentz.Vector.toCoord_apply_eq_toCoordFull_apply]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, LinearEquiv.apply_symm_apply,
    Equiv.piCongrLeft'_apply]
  congr
  funext x
  fin_cases x
  simp [Lorentz.Vector.indexEquiv]

/-!

## Derivatives

-/

/-- The derivative of a function `SpaceTime d â†’ â„` along the `Î¼` coordinte. -/
noncomputable def deriv {M : Type} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ M) : SpaceTime d â†’ M :=
  fun y => fderiv â„ f y (Tensor.basis _ (fun x => Fin.cast (by simp) Î¼))

@[inherit_doc deriv]
scoped notation "âˆ‚_" => deriv

variable {M : Type} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
lemma deriv_eq {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ M) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ f y =
    fderiv â„ f y (Tensor.basis _ (fun x => Fin.cast (by simp) Î¼)) := by
  rfl

@[simp]
lemma deriv_zero {d : â„•} (Î¼ : Fin (1 + d)) : SpaceTime.deriv Î¼ (fun _ => (0 : â„)) = 0 := by
  ext y
  rw [SpaceTime.deriv_eq]
  simp

lemma deriv_eq_deriv_on_coord {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ f y = fderiv â„
      (fun y => (f ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr.symm
            (Finsupp.equivFunOnFinite.symm y))))
      â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
    â‡‘(Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1) := by
  change _ = fderiv â„ (f âˆ˜ Lorentz.Vector.fromCoordFullContinuous)
    â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
    â‡‘(Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)
  rw [ContinuousLinearEquiv.comp_right_fderiv]
  rw [deriv_eq]
  congr
  simp [Lorentz.Vector.fromCoordFullContinuous, Lorentz.Vector.toCoordFull]
  exact (LinearEquiv.eq_symm_apply _).mpr rfl

lemma neg_deriv {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) :
    - SpaceTime.deriv Î¼ f = SpaceTime.deriv Î¼ (fun y => - f y) := by
  ext y
  rw [SpaceTime.deriv_eq]
  simp only [Pi.neg_apply, fderiv_neg, Nat.succ_eq_add_one, Nat.reduceAdd, C_eq_color,
    ContinuousLinearMap.neg_apply, neg_inj]
  rfl

lemma neg_deriv_apply {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) (y : SpaceTime d) :
    - SpaceTime.deriv Î¼ f y = SpaceTime.deriv Î¼ (fun y => - f y) y:= by
  rw [â† SpaceTime.neg_deriv]
  rfl

@[fun_prop]
lemma coord_differentiable {d : â„•} (Î¼ : Fin (1 + d)) :
    Differentiable â„ (ð” Î¼) := by
  let Ï† : (Fin 1 âŠ• Fin d) â†’ (SpaceTime d) â†’ â„ := fun b y => y b
  change Differentiable â„ (fun y => Ï† _ _)
  have h : Differentiable â„ (flip Ï†) := by
    change Differentiable â„ Lorentz.Vector.toCoord
    fun_prop
  rw [differentiable_pi] at h
  exact h (finSumFinEquiv.symm Î¼)

lemma deriv_coord_same {d : â„•} (Î¼ : Fin (1 + d)) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î¼) y = 1 := by
  rw [SpaceTime.deriv_eq_deriv_on_coord]
  let Ï† : ((x : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] x)))
    â†’ (((j : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] j))) â†’ â„)
    â†’ â„ := fun b y => y b
  conv_lhs =>
    enter [1, 2, y]
    rw [coord_on_repr]
    change Ï† _ y
  have h1 : (fun y => fun i => Ï† i y) = id := by rfl
  have h2 (x) : fderiv â„ (fun y => fun i => Ï† i y) x = ContinuousLinearMap.id _ _ := by
    rw [h1]
    simp
  have h3 (x) : âˆ€ (i), DifferentiableAt â„ (Ï† i) x := by
    have h3' : DifferentiableAt â„ (flip Ï†) x := by
      change DifferentiableAt â„ ((fun y => fun i => Ï† i y)) x
      rw [h1]
      exact differentiableAt_id
    rw [differentiableAt_pi] at h3'
    intro i
    have h3'' := h3' i
    exact h3''
  conv at h2 =>
    enter [x]
    rw [fderiv_pi (h3 x)]
  have h2' := h2 ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
  change (ContinuousLinearMap.pi fun i =>
    fderiv â„ (Ï† i) â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y))
    ((Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)) (fun _ => Fin.cast (by simp) Î¼) = _
  rw [h2']
  simp

lemma deriv_coord_diff {d : â„•} (Î¼ Î½ : Fin (1 + d)) (h : Î¼ â‰  Î½) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î½) y = 0 := by
  rw [SpaceTime.deriv_eq_deriv_on_coord]
  let Ï† : ((x : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] x)))
    â†’ (((j : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] j))) â†’ â„)
    â†’ â„ := fun b y => y b
  conv_lhs =>
    enter [1, 2, y]
    rw [coord_on_repr]
    change Ï† _ y
  have h1 : (fun y => fun i => Ï† i y) = id := by rfl
  have h2 (x) : fderiv â„ (fun y => fun i => Ï† i y) x = ContinuousLinearMap.id _ _ := by
    rw [h1]
    simp
  have h3 (x) : âˆ€ (i), DifferentiableAt â„ (Ï† i) x := by
    have h3' : DifferentiableAt â„ (flip Ï†) x := by
      change DifferentiableAt â„ ((fun y => fun i => Ï† i y)) x
      rw [h1]
      exact differentiableAt_id
    rw [differentiableAt_pi] at h3'
    intro i
    have h3'' := h3' i
    exact h3''
  conv at h2 =>
    enter [x]
    rw [fderiv_pi (h3 x)]
  have h2' := h2 ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
  change (ContinuousLinearMap.pi fun i => fderiv â„ (Ï† i)
    â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y))
    ((Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)) (fun _ => Fin.cast (by simp) Î½) = _
  rw [h2']
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, ContinuousLinearMap.coe_id', id_eq]
  rw [Finsupp.single_apply]
  simp only [ite_eq_right_iff, one_ne_zero, imp_false]
  intro hn
  have hnx := congrFun hn 0
  simp at hnx
  omega

lemma deriv_coord_eq_if {d : â„•} (Î¼ Î½ : Fin (1 + d)) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î½) y = if Î¼ = Î½ then 1 else 0 := by
  by_cases h : Î¼ = Î½
  Â· simp only [h, â†“reduceIte]
    exact SpaceTime.deriv_coord_same Î½ y
  Â· rw [if_neg h]
    exact SpaceTime.deriv_coord_diff Î¼ Î½ h y

@[simp]
lemma deriv_toTimeAndSpace {d : â„•} (Î¼ : Fin (1 + d)) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (toTimeAndSpace) y = toTimeAndSpace
      ((Tensor.basis _ (fun x => Fin.cast (by simp) Î¼))) := by
  simp [SpaceTime.deriv]
  rw [ContinuousLinearEquiv.fderiv]
  rfl

lemma deriv_comp_toTimeAndSpace_natAdd {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M] {d : â„•}
    (i : Fin (d)) (f : Time Ã— Space d â†’ M) (y : SpaceTime d) :
    SpaceTime.deriv (Fin.natAdd 1 i) (f âˆ˜ toTimeAndSpace) y =
    fderiv â„ f (toTimeAndSpace y) (0, Space.basis i) := by
  rw [SpaceTime.deriv_eq]
  have h1 := toTimeAndSpace.comp_right_fderiv (f := f) (x := y)
  conv_lhs =>
    enter [1]
    rw [h1]
  simp only [C_eq_color, Nat.succ_eq_add_one, Nat.reduceAdd, ContinuousLinearMap.coe_comp',
    ContinuousLinearEquiv.coe_coe, Function.comp_apply]
  rw [toTimeAndSpace_basis_natAdd]

end SpaceTime

end
