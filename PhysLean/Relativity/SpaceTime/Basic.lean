/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.Tensors.RealTensor.Vector.Basic
import PhysLean.ClassicalMechanics.Space.Basic
import PhysLean.ClassicalMechanics.Time.Basic
/-!
# Space time

This file introduce 4d Minkowski spacetime.

-/

noncomputable section

TODO "6V2DR" "SpaceTime should be refactored into a structure, or similar, to prevent casting."

/-- The space-time -/
abbrev SpaceTime (d : â„• := 3) := Lorentz.Vector d

namespace SpaceTime

open Manifold
open Matrix
open Complex
open ComplexConjugate
open TensorSpecies

/-- The space part of spacetime. -/
@[simp]
def space {d : â„•} (x : SpaceTime d) : Space d :=
  fun i => x (Sum.inr i)

/-- The function `space` is equivariant with respect to rotations. -/
informal_lemma space_equivariant where
  deps := [``space]
  tag := "7MTYX"

/-- The time part of spacetime. -/
@[simp]
def time {d : â„•} (x : SpaceTime d) : Time := x (Sum.inl 0)

/-- For a given `Î¼ : Fin (1 + d)` `coord Î¼ p` is the coordinate of
  `p` in the direction `Î¼`.

  This is denoted `ð” Î¼ p`, where `ð”` is typed with `\MCx`. -/
def coord {d : â„•} (Î¼ : Fin (1 + d)) : SpaceTime d â†’â‚—[â„] â„ where
  toFun := flip (Lorentz.Vector.toCoord (d := d)) (finSumFinEquiv.symm Î¼)
  map_add' x1 x2 := by
    simp [flip]
  map_smul' c x := by
    simp [flip]

@[inherit_doc coord]
scoped notation "ð”" => coord

lemma coord_apply {d : â„•} (Î¼ : Fin (1 + d)) (y : SpaceTime d) :
    ð” Î¼ y = y (finSumFinEquiv.symm Î¼) := by
  rfl

open realLorentzTensor
open Tensor

lemma coord_on_repr {d : â„•} (Î¼ : Fin (1 + d))
    (y : ComponentIdx (S := realLorentzTensor d) ![Color.up] â†’ â„) :
    ð” Î¼ ((Tensor.basis (S := realLorentzTensor d)
      ![Color.up]).repr.symm (Finsupp.equivFunOnFinite.symm y)) =
    y (fun _ => Fin.cast (by simp) Î¼) := by
  change ð” Î¼ (Lorentz.Vector.toCoordFull.symm y) = _
  rw [coord_apply]
  rw [Lorentz.Vector.toCoord_apply_eq_toCoordFull_apply]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, LinearEquiv.apply_symm_apply,
    Equiv.piCongrLeft'_apply]
  congr
  funext x
  fin_cases x
  simp [Lorentz.Vector.indexEquiv]

/-!

## Derivatives

-/

/-- The derivative of a function `SpaceTime d â†’ â„` along the `Î¼` coordinte. -/
noncomputable def deriv {M : Type} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ M) : SpaceTime d â†’ M :=
  fun y => fderiv â„ f y (Tensor.basis _ (fun x => Fin.cast (by simp) Î¼))

@[inherit_doc deriv]
scoped notation "âˆ‚_" => deriv

/-- The derivative with respect to time. -/
scoped notation "âˆ‚â‚œ" => deriv 0

lemma deriv_eq {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ f y =
    fderiv â„ f y (Tensor.basis _ (fun x => Fin.cast (by simp) Î¼)) := by
  rfl

@[simp]
lemma deriv_zero {d : â„•} (Î¼ : Fin (1 + d)) : SpaceTime.deriv Î¼ (fun _ => (0 : â„)) = 0 := by
  ext y
  rw [SpaceTime.deriv_eq]
  simp

lemma deriv_eq_deriv_on_coord {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ f y = fderiv â„
      (fun y => (f ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr.symm
            (Finsupp.equivFunOnFinite.symm y))))
      â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
    â‡‘(Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1) := by
  change _ = fderiv â„ (f âˆ˜ Lorentz.Vector.fromCoordFullContinuous)
    â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
    â‡‘(Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)
  rw [ContinuousLinearEquiv.comp_right_fderiv]
  rw [deriv_eq]
  congr
  simp [Lorentz.Vector.fromCoordFullContinuous, Lorentz.Vector.toCoordFull]
  exact (LinearEquiv.eq_symm_apply _).mpr rfl

lemma neg_deriv {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) :
    - SpaceTime.deriv Î¼ f = SpaceTime.deriv Î¼ (fun y => - f y) := by
  ext y
  rw [SpaceTime.deriv_eq]
  simp only [Pi.neg_apply, fderiv_neg, Nat.succ_eq_add_one, Nat.reduceAdd, C_eq_color,
    ContinuousLinearMap.neg_apply, neg_inj]
  rfl

lemma neg_deriv_apply {d : â„•} (Î¼ : Fin (1 + d)) (f : SpaceTime d â†’ â„) (y : SpaceTime d) :
    - SpaceTime.deriv Î¼ f y = SpaceTime.deriv Î¼ (fun y => - f y) y:= by
  rw [â† SpaceTime.neg_deriv]
  rfl

@[fun_prop]
lemma coord_differentiable {d : â„•} (Î¼ : Fin (1 + d)) :
    Differentiable â„ (ð” Î¼) := by
  let Ï† : (Fin 1 âŠ• Fin d) â†’ (SpaceTime d) â†’ â„ := fun b y => y b
  change Differentiable â„ (fun y => Ï† _ _)
  have h : Differentiable â„ (flip Ï†) := by
    change Differentiable â„ Lorentz.Vector.toCoord
    fun_prop
  rw [differentiable_pi] at h
  exact h (finSumFinEquiv.symm Î¼)

lemma deriv_coord_same {d : â„•} (Î¼ : Fin (1 + d)) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î¼) y = 1 := by
  rw [SpaceTime.deriv_eq_deriv_on_coord]
  let Ï† : ((x : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] x)))
    â†’ (((j : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] j))) â†’ â„)
    â†’ â„ := fun b y => y b
  conv_lhs =>
    enter [1, 2, y]
    rw [coord_on_repr]
    change Ï† _ y
  have h1 : (fun y => fun i => Ï† i y) = id := by rfl
  have h2 (x) : fderiv â„ (fun y => fun i => Ï† i y) x = ContinuousLinearMap.id _ _ := by
    rw [h1]
    simp
  have h3 (x) : âˆ€ (i), DifferentiableAt â„ (Ï† i) x := by
    have h3' : DifferentiableAt â„ (flip Ï†) x := by
      change DifferentiableAt â„ ((fun y => fun i => Ï† i y)) x
      rw [h1]
      exact differentiableAt_id
    rw [differentiableAt_pi] at h3'
    intro i
    have h3'' := h3' i
    exact h3''
  conv at h2 =>
    enter [x]
    rw [fderiv_pi (h3 x)]
  have h2' := h2 ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
  change (ContinuousLinearMap.pi fun i =>
    fderiv â„ (Ï† i) â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y))
    ((Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)) (fun _ => Fin.cast (by simp) Î¼) = _
  rw [h2']
  simp

lemma deriv_coord_diff {d : â„•} (Î¼ Î½ : Fin (1 + d)) (h : Î¼ â‰  Î½) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î½) y = 0 := by
  rw [SpaceTime.deriv_eq_deriv_on_coord]
  let Ï† : ((x : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] x)))
    â†’ (((j : Fin (Nat.succ 0)) â†’ Fin ((realLorentzTensor d).repDim (![Color.up] j))) â†’ â„)
    â†’ â„ := fun b y => y b
  conv_lhs =>
    enter [1, 2, y]
    rw [coord_on_repr]
    change Ï† _ y
  have h1 : (fun y => fun i => Ï† i y) = id := by rfl
  have h2 (x) : fderiv â„ (fun y => fun i => Ï† i y) x = ContinuousLinearMap.id _ _ := by
    rw [h1]
    simp
  have h3 (x) : âˆ€ (i), DifferentiableAt â„ (Ï† i) x := by
    have h3' : DifferentiableAt â„ (flip Ï†) x := by
      change DifferentiableAt â„ ((fun y => fun i => Ï† i y)) x
      rw [h1]
      exact differentiableAt_id
    rw [differentiableAt_pi] at h3'
    intro i
    have h3'' := h3' i
    exact h3''
  conv at h2 =>
    enter [x]
    rw [fderiv_pi (h3 x)]
  have h2' := h2 ((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y)
  change (ContinuousLinearMap.pi fun i => fderiv â„ (Ï† i)
    â‡‘((Tensor.basis (S := realLorentzTensor d) ![Color.up]).repr y))
    ((Finsupp.single (fun x => Fin.cast (by simp) Î¼) 1)) (fun _ => Fin.cast (by simp) Î½) = _
  rw [h2']
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, ContinuousLinearMap.coe_id', id_eq]
  rw [Finsupp.single_apply]
  simp only [ite_eq_right_iff, one_ne_zero, imp_false]
  intro hn
  have hnx := congrFun hn 0
  simp at hnx
  omega

lemma deriv_coord_eq_if {d : â„•} (Î¼ Î½ : Fin (1 + d)) (y : SpaceTime d) :
    SpaceTime.deriv Î¼ (ð” Î½) y = if Î¼ = Î½ then 1 else 0 := by
  by_cases h : Î¼ = Î½
  Â· simp only [h, â†“reduceIte]
    exact SpaceTime.deriv_coord_same Î½ y
  Â· rw [if_neg h]
    exact SpaceTime.deriv_coord_diff Î¼ Î½ h y

/-- The divergence of a function `SpaceTime d â†’ EuclideanSpace â„ (Fin d)`. -/
noncomputable def spaceDiv {d : â„•} (f : SpaceTime d â†’ EuclideanSpace â„ (Fin d)) :
    SpaceTime d â†’ â„ :=
  âˆ‘ j, SpaceTime.deriv (finSumFinEquiv (Sum.inr j)) (fun y => f y j)

@[inherit_doc spaceDiv]
scoped[SpaceTime] notation "âˆ‡â¬" E => spaceDiv E

/-- The curl of a function `SpaceTime â†’ EuclideanSpace â„ (Fin 3)`. -/
def spaceCurl (f : SpaceTime â†’ EuclideanSpace â„ (Fin 3)) :
    SpaceTime â†’ EuclideanSpace â„ (Fin 3) := fun x j =>
  match j with
  | 0 => deriv 1 (fun y => f y 2) x - deriv 2 (fun y => f y 1) x
  | 1 => deriv 2 (fun y => f y 0) x - deriv 0 (fun y => f y 2) x
  | 2 => deriv 0 (fun y => f y 1) x - deriv 1 (fun y => f y 0) x

@[inherit_doc spaceCurl]
scoped[SpaceTime] notation "âˆ‡Ã—" => spaceCurl

/-- The gradient of a function `SpaceTime â†’ EuclideanSpace â„ (Fin 3)`. -/
def spaceGrad (f : SpaceTime â†’ â„) :
    SpaceTime â†’ EuclideanSpace â„ (Fin 3) := fun x j =>
  match j with
  | 0 => deriv 0 f x
  | 1 => deriv 1 f x
  | 2 => deriv 2 f x

@[inherit_doc spaceGrad]
scoped[SpaceTime] notation "âˆ‡" => spaceGrad

end SpaceTime

end
