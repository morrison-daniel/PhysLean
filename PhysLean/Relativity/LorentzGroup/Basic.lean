/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Relativity.MinkowskiMatrix
import PhysLean.Meta.TODO.Basic
import Mathlib.Analysis.Complex.Basic
import Mathlib.Topology.Instances.Matrix
/-!
# The Lorentz Group

We define the Lorentz group.

## References

- *Lorentz Transformations, Rotations, and Boosts*, Jaffe.
<https://cdn.ku.edu.tr/cdn/files/amostafazadeh/phys517_518/phys517_2016f/Handouts/A_Jaffi_Lorentz_Group.pdf>

-/
TODO "6VZHM" "Define the Lie group structure on the Lorentz group."

noncomputable section

open Matrix
open Complex
open ComplexConjugate

/-!
## Matrices which preserves the Minkowski metric

We start studying the properties of matrices which preserve `Î·Lin`.
These matrices form the Lorentz group, which we will define in the next section at `lorentzGroup`.

-/
variable {d : â„•}

open minkowskiMatrix in
/-- The Lorentz group is the subset of matrices for which
  `Î› * dual Î› = 1`. -/
def LorentzGroup (d : â„•) : Set (Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„) :=
    {Î› : Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„ | Î› * dual Î› = 1}

namespace LorentzGroup
/-- Notation for the Lorentz group. -/
scoped[LorentzGroup] notation (name := lorentzGroup_notation) "ð“›" => LorentzGroup

open minkowskiMatrix
variable {Î› Î›' : Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„}

/-!

# Membership conditions

-/

lemma mem_iff_self_mul_dual : Î› âˆˆ LorentzGroup d â†” Î› * dual Î› = 1 := by
  rfl

lemma mem_iff_dual_mul_self : Î› âˆˆ LorentzGroup d â†” dual Î› * Î› = 1 := by
  rw [mem_iff_self_mul_dual]
  exact mul_eq_one_comm

lemma mem_iff_transpose : Î› âˆˆ LorentzGroup d â†” Î›áµ€ âˆˆ LorentzGroup d := by
  refine Iff.intro (fun h â†¦ ?_) (fun h â†¦ ?_)
  Â· have h1 := congrArg transpose ((mem_iff_dual_mul_self).mp h)
    rw [dual, transpose_mul, transpose_mul, transpose_mul, minkowskiMatrix.eq_transpose,
      â† mul_assoc, transpose_one] at h1
    rw [mem_iff_self_mul_dual, â† h1, dual]
    noncomm_ring
  Â· have h1 := congrArg transpose ((mem_iff_dual_mul_self).mp h)
    rw [dual, transpose_mul, transpose_mul, transpose_mul, minkowskiMatrix.eq_transpose,
      â† mul_assoc, transpose_one, transpose_transpose] at h1
    rw [mem_iff_self_mul_dual, â† h1, dual]
    noncomm_ring

lemma mem_iff_neg_mem : Î› âˆˆ LorentzGroup d â†” -Î› âˆˆ LorentzGroup d := by
  rw [mem_iff_self_mul_dual, mem_iff_self_mul_dual]
  simp [dual]

lemma mem_mul (hÎ› : Î› âˆˆ LorentzGroup d) (hÎ›' : Î›' âˆˆ LorentzGroup d) : Î› * Î›' âˆˆ LorentzGroup d := by
  rw [mem_iff_dual_mul_self, dual_mul]
  trans dual Î›' * (dual Î› * Î›) * Î›'
  Â· noncomm_ring
  Â· rw [(mem_iff_dual_mul_self).mp hÎ›]
    simp [(mem_iff_dual_mul_self).mp hÎ›']

lemma one_mem : 1 âˆˆ LorentzGroup d := by
  rw [mem_iff_dual_mul_self]
  simp

lemma dual_mem (h : Î› âˆˆ LorentzGroup d) : dual Î› âˆˆ LorentzGroup d := by
  rw [mem_iff_dual_mul_self, dual_dual]
  exact mem_iff_self_mul_dual.mp h

/--
A matrix `Î›` is in the Lorentz group if and only if it satisfies `Î›áµ€ * Î· * Î› = Î·`.
-/
lemma mem_iff_transpose_mul_minkowskiMatrix_mul_self
    (Î› : Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„) :
    Î› âˆˆ LorentzGroup d â†” Î›áµ€ * Î· * Î› = Î· := by
  rw [mem_iff_dual_mul_self]
  rw [dual]
  constructor
  Â· intro h
    have h' : Î· * ((Î· * Î›áµ€ * Î·) * Î›) = Î· * 1 := congr_arg (Î· * Â·) h
    rw [mul_one] at h'
    simp_rw [â† mul_assoc, sq, one_mul] at h'
    exact h'
  Â· intro h
    calc
      (Î· * Î›áµ€ * Î·) * Î› = Î· * (Î›áµ€ * Î· * Î›) := by simp_rw [mul_assoc]
      _ = Î· * Î· := by rw [h]
      _ = 1 := by rw [minkowskiMatrix.sq]

end LorentzGroup

/-!

# The Lorentz group as a group

-/

/-- The instance of a group on `LorentzGroup d`. -/
@[simps! mul_coe one_coe div]
instance lorentzGroupIsGroup : Group (LorentzGroup d) where
  mul A B := âŸ¨A.1 * B.1, LorentzGroup.mem_mul A.2 B.2âŸ©
  mul_assoc A B C := Subtype.eq (Matrix.mul_assoc A.1 B.1 C.1)
  one := âŸ¨1, LorentzGroup.one_memâŸ©
  one_mul A := Subtype.eq (Matrix.one_mul A.1)
  mul_one A := Subtype.eq (Matrix.mul_one A.1)
  inv A := âŸ¨minkowskiMatrix.dual A.1, LorentzGroup.dual_mem A.2âŸ©
  inv_mul_cancel A := Subtype.eq (LorentzGroup.mem_iff_dual_mul_self.mp A.2)

/-- `LorentzGroup` has the subtype topology. -/
instance : TopologicalSpace (LorentzGroup d) := instTopologicalSpaceSubtype

namespace LorentzGroup

open minkowskiMatrix

variable {Î› Î›' : LorentzGroup d}

lemma inv_eq_dual (Î› : LorentzGroup d) :
    (Î›â»Â¹ : LorentzGroup d) = âŸ¨minkowskiMatrix.dual Î›.1, LorentzGroup.dual_mem Î›.2âŸ© := by
  rfl

lemma coe_inv : (Î›â»Â¹).1 = Î›.1â»Â¹:= (inv_eq_left_inv (mem_iff_dual_mul_self.mp Î›.2)).symm

/-- The underlying matrix of a Lorentz transformation is invertible. -/
instance (M : LorentzGroup d) : Invertible M.1 where
  invOf := Mâ»Â¹
  invOf_mul_self := by
    rw [â† coe_inv]
    exact (mem_iff_dual_mul_self.mp M.2)
  mul_invOf_self := by
    rw [â† coe_inv]
    exact (mem_iff_self_mul_dual.mp M.2)

lemma subtype_inv_mul : (Subtype.val Î›)â»Â¹ * (Subtype.val Î›) = 1 := inv_mul_of_invertible _

lemma subtype_mul_inv : (Subtype.val Î›) * (Subtype.val Î›)â»Â¹ = 1 := mul_inv_of_invertible _

@[simp]
lemma mul_minkowskiMatrix_mul_transpose :
    (Subtype.val Î›) * minkowskiMatrix * (Subtype.val Î›).transpose = minkowskiMatrix := by
  have h2 := Î›.prop
  rw [LorentzGroup.mem_iff_self_mul_dual] at h2
  simp only [dual] at h2
  refine (right_inv_eq_left_inv minkowskiMatrix.sq ?_).symm
  rw [â† h2]
  noncomm_ring

@[simp]
lemma transpose_mul_minkowskiMatrix_mul_self :
    (Subtype.val Î›).transpose * minkowskiMatrix * (Subtype.val Î›) = minkowskiMatrix := by
  have h2 := Î›.prop
  rw [LorentzGroup.mem_iff_dual_mul_self] at h2
  simp only [dual] at h2
  refine right_inv_eq_left_inv ?_ minkowskiMatrix.sq
  rw [â† h2]
  noncomm_ring

/-!

## Transpose of a Lorentz transformation

-/

/-- The transpose of a matrix in the Lorentz group is an element of the Lorentz group. -/
def transpose (Î› : LorentzGroup d) : LorentzGroup d :=
  âŸ¨Î›.1áµ€, mem_iff_transpose.mp Î›.2âŸ©

@[simp]
lemma transpose_one : @transpose d 1 = 1 := Subtype.eq Matrix.transpose_one

@[simp]
lemma transpose_mul : transpose (Î› * Î›') = transpose Î›' * transpose Î› :=
  Subtype.eq (Matrix.transpose_mul Î›.1 Î›'.1)

lemma transpose_val : (transpose Î›).1 = Î›.1áµ€ := rfl

lemma transpose_inv : (transpose Î›)â»Â¹ = transpose Î›â»Â¹ := by
  refine Subtype.eq ?_
  rw [transpose_val, coe_inv, transpose_val, coe_inv, Matrix.transpose_nonsing_inv]

lemma comm_minkowskiMatrix : Î›.1 * minkowskiMatrix = minkowskiMatrix * (transpose Î›â»Â¹).1 := by
  conv_rhs => rw [â† @mul_minkowskiMatrix_mul_transpose d Î›]
  rw [â† transpose_inv, coe_inv, transpose_val]
  exact Eq.symm (mul_inv_cancel_right_of_invertible _ _)

lemma minkowskiMatrix_comm : minkowskiMatrix * Î›.1 = (transpose Î›â»Â¹).1 * minkowskiMatrix := by
  conv_rhs => rw [â† @transpose_mul_minkowskiMatrix_mul_self d Î›]
  rw [â† transpose_inv, coe_inv, transpose_val]
  rw [â† mul_assoc, â† mul_assoc]
  have h1 : ((Î›.1)áµ€â»Â¹ * (Î›.1)áµ€) = 1 := by
    rw [â† transpose_val]
    simp only [subtype_inv_mul]
  rw [h1]
  simp

/-!

## Negation of a Lorentz transformation

-/

/-- The negation of a Lorentz transform. -/
instance : Neg (LorentzGroup d) where
  neg Î› := âŸ¨-Î›.1, mem_iff_neg_mem.mp Î›.2âŸ©

@[simp]
lemma coe_neg : (-Î›).1 = -Î›.1 := rfl

lemma inv_neg : (-Î›)â»Â¹ = -Î›â»Â¹ := by
  refine Subtype.eq ?_
  simp [inv_eq_dual, dual]

/-!

## Lorentz group as a topological group

We now show that the Lorentz group is a topological group.
We do this by showing that the natural map from the Lorentz group to `GL (Fin 4) â„` is an
embedding.

-/

/-- The homomorphism of the Lorentz group into `GL (Fin 4) â„`. -/
def toGL : LorentzGroup d â†’* GL (Fin 1 âŠ• Fin d) â„ where
  toFun A := âŸ¨A.1, (Aâ»Â¹).1, mul_eq_one_comm.mpr $ mem_iff_dual_mul_self.mp A.2,
    mem_iff_dual_mul_self.mp A.2âŸ©
  map_one' :=
    (GeneralLinearGroup.ext_iff _ 1).mpr fun _ => congrFun rfl
  map_mul' _ _ :=
    (GeneralLinearGroup.ext_iff _ _).mpr fun _ => congrFun rfl

lemma toGL_injective : Function.Injective (@toGL d) := by
  refine fun A B h => Subtype.eq ?_
  rw [@Units.ext_iff] at h
  exact h

/-- The homomorphism from the Lorentz Group into the monoid of matrices times the opposite of
  the monoid of matrices. -/
@[simps!]
def toProd : LorentzGroup d â†’* (Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„) Ã—
    (Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„)áµáµ’áµ– :=
  MonoidHom.comp (Units.embedProduct _) toGL

lemma toProd_eq_transpose_Î· : toProd Î› = (Î›.1, MulOpposite.op $ minkowskiMatrix.dual Î›.1) := rfl

lemma toProd_injective : Function.Injective (@toProd d) := by
  intro A B h
  rw [toProd_eq_transpose_Î·, toProd_eq_transpose_Î·] at h
  rw [Prod.mk_inj] at h
  exact Subtype.eq h.1

lemma toProd_continuous : Continuous (@toProd d) := by
  refine continuous_prodMk.mpr âŸ¨continuous_iff_le_induced.mpr fun U a â†¦ a,
    MulOpposite.continuous_op.comp' ((continuous_const.matrix_mul (continuous_iff_le_induced.mpr
      fun U a => a).matrix_transpose).matrix_mul continuous_const)âŸ©

open Topology

/-- The embedding from the Lorentz Group into the monoid of matrices times the opposite of
  the monoid of matrices. -/
lemma toProd_embedding : IsEmbedding (@toProd d) where
  injective := toProd_injective
  eq_induced :=
    (isInducing_iff â‡‘toProd).mp (IsInducing.of_comp toProd_continuous continuous_fst
      ((isInducing_iff (Prod.fst âˆ˜ â‡‘toProd)).mpr rfl))

/-- The embedding from the Lorentz Group into `GL (Fin 4) â„`. -/
lemma toGL_embedding : IsEmbedding (@toGL d).toFun where
  injective := toGL_injective
  eq_induced := by
    refine ((fun {X} {t t'} => TopologicalSpace.ext_iff.mpr) fun _ â†¦ ?_).symm
    rw [TopologicalSpace.ext_iff.mp toProd_embedding.eq_induced _, isOpen_induced_iff,
      isOpen_induced_iff]
    exact exists_exists_and_eq_and

/-- The embedding of the Lorentz group into `GL(n, â„)` gives `LorentzGroup d` an instance
  of a topological group. -/
instance : IsTopologicalGroup (LorentzGroup d) :=
  IsInducing.topologicalGroup toGL toGL_embedding.toIsInducing

/-!

## To Complex matrices

-/

/-- The monoid homomorphisms taking the lorentz group to complex matrices. -/
def toComplex : LorentzGroup d â†’* Matrix (Fin 1 âŠ• Fin d) (Fin 1 âŠ• Fin d) â„‚ where
  toFun Î› := Î›.1.map ofRealHom
  map_one' := by
    ext i j
    simp only [lorentzGroupIsGroup_one_coe, map_apply, ofRealHom_eq_coe]
    simp only [Matrix.one_apply, ofReal_one, ofReal_zero]
    split_ifs
    Â· rfl
    Â· rfl
  map_mul' Î› Î›' := by
    ext i j
    simp only [lorentzGroupIsGroup_mul_coe, map_apply, ofRealHom_eq_coe]
    simp only [â† Matrix.map_mul, RingHom.map_matrix_mul]
    rfl

/-- The image of a Lorentz transformation under `toComplex` is invertible. -/
instance (M : LorentzGroup d) : Invertible (toComplex M) where
  invOf := toComplex Mâ»Â¹
  invOf_mul_self := by
    rw [â† toComplex.map_mul, Group.inv_mul_cancel]
    simp
  mul_invOf_self := by
    rw [â† toComplex.map_mul]
    rw [@mul_inv_cancel]
    simp

lemma toComplex_inv (Î› : LorentzGroup d) : (toComplex Î›)â»Â¹ = toComplex Î›â»Â¹ := by
  refine inv_eq_right_inv ?h
  rw [â† toComplex.map_mul, mul_inv_cancel]
  simp

@[simp]
lemma toComplex_mul_minkowskiMatrix_mul_transpose (Î› : LorentzGroup d) :
    toComplex Î› * minkowskiMatrix.map ofRealHom * (toComplex Î›)áµ€ =
    minkowskiMatrix.map ofRealHom := by
  simp only [toComplex, MonoidHom.coe_mk, OneHom.coe_mk]
  have h1 : ((Î›.1).map â‡‘ofRealHom)áµ€ = (Î›.1áµ€).map ofRealHom := rfl
  rw [h1]
  trans (Î›.1 * minkowskiMatrix * Î›.1áµ€).map ofRealHom
  Â· simp only [Matrix.map_mul]
  simp only [mul_minkowskiMatrix_mul_transpose]

@[simp]
lemma toComplex_transpose_mul_minkowskiMatrix_mul_self (Î› : LorentzGroup d) :
    (toComplex Î›)áµ€ * minkowskiMatrix.map ofRealHom * toComplex Î› =
    minkowskiMatrix.map ofRealHom := by
  simp only [toComplex, MonoidHom.coe_mk, OneHom.coe_mk]
  have h1 : ((Î›.1).map ofRealHom)áµ€ = (Î›.1áµ€).map ofRealHom := rfl
  rw [h1]
  trans (Î›.1áµ€ * minkowskiMatrix * Î›.1).map ofRealHom
  Â· simp only [Matrix.map_mul]
  simp only [transpose_mul_minkowskiMatrix_mul_self]

lemma toComplex_mulVec_ofReal (v : Fin 1 âŠ• Fin d â†’ â„) (Î› : LorentzGroup d) :
    toComplex Î› *áµ¥ (ofRealHom âˆ˜ v) = ofRealHom âˆ˜ (Î› *áµ¥ v) := by
  simp only [toComplex, MonoidHom.coe_mk, OneHom.coe_mk]
  funext i
  rw [â† RingHom.map_mulVec]
  rfl

/-- The parity transformation. -/
def parity : LorentzGroup d := âŸ¨minkowskiMatrix, by
  rw [mem_iff_dual_mul_self]
  simp only [dual_eta, minkowskiMatrix.sq]âŸ©

/-!

## Equality conditions

-/

lemma eq_of_mulVec_eq {Î› Î›' : LorentzGroup d}
    (h : âˆ€ (x : Fin 1 âŠ• Fin d â†’ â„), Î›.1 *áµ¥ x = Î›'.1 *áµ¥ x) : Î› = Î›' := by
  apply Subtype.eq
  exact ext_of_mulVec_single fun i => h (Pi.single i 1)

end LorentzGroup
