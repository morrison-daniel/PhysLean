/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.QFT.PerturbationTheory.WickAlgebra.WickTerm
/-!

# Wick's theorem

This file contrains the time-dependent version of Wick's theorem
for lists of fields containing both fermions and bosons.

Wick's theorem is related to Isserlis' theorem in mathematics.

-/

namespace FieldSpecification
variable {ð“• : FieldSpecification}
open FieldOpFreeAlgebra
open WickAlgebra
open PhysLean.List
open WickContraction
open FieldStatistic

/-!

## Wick terms

-/

/-!

## Wick's theorem

-/

lemma wicks_theorem_congr {Ï†s Ï†s' : List ð“•.FieldOp} (h : Ï†s = Ï†s') :
    âˆ‘ (Ï†sÎ› : WickContraction Ï†s.length), Ï†sÎ›.wickTerm
    = âˆ‘ (Ï†s'Î› : WickContraction Ï†s'.length), Ï†s'Î›.wickTerm := by
  subst h
  rfl

/--
For a list `Ï†s` of `ð“•.FieldOp`, Wick's theorem states that

`ð“£(Ï†s) = âˆ‘ Ï†sÎ›, Ï†sÎ›.wickTerm`

where the sum is over all Wick contraction `Ï†sÎ›`.

The proof is via induction on `Ï†s`.
- The base case `Ï†s = []` is handled by `wickTerm_empty_nil`.

The inductive step works as follows:

For the LHS:
1. `timeOrder_eq_maxTimeField_mul_finset` is used to write
  `ð“£(Ï†â‚€â€¦Ï†â‚™)` as `ð“¢(Ï†áµ¢,Ï†â‚€â€¦Ï†áµ¢â‚‹â‚) â€¢ Ï†áµ¢ * ð“£(Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†â‚™)` where `Ï†áµ¢` is
  the maximal time field in `Ï†â‚€â€¦Ï†â‚™`
2. The induction hypothesis is then used on `ð“£(Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†â‚™)` to expand it as a sum over
  Wick contractions of `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†â‚™`.
3. This gives terms of the form `Ï†áµ¢ * Ï†sÎ›.wickTerm` on which
  `mul_wickTerm_eq_sum` is used where `Ï†sÎ›` is a Wick contraction of `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†`,
  to rewrite terms as a sum over optional uncontracted elements of `Ï†sÎ›`

On the LHS we now have a sum over Wick contractions `Ï†sÎ›` of `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†` (from 2) and optional
uncontracted elements of `Ï†sÎ›` (from 3)

For the RHS:
1. The sum over Wick contractions of `Ï†â‚€â€¦Ï†â‚™` on the RHS
  is split via `insertLift_sum` into a sum over Wick contractions `Ï†sÎ›` of `Ï†â‚€â€¦Ï†áµ¢â‚‹â‚Ï†áµ¢â‚Šâ‚Ï†` and
  sum over optional uncontracted elements of `Ï†sÎ›`.

Both sides are now sums over the same thing and their terms equate by the nature of the
lemmas used.
-/
theorem wicks_theorem : (Ï†s : List ð“•.FieldOp) â†’ ð“£(ofFieldOpList Ï†s) =
    âˆ‘ (Ï†sÎ› : WickContraction Ï†s.length), Ï†sÎ›.wickTerm
  | [] => by
    rw [timeOrder_ofFieldOpList_nil]
    simp only [List.length_nil]
    rw [sum_WickContraction_nil]
    simp only [wickTerm_empty_nil]
  | Ï† :: Ï†s => by
    have ih := wicks_theorem (eraseMaxTimeField Ï† Ï†s)
    conv_lhs => rw [timeOrder_eq_maxTimeField_mul_finset, ih, Finset.mul_sum]
    have h1 : Ï† :: Ï†s =
        (eraseMaxTimeField Ï† Ï†s).insertIdx (maxTimeFieldPosFin Ï† Ï†s) (maxTimeField Ï† Ï†s) := by
      simp only [eraseMaxTimeField, insertionSortDropMinPos, List.length_cons, Nat.succ_eq_add_one,
        maxTimeField, insertionSortMin, List.get_eq_getElem]
      erw [insertIdx_eraseIdx_fin]
    conv_rhs => rw [wicks_theorem_congr h1]
    conv_rhs => rw [insertLift_sum]
    apply Finset.sum_congr rfl
    intro c _
    rw [Algebra.smul_mul_assoc, mul_wickTerm_eq_sum
      (maxTimeField Ï† Ï†s) (eraseMaxTimeField Ï† Ï†s) (maxTimeFieldPosFin Ï† Ï†s) c]
    trans (1 : â„‚) â€¢ âˆ‘ k : Option { x // x âˆˆ c.uncontracted },
      (c â†©Î› (maxTimeField Ï† Ï†s) (maxTimeFieldPosFin Ï† Ï†s) k).wickTerm
    swap
    Â· simp
    rw [smul_smul]
    simp only [instCommGroup.eq_1, exchangeSign_mul_self, Nat.succ_eq_add_one, Fintype.sum_option,
      Finset.univ_eq_attach, smul_add, one_smul]
    Â· exact fun k => timeOrder_maxTimeField _ _ k
    Â· exact fun k => lt_maxTimeFieldPosFin_not_timeOrder _ _ k
termination_by Ï†s => Ï†s.length

end FieldSpecification
