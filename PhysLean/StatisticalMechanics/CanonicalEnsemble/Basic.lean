/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina, Joseph Tooby-Smith
-/
import PhysLean.Thermodynamics.Temperature.Basic
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import Mathlib.Analysis.Calculus.ParametricIntegral
import PhysLean.Meta.Informal.SemiFormal
import PhysLean.Meta.Linters.Sorry
import Mathlib.Analysis.SpecialFunctions.Log.Summable
import Mathlib.MeasureTheory.Integral.Prod
/-!

# Canonical Ensemble: General Theory

A canonical ensemble describes a physical system in thermal equilibrium with a heat bath at a
fixed temperature.

In this file we define the canonical ensemble, its partition function, the
probability of being in a given microstate, the mean energy, the entropy and
the Helmholtz free energy

We also define the addition of two canonical ensembles, and prove results related
to the properties of additions of canonical ensembles

We develop a general measure-theoretic framework designed to be applicable to both classical
continuous systems (like an ideal gas) and discrete systems (like a spin lattice).

## The Semi-Classical Framework

In classical statistical mechanics quantities like the partition function and entropy must be
dimensionless to be physically meaningful. A naive integration over a classical
phase space yields dimensionful quantities, leading to ambiguities (e.g., the Gibbs paradox).

Following the "semi-classical" approach (see references) we introduce a constant with
units of action (`phase_space_unit`, identified with Planck's constant `h`) and the number of
degrees of freedom (`dof`) to correctly normalize the phase space volume.

This file distinguishes between:
1.  **Mathematical quantities**: Raw results of integration over the given measure
    (e.g., `mathematicalPartitionFunction`, `differentialEntropy`).
2.  **Physical/Thermodynamic quantities**: Dimensionless, physically meaningful quantities derived
    from the mathematical ones using the semi-classical normalization
    (e.g., `partitionFunction`, `helmholtzFreeEnergy`, `thermodynamicEntropy`).

## Main Definitions

- `CanonicalEnsemble`: The core structure, including `energy`, `dof`, and `phase_space_unit`.
- `mathematicalPartitionFunction`: The raw integral of the Boltzmann factor, `âˆ« exp(-Î²E) dÎ¼`.
- `partitionFunction`: The dimensionless physical partition function, `Z = Z_math / h^dof`.
- `probability`: The probability density function.
- `meanEnergy`: The expectation value of the energy.
- `differentialEntropy`: The entropy defined as `-k_B âˆ« Ï log Ï dÎ¼`, which can be negative.
- `helmholtzFreeEnergy`: The physical free energy `F = -k_B T log(Z)`.
- `thermodynamicEntropy`: The absolute physical entropy, defined via `S = (U - F) / T`.

## Key Theorems

- The relationship between the mathematical `helmholtzPotential` and the physical
  `helmholtzFreeEnergy`.
- The connection between `thermodynamicEntropy` and `differentialEntropy`, showing
  they differ by a constant related to the `phase_space_unit`.

##Â References
- L. D. Landau and E. M. Lifshitz, *Statistical Physics, Part 1*.
- https://www.damtp.cam.ac.uk/user/tong/statphys/statmechhtml/S1.html#E23.

-/

open MeasureTheory Real Temperature
open scoped Temperature

/-- A Canonical ensemble is described by a type `Î¹`, corresponding to the type of microstates,
and a map `Î¹ â†’ â„` which associates which each microstate an energy
and physical constants needed to define dimensionless thermodynamic quantities. -/
structure CanonicalEnsemble (Î¹ : Type) [MeasurableSpace Î¹] : Type where
  /-- The energy of associated with a mircrostate of the canonical ensemble. -/
  energy : Î¹ â†’ â„
  /-- The number of degrees of freedom, used to make the partition function dimensionless.
  For a classical system of N particles in 3D, this is `3N`. For a system of N spins,
  this is typically `0` as the state space is already discrete. -/
  dof : â„•
  /-- The unit of action used to make the phase space volume dimensionless.
  This constant is necessary to define an absolute (rather than relative) thermodynamic
  entropy. In the semi-classical approach, this unit is identified with Planck's constant `h`.
  For discrete systems with a counting measure, this unit should be set to `1`. -/
  phase_space_unit : â„ := 1
  /-- Assumption that the phase space unit is positive. -/
  h_pos : 0 < phase_space_unit := by positivity
  energy_measurable : Measurable energy
  /-- The measure on the indexing set of microstates. -/
  Î¼ : MeasureTheory.Measure Î¹ := by volume_tac
  [Î¼_sigmaFinite : SigmaFinite Î¼]

namespace CanonicalEnsemble
open Real Temperature

variable {Î¹ Î¹1 : Type} [MeasurableSpace Î¹]
  [MeasurableSpace Î¹1] (ğ“’ : CanonicalEnsemble Î¹) (ğ“’1 : CanonicalEnsemble Î¹1)

instance : SigmaFinite ğ“’.Î¼ := ğ“’.Î¼_sigmaFinite

@[ext]
lemma ext {ğ“’ ğ“’' : CanonicalEnsemble Î¹} (h_energy : ğ“’.energy = ğ“’'.energy)
    (h_dof : ğ“’.dof = ğ“’'.dof) (h_h : ğ“’.phase_space_unit = ğ“’'.phase_space_unit)
    (h_Î¼ : ğ“’.Î¼ = ğ“’'.Î¼) : ğ“’ = ğ“’' := by
  cases ğ“’; cases ğ“’'; simp_all

@[fun_prop]
lemma energy_measurable' : Measurable ğ“’.energy := ğ“’.energy_measurable

/-- The addition of two `CanonicalEnsemble`. The degrees of freedom are added.
Note: This is only physically meaningful if the two systems share the same `phase_space_unit`. -/
noncomputable instance {Î¹1 Î¹2 : Type} [MeasurableSpace Î¹1] [MeasurableSpace Î¹2] :
    HAdd (CanonicalEnsemble Î¹1) (CanonicalEnsemble Î¹2)
    (CanonicalEnsemble (Î¹1 Ã— Î¹2)) where
  hAdd := fun ğ“’1 ğ“’2 => {
    energy := fun (i : Î¹1 Ã— Î¹2) => ğ“’1.energy i.1 + ğ“’2.energy i.2
    dof := ğ“’1.dof + ğ“’2.dof
    phase_space_unit := ğ“’1.phase_space_unit
    h_pos := ğ“’1.h_pos
    Î¼ := ğ“’1.Î¼.prod ğ“’2.Î¼
    energy_measurable := by fun_prop
  }

/-- The canonical ensemble with no microstates. -/
def empty : CanonicalEnsemble Empty where
  energy := isEmptyElim
  dof := 0
  Î¼ := 0
  energy_measurable := by fun_prop

/-- Given a measurable equivalence `e : Î¹1 â‰ƒáµ Î¹`, this is the corresponding canonical ensemble
on `Î¹1`. The physical properties (`dof`, `phase_space_unit`) are unchanged. -/
noncomputable def congr (e : Î¹1 â‰ƒáµ Î¹) : CanonicalEnsemble Î¹1 where
  energy := fun i => ğ“’.energy (e i)
  dof := ğ“’.dof
  phase_space_unit := ğ“’.phase_space_unit
  h_pos := ğ“’.h_pos
  Î¼ := ğ“’.Î¼.map e.symm
  energy_measurable := by
    apply Measurable.comp
    Â· fun_prop
    Â· exact MeasurableEquiv.measurable e
  Î¼_sigmaFinite := MeasurableEquiv.sigmaFinite_map e.symm

@[simp]
lemma congr_energy_comp_symmm (e : Î¹1 â‰ƒáµ Î¹) :
    (ğ“’.congr e).energy âˆ˜ e.symm = ğ“’.energy := by
  funext i
  simp [congr]

/-- Scalar multiplication of `CanonicalEnsemble`, defined such that
`nsmul n ğ“’` represents `n` non-interacting, distinguishable copies of the ensemble `ğ“’`. -/
noncomputable def nsmul (n : â„•) (ğ“’ : CanonicalEnsemble Î¹) : CanonicalEnsemble (Fin n â†’ Î¹) where
  energy := fun f => âˆ‘ i, ğ“’.energy (f i)
  dof := n * ğ“’.dof
  phase_space_unit := ğ“’.phase_space_unit
  h_pos := ğ“’.h_pos
  Î¼ := MeasureTheory.Measure.pi fun _ => ğ“’.Î¼
  energy_measurable := by fun_prop

set_option linter.unusedVariables false in
/-- The microstates of a canonical ensemble. -/
@[nolint unusedArguments]
abbrev microstates (ğ“’ : CanonicalEnsemble Î¹) : Type := Î¹

/-! ## Properties of physical parameters -/

@[simp]
lemma dof_add (ğ“’1 : CanonicalEnsemble Î¹) (ğ“’2 : CanonicalEnsemble Î¹1) :
    (ğ“’1 + ğ“’2).dof = ğ“’1.dof + ğ“’2.dof := rfl

@[simp]
lemma phase_space_unit_add (ğ“’1 : CanonicalEnsemble Î¹) (ğ“’2 : CanonicalEnsemble Î¹1) :
    (ğ“’1 + ğ“’2).phase_space_unit = ğ“’1.phase_space_unit := rfl

@[simp]
lemma dof_nsmul (n : â„•) : (nsmul n ğ“’).dof = n * ğ“’.dof := rfl

@[simp]
lemma phase_space_unit_nsmul (n : â„•) :
    (nsmul n ğ“’).phase_space_unit = ğ“’.phase_space_unit := rfl

@[simp]
lemma dof_congr (e : Î¹1 â‰ƒáµ Î¹) :
    (ğ“’.congr e).dof = ğ“’.dof := rfl

@[simp]
lemma phase_space_unit_congr (e : Î¹1 â‰ƒáµ Î¹) :
    (ğ“’.congr e).phase_space_unit = ğ“’.phase_space_unit := rfl

/-! ## The measure -/

lemma Î¼_add : (ğ“’ + ğ“’1).Î¼ = ğ“’.Î¼.prod ğ“’1.Î¼ := rfl

lemma Î¼_nsmul (n : â„•) : (nsmul n ğ“’).Î¼ = MeasureTheory.Measure.pi fun _ => ğ“’.Î¼ := rfl

lemma Î¼_nsmul_zero_eq : (nsmul 0 ğ“’).Î¼ = Measure.pi (fun _ => 0) := by
  simp [nsmul, Î¼_nsmul]
  congr
  funext x
  exact Fin.elim0 x

/-!

## The energy of the microstates

-/

@[simp]
lemma energy_add_apply (i : microstates (ğ“’ + ğ“’1)) :
    (ğ“’ + ğ“’1).energy i = ğ“’.energy i.1 + ğ“’1.energy i.2 := rfl

@[simp]
lemma energy_nsmul_apply (n : â„•) (f : Fin n â†’ microstates ğ“’) :
    (nsmul n ğ“’).energy f = âˆ‘ i, ğ“’.energy (f i) := rfl

@[simp]
lemma energy_congr_apply (e : Î¹1 â‰ƒáµ Î¹) (i : Î¹1) :
    (ğ“’.congr e).energy i = ğ“’.energy (e i) := rfl

/-! ## Induction for nsmul -/

open MeasureTheory

lemma nsmul_succ (n : â„•) [SigmaFinite ğ“’.Î¼] : nsmul n.succ ğ“’ = (ğ“’ + nsmul n ğ“’).congr
    (MeasurableEquiv.piFinSuccAbove (fun _ => Î¹) 0) := by
  ext1
  Â· ext x
    simp only [Nat.succ_eq_add_one, energy_nsmul_apply, congr_energy_comp_symmm,
      MeasurableEquiv.piFinSuccAbove_apply, Fin.insertNthEquiv_zero, Fin.consEquiv_symm_apply,
      energy_add_apply, MeasurableEquiv.symm_apply_apply]
    exact Fin.sum_univ_succAbove (fun i => ğ“’.energy (x i)) 0
  Â· simp [Nat.succ_eq_add_one, Nat.succ_mul, dof_nsmul, add_comm, add_left_comm, add_assoc]
  Â· simp
  Â· refine Eq.symm (MeasureTheory.MeasurePreserving.map_eq ?_)
    refine MeasurePreserving.symm _ ?_
    exact MeasureTheory.measurePreserving_piFinSuccAbove (n := n) (fun _ => ğ“’.Î¼) 0

/-!

## Non zero nature of the measure

-/

instance [NeZero ğ“’.Î¼] [NeZero ğ“’1.Î¼] : NeZero (ğ“’ + ğ“’1).Î¼ := by
  simp [Î¼_add]
  refine { out := ?_ }
  rw [â† @Measure.measure_univ_pos]
  have h1 : (ğ“’.Î¼.prod (ğ“’1.Î¼)) Set.univ =
      (ğ“’.Î¼ Set.univ) * (ğ“’1.Î¼ Set.univ) := by
    rw [â† @Measure.prod_prod]
    simp
  rw [h1]
  exact NeZero.pos (ğ“’.Î¼ Set.univ * ğ“’1.Î¼ Set.univ)

instance Î¼_neZero_congr [NeZero ğ“’.Î¼] (e : Î¹1 â‰ƒáµ Î¹) :
    NeZero (ğ“’.congr e).Î¼ := by
  refine { out := ?_ }
  rw [â† @Measure.measure_univ_pos]
  simp only [Measure.measure_univ_pos, ne_eq]
  refine (Measure.map_ne_zero_iff ?_).mpr ?_
  Â· fun_prop
  Â· exact Ne.symm (NeZero.ne' _)

instance [NeZero ğ“’.Î¼] (n : â„•) : NeZero (nsmul n ğ“’).Î¼ := by
  induction n with
  | zero =>
    rw [Î¼_nsmul_zero_eq]
    rw [@neZero_iff]
    simp only [ne_eq]
    refine Measure.measure_univ_ne_zero.mp ?_
    simp
  | succ n ih =>
    rw [nsmul_succ]
    infer_instance

/-!

## The Boltzmann measure

-/

/-- The Boltzmann measure on the space of microstates. -/
noncomputable def Î¼Bolt (T : Temperature) : MeasureTheory.Measure Î¹ :=
  ğ“’.Î¼.withDensity (fun i => ENNReal.ofReal (exp (- T.Î² * ğ“’.energy i)))

instance (T : Temperature) : SigmaFinite (ğ“’.Î¼Bolt T) :=
  inferInstanceAs
    (SigmaFinite (ğ“’.Î¼.withDensity (fun i => ENNReal.ofReal (exp (- Î² T * ğ“’.energy i)))))

@[simp]
lemma Î¼Bolt_add (T : Temperature) :
    (ğ“’ + ğ“’1).Î¼Bolt T = (ğ“’.Î¼Bolt T).prod (ğ“’1.Î¼Bolt T) := by
  simp_rw [Î¼Bolt, Î¼_add]
  rw [MeasureTheory.prod_withDensity]
  congr
  funext i
  rw [â† ENNReal.ofReal_mul, â† Real.exp_add]
  simp only [energy_add_apply, neg_mul]
  ring_nf
  Â· exact exp_nonneg _
  Â· fun_prop
  Â· fun_prop

lemma Î¼Bolt_congr (e : Î¹1 â‰ƒáµ Î¹) (T : Temperature) : (ğ“’.congr e).Î¼Bolt T =
    (ğ“’.Î¼Bolt T).map e.symm := by
  simp [congr, Î¼Bolt]
  refine Measure.ext_of_lintegral _ fun Ï† hÏ† â†¦ ?_
  rw [lintegral_withDensity_eq_lintegral_mulâ‚€]
  rw [lintegral_map, lintegral_map, lintegral_withDensity_eq_lintegral_mulâ‚€]
  congr
  funext i
  simp only [Pi.mul_apply, MeasurableEquiv.apply_symm_apply]
  repeat fun_prop

lemma Î¼Bolt_nsmul [SigmaFinite ğ“’.Î¼] (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).Î¼Bolt T = MeasureTheory.Measure.pi fun _ => (ğ“’.Î¼Bolt T) := by
  induction n with
  | zero =>
    simp [nsmul, Î¼Bolt]
    congr
    funext x
    exact Fin.elim0 x
  | succ n ih =>
    rw [nsmul_succ, Î¼Bolt_congr]
    rw [Î¼Bolt_add]
    refine MeasurePreserving.map_eq ?_
    refine MeasurePreserving.symm _ ?_
    rw [ih]
    exact MeasureTheory.measurePreserving_piFinSuccAbove (fun _ => ğ“’.Î¼Bolt T) 0

  lemma Î¼Bolt_ne_zero_of_Î¼_ne_zero (T : Temperature) (h : ğ“’.Î¼ â‰  0) :
    ğ“’.Î¼Bolt T â‰  0 := by
  simp [Î¼Bolt] at âŠ¢ h
  rw [Measure.ext_iff'] at âŠ¢ h
  simp only [Measure.coe_zero, Pi.zero_apply]
  have hs : {x | ENNReal.ofReal (rexp (-(â†‘T.Î² * ğ“’.energy x))) â‰  0} = Set.univ := by
    ext i
    simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le, Set.mem_setOf_eq, Set.mem_univ, iff_true]
    exact exp_pos _
  conv =>
    enter [1, s]
    rw [MeasureTheory.withDensity_apply_eq_zero' (by fun_prop), hs]
    simp
  simpa using h

instance (T : Temperature) [NeZero ğ“’.Î¼] : NeZero (ğ“’.Î¼Bolt T) := by
  refine { out := ?_ }
  apply Î¼Bolt_ne_zero_of_Î¼_ne_zero
  exact Ne.symm (NeZero.ne' ğ“’.Î¼)

instance (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [IsFiniteMeasure (ğ“’1.Î¼Bolt T)] :
    IsFiniteMeasure ((ğ“’ + ğ“’1).Î¼Bolt T) := by
  simp only [Î¼Bolt_add]; infer_instance

instance (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] (n : â„•) :
    IsFiniteMeasure ((nsmul n ğ“’).Î¼Bolt T) := by
  simp [Î¼Bolt_nsmul]; infer_instance

/-!

## The Mathematical Partition Function

-/

/-- The mathematical partition function, defined as the integral of the Boltzmann factor.
This quantity may have physical dimensions. See `CanonicalEnsemble.partitionFunction` for
the dimensionless physical version. -/
noncomputable def mathematicalPartitionFunction (T : Temperature) : â„ := (ğ“’.Î¼Bolt T).real Set.univ

lemma mathematicalPartitionFunction_eq_integral (T : Temperature) :
    mathematicalPartitionFunction ğ“’ T = âˆ« i, exp (- T.Î² * ğ“’.energy i) âˆ‚ğ“’.Î¼ := by
  trans âˆ« i, 1 âˆ‚ğ“’.Î¼Bolt T
  Â· simp only [integral_const, smul_eq_mul, mul_one]
    rfl
  rw [Î¼Bolt]
  erw [integral_withDensity_eq_integral_smul]
  congr
  funext x
  simp [HSMul.hSMul, SMul.smul]
  Â· exact exp_nonneg _
  Â· fun_prop

lemma mathematicalPartitionFunction_add {T : Temperature} :
    (ğ“’ + ğ“’1).mathematicalPartitionFunction T =
    ğ“’.mathematicalPartitionFunction T * ğ“’1.mathematicalPartitionFunction T := by
  simp_rw [mathematicalPartitionFunction, Î¼Bolt_add]
  rw [â† measureReal_prod_prod, Set.univ_prod_univ]

@[simp]
lemma mathematicalPartitionFunction_congr (e : Î¹1 â‰ƒáµ Î¹) (T : Temperature) :
    (ğ“’.congr e).mathematicalPartitionFunction T = ğ“’.mathematicalPartitionFunction T := by
  rw [mathematicalPartitionFunction_eq_integral, mathematicalPartitionFunction_eq_integral]
  simp only [congr]
  rw [integral_map_equiv]
  simp

/-- The `mathematicalPartitionFunction_nsmul` function of `n` copies of a canonical ensemble. -/
lemma mathematicalPartitionFunction_nsmul (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).mathematicalPartitionFunction T = (ğ“’.mathematicalPartitionFunction T) ^ n := by
  simp_rw [mathematicalPartitionFunction, Î¼Bolt_nsmul, measureReal_def, Measure.pi_univ]
  simp [ENNReal.toReal_prod]

lemma mathematicalPartitionFunction_nonneg (T : Temperature) :
    0 â‰¤ ğ“’.mathematicalPartitionFunction T := by
  rw [mathematicalPartitionFunction]; exact measureReal_nonneg

lemma mathematicalPartitionFunction_eq_zero_iff (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] :
    mathematicalPartitionFunction ğ“’ T = 0 â†” ğ“’.Î¼ = 0 := by
  simp [mathematicalPartitionFunction]
  rw [measureReal_def]
  rw [ENNReal.toReal_eq_zero_iff]
  simp only [measure_ne_top, or_false]
  rw [Î¼Bolt]
  rw [MeasureTheory.withDensity_apply_eq_zero']
  simp only [neg_mul, ne_eq, ENNReal.ofReal_eq_zero, not_le, Set.inter_univ]
  let s : Set Î¹ := {x | 0 < rexp (-(T.Î² * ğ“’.energy x))}
  have h : s = Set.univ := by
    ext i
    simp [s]
    exact exp_pos (-(T.Î² * ğ“’.energy i))
  change ğ“’.Î¼ s = 0 â†” ğ“’.Î¼ = 0
  rw [h]
  simp only [Measure.measure_univ_eq_zero, s]
  fun_prop

open NNReal

lemma mathematicalPartitionFunction_comp_ofÎ²_apply (Î² : â„â‰¥0) :
    ğ“’.mathematicalPartitionFunction (ofÎ² Î²) =
    (ğ“’.Î¼.withDensity (fun i => ENNReal.ofReal (exp (- Î² * ğ“’.energy i)))).real Set.univ := by
  simp only [mathematicalPartitionFunction, Î¼Bolt, Î²_ofÎ², neg_mul]

/-- The partition function is strictly positive provided the underlying
measure is non-zero and the Boltzmann measure is finite. -/
lemma mathematicalPartitionFunction_pos (T : Temperature)
    [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼] :
    0 < ğ“’.mathematicalPartitionFunction T := by
  have hÎ¼ : ğ“’.Î¼Bolt T â‰  0 := (NeZero.ne (ğ“’.Î¼Bolt T))
  simp [mathematicalPartitionFunction]

open NNReal Constants

/-! ## The probability density -/

/-- The probability density function of the canonical ensemble.
Note: In the general measure-theoretic case, this is a density with respect to the
underlying measure `ğ“’.Î¼` and is not necessarily less than or equal to 1. In the
case of a finite ensemble with the counting measure, this value corresponds to the
probability of the microstate. -/
noncomputable def probability (T : Temperature) (i : Î¹) : â„ :=
  (exp (- T.Î² * ğ“’.energy i)) / ğ“’.mathematicalPartitionFunction T

/-! ## The probability measure -/

lemma probability_add {T : Temperature} (i : Î¹ Ã— Î¹1) :
    (ğ“’ + ğ“’1).probability T i = ğ“’.probability T i.1 * ğ“’1.probability T i.2 := by
  simp [probability, mathematicalPartitionFunction_add, mul_add, Real.exp_add]
  ring

@[simp]
lemma probability_congr (e : Î¹1 â‰ƒáµ Î¹) (T : Temperature) (i : Î¹1) :
    (ğ“’.congr e).probability T i = ğ“’.probability T (e i) := by
  simp [probability]

lemma probability_nsmul (n : â„•) (T : Temperature) (f : Fin n â†’ Î¹) :
    (nsmul n ğ“’).probability T f = âˆ i, ğ“’.probability T (f i) := by
  induction n with
  | zero =>
    simp [probability, mathematicalPartitionFunction_nsmul]
  | succ n ih =>
    rw [nsmul_succ]
    rw [probability_congr]
    rw [probability_add]
    simp only [MeasurableEquiv.piFinSuccAbove_apply, Fin.insertNthEquiv_zero,
      Fin.consEquiv_symm_apply]
    rw [ih]
    exact Eq.symm (Fin.prod_univ_succAbove (fun i => ğ“’.probability T (f i)) 0)

/-- The probability measure associated with the Boltzmann distribution of a
  canonical ensemble. -/
noncomputable def Î¼Prod (T : Temperature) : MeasureTheory.Measure Î¹ :=
  (ğ“’.Î¼Bolt T Set.univ)â»Â¹ â€¢ ğ“’.Î¼Bolt T

instance (T : Temperature) : SigmaFinite (ğ“’.Î¼Prod T) :=
  inferInstanceAs (SigmaFinite ((ğ“’.Î¼Bolt T Set.univ)â»Â¹ â€¢ ğ“’.Î¼Bolt T))

instance (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)]
  [NeZero ğ“’.Î¼] : IsProbabilityMeasure (ğ“’.Î¼Prod T) := inferInstanceAs <|
  IsProbabilityMeasure ((ğ“’.Î¼Bolt T Set.univ)â»Â¹ â€¢ ğ“’.Î¼Bolt T)

instance {T} : IsFiniteMeasure (ğ“’.Î¼Prod T) := by
  rw [Î¼Prod]
  infer_instance

lemma Î¼Prod_add {T : Temperature} [IsFiniteMeasure (ğ“’.Î¼Bolt T)]
    [IsFiniteMeasure (ğ“’1.Î¼Bolt T)] : (ğ“’ + ğ“’1).Î¼Prod T = (ğ“’.Î¼Prod T).prod (ğ“’1.Î¼Prod T) := by
  rw [Î¼Prod, Î¼Prod, Î¼Prod, Î¼Bolt_add]
  rw [MeasureTheory.Measure.prod_smul_left, MeasureTheory.Measure.prod_smul_right]
  rw [smul_smul]
  congr
  trans ((ğ“’.Î¼Bolt T) Set.univ * (ğ“’1.Î¼Bolt T) Set.univ)â»Â¹
  swap
  Â· by_cases h : (ğ“’.Î¼Bolt T) Set.univ = 0
    Â· simp [h]
    by_cases h1 : (ğ“’1.Î¼Bolt T) Set.univ = 0
    Â· simp [h1]
    rw [ENNReal.mul_inv]
    Â· simp
    Â· simp
  Â· rw [â† @Measure.prod_prod]
    simp

lemma Î¼Prod_congr (e : Î¹1 â‰ƒáµ Î¹) (T : Temperature) :
    (ğ“’.congr e).Î¼Prod T = (ğ“’.Î¼Prod T).map e.symm := by
  simp [Î¼Prod, Î¼Bolt_congr]
  congr 2
  rw [MeasurableEquiv.map_apply]
  simp

lemma Î¼Prod_nsmul (n : â„•) (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] :
    (nsmul n ğ“’).Î¼Prod T = MeasureTheory.Measure.pi fun _ => ğ“’.Î¼Prod T := by
  induction n with
  | zero =>
    simp [nsmul, Î¼Prod, Î¼Bolt]
    congr
    funext x
    exact Fin.elim0 x
  | succ n ih =>
    rw [nsmul_succ]
    rw [Î¼Prod_congr]
    rw [Î¼Prod_add]
    refine MeasurePreserving.map_eq ?_
    refine MeasurePreserving.symm _ ?_
    rw [ih]
    exact MeasureTheory.measurePreserving_piFinSuccAbove (fun _ => ğ“’.Î¼Prod T) 0

/-!

## Integrability of energy

-/

@[fun_prop]
lemma integrable_energy_add (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)]
    [IsFiniteMeasure (ğ“’1.Î¼Bolt T)]
    (h : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) (h1 : Integrable ğ“’1.energy (ğ“’1.Î¼Prod T)) :
    Integrable (ğ“’ + ğ“’1).energy ((ğ“’ + ğ“’1).Î¼Prod T) := by
  rw [Î¼Prod_add]
  refine Integrable.add'' ?_ ?_
  Â· have h1 : (fun (i : Î¹ Ã— Î¹1) => ğ“’.energy i.1)
      = fun (i : Î¹ Ã— Î¹1) => ğ“’.energy i.1 * (fun (i : Î¹1) => 1) i.2 := by
      funext i
      simp
    rw [h1]
    apply Integrable.mul_prod (f := ğ“’.energy) (g := (fun (i : Î¹1) => 1))
    Â· fun_prop
    Â· fun_prop
  Â· have h1 : (fun (i : Î¹ Ã— Î¹1) => ğ“’1.energy i.2)
      = fun (i : Î¹ Ã— Î¹1) => (fun (i : Î¹) => 1) i.1 * ğ“’1.energy i.2 := by
      funext i
      simp
    rw [h1]
    apply Integrable.mul_prod (f := (fun (i : Î¹) => 1)) (g := ğ“’1.energy)
    Â· fun_prop
    Â· fun_prop

@[fun_prop]
lemma integrable_energy_congr (T : Temperature) (e : Î¹1 â‰ƒáµ Î¹)
    (h : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) :
    Integrable (ğ“’.congr e).energy ((ğ“’.congr e).Î¼Prod T) := by
  simp [Î¼Prod_congr]
  refine (integrable_map_equiv e.symm (ğ“’.congr e).energy).mpr ?_
  simp only [congr_energy_comp_symmm]
  exact h

@[fun_prop]
lemma integrable_energy_nsmul (n : â„•) (T : Temperature)
    [IsFiniteMeasure (ğ“’.Î¼Bolt T)]
    (h : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) :
    Integrable (nsmul n ğ“’).energy ((nsmul n ğ“’).Î¼Prod T) := by
  induction n with
  | zero =>
    simp [nsmul, Î¼Prod_nsmul]
  | succ n ih =>
    rw [nsmul_succ]
    apply integrable_energy_congr
    apply integrable_energy_add
    Â· exact h
    Â· exact ih

/-!

## The mean energy

-/

/-- The mean energy of the canonical ensemble at temperature `T`. -/
noncomputable def meanEnergy (T : Temperature) : â„ := âˆ« i, ğ“’.energy i âˆ‚ğ“’.Î¼Prod T

lemma meanEnergy_add {T : Temperature}
    [IsFiniteMeasure (ğ“’1.Î¼Bolt T)] [IsFiniteMeasure (ğ“’.Î¼Bolt T)]
    [NeZero ğ“’.Î¼] [NeZero ğ“’1.Î¼]
    (h1 : Integrable ğ“’.energy (ğ“’.Î¼Prod T))
    (h2 : Integrable ğ“’1.energy (ğ“’1.Î¼Prod T)) :
    (ğ“’ + ğ“’1).meanEnergy T = ğ“’.meanEnergy T + ğ“’1.meanEnergy T := by
  rw [meanEnergy]
  simp only [energy_add_apply]
  rw [Î¼Prod_add]
  rw [MeasureTheory.integral_prod]
  simp only
  conv_lhs =>
    enter [2, x]
    rw [integral_add (integrable_const _) h2]
    rw [integral_const]
    simp
  rw [integral_add h1 (integrable_const _)]
  rw [integral_const]
  simp
  rfl
  Â· simpa [Î¼Prod_add] using integrable_energy_add ğ“’ ğ“’1 T h1 h2

lemma meanEnergy_congr (e : Î¹1 â‰ƒáµ Î¹) (T : Temperature) :
    (ğ“’.congr e).meanEnergy T = ğ“’.meanEnergy T := by
  simp [meanEnergy, Î¼Prod_congr]
  refine MeasurePreserving.integral_comp' ?_ ğ“’.energy
  refine { measurable := ?_, map_eq := ?_ }
  Â· exact MeasurableEquiv.measurable e
  Â· exact MeasurableEquiv.map_map_symm e

lemma meanEnergy_nsmul (n : â„•) (T : Temperature)
    [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼]
    (h1 : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) :
    (nsmul n ğ“’).meanEnergy T = n * ğ“’.meanEnergy T := by
  induction n with
  | zero =>
    simp [nsmul, meanEnergy, Î¼Prod_nsmul]
  | succ n ih =>
    rw [nsmul_succ, meanEnergy_congr, meanEnergy_add, ih]
    simp only [Nat.cast_add, Nat.cast_one]
    ring
    Â· exact h1
    Â· exact integrable_energy_nsmul ğ“’ n T h1

/-!

## The differential entropy

-/

/-- The (differential) entropy of the canonical ensemble. In the continuous case, this quantity
is not absolute but depends on the choice of units for the measure. It can be negative.
See `thermodynamicEntropy` for the absolute physical quantity. -/
noncomputable def differentialEntropy (T : Temperature) : â„ :=
  - kB * âˆ« i, log (probability ğ“’ T i) âˆ‚ğ“’.Î¼Prod T

/-- Probabilities are non-negative,
assuming a positive partition function. -/
lemma probability_nonneg
    (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼] (i : Î¹) :
    0 â‰¤ ğ“’.probability T i := by
  -- Use positivity of the (mathematical) partition function (already defined above)
  have hpos := mathematicalPartitionFunction_pos (ğ“’:=ğ“’) (T:=T)
  simp [CanonicalEnsemble.probability, div_nonneg, Real.exp_nonneg, hpos.le]

/-- Probabilities are strictly positive. -/
lemma probability_pos
    (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼] (i : Î¹) :
    0 < ğ“’.probability T i := by
  have hZpos := mathematicalPartitionFunction_pos (ğ“’:=ğ“’) (T:=T)
  simp [probability, div_pos, Real.exp_pos, hZpos]

/-- General entropy non-negativity under a pointwise upper bound `probability â‰¤ 1`.
This assumption holds automatically in the finite/counting case (since sums bound each term),
but can fail in general (continuous) settings; hence we separate it as a hypothesis.
Finite case: see `CanonicalEnsemble.entropy_nonneg` in `Finite`. -/
lemma differentialEntropy_nonneg_of_prob_le_one
    (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼]
    (hInt : Integrable (fun i => Real.log (ğ“’.probability T i)) (ğ“’.Î¼Prod T))
    (hP_le_one : âˆ€ i, ğ“’.probability T i â‰¤ 1) :
    0 â‰¤ ğ“’.differentialEntropy T := by
  have hPoint :
      (fun i => Real.log (ğ“’.probability T i)) â‰¤áµ[ğ“’.Î¼Prod T] fun _ => 0 := by
    refine Filter.Eventually.of_forall ?_
    intro i
    have hpos := probability_pos (ğ“’:=ğ“’) (T:=T) i
    have hle  := hP_le_one i
    have hle' : ğ“’.probability T i â‰¤ Real.exp 0 := by
      simpa [Real.exp_zero] using hle
    exact (log_le_iff_le_exp hpos).mpr hle'
  have hInt0 : Integrable (fun _ : Î¹ => (0 : â„)) (ğ“’.Î¼Prod T) := integrable_const _
  have hIntLe : (âˆ« i, Real.log (ğ“’.probability T i) âˆ‚ğ“’.Î¼Prod T)
      â‰¤ (âˆ« _i, (0 : â„) âˆ‚ğ“’.Î¼Prod T) :=
    integral_mono_ae hInt hInt0 hPoint
  have hent :
      ğ“’.differentialEntropy T
        = - kB * (âˆ« i, Real.log (ğ“’.probability T i) âˆ‚ğ“’.Î¼Prod T) := rfl
  have hkB : 0 â‰¤ kB := kB_nonneg
  have hIle0 : (âˆ« i, Real.log (ğ“’.probability T i) âˆ‚ğ“’.Î¼Prod T) â‰¤ 0 := by
    simpa [integral_const] using hIntLe
  have hProd :
      0 â‰¤ - kB * (âˆ« i, Real.log (ğ“’.probability T i) âˆ‚ğ“’.Î¼Prod T) :=
    mul_nonneg_of_nonpos_of_nonpos (neg_nonpos.mpr hkB) hIle0
  simpa [hent] using hProd

/-!

## Thermodynamic Quantities

These are the dimensionless physical quantities derived from the mathematical definitions
by incorporating the fundamental phase space volume `ğ“’.phase_space_unit ^ ğ“’.dof`.
-/

open Constants

/-- The dimensionless thermodynamic partition function, `Z = Z_math / h^dof`. -/
noncomputable def partitionFunction (T : Temperature) : â„ :=
  ğ“’.mathematicalPartitionFunction T / (ğ“’.phase_space_unit ^ ğ“’.dof)

/-- The Helmholtz free energy, `F = -k_B T log(Z)`. This is the central
quantity from which other thermodynamic properties are derived. -/
noncomputable def helmholtzFreeEnergy (T : Temperature) : â„ :=
  - kB * T.val * Real.log (ğ“’.partitionFunction T)

/-- The absolute thermodynamic entropy, defined via the fundamental relation `S = (U - F) / T`.

The hypothesis `hT : 0 < T.val` encodes the physically required strict positivity of the
temperature. It is intentionally retained (even though not needed for mere division by `T.val`)
so that subsequent lemmas may rely on it (e.g. for monotonicity, differentiability,
or asymptotic considerations near absolute zero). We mark it as an unused argument explicitly. -/
noncomputable def thermodynamicEntropy (T : Temperature) (hT : 0 < T.val) : â„ :=
  let _h : T.val â‰  0 := ne_of_gt hT
  (ğ“’.meanEnergy T - ğ“’.helmholtzFreeEnergy T) / T.val

/-!

## Relations between Mathematical and Thermodynamic Quantities

-/

/-- An intermediate potential defined from the mathematical partition function. See
`helmholtzFreeEnergy` for the physical thermodynamic quantity. -/
noncomputable def helmholtzPotential (T : Temperature) : â„ :=
  - kB * T.val * Real.log (ğ“’.mathematicalPartitionFunction T)

/-- The relationship between the physical Helmholtz Free Energy and the Helmholtz Potential. -/
lemma helmholtzFreeEnergy_eq_helmholtzPotential_add_correction (T : Temperature)
    [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼] :
    ğ“’.helmholtzFreeEnergy T = ğ“’.helmholtzPotential T +
      kB * T.val * ğ“’.dof * Real.log (ğ“’.phase_space_unit) := by
  have hZ_pos := mathematicalPartitionFunction_pos ğ“’ T
  have h_pow_pos : 0 < ğ“’.phase_space_unit ^ ğ“’.dof := pow_pos ğ“’.h_pos _
  simp_rw [helmholtzFreeEnergy, helmholtzPotential, partitionFunction,
    Real.log_div hZ_pos.ne' h_pow_pos.ne']
  have h_log_pow : Real.log (ğ“’.phase_space_unit ^ ğ“’.dof)
      = (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit := by
    simp
  simp [sub_eq_add_neg, h_log_pow, mul_add, add_comm, add_left_comm, add_assoc,
        mul_comm, mul_left_comm, mul_assoc]

/-- General identity: S_diff = kB Î² âŸ¨EâŸ© + kB log Z_math.
This connects the differential entropy to the mean energy and the mathematical partition function.
Integrability of `log (probability â€¦)` follows from the pointwise formula. -/
lemma differentialEntropy_eq_kB_beta_meanEnergy_add_kB_log_mathZ
    (T : Temperature) [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼]
    (hE : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) :
    ğ“’.differentialEntropy T = kB * (T.Î² : â„) * ğ“’.meanEnergy T +
      kB * Real.log (ğ“’.mathematicalPartitionFunction T) := by
  set Z := ğ“’.mathematicalPartitionFunction T
  have hZpos := mathematicalPartitionFunction_pos ğ“’ T
  have h_log_prob : âˆ€ i, Real.log (ğ“’.probability T i) =
      - (T.Î² : â„) * ğ“’.energy i - Real.log Z := by
    intro i
    have : 0 < Z := hZpos
    rw [probability, Real.log_div (exp_pos _).ne' this.ne', Real.log_exp]
  have h_int_log :
      Integrable (fun i => Real.log (ğ“’.probability T i)) (ğ“’.Î¼Prod T) := by
    simp_rw [h_log_prob]
    exact (hE.const_mul _).add (integrable_const _)
  unfold differentialEntropy
  rw [integral_congr_ae (ae_of_all _ h_log_prob)]
  have h_split :
      (fun i => - (T.Î² : â„) * ğ“’.energy i - Real.log Z)
        = (fun i => (- (T.Î² : â„)) * ğ“’.energy i + (- Real.log Z)) := by
    funext i; ring
  simp_rw [h_split]
  have h_int1 : Integrable (fun _ : Î¹ => - Real.log Z) (ğ“’.Î¼Prod T) :=
    (integrable_const _)
  have h_intE : Integrable (fun i => (- (T.Î² : â„)) * ğ“’.energy i) (ğ“’.Î¼Prod T) :=
    (hE.const_mul _)
  rw [integral_add h_intE h_int1,
      integral_const_mul, meanEnergy, integral_const]
  have hÎ¼ : (ğ“’.Î¼Prod T) Set.univ = 1 := measure_univ
  simp [hÎ¼, mul_add, add_comm, add_left_comm, add_assoc, sub_eq_add_neg,
        mul_comm, mul_left_comm, mul_assoc, differentialEntropy, probability,
       mul_comm, mul_left_comm, mul_assoc, h_int_log]

/--  Auxiliary identity: `kB Â· Î² = 1 / T`.
`Î²` is defined as `1 / (kB Â· T)` (see `Temperature.Î²`).
The proof just clears denominators with `field_simp`. -/
@[simp]
private lemma kB_mul_beta (T : Temperature) (hT : 0 < T.val) :
    (kB : â„) * (T.Î² : â„) = 1 / T.val := by
  have hkB : (kB : â„) â‰  0 := kB_neq_zero
  have hT0 : (T.val : â„) â‰  0 := by
    exact_mod_cast (ne_of_gt hT)
  field_simp [Temperature.Î², hkB, hT0]
  rw [mul_div_mul_left (â†‘T.val) T.toReal hkB]
  erw [propext (div_eq_one_iff_eq hT0)]

/-- Fundamental relation between thermodynamic and differential entropy:
S_thermo = S_diff - kB * dof * log h. -/
theorem thermodynamicEntropy_eq_differentialEntropy_sub_correction
    (T  : Temperature) (hT : 0 < T.val)
    [IsFiniteMeasure (ğ“’.Î¼Bolt T)] [NeZero ğ“’.Î¼]
    (hE : Integrable ğ“’.energy (ğ“’.Î¼Prod T)) :
    ğ“’.thermodynamicEntropy T hT
      = ğ“’.differentialEntropy T
        - kB * ğ“’.dof * Real.log ğ“’.phase_space_unit := by
  set U : â„ := ğ“’.meanEnergy T
  have h_kBÎ² : (kB : â„) * (T.Î² : â„) = 1 / T.val := by
    unfold Temperature.Î²
    change kB * (âŸ¨1 / (kB * (T : â„)), _âŸ© : â„â‰¥0) = 1 / T.val
    simp [Temperature.toReal, one_div, mul_comm, mul_left_comm, mul_assoc,
      kB_neq_zero, (ne_of_gt hT)]
  have hDiff :=
    ğ“’.differentialEntropy_eq_kB_beta_meanEnergy_add_kB_log_mathZ T hE
  have hS :
      ğ“’.differentialEntropy T
        = U / T.val +
            kB * Real.log (ğ“’.mathematicalPartitionFunction T) := by
    rw [hDiff]
    simp [U, h_kBÎ², div_eq_mul_inv, mul_comm, mul_left_comm, mul_assoc]
  have h_log_partition :
      Real.log (ğ“’.partitionFunction T)
        = Real.log (ğ“’.mathematicalPartitionFunction T)
            - (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit := by
    have hZ_pos := mathematicalPartitionFunction_pos ğ“’ T
    have h_pow_pos : 0 < ğ“’.phase_space_unit ^ ğ“’.dof := pow_pos ğ“’.h_pos _
    have h_log_pow :
        Real.log (ğ“’.phase_space_unit ^ ğ“’.dof)
          = (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit := by
      have hne : (ğ“’.phase_space_unit : â„) â‰  0 := ne_of_gt ğ“’.h_pos
      simp
    simp [partitionFunction,
      Real.log_div hZ_pos.ne' h_pow_pos.ne', h_log_pow,
      sub_eq_add_neg, mul_comm, mul_left_comm, mul_assoc]
  set A :=
    Real.log (ğ“’.mathematicalPartitionFunction T)
      - (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit
  have hA : Real.log (ğ“’.partitionFunction T) = A := by
    simpa [A] using h_log_partition
  have hF :
      ğ“’.helmholtzFreeEnergy T = - kB * T.val * A := by
    simp [helmholtzFreeEnergy, hA, A]
  have hLHS :
      ğ“’.thermodynamicEntropy T hT
        = U / T.val + kB * A := by
    unfold thermodynamicEntropy
    have hval0 : (T.val : â„) â‰  0 := by exact_mod_cast (ne_of_gt hT)
    simp [hF, U, sub_eq_add_neg, div_eq_mul_inv, mul_comm, mul_left_comm, mul_assoc,
      add_comm, add_left_comm, add_assoc, mul_add, add_mul, hval0]
  have hRHS :
      U / T.val + kB * A
        = ğ“’.differentialEntropy T
            - kB * ğ“’.dof * Real.log ğ“’.phase_space_unit := by
    simp_rw [hS, A]
    have hDistrib :
        kB * (Real.log (ğ“’.mathematicalPartitionFunction T)
              - (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit)
          =
          kB * Real.log (ğ“’.mathematicalPartitionFunction T)
            - kB * (ğ“’.dof : â„) * Real.log ğ“’.phase_space_unit := by
      ring
    simp [hDistrib, sub_eq_add_neg, add_comm, add_left_comm, add_assoc,
      mul_comm, mul_left_comm, mul_assoc, U]
    ring
  exact hLHS.trans hRHS

end CanonicalEnsemble
