/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Analysis.Calculus.LogDeriv
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.SpecialFunctions.Log.Basic
import PhysLean.StatisticalMechanics.Temperature
import PhysLean.Meta.Informal.SemiFormal
/-!

# Canonical ensemble

A canonical ensemble describes a system in thermal equilibrium with a heat bath at a
fixed temperature.

In this file we define the canonical ensemble, its partition function, the
probability of being in a given microstate, the mean energy, the entropy and
the Helmholtz free energy.

We also define the addition of two canonical ensembles, and prove results related
to the properties of additions of canonical ensembles.

##Â References

- https://www.damtp.cam.ac.uk/user/tong/statphys/statmechhtml/S1.html#E23

-/

/-- A Canonical ensemble is described by a type `Î¹`, corresponding to the type of microstates,
  and a map `Î¹ â†’ â„` which associates which each microstate an energy. -/
def CanonicalEnsemble (Î¹ : Type) : Type := Î¹ â†’ â„

namespace CanonicalEnsemble
open Real Temperature

variable {Î¹ Î¹1 : Type} (ğ“’ : CanonicalEnsemble Î¹) (ğ“’1 : CanonicalEnsemble Î¹1)

/-- The addition of two `CanonicalEnsemble`. -/
instance {Î¹1 Î¹2 : Type} : HAdd (CanonicalEnsemble Î¹1) (CanonicalEnsemble Î¹2)
    (CanonicalEnsemble (Î¹1 Ã— Î¹2)) where
  hAdd := fun ğ“’1 ğ“’2 => fun (i : Î¹1 Ã— Î¹2) => ğ“’1 i.1 + ğ“’2 i.2

/-- Scalar multiplication of `CanonicalEnsemble`, defined such that
  `nsmul n ğ“’` is `n` coppies of the canonical ensemble `ğ“’`. -/
def nsmul (n : â„•) (ğ“’1 : CanonicalEnsemble Î¹) : CanonicalEnsemble (Fin n â†’ Î¹) :=
  fun f => âˆ‘ i, ğ“’1 (f i)

set_option linter.unusedVariables false in
/-- The microstates of a the canonical ensemble -/
@[nolint unusedArguments]
abbrev microstates {Î¹ : Type} (ğ“’ : CanonicalEnsemble Î¹) : Type := Î¹

/-!

## The energy of the microstates

-/
/-- The energy of associated with a mircrostate of the canonical ensemble. -/
abbrev energy (ğ“’ : CanonicalEnsemble Î¹) : microstates ğ“’ â†’ â„ := ğ“’

@[simp]
lemma energy_add_apply (i : microstates (ğ“’ + ğ“’1)) :
    (ğ“’ + ğ“’1).energy i = ğ“’.energy i.1 + ğ“’1.energy i.2 := by
  simp [energy]
  rfl

@[simp]
lemma energy_nsmul_apply (n : â„•) (f : Fin n â†’ microstates ğ“’) :
    (nsmul n ğ“’).energy f = âˆ‘ i, ğ“’.energy (f i) := by
  simp [energy, nsmul]

/-!

## The partition function of the canonical ensemble

-/

/-- The partition function of the canonical ensemble. -/
noncomputable def partitionFunction [Fintype Î¹] (T : Temperature) : â„ :=
  âˆ‘ i, exp (- Î² (T) * ğ“’.energy i)

lemma partitionFunction_add [Fintype Î¹] [Fintype Î¹1] :
    (ğ“’ + ğ“’1).partitionFunction T = ğ“’.partitionFunction T * ğ“’1.partitionFunction T := by
  simp [partitionFunction]
  rw [Fintype.sum_prod_type]
  rw [Finset.sum_mul]
  congr
  funext i
  rw [Finset.mul_sum]
  congr
  funext j
  rw [â† Real.exp_add]
  congr
  simp [energy]
  ring

/-- The partition function of `n` copies of a canonical ensemble. -/
semiformal_result "ERA5D" partitionFunction_nsmul [Fintype Î¹] (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).partitionFunction T = (ğ“’.partitionFunction T) ^ n

lemma partitionFunction_pos [Fintype Î¹] [Nonempty Î¹] (T : Temperature) :
    0 < partitionFunction ğ“’ T := by
  rw [partitionFunction]
  apply Finset.sum_pos
  Â· intro i hi
    exact exp_pos (-T.Î² * ğ“’.energy i)
  Â· simp

@[simp]
lemma partitionFunction_neq_zero [Fintype Î¹] [Nonempty Î¹] (T : Temperature) :
    partitionFunction ğ“’ T â‰  0:= by
  have h1 := partitionFunction_pos ğ“’ T
  exact Ne.symm (ne_of_lt h1)

/-- The partition function of the canonical ensemble as a function of `Î²` -/
noncomputable def partitionFunctionÎ² [Fintype Î¹] (Î² : â„) : â„ :=
  âˆ‘ i, exp (- Î² * ğ“’.energy i)

lemma partitionFunctionÎ²_def [Fintype Î¹]:
    partitionFunctionÎ² ğ“’ = fun Î² => âˆ‘ i, exp (- Î² * ğ“’.energy i) := by rfl

@[fun_prop]
lemma partitionFunctionÎ²_differentiable [Fintype Î¹] :
    Differentiable â„ ğ“’.partitionFunctionÎ² := by
  rw [partitionFunctionÎ²_def]
  fun_prop

lemma partitionFunction_eq_partitionFunctionÎ² [Fintype Î¹] (T : Temperature) :
    partitionFunction ğ“’ T = partitionFunctionÎ² ğ“’ (Î² T) := by
  simp [partitionFunction, partitionFunctionÎ², Î²]

/-!

## The probability of being in a given microstate
-/

/-- The probability of been in a given microstate. -/
noncomputable def probability [Fintype Î¹] (i : microstates ğ“’) (T : Temperature) : â„ :=
  exp (- Î² (T) * ğ“’.energy i) / partitionFunction ğ“’ T

/-- Probability of a microstate in a canonical ensemble is less then or equal to `1`. -/
semiformal_result "ERDAR" probability_le_one [Fintype Î¹] (i : microstates ğ“’) (T : Temperature) :
    ğ“’.probability i T â‰¤ 1

/-- Probability of a microstate in a canonical ensemble is non-negative. -/
semiformal_result "ERBG6" probability_nonneg [Fintype Î¹] (i : microstates ğ“’) (T : Temperature) :
    0 â‰¤ ğ“’.probability i T

lemma probability_neq_zero [Fintype Î¹] [Nonempty Î¹] (i : microstates ğ“’) (T : Temperature) :
    probability ğ“’ i T â‰  0 := by
  rw [probability]
  field_simp

@[simp]
lemma probability_add [Fintype Î¹] [Fintype Î¹1]
    (i : microstates (ğ“’ + ğ“’1)) (T : Temperature) :
    (ğ“’ + ğ“’1).probability i T = ğ“’.probability i.1 T * ğ“’1.probability i.2 T := by
  simp [probability]
  rw [partitionFunction_add ğ“’ ğ“’1]
  field_simp
  congr
  rw [â† Real.exp_add]
  ring_nf

/-- The probability of a microstate in `n` copies of a canonical ensemble is
  equal to the product of the probability of the corresponding individual microstates. -/
semiformal_result "ERDAE" probability_nsmul [Fintype Î¹] (n : â„•)
    (f : microstates (nsmul n ğ“’)) (T : Temperature) :
    (nsmul n ğ“’).probability f T = âˆ i, ğ“’.probability (f i) T

@[simp]
lemma sum_probability_eq_one [Fintype Î¹] [Nonempty Î¹] (T : Temperature) :
    âˆ‘ i, probability ğ“’ i T = 1 := by
  simp [probability]
  rw [â† Finset.sum_div]
  field_simp
  rw [partitionFunction]
  ring_nf

/-!

## The mean energy of the canonical ensemble

-/

/-- The mean energy of the canonical ensemble. -/
noncomputable def meanEnergy [Fintype Î¹] (T : Temperature) : â„ :=
  âˆ‘ i, ğ“’.energy i * probability ğ“’ i T

@[simp]
lemma meanEnergy_add [Fintype Î¹] [Nonempty Î¹] (ğ“’1 : CanonicalEnsemble Î¹1) [Fintype Î¹1]
    [Nonempty Î¹1]
    (T : Temperature) :
    (ğ“’ + ğ“’1).meanEnergy T = ğ“’.meanEnergy T + ğ“’1.meanEnergy T := by
  simp [meanEnergy]
  conv_lhs =>
    enter [2, x]
    rw [add_mul]
  rw [Finset.sum_add_distrib]
  congr 1
  Â· rw [Fintype.sum_prod_type]
    simp only
    congr
    funext i
    rw [â† Finset.mul_sum, â† Finset.mul_sum]
    simp
  Â· rw [Fintype.sum_prod_type]
    rw [Finset.sum_comm]
    simp only
    congr
    funext i
    rw [â† Finset.mul_sum, â† Finset.sum_mul]
    simp

/-- The mean energy of `n` copies of a canonical ensemble is equal
  to `n` times the mean energy of the canonical ensemble.

  Note, can't make this `SMul` since the target type depends on the
  value of `n`. -/
semiformal_result "ERBAH" meanEnergy_nsmul [Fintype Î¹] (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).meanEnergy T = n * ğ“’.meanEnergy T

lemma meanEnergy_eq_logDeriv_partitionFunctionÎ² [Fintype Î¹] (T : Temperature) :
    meanEnergy ğ“’ T = - logDeriv (partitionFunctionÎ² ğ“’) (Î² T) := by
  rw [logDeriv_apply]
  nth_rewrite 1 [partitionFunctionÎ²_def]
  rw [deriv_sum]
  Â· simp [meanEnergy]
    rw [@neg_div]
    simp only [neg_neg]
    rw [Finset.sum_div]
    congr
    funext i
    simp [probability]
    rw [partitionFunction_eq_partitionFunctionÎ² ğ“’ T]
    ring
  Â· intro i
    fun_prop

open Constants

/-!

## Entropy

-/

/-- The entropy of the canonical ensemble. -/
noncomputable def entropy [Fintype Î¹] (T : Temperature) : â„ :=
  - kB * âˆ‘ i, probability ğ“’ i T * log (probability ğ“’ i T)

/-- Entropy is addative on adding canonical ensembles. -/
@[simp]
lemma entropy_add [Fintype Î¹] [Nonempty Î¹] (ğ“’1 : CanonicalEnsemble Î¹1) [Fintype Î¹1]
    [Nonempty Î¹1] (T : Temperature) :
    (ğ“’ + ğ“’1).entropy T = ğ“’.entropy T + ğ“’1.entropy T := by
  simp [entropy]
  conv_lhs =>
    enter [1, 2, 2, x]
    rw [log_mul (probability_neq_zero ğ“’ x.1 T) (probability_neq_zero ğ“’1 x.2 T)]
    rw [mul_add]
  rw [Finset.sum_add_distrib, mul_add, neg_add]
  congr 1
  Â· simp
    left
    rw [Fintype.sum_prod_type]
    simp only
    congr
    funext i
    rw [â† Finset.sum_mul, â† Finset.mul_sum]
    simp
  Â· rw [Fintype.sum_prod_type]
    rw [Finset.sum_comm]
    simp only [neg_inj, mul_eq_mul_left_iff, NNReal.coe_eq_zero]
    left
    congr
    funext i
    rw [â† Finset.sum_mul, â† Finset.sum_mul]
    simp

/-- The entropy of `n` copies of a canonical ensemble is equal
  to `n` times the entropy of the canonical ensemble. -/
semiformal_result "ERBCV" entropy_nsmul [Fintype Î¹] (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).entropy T = n * ğ“’.entropy T

/-!

## Helmholtz free energy

-/

/-- The (Helmholtz) free energy of the canonical ensemble. -/
noncomputable def helmholtzFreeEnergy [Fintype Î¹] (T : Temperature) : â„ :=
  ğ“’.meanEnergy T - T * ğ“’.entropy T

/-- The Helmholtz free energy is addative. -/
@[simp]
lemma helmholtzFreeEnergy_add [Fintype Î¹] [Nonempty Î¹]
    [Fintype Î¹1] [Nonempty Î¹1] (T : Temperature) :
    (ğ“’ + ğ“’1).helmholtzFreeEnergy T = ğ“’.helmholtzFreeEnergy T + ğ“’1.helmholtzFreeEnergy T := by
  simp [helmholtzFreeEnergy]
  ring

/-- The free energy of `n` copies of a canonical ensemble is equal
  to `n` times the entropy of the canonical ensemble. -/
semiformal_result "ERC72" helmholtzFreeEnergy_nsmul [Fintype Î¹] (n : â„•) (T : Temperature) :
    (nsmul n ğ“’).helmholtzFreeEnergy T = n * ğ“’.helmholtzFreeEnergy T

end CanonicalEnsemble
