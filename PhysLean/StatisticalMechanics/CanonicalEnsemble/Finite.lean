/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina, Joseph Tooby-Smith
-/
import PhysLean.StatisticalMechanics.CanonicalEnsemble.Basic
/-!

# Finite canonical ensemble

A canonical ensemble describes a system in thermal equilibrium with a heat bath at a
fixed temperature.

In this file we define the canonical ensemble, its partition function, the
probability of being in a given microstate, the mean energy, the entropy and
the Helmholtz free energy.

We also define the addition of two canonical ensembles, and prove results related
to the properties of additions of canonical ensembles.

##Â References

- https://www.damtp.cam.ac.uk/user/tong/statphys/statmechhtml/S1.html#E23

## Implementation note

This file only deals with finite canonical ensembles.
When the more general theory of canonical ensembles is implemented,
this file should be modified.

-/

namespace CanonicalEnsemble

open Real Temperature MeasureTheory

variable {Î¹ : Type} [Fintype Î¹] [MeasurableSpace Î¹]
  [MeasurableSingletonClass Î¹] (ğ“’ : CanonicalEnsemble Î¹)

variable {Î¹1 : Type} [Fintype Î¹1] [MeasurableSpace Î¹1]
  [MeasurableSingletonClass Î¹1] (ğ“’1 : CanonicalEnsemble Î¹1)

/-- A finite `CanonicalEnsemble` is one which has a finite
  type of microstates and the counting measure on them. -/
class IsFinite [Fintype Î¹] : Prop where
  Î¼_eq_count : ğ“’.Î¼ = Measure.count

instance [IsFinite ğ“’] [IsFinite ğ“’1] : IsFinite (ğ“’ + ğ“’1) where
  Î¼_eq_count := by
    rw [Î¼_add]
    rw [IsFinite.Î¼_eq_count, IsFinite.Î¼_eq_count]
    refine Measure.prod_eq ?_
    intro s t hs ht
    rw [Measure.count_apply, Measure.count_apply, Measure.count_apply]
    rw [â† ENat.toENNReal_mul]
    congr
    simp [Set.encard, ENat.card_congr (Equiv.Set.prod ..)]
    Â· exact ht
    Â· exact hs
    Â· exact MeasurableSet.prod hs ht

instance [IsFinite ğ“’] (e : Î¹1 â‰ƒáµ Î¹) : IsFinite (congr ğ“’ e) where
  Î¼_eq_count := by
    simp [congr]
    rw [IsFinite.Î¼_eq_count]
    refine Measure.ext_iff.mpr ?_
    intro s hs
    rw [@MeasurableEquiv.map_apply]
    rw [Measure.count_apply, Measure.count_apply]
    simp

    rw [@MeasurableEquiv.preimage_symm]
    rw [â† Set.Finite.cast_ncard_eq, â† Set.Finite.cast_ncard_eq,]
    congr 1
    change (e.toEmbedding '' s).ncard = _
    rw [Set.ncard_map]
    Â· exact Set.toFinite s
    Â· exact Set.toFinite (â‡‘e '' s)
    Â· exact hs
    Â· exact (MeasurableEquiv.measurableSet_preimage e.symm).mpr hs

instance [IsFinite ğ“’] (n : â„•) : IsFinite (nsmul n ğ“’) where
  Î¼_eq_count := by
    induction n with
    | zero =>
      rw [@Measure.ext_iff_singleton]
      simp [nsmul]
      rw [â† Set.univ_pi_singleton]
      simp
    | succ n ih =>
      rw [nsmul_succ]
      haveI : IsFinite (nsmul n ğ“’) := âŸ¨ihâŸ©
      rw [IsFinite.Î¼_eq_count]
      exact Pi.instFintype

instance [IsFinite ğ“’] : IsFiniteMeasure (ğ“’.Î¼) := by
  rw [IsFinite.Î¼_eq_count]
  infer_instance

/-- In the finite (counting) case a nonempty index type gives a nonzero measure. -/
instance [IsFinite ğ“’] [Nonempty Î¹] : NeZero ğ“’.Î¼ := by
  classical
  refine âŸ¨?_âŸ©
  intro hÎ¼
  obtain âŸ¨iâ‚€âŸ© := (inferInstance : Nonempty Î¹)
  have hone : ğ“’.Î¼ {iâ‚€} = 1 := by
    simp [IsFinite.Î¼_eq_count (ğ“’:=ğ“’)]
  simp_all only [Measure.coe_zero, Pi.zero_apply, zero_ne_one]

/--
Entropy of the finite canonical ensemble (Shannon entropy). -/
noncomputable def entropy (T : Temperature) : â„ :=
  ğ“’.differentialEntropy T

omit [Fintype Î¹] [MeasurableSingletonClass Î¹] in
@[simp] lemma entropy_eq_differentialEntropy (T : Temperature) :
    ğ“’.entropy T = ğ“’.differentialEntropy T := rfl

lemma partitionFunction_of_fintype [IsFinite ğ“’] (T : Temperature) :
    ğ“’.partitionFunction T = âˆ‘ i, exp (- Î² T * ğ“’.energy i) := by
  rw [partitionFunction_eq_integral]
  rw [MeasureTheory.integral_fintype]
  simp [IsFinite.Î¼_eq_count]
  Â· rw [IsFinite.Î¼_eq_count]
    exact Integrable.of_finite

@[simp]
lemma Î¼Bolt_of_fintype (T : Temperature) [IsFinite ğ“’] (i : Î¹) :
    (ğ“’.Î¼Bolt T).real {i} = Real.exp (- Î² T * ğ“’.energy i) := by
  rw [Î¼Bolt]
  simp only [neg_mul]
  rw [@measureReal_def]
  simp [IsFinite.Î¼_eq_count]
  exact Real.exp_nonneg _

instance {T} [IsFinite ğ“’] : IsFiniteMeasure (ğ“’.Î¼Bolt T) := by
  rw [Î¼Bolt]
  refine isFiniteMeasure_withDensity_ofReal ?_
  exact HasFiniteIntegral.of_finite

@[simp]
lemma Î¼Prod_of_fintype (T : Temperature) [IsFinite ğ“’] (i : Î¹) :
    (ğ“’.Î¼Prod T).real {i} = ğ“’.probability T i := by
  rw [Î¼Prod]
  simp [probability]
  ring_nf
  rw [mul_comm]
  rfl

lemma meanEnergy_of_fintype [IsFinite ğ“’] (T : Temperature) :
    ğ“’.meanEnergy T = âˆ‘ i, ğ“’.energy i * ğ“’.probability T i := by
  simp [meanEnergy]
  rw [MeasureTheory.integral_fintype]
  simp [mul_comm]
  exact Integrable.of_finite

open Constants

lemma entropy_of_fintype [IsFinite ğ“’] (T : Temperature) :
    ğ“’.entropy T = - kB * âˆ‘ i, ğ“’.probability T i * log (ğ“’.probability T i) := by
  simp [entropy, differentialEntropy]
  rw [MeasureTheory.integral_fintype]
  simp only [Î¼Prod_of_fintype, smul_eq_mul, true_or]
  exact Integrable.of_finite

lemma probability_le_one [IsFinite ğ“’] [Nonempty Î¹] (T : Temperature) (i : Î¹) :
    ğ“’.probability T i â‰¤ 1 := by
  classical
  unfold probability
  have hnum_le : Real.exp (- Î² T * ğ“’.energy i) â‰¤ ğ“’.partitionFunction T := by
    rw [partitionFunction_of_fintype (ğ“’:=ğ“’) T]
    simpa using
      (Finset.single_le_sum
        (s := Finset.univ)
        (f := fun j : Î¹ => Real.exp (- Î² T * ğ“’.energy j))
        (by intro _ _; exact Real.exp_nonneg _)
        (Finset.mem_univ i))
  have hZpos :
      0 < ğ“’.partitionFunction T :=
    partitionFunction_pos (ğ“’:=ğ“’) (T:=T)
  have := (div_le_div_iff_of_pos_right hZpos).mpr hnum_le
  simpa [probability, div_self hZpos.ne'] using this

/-- Finite specialization of the general strict positivity of the partition function. -/
lemma partitionFunction_pos_finite [IsFinite ğ“’] [Nonempty Î¹] (T : Temperature) :
    0 < ğ“’.partitionFunction T :=
  partitionFunction_pos (ğ“’:=ğ“’) (T:=T)

/-- Finite specialization of probability nonnegativity. -/
lemma probability_nonneg_finite [IsFinite ğ“’] [Nonempty Î¹] (T : Temperature) (i : Î¹) :
    0 â‰¤ ğ“’.probability T i :=
  probability_nonneg (ğ“’:=ğ“’) (T:=T) i

/-- The sum of probabilities over all microstates is 1. -/
lemma sum_probability_eq_one [IsFinite ğ“’] [Nonempty Î¹] (T : Temperature) :
    âˆ‘ i, ğ“’.probability T i = 1 := by
  classical
  simp_rw [probability]
  rw [â† Finset.sum_div, partitionFunction_of_fintype (ğ“’:=ğ“’) T]
  have hZpos : 0 < ğ“’.partitionFunction T := partitionFunction_pos (ğ“’:=ğ“’) (T:=T)
  have hne : (âˆ‘ i, Real.exp (- Î² T * ğ“’.energy i)) â‰  0 := by
    simpa [partitionFunction_of_fintype (ğ“’:=ğ“’) T] using hZpos.ne'
  exact div_self hne

/-- The entropy of a finite canonical ensemble is non-negative (Shannon entropy). -/
lemma entropy_nonneg [IsFinite ğ“’] [Nonempty Î¹] (T : Temperature) :
    0 â‰¤ ğ“’.entropy T := by
  have hInt :
      Integrable (fun i => Real.log (ğ“’.probability T i)) (ğ“’.Î¼Prod T) := by
    classical
    simp only [probability, neg_mul, Integrable.of_finite]
  refine differentialEntropy_nonneg_of_prob_le_one (ğ“’:=ğ“’) (T:=T) hInt
    (probability_le_one (ğ“’:=ğ“’) (T:=T))

end CanonicalEnsemble
