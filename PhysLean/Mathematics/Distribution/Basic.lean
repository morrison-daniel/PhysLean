/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import Mathlib.Analysis.Distribution.FourierSchwartz

/-!
# Distributions

This file defines distributions `E â†’d[ğ•œ] F`, which is a way to generalise functions `E â†’ F`.
Mathematically, a distribution `u : E â†’d[ğ•œ] F` takes in a test function `Î· : E â†’ ğ•œ` that is smooth
with rapidly decreasing iterated derivatives, and outputs a value in `F`. This operation is required
to be linear and continuous. Note that the space of test functions is called the Schwartz space and
is denoted `ğ“¢(E, ğ•œ)`.

`E` is required to be a normed vector space over `â„`, and `F` can be a normed vector space over `â„`
or `â„‚` (which is the field denoted `ğ•œ`).

## Important Results
- `Distribution.derivative` and `Distribution.fourierTransform` allow us to make sense of these
  operations that might not make sense a priori on general functions.

## Examples
- `Distribution.diracDelta`: Dirac delta distribution at a point `a : E` is a distribution
  that takes in a test function `Î· : ğ“¢(E, ğ•œ)` and outputs `Î· a`.

## TODO
- In the future, any function of polynomial growth can be interpreted as a distribution. This will
  be helpful for defining the distributions that correspond to `H` (Heaviside step function), or
  `cos(x)`.
- Generalise `derivative` to higher dimensional domain.

-/

open SchwartzMap NNReal
noncomputable section

/-- An `F`-valued distribution on `E` (where `E` is a normed vector space over `â„` and `F` is a
normed vector space over `ğ•œ`) is a continuous linear map `ğ“¢(E, ğ•œ) â†’L[ğ•œ] F` where `ğ’®(E, ğ•œ)` is
the Schwartz space of smooth functions `E â†’ ğ•œ` with rapidly decreasing iterated derivatives. This
is notated as `E â†’d[ğ•œ] F`.

This should be seen as a generalisation of functions `E â†’ F`. -/
abbrev Distribution (ğ•œ E F : Type) [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
    [NormedSpace â„ E] [NormedSpace ğ•œ F] : Type :=
  ğ“¢(E, ğ•œ) â†’L[ğ•œ] F

@[inherit_doc] notation:25 E:arg "â†’d[" ğ•œ:25 "] " F:0 => Distribution ğ•œ E F

variable (ğ•œ : Type) {E F : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]

namespace Distribution

section NormedSpace

variable [NormedSpace â„ E] [NormedSpace ğ•œ F]

/-- The construction of a distribution from the following data:
1. We take a finite set `s` of pairs `(k, n) âˆˆ â„• Ã— â„•` that will be explained later.
2. We take a linear map `u` that evaluates the given Schwartz function `Î·`. At this stage we don't
  need `u` to be continuous.
3. Recall that a Schwartz function `Î·` satisfies a bound
  `â€–xâ€–áµ * â€–(dâ¿/dxâ¿) Î·â€– < Mâ‚™â‚–` where `Mâ‚™â‚– : â„` only depends on `(k, n) : â„• Ã— â„•`.
4. This step is where `s` is used: for each test function `Î·`, the norm `â€–u Î·â€–` is required to be
  bounded by `C * (â€–xâ€–áµ * â€–(dâ¿/dxâ¿) Î·â€–)` for some `x : â„` and for some `(k, n) âˆˆ s`, where
  `C â‰¥ 0` is a global scalar.
-/
def ofLinear (s : Finset (â„• Ã— â„•)) (u : ğ“¢(E, ğ•œ) â†’â‚—[ğ•œ] F)
    (hu : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(E, ğ•œ), âˆƒ (k : â„•) (n : â„•) (x : E), (k, n) âˆˆ s âˆ§
      â€–u Î·â€– â‰¤ C * (â€–xâ€– ^ k * â€–iteratedFDeriv â„ n Î· xâ€–)) : E â†’d[ğ•œ] F :=
  mkCLMtoNormedSpace u (by simp) (by simp) <| by
    obtain âŸ¨C, hC, huâŸ© := hu
    refine âŸ¨s, C, hC, fun Î· â†¦ ?_âŸ©
    obtain âŸ¨k, n, x, hkn, hÎ·âŸ© := hu Î·
    have hs : s.Nonempty := âŸ¨(k, n), hknâŸ©
    refine hÎ·.trans <| mul_le_mul_of_nonneg_left ((le_seminorm ğ•œ k n Î· x).trans ?_) hC
    rw [Seminorm.finset_sup_apply]
    refine (NNReal.coe_le_coe (râ‚ := âŸ¨SchwartzMap.seminorm ğ•œ k n Î·, apply_nonneg _ _âŸ©)).2 ?_
    convert s.le_sup hkn
      (f := fun kn : â„• Ã— â„• â†¦ (âŸ¨SchwartzMap.seminorm ğ•œ kn.1 kn.2 Î·, apply_nonneg _ _âŸ© : â„â‰¥0))

@[simp] lemma ofLinear_apply (s : Finset (â„• Ã— â„•)) (u : ğ“¢(E, ğ•œ) â†’â‚—[ğ•œ] F)
    (hu : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(E, ğ•œ), âˆƒ (k : â„•) (n : â„•) (x : E), (k, n) âˆˆ s âˆ§
      â€–u Î·â€– â‰¤ C * (â€–xâ€– ^ k * â€–iteratedFDeriv â„ n Î· xâ€–))
    (Î· : ğ“¢(E, ğ•œ)) :
    ofLinear ğ•œ s u hu Î· = u Î· :=
  rfl

/-- Dirac delta distribution `diracDelta ğ•œ a : E â†’d[ğ•œ] ğ•œ` takes in a test function `Î· : ğ“¢(E, ğ•œ)`
and outputs `Î· a`. Intuitively this is an infinite density at a single point `a`. -/
def diracDelta (a : E) : E â†’d[ğ•œ] ğ•œ :=
  delta ğ•œ ğ•œ a

@[simp] lemma diracDelta_apply (a : E) (Î· : ğ“¢(E, ğ•œ)) :
    diracDelta ğ•œ a Î· = Î· a :=
  rfl

/-- Dirac delta in a given direction `v : F`. `diracDelta' ğ•œ a v` takesn in a test function
`Î· : ğ“¢(E, ğ•œ)` and outputs `Î· a â€¢ v`. Intuitively this is an infinitely intense vector field
at a single point `a` pointing at the direction `v`. -/
def diracDelta' (a : E) (v : F) : E â†’d[ğ•œ] F :=
  ContinuousLinearMap.smulRight (diracDelta ğ•œ a) v

@[simp] lemma diracDelta'_apply (a : E) (v : F) (Î· : ğ“¢(E, ğ•œ)) :
    diracDelta' ğ•œ a v Î· = Î· a â€¢ v :=
  rfl

end NormedSpace

section RCLike

/-- Definition of derivative of distribution: Let `u` be a distribution. Then its derivative is
`u'` where given a test function `Î·`, `u' Î· := -u(Î·')`. This agrees with the distribution generated
by the derivative of a differentiable function (with suitable conditions) (to be defined later),
because of integral by parts (where the boundary conditions are `0` by the test functions being
rapidly decreasing). -/
def derivative : (â„ â†’d[ğ•œ] ğ•œ) â†’â‚—[ğ•œ] (â„ â†’d[ğ•œ] ğ•œ) where
  toFun u := (ContinuousLinearEquiv.neg ğ•œ).toContinuousLinearMap.comp <| u.comp <|
    SchwartzMap.derivCLM ğ•œ
  map_add' uâ‚ uâ‚‚ := by simp
  map_smul' c u := by simp

@[simp] lemma derivative_apply (u : â„â†’d[ğ•œ] ğ•œ) (Î· : ğ“¢(â„, ğ•œ)) :
    u.derivative ğ•œ Î· = -u (derivCLM ğ•œ Î·) :=
  rfl

end RCLike

section fderiv

variable [NormedAddCommGroup E]
  [NormedSpace â„ E] [NormedAddCommGroup F] [NormedSpace â„ F] [RCLike ğ•œ]
  [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The FrÃ©chet derivative of a distribution.

Informally, for a distribution `u : E â†’d[ğ•œ] F`,
the FrÃ©chet derivative `fderiv u x v` corresponds to the dervative of `u` at the
point `x` in the direction `v`. For example, if `F = â„Â³`
then `fderiv u x v` is a vector in `â„Â³` corrsponding to
`(vâ‚ âˆ‚uâ‚/âˆ‚xâ‚ + vâ‚‚ âˆ‚uâ‚/âˆ‚xâ‚‚ + vâ‚ƒ âˆ‚uâ‚/âˆ‚xâ‚ƒ, vâ‚ âˆ‚uâ‚‚/âˆ‚xâ‚ + vâ‚‚ âˆ‚uâ‚‚/âˆ‚xâ‚‚ + vâ‚ƒ âˆ‚uâ‚‚/âˆ‚xâ‚ƒ,...)`.

Formally, for a distribution `u : E â†’d[ğ•œ] F`, this is actually defined
the distribution which takes test function `Î· : E â†’ ğ•œ` to
`- u (SchwartzMap.evalCLM v (SchwartzMap.fderivCLM ğ•œ Î·))`.

Note that, unlike for functions, the FrÃ©chet derivative of a distribution always exists.
-/
def fderivD [FiniteDimensional â„ E] : (E â†’d[ğ•œ] F) â†’â‚—[ğ•œ] (E â†’d[ğ•œ] (E â†’L[â„] F)) where
  toFun u := {
    toFun Î· := LinearMap.toContinuousLinearMap {
      toFun v := ContinuousLinearEquiv.neg ğ•œ <| u <|
        SchwartzMap.evalCLM (ğ•œ := ğ•œ) v <|
        SchwartzMap.fderivCLM ğ•œ (E := E) (F := ğ•œ) Î·
      map_add' v1 v2 := by
        simp only [ContinuousLinearEquiv.neg_apply]
        trans -u ((SchwartzMap.evalCLM (ğ•œ := ğ•œ) v1) ((fderivCLM ğ•œ) Î·) +
          (SchwartzMap.evalCLM (ğ•œ := ğ•œ) v2) ((fderivCLM ğ•œ) Î·))
        swap
        Â· simp only [map_add, neg_add_rev]
          abel
        congr
        ext x
        simp only [SchwartzMap.evalCLM, mkCLM, mkLM, map_add, ContinuousLinearMap.coe_mk',
          LinearMap.coe_mk, AddHom.coe_mk, fderivCLM_apply, add_apply]
        rfl
      map_smul' a v1 := by
        simp only [ContinuousLinearEquiv.neg_apply, RingHom.id_apply, smul_neg, neg_inj]
        trans u (a â€¢ (SchwartzMap.evalCLM (ğ•œ := ğ•œ) v1) ((fderivCLM ğ•œ) Î·))
        swap
        Â· simp
        congr
        ext x
        simp only [SchwartzMap.evalCLM, mkCLM, mkLM, map_smul, ContinuousLinearMap.coe_mk',
          LinearMap.coe_mk, AddHom.coe_mk, fderivCLM_apply, smul_apply]
        rfl}
    map_add' Î·1 Î·2 := by
      ext x
      simp only [map_add, ContinuousLinearEquiv.neg_apply, neg_add_rev,
        LinearMap.coe_toContinuousLinearMap', LinearMap.coe_mk, AddHom.coe_mk,
        ContinuousLinearMap.add_apply]
    map_smul' a Î· := by
      ext x
      simp
    cont := by
      refine continuous_clm_apply.mpr ?_
      intro y
      simp only [ContinuousLinearEquiv.neg_apply, LinearMap.coe_toContinuousLinearMap',
        LinearMap.coe_mk, AddHom.coe_mk]
      fun_prop
  }
  map_add' uâ‚ uâ‚‚ := by
    ext Î·
    simp only [ContinuousLinearMap.add_apply, ContinuousLinearEquiv.neg_apply, neg_add_rev,
      ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk,
      LinearMap.coe_toContinuousLinearMap']
    abel
  map_smul' c u := by
    ext
    simp

end fderiv

section Complex

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]
  [NormedSpace â„‚ F]

variable (E F) in
/-- Definition of Fourier transform of distribution: Let `u` be a distribution. Then its Fourier
transform is `F{u}` where given a test function `Î·`, `F{u}(Î·) := u(F{Î·})`. -/
def fourierTransform : (E â†’d[â„‚] F) â†’â‚—[â„‚] (E â†’d[â„‚] F) where
  toFun u := u.comp <| fourierTransformCLM â„‚ (E := â„‚) (V := E)
  map_add' uâ‚ uâ‚‚ := by simp
  map_smul' c u := by simp

@[simp] lemma fourierTransform_apply (u : E â†’d[â„‚] F) (Î· : ğ“¢(E, â„‚)) :
    u.fourierTransform E F Î· = u (fourierTransformCLM â„‚ Î·) :=
  rfl

end Complex

end Distribution
