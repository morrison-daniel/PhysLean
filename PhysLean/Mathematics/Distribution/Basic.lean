/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import Mathlib.Analysis.Distribution.FourierSchwartz

/-!
# Distributions

This file defines distributions, which are continuous linear functionals that take in as test
functions those `â„ â†’ E` that are smooth functions with rapidly decreasing iterated derivatives.
(The space of all these test functions is called the Schwartz space `ğ“¢(â„, E)`.)

`E` can be a normed vector space over `â„` or `â„‚`, and the continuous linear functionals are also
required to output values in `â„` or `â„‚` respectively.

## Important Results
- `Distribution.ofLinear`: constructs a distribution from a linear functional `F` and some
  conditions that implies that `F` is continuous.

## Examples
- `Distribution.diracDelta`: takes in a direction `v : E`, and returns the Dirac delta distribution
  in that direction. Given the test function `Î·`, `diracDelta v Î· = âŸ¨v, Î· 0âŸ©`.
- `Distribution.diracDelta'`: a slight generalisation of `diracDelta` where the inner product
  `âŸ¨v, â”€âŸ©` is replaced by a continuous linear map `E â†’L[ğ•œ] ğ•œ`.

-/

open SchwartzMap NNReal
noncomputable section

/-- A distribution on `E` (normed vector space over `ğ•œ`) is a continuous linear map
`ğ“¢(â„, E) â†’L[ğ•œ] ğ•œ` where `ğ’®(â„, E)` is the Schwarz space of smooth functions `â„ â†’ E` with rapidly
decreasing iterated derivatives. This is notated as `â„ â†’d[ğ•œ] E`. -/
abbrev Distribution (ğ•œ : Type) [RCLike ğ•œ] (E : Type) [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [NormedSpace â„ E] [IsScalarTower â„ ğ•œ E] : Type :=
  ğ“¢(â„, E) â†’L[ğ•œ] ğ•œ

@[inherit_doc] notation:25 "â„â†’d[" ğ•œ:25 "] " E:0 => Distribution ğ•œ E

variable (ğ•œ : Type) [RCLike ğ•œ] (E : Type) [NormedAddCommGroup E] [NormedSpace â„ E]

namespace Distribution

section NormedSpace

variable [NormedSpace ğ•œ E]

/-- We construct a distribution from the following data:
1. We take a finite set `s` of pairs `(k, n) âˆˆ â„• Ã— â„•` that will be explained later.
2. We take a linear map `f` that evaluates the given Schwartz function `Î·`. At this stage we don't
   need `f` to be continuous.
3. Recall that a Schwartz function `Î·` satisfies a bound
   `|x|áµ * â€–(dâ¿/dxâ¿) Î·â€– < Mâ‚™â‚–` where `Mâ‚™â‚– : â„` only depends on `(k, n) : â„• Ã— â„•`.
4. This step is where `s` is used: for each test function `Î·`, the norm `â€–f Î·â€–` is required to be
   bounded by `C * (|x|áµ * â€–(dâ¿/dxâ¿) Î·â€–)` for some `x : â„` and for some `(k, n) âˆˆ s`, where
   `C â‰¥ 0` is a global scalar.
-/
def ofLinear (s : Finset (â„• Ã— â„•)) (f : ğ“¢(â„, E) â†’â‚—[ğ•œ] ğ•œ)
    (hf : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(â„, E), âˆƒ (k : â„•) (n : â„•) (x : â„), (k, n) âˆˆ s âˆ§
      â€–f Î·â€– â‰¤ C * (|x| ^ k * â€–iteratedDeriv n Î· xâ€–)) : â„â†’d[ğ•œ] E :=
  mkCLMtoNormedSpace f (by simp) (by simp) <| by
    obtain âŸ¨C, hC, hfâŸ© := hf
    refine âŸ¨s, C, hC, fun Î· â†¦ ?_âŸ©
    obtain âŸ¨k, n, x, hkn, hÎ·âŸ© := hf Î·
    have hs : s.Nonempty := âŸ¨(k, n), hknâŸ©
    refine hÎ·.trans <| mul_le_mul_of_nonneg_left ((le_seminorm' ğ•œ k n Î· x).trans ?_) hC
    rw [Seminorm.finset_sup_apply]
    refine (NNReal.coe_le_coe (râ‚ := âŸ¨SchwartzMap.seminorm ğ•œ k n Î·, apply_nonneg _ _âŸ©)).2 ?_
    convert s.le_sup hkn
      (f := fun kn : â„• Ã— â„• â†¦ (âŸ¨SchwartzMap.seminorm ğ•œ kn.1 kn.2 Î·, apply_nonneg _ _âŸ© : â„â‰¥0))

@[simp] lemma ofLinear_apply (s : Finset (â„• Ã— â„•)) (f : ğ“¢(â„, E) â†’â‚—[ğ•œ] ğ•œ)
    (hf : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(â„, E), âˆƒ (k : â„•) (n : â„•) (x : â„), (k, n) âˆˆ s âˆ§
      â€–f Î·â€– â‰¤ C * (|x| ^ k * â€–iteratedDeriv n Î· xâ€–))
    (Î· : ğ“¢(â„, E)) :
    ofLinear ğ•œ E s f hf Î· = f Î· :=
  rfl

/-- Dirac delta given a continuous linear function `dir : E â†’L[ğ•œ] ğ•œ`. This is a generalisation of
`diracDelta` which takes in a specified direction `v`, and evaluate the test function `Î·` to give
`âŸ¨v, Î· aâŸ©`. Here `dir` acts like `âŸ¨v, â”€âŸ©`. -/
def diracDelta' (dir : E â†’L[ğ•œ] ğ•œ) (a : â„) : â„â†’d[ğ•œ] E :=
  dir.comp (delta ğ•œ E a)

@[simp] lemma diracDelta'_apply (dir : E â†’L[ğ•œ] ğ•œ) (a : â„) (Î· : ğ“¢(â„, E)) :
    diracDelta' ğ•œ E dir a Î· = dir (Î· a) :=
  rfl

end NormedSpace


section InnerProductSpace

variable [InnerProductSpace ğ•œ E]

/-- Dirac delta given a direction `v`. It evaluates a test function `Î·` to give `âŸ¨v, Î· aâŸ©`.
For a generalisation repalcing `âŸ¨v, â”€âŸ©` with a continuous linear function, use `diracDelta'`. -/
def diracDelta (v : E) (a : â„) : â„â†’d[ğ•œ] E :=
  diracDelta' ğ•œ E (innerSL ğ•œ v) a

@[simp] lemma diracDelta_apply (v : E) (a : â„) (Î· : ğ“¢(â„, E)) :
    diracDelta ğ•œ E v a Î· = inner ğ•œ v (Î· a) :=
  rfl

end InnerProductSpace


section RCLike

/-- Definition of derivative of distribution: Let `f` be a distribution. Then its derivative is
`f'` where given a test function `Î·`, `f' Î· := -f(Î·')`. -/
def derivative : (â„â†’d[ğ•œ] ğ•œ) â†’â‚—[ğ•œ] (â„â†’d[ğ•œ] ğ•œ) where
  toFun f := (ContinuousLinearEquiv.neg ğ•œ).toContinuousLinearMap.comp <| f.comp <|
    SchwartzMap.derivCLM ğ•œ
  map_add' fâ‚ fâ‚‚ := by simp
  map_smul' c f := by simp

@[simp] lemma derivative_apply (f : â„â†’d[ğ•œ] ğ•œ) (Î· : ğ“¢(â„, ğ•œ)) :
    f.derivative ğ•œ Î· = -f (SchwartzMap.derivCLM ğ•œ Î·) :=
  rfl

open MeasureTheory in
/-- A measurable function `f` that is bounded by `C + |x|^n` can be made into a distribution. -/
def ofPolynomialGrowth (f : â„ â†’ ğ•œ) (hfm : AEStronglyMeasurable f)
    (hfp : âˆƒ (a C : â„) (n : â„•), (fun x â†¦ â€–f xâ€–) â‰¤áµ[(volume)] (fun x â†¦ C + a * â€–xâ€–^n)) :
    â„â†’d[ğ•œ] ğ•œ :=
  ofLinear ğ•œ ğ•œ { (0, 0) }
    { toFun Î· := âˆ« x, f x * Î· x
      map_add' Î·â‚ Î·â‚‚ := sorry
      map_smul' c Î· := sorry }
    sorry

end RCLike


section Complex

variable (E : Type) [NormedAddCommGroup E] [NormedSpace â„‚ E]

/-- Definition of Fourier transform of distribution: Let `f` be a distribution. Then its Fourier
transform is `F(f)` where given a test function `Î·`, `F(f)(Î·) := f(F(Î·))`. -/
def fourierTransform : (â„â†’d[â„‚] E) â†’â‚—[â„‚] (â„â†’d[â„‚] E) where
  toFun f := f.comp <| SchwartzMap.fourierTransformCLM â„‚ (E := E) (V := â„)
  map_add' fâ‚ fâ‚‚ := by simp
  map_smul' c f := by simp

@[simp] lemma fourierTransform_apply (f : â„â†’d[â„‚] E) (Î· : ğ“¢(â„, E)) :
    fourierTransform E f Î· = f (SchwartzMap.fourierTransformCLM â„‚ Î·) :=
  rfl

end Complex

end Distribution
