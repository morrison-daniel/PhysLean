/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Joseph Tooby-Smith
-/
import Mathlib.Analysis.Distribution.FourierSchwartz
import PhysLean.Meta.TODO.Basic
/-!

# Distributions

## i. Overview of distributions

Distributions are often used implicitly in physics, for example the correct way to handle
a dirac delta function is to treat it as a distribution. In this file we will
define distributions and some properties on them.

The distributions from a space `E` to space `F` can be thought of as a generalization of
functions from `E` to `F`. We give a more precise definition of distributions below.

## ii. Key results

- `E â†’d[ğ•œ] F` is the type of distributions from `E` to `F`.
- `Distribution.derivative` and `Distribution.fourierTransform` allow us to make sense of these
  operations that might not make sense a priori on general functions.

## iii. Table of Content

- A. The definition of a distribution
- B. Construction of distributions from linear maps
- C. Derivatives of distributions
- D. Fourier transform of distributions
- E. Specific distributions

## iv. Implementation notes

- In this file we will define distributions generally, in `PhysLean.SpaceAndTime.Distributions`
  we define properties of distributions directly related to `Space`.

-/

open SchwartzMap NNReal
noncomputable section

/-!

## A. The definition of a distribution

In physics, we often encounter mathematical objects like the Dirac delta function `Î´(x)`
that are not functions in the traditional sense.
Distributions provide a rigorous framework for handling such objects.

The core idea is to define a "generalized function" not by its value at each point,
but by how it acts on a set of well-behaved "test functions".

These test functions, typically denoted `Î·`. The choice of test functions depends on the application
here we choose test functions which are smooth and decay
rapidly at infinity (called Schwartz maps). Thus really the distributions we are defining here
are called tempered distributions.

A distribution `u` is a linear map that takes a test function `Î·` and produces a value,
which can be a scalar or a vector. This action is written as `âŸªu,Î·âŸ«`.

Two key examples illustrate this concept:

1. **Ordinary Functions:** Any well-behaved function `f(x)` can be viewed as a distribution.
  Its action on a test function `Î·` is defined by integration:
  `u_f(Î·) = âˆ« f(x) Î·(x) dx`
  This integral "tests" the function `f` using `Î·`.

2. **Dirac Delta:** The Dirac delta `Î´_a` (centered at `a`) is a distribution whose action is to
  simply evaluate the test function at `a`:
  `Î´_a(Î·) = Î·(a)`

Formally, a distribution is a *continuous linear map* from the space of Schwartz functions
`ğ“¢(E, ğ•œ)` to a
vector space `F` over `ğ•œ`. This definition allows us to rigorously define concepts
like derivatives and Fourier transforms for these generalized functions, as we will see below.

We use the notation `E â†’d[ğ•œ] F` to denote the space of distributions from `E` to `F`
where `E` is a normed vector space over `â„` and `F` is a normed vector space over `ğ•œ`.

-/

/-- An `F`-valued distribution on `E` (where `E` is a normed vector space over `â„` and `F` is a
normed vector space over `ğ•œ`) is a continuous linear map `ğ“¢(E, ğ•œ) â†’L[ğ•œ] F` where `ğ’®(E, ğ•œ)` is
the Schwartz space of smooth functions `E â†’ ğ•œ` with rapidly decreasing iterated derivatives. This
is notated as `E â†’d[ğ•œ] F`.

This should be seen as a generalisation of functions `E â†’ F`. -/
abbrev Distribution (ğ•œ E F : Type) [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
    [NormedSpace â„ E] [NormedSpace ğ•œ F] : Type :=
  ğ“¢(E, ğ•œ) â†’L[ğ•œ] F

@[inherit_doc] notation:25 E:arg "â†’d[" ğ•œ:25 "] " F:0 => Distribution ğ•œ E F

variable (ğ•œ : Type) {E F : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]

namespace Distribution

section NormedSpace

variable [NormedSpace â„ E] [NormedSpace ğ•œ F]

/-!

## B. Construction of distributions from linear maps

Distributions are defined as **continuous** linear maps from `ğ“¢(E, ğ•œ)` to `F`.
It is possible to define a constructor of distributions from just linear maps
`ğ“¢(E, ğ•œ) â†’â‚—[ğ•œ] F` (without the continuity requirement) by imposing a condition
on the size of `u` applied to `Î·`.

-/

/-- The construction of a distribution from the following data:
1. We take a finite set `s` of pairs `(k, n) âˆˆ â„• Ã— â„•` that will be explained later.
2. We take a linear map `u` that evaluates the given Schwartz function `Î·`. At this stage we don't
  need `u` to be continuous.
3. Recall that a Schwartz function `Î·` satisfies a bound
  `â€–xâ€–áµ * â€–(dâ¿/dxâ¿) Î·â€– < Mâ‚™â‚–` where `Mâ‚™â‚– : â„` only depends on `(k, n) : â„• Ã— â„•`.
4. This step is where `s` is used: for each test function `Î·`, the norm `â€–u Î·â€–` is required to be
  bounded by `C * (â€–xâ€–áµ * â€–(dâ¿/dxâ¿) Î·â€–)` for some `x : â„` and for some `(k, n) âˆˆ s`, where
  `C â‰¥ 0` is a global scalar.
-/
def ofLinear (s : Finset (â„• Ã— â„•)) (u : ğ“¢(E, ğ•œ) â†’â‚—[ğ•œ] F)
    (hu : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(E, ğ•œ), âˆƒ (k : â„•) (n : â„•) (x : E), (k, n) âˆˆ s âˆ§
      â€–u Î·â€– â‰¤ C * (â€–xâ€– ^ k * â€–iteratedFDeriv â„ n Î· xâ€–)) : E â†’d[ğ•œ] F :=
  mkCLMtoNormedSpace u (by simp) (by simp) <| by
    obtain âŸ¨C, hC, huâŸ© := hu
    refine âŸ¨s, C, hC, fun Î· â†¦ ?_âŸ©
    obtain âŸ¨k, n, x, hkn, hÎ·âŸ© := hu Î·
    refine hÎ·.trans <| mul_le_mul_of_nonneg_left ((le_seminorm ğ•œ k n Î· x).trans ?_) hC
    rw [Seminorm.finset_sup_apply]
    refine (NNReal.coe_le_coe (râ‚ := âŸ¨SchwartzMap.seminorm ğ•œ k n Î·, apply_nonneg _ _âŸ©)).2 ?_
    convert s.le_sup hkn
      (f := fun kn : â„• Ã— â„• â†¦ (âŸ¨SchwartzMap.seminorm ğ•œ kn.1 kn.2 Î·, apply_nonneg _ _âŸ© : â„â‰¥0))

@[simp] lemma ofLinear_apply (s : Finset (â„• Ã— â„•)) (u : ğ“¢(E, ğ•œ) â†’â‚—[ğ•œ] F)
    (hu : âˆƒ C : â„, 0 â‰¤ C âˆ§ âˆ€ Î· : ğ“¢(E, ğ•œ), âˆƒ (k : â„•) (n : â„•) (x : E), (k, n) âˆˆ s âˆ§
      â€–u Î·â€– â‰¤ C * (â€–xâ€– ^ k * â€–iteratedFDeriv â„ n Î· xâ€–))
    (Î· : ğ“¢(E, ğ•œ)) :
    ofLinear ğ•œ s u hu Î· = u Î· :=
  rfl

end NormedSpace

/-!

## C. Derivatives of distributions

Given a distribution `u : E â†’d[ğ•œ] F`, we can define the derivative of that distribution.
In general when defining an operation on a distribution, we do it by applying a similar
operation instead to the Schwartz maps it acts on.

Thus the derivative of `u` is the distribution which takes `Î·` to `âŸªu, - Î·'âŸ«`
where `Î·'` is the derivative of `Î·`.

-/

section fderiv

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The FrÃ©chet derivative of a distribution.

Informally, for a distribution `u : E â†’d[ğ•œ] F`,
the FrÃ©chet derivative `fderiv u x v` corresponds to the derivative of `u` at the
point `x` in the direction `v`. For example, if `F = â„Â³`
then `fderiv u x v` is a vector in `â„Â³` corresponding to
`(vâ‚ âˆ‚uâ‚/âˆ‚xâ‚ + vâ‚‚ âˆ‚uâ‚/âˆ‚xâ‚‚ + vâ‚ƒ âˆ‚uâ‚/âˆ‚xâ‚ƒ, vâ‚ âˆ‚uâ‚‚/âˆ‚xâ‚ + vâ‚‚ âˆ‚uâ‚‚/âˆ‚xâ‚‚ + vâ‚ƒ âˆ‚uâ‚‚/âˆ‚xâ‚ƒ,...)`.

Formally, for a distribution `u : E â†’d[ğ•œ] F`, this is actually defined
the distribution which takes test function `Î· : E â†’ ğ•œ` to
`- u (SchwartzMap.evalCLM v (SchwartzMap.fderivCLM ğ•œ Î·))`.

Note that, unlike for functions, the FrÃ©chet derivative of a distribution always exists.
-/
def fderivD [FiniteDimensional â„ E] : (E â†’d[ğ•œ] F) â†’â‚—[ğ•œ] (E â†’d[ğ•œ] (E â†’L[â„] F)) where
  toFun u := {
    toFun Î· := LinearMap.toContinuousLinearMap {
      toFun v := ContinuousLinearEquiv.neg ğ•œ <| u <|
        SchwartzMap.evalCLM (ğ•œ := ğ•œ) v <|
        SchwartzMap.fderivCLM ğ•œ (E := E) (F := ğ•œ) Î·
      map_add' v1 v2 := by
        simp only [ContinuousLinearEquiv.neg_apply]
        trans -u ((SchwartzMap.evalCLM (ğ•œ := ğ•œ) v1) ((fderivCLM ğ•œ) Î·) +
          (SchwartzMap.evalCLM (ğ•œ := ğ•œ) v2) ((fderivCLM ğ•œ) Î·))
        swap
        Â· simp only [map_add, neg_add_rev]
          abel
        congr
        ext x
        simp only [SchwartzMap.evalCLM, mkCLM, mkLM, map_add, ContinuousLinearMap.coe_mk',
          LinearMap.coe_mk, AddHom.coe_mk, fderivCLM_apply, add_apply]
        rfl
      map_smul' a v1 := by
        simp only [ContinuousLinearEquiv.neg_apply, RingHom.id_apply, smul_neg, neg_inj]
        trans u (a â€¢ (SchwartzMap.evalCLM (ğ•œ := ğ•œ) v1) ((fderivCLM ğ•œ) Î·))
        swap
        Â· simp
        congr
        ext x
        simp only [SchwartzMap.evalCLM, mkCLM, mkLM, map_smul, ContinuousLinearMap.coe_mk',
          LinearMap.coe_mk, AddHom.coe_mk, fderivCLM_apply, smul_apply]
        rfl}
    map_add' Î·1 Î·2 := by
      ext x
      simp only [map_add, ContinuousLinearEquiv.neg_apply,
        LinearMap.coe_toContinuousLinearMap', LinearMap.coe_mk, AddHom.coe_mk,
        ContinuousLinearMap.add_apply]
    map_smul' a Î· := by
      ext x
      simp
    cont := by
      refine continuous_clm_apply.mpr ?_
      intro y
      simp only [ContinuousLinearEquiv.neg_apply, LinearMap.coe_toContinuousLinearMap',
        LinearMap.coe_mk, AddHom.coe_mk]
      fun_prop
  }
  map_add' uâ‚ uâ‚‚ := by
    ext Î·
    simp only [ContinuousLinearMap.add_apply, ContinuousLinearEquiv.neg_apply, neg_add_rev,
      ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk,
      LinearMap.coe_toContinuousLinearMap']
    abel
  map_smul' c u := by
    ext
    simp

lemma fderivD_apply [FiniteDimensional â„ E] (u : E â†’d[ğ•œ] F) (Î· : ğ“¢(E, ğ•œ)) (v : E) :
    fderivD ğ•œ u Î· v = - u (SchwartzMap.evalCLM (ğ•œ := ğ•œ) v (SchwartzMap.fderivCLM ğ•œ Î·)) := by
  rfl

TODO "01-09-25-JTS" "For distributions, prove that the derivative fderivD commutes with
  integrals and sums. This may require defining the integral of families of distributions
  although it is expected this will follow from the definition of a distribution."

end fderiv

/-!

## D. Fourier transform of distributions

As with derivatives of distributions we can define the fourier transform of a distribution
by taking the fourier transform of the underlying Schwartz maps. Thus the fourier transform
of the distribution `u` is the distribution which takes `Î·` to `âŸªu, F[Î·]âŸ«` where `F[Î·]` is the
fourier transform of `Î·`.

-/

section Complex

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]
  [NormedSpace â„‚ F]

variable (E F) in
/-- Definition of Fourier transform of distribution: Let `u` be a distribution. Then its Fourier
transform is `F{u}` where given a test function `Î·`, `F{u}(Î·) := u(F{Î·})`. -/
def fourierTransform : (E â†’d[â„‚] F) â†’â‚—[â„‚] (E â†’d[â„‚] F) where
  toFun u := u.comp <| fourierTransformCLM â„‚ (E := â„‚) (V := E)
  map_add' uâ‚ uâ‚‚ := by simp
  map_smul' c u := by simp

@[simp] lemma fourierTransform_apply (u : E â†’d[â„‚] F) (Î· : ğ“¢(E, â„‚)) :
    u.fourierTransform E F Î· = u (fourierTransformCLM â„‚ Î·) :=
  rfl

end Complex

/-!

## E. Specific distributions

We now define specific distributions, which are used throughout physics. In particular, we define:
- The constant distribution.
- The dirac delta distribution.
- The heaviside step function.

-/

section constant
/-!

### E.1. The constant distribution

The constant distribution is the distribution which corresponds to a constant function,
it takes `Î·` to the integral of `Î·` over the volume measure.

-/
open MeasureTheory
section
variable (E : Type) [NormedAddCommGroup E]
  [NormedSpace â„ E] [NormedSpace â„ F]
  [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
  [MeasureSpace E] [BorelSpace E] [SecondCountableTopology E]

/-- The constant distribution `E â†’d[ğ•œ] F`, for a given `c : F` this corresponds
  to the integral `âˆ« x, Î· x â€¢ c âˆ‚MeasureTheory.volume`. -/
def const [hÎ¼ : Measure.HasTemperateGrowth (volume (Î± := E))] (c : F) : E â†’d[ğ•œ] F := by
  refine mkCLMtoNormedSpace
    (fun Î· => âˆ« x, Î· x â€¢ c âˆ‚MeasureTheory.volume) ?_
    ?_ ?_
  Â· intro Î·1 Î·2
    simp [add_smul]
    by_cases hc : c = 0
    Â· subst hc
      simp
    rw [MeasureTheory.integral_add]
    Â· refine (integrable_smul_const hc).mpr ?_
      exact integrable Î·1
    Â· refine (integrable_smul_const hc).mpr ?_
      exact integrable Î·2
  Â· intro a Î·
    simp only [smul_apply, RingHom.id_apply, smul_assoc]
    rw [MeasureTheory.integral_smul]
  rcases hÎ¼.exists_integrable with âŸ¨n, hâŸ©
  let m := (n, 0)
  use Finset.Iic m, â€–câ€– * (2 ^ n * âˆ« x, (1 + â€–xâ€–) ^ (- (n : â„)) âˆ‚(volume (Î± := E)))
  refine âŸ¨by positivity, fun Î· â†¦ (norm_integral_le_integral_norm _).trans ?_âŸ©
  have h' : âˆ€ x, â€–Î· xâ€– â‰¤ (1 + â€–xâ€–) ^ (-(n : â„)) *
    (2 ^ n * ((Finset.Iic m).sup (fun m' => SchwartzMap.seminorm ğ•œ m'.1 m'.2) Î·)) := by
    intro x
    rw [Real.rpow_neg (by positivity), â† div_eq_inv_mul,
      le_div_iffâ‚€' (by positivity), Real.rpow_natCast]
    simpa using one_add_le_sup_seminorm_apply (m := m) (k := n) (n := 0) le_rfl le_rfl Î· x
  conv_lhs =>
    enter [2, x]
    rw [norm_smul, mul_comm]
  conv_lhs =>
    rw [MeasureTheory.integral_const_mul]
  rw [mul_assoc]
  by_cases hc : c = 0
  Â· subst hc
    simp
  refine (mul_le_mul_iff_of_pos_left ?_).mpr ?_
  Â· positivity
  apply (integral_mono (by simpa using Î·.integrable_pow_mul ((volume)) 0) _ h').trans
  Â· unfold schwartzSeminormFamily
    rw [integral_mul_const, â† mul_assoc, mul_comm (2 ^ n)]
  apply h.mul_const

lemma const_apply [hÎ¼ : Measure.HasTemperateGrowth (volume (Î± := E))] (c : F)
    (Î· : ğ“¢(E, ğ•œ)) :
    const ğ•œ E c Î· = âˆ« x, Î· x â€¢ c âˆ‚MeasureTheory.volume := by rfl
end
section

variable [NormedSpace â„ E] [NormedSpace â„ F]
  [MeasureSpace E] [BorelSpace E] [SecondCountableTopology E]

@[simp]
lemma fderivD_const [hÎ¼ : Measure.IsAddHaarMeasure (volume (Î± := E))]
    [FiniteDimensional â„ E] (c : F) :
    fderivD â„ (const â„ E c) = 0 := by
  ext Î· v
  rw [fderivD_apply, const_apply]
  simp only [ContinuousLinearMap.zero_apply, neg_eq_zero]
  trans -âˆ« (x : E), Î· x â€¢ (fderiv â„ (fun y => c) x) v âˆ‚volume
  swap
  Â· simp
  rw [integral_smul_fderiv_eq_neg_fderiv_smul_of_integrable]
  simp
  rfl
  Â· apply MeasureTheory.Integrable.smul_const
    change Integrable (SchwartzMap.evalCLM (ğ•œ := â„) v (SchwartzMap.fderivCLM â„ Î·)) volume
    exact integrable ((SchwartzMap.evalCLM v) ((fderivCLM â„) Î·))
  Â· simp
  Â· apply MeasureTheory.Integrable.smul_const
    exact integrable Î·
  Â· exact SchwartzMap.differentiable Î·
  Â· simp

end
end constant

/-!

### E.2. The dirac delta distribution

The dirac delta distribution centered at `a : E` is the distribution which takes
`Î·` to `Î· a`. We also define `diracDelta'` which takes in an element of `v` of `F` and
outputs `Î· a â€¢ v`.

-/

section DiracDelta

variable [NormedSpace â„ E] [NormedSpace ğ•œ F]

/-- Dirac delta distribution `diracDelta ğ•œ a : E â†’d[ğ•œ] ğ•œ` takes in a test function `Î· : ğ“¢(E, ğ•œ)`
and outputs `Î· a`. Intuitively this is an infinite density at a single point `a`. -/
def diracDelta (a : E) : E â†’d[ğ•œ] ğ•œ :=
  delta ğ•œ ğ•œ a

@[simp] lemma diracDelta_apply (a : E) (Î· : ğ“¢(E, ğ•œ)) :
    diracDelta ğ•œ a Î· = Î· a :=
  rfl

/-- Dirac delta in a given direction `v : F`. `diracDelta' ğ•œ a v` takes in a test function
`Î· : ğ“¢(E, ğ•œ)` and outputs `Î· a â€¢ v`. Intuitively this is an infinitely intense vector field
at a single point `a` pointing at the direction `v`. -/
def diracDelta' (a : E) (v : F) : E â†’d[ğ•œ] F :=
  ContinuousLinearMap.smulRight (diracDelta ğ•œ a) v

@[simp] lemma diracDelta'_apply (a : E) (v : F) (Î· : ğ“¢(E, ğ•œ)) :
    diracDelta' ğ•œ a v Î· = Î· a â€¢ v :=
  rfl

end DiracDelta
/-!

### E.3. The heviside step function

The heaviside step function on `EuclideanSpace â„ (Fin d.succ)` is the distribution
from `EuclideanSpace â„ (Fin d.succ)` to `â„` which takes a `Î·` to the integral of `Î·` in the
upper-half plane (determined by the last coordinate in `EuclideanSpace â„ (Fin d.succ)`).

-/
open MeasureTheory

/-- The Heaviside step distribution defined on `(EuclideanSpace â„ (Fin d.succ)) `
  equal to `1` in the positive `z`-direction and `0` in the negative `z`-direction. -/
def heavisideStep (d : â„•) : (EuclideanSpace â„ (Fin d.succ)) â†’d[â„] â„ := by
  refine mkCLMtoNormedSpace
    (fun Î· =>
    âˆ« x in {x : EuclideanSpace â„ (Fin d.succ) | 0 < x (Fin.last d)}, Î· x âˆ‚MeasureTheory.volume) ?_
    ?_ ?_
  Â· intro Î·1 Î·2
    simp only [Nat.succ_eq_add_one, add_apply]
    rw [MeasureTheory.integral_add]
    Â· apply MeasureTheory.Integrable.restrict
      exact integrable Î·1
    Â· apply MeasureTheory.Integrable.restrict
      exact integrable Î·2
  Â· intro a Î·
    simp only [smul_apply, RingHom.id_apply]
    rw [MeasureTheory.integral_smul]
  haveI hÎ¼ : (volume (Î± := EuclideanSpace â„ (Fin d.succ))).HasTemperateGrowth := by
    infer_instance
  rcases hÎ¼.exists_integrable with âŸ¨n, hâŸ©
  let m := (n, 0)
  use Finset.Iic m, 2 ^ n *
    âˆ« x, (1 + â€–xâ€–) ^ (- (n : â„)) âˆ‚(volume (Î± := EuclideanSpace â„ (Fin d.succ)))
  refine âŸ¨by positivity, fun Î· â†¦ (norm_integral_le_integral_norm _).trans ?_âŸ©
  trans âˆ« x, â€–Î· xâ€– âˆ‚(volume (Î± := EuclideanSpace â„ (Fin d.succ)))
  Â· refine setIntegral_le_integral ?_ ?_
    Â· have hi := integrable Î· (Î¼ := volume)
      fun_prop
    Â· filter_upwards with x
      simp
  Â· have h' : âˆ€ x, â€–Î· xâ€– â‰¤ (1 + â€–xâ€–) ^ (-(n : â„)) *
      (2 ^ n * ((Finset.Iic m).sup (fun m' => SchwartzMap.seminorm â„ m'.1 m'.2) Î·)) := by
      intro x
      rw [Real.rpow_neg (by positivity), â† div_eq_inv_mul,
        le_div_iffâ‚€' (by positivity), Real.rpow_natCast]
      simpa using one_add_le_sup_seminorm_apply (m := m) (k := n) (n := 0) le_rfl le_rfl Î· x
    apply (integral_mono (by simpa using Î·.integrable_pow_mul ((volume)) 0) _ h').trans
    Â· unfold schwartzSeminormFamily
      rw [integral_mul_const, â† mul_assoc, mul_comm (2 ^ n)]
    apply h.mul_const

lemma heavisideStep_apply (d : â„•) (Î· : ğ“¢(EuclideanSpace â„ (Fin d.succ), â„)) :
    heavisideStep d Î· = âˆ« x in {x : EuclideanSpace â„ (Fin d.succ) | 0 < x (Fin.last d)},
      Î· x âˆ‚MeasureTheory.volume := by
  rfl

end Distribution
