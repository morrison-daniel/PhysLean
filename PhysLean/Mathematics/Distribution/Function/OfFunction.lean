/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Mathematics.Distribution.Function.IsDistBounded
/-!

# Distributions from bounded functions

In this module we define distributions from functions `f : EuclideanSpace â„ (Fin d.succ) â†’ F`
whose norm is bounded by `c1 * â€–xâ€– ^ (-d : â„) + c2 * â€–xâ€– ^ n`
for some constants `c1`, `c2` and `n`.

This gives a convenient way to construct distributions from functions, without needing
to reference the underlying Schwartz maps.

## Key definition

- `ofFunction`: Creates a distribution from a `f` satisfying `IsDistBounded f`.

-/
open SchwartzMap NNReal
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace â„ E] [NormedSpace â„ F]

namespace Distribution

open MeasureTheory

/-- A distribution `(EuclideanSpace â„ (Fin 3)) â†’d[â„] F` from a function
  `f : EuclideanSpace â„ (Fin 3) â†’ F` bounded by `c1 * â€–xâ€– ^ (-2 : â„) + c2 * â€–xâ€– ^ n`.
-/
def ofFunction {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) :
    (EuclideanSpace â„ (Fin dm1.succ)) â†’d[â„] F := by
  refine mkCLMtoNormedSpace (fun Î· => âˆ« x, Î· x â€¢ f x) ?_ ?_ ?_
  Â· /- Addition -/
    intro Î· Îº
    simp only [add_apply]
    conv_lhs =>
      enter [2, a]
      rw [add_smul]
    rw [integral_add]
    Â· apply hf.schwartzMap_smul_integrable hae
    Â· exact hf.schwartzMap_smul_integrable hae
  Â· /- SMul-/
    intro a Î·
    simp only [smul_apply, smul_eq_mul, RingHom.id_apply]
    conv_lhs =>
      enter [2, a]
      rw [â† smul_smul]
    rw [integral_smul]
  /- boundedness -/
  obtain âŸ¨r, hrâŸ© := hf.norm_inv_mul_exists_pow_integrable f hae
  use Finset.Iic (r, 0), 2 ^ r * âˆ« x, â€–f xâ€– * â€–((1 + â€–xâ€–) ^ r)â»Â¹â€–
  refine âŸ¨by positivity, fun Î· â†¦ (norm_integral_le_integral_norm _).trans ?_âŸ©
  rw [â† integral_const_mul, â† integral_mul_const]
  refine integral_mono_of_nonneg ?_ ?_ ?_
  Â· filter_upwards with x
    positivity
  Â· apply Integrable.mul_const
    apply Integrable.const_mul
    exact hr
  Â· filter_upwards with x
    simp [norm_smul]
    trans (2 ^ r *
      ((Finset.Iic (r, 0)).sup (schwartzSeminormFamily â„ (EuclideanSpace â„ (Fin (dm1 + 1))) â„)) Î·
      *(|1 + â€–xâ€–| ^ r)â»Â¹) * â€–f xâ€–; swap
    Â· apply le_of_eq
      ring
    apply mul_le_mul_of_nonneg ?_ (by rfl) (by positivity) (by positivity)
    have h0 := one_add_le_sup_seminorm_apply (ğ•œ := â„) (m := (r, 0))
      (k := r) (n := 0) le_rfl le_rfl Î· x
    rw [Lean.Grind.Field.IsOrdered.le_mul_inv_iff_mul_le _ _ (by positivity)]
    convert h0 using 1
    simp only [Nat.succ_eq_add_one, norm_iteratedFDeriv_zero, Real.norm_eq_abs]
    ring_nf
    congr
    rw [abs_of_nonneg (by positivity)]

lemma ofFunction_apply {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    ofFunction f hf hae Î· = âˆ« x, Î· x â€¢ f x := rfl

@[simp]
lemma ofFunction_zero_eq_zero {dm1 : â„•} :
    ofFunction (fun _ : EuclideanSpace â„ (Fin (dm1 + 1)) => (0 : F))
      âŸ¨0, 0, 0, 0, by simpâŸ© (by fun_prop) = 0 := by
  ext Î·
  simp [ofFunction_apply]

lemma ofFunction_smul {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (c : â„) :
    ofFunction (c â€¢ f) (by fun_prop) (by fun_prop) = c â€¢ ofFunction f hf hae := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [ofFunction_apply]
  simp only [Nat.succ_eq_add_one, Pi.smul_apply]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

lemma ofFunction_smul_fun {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) (c : â„) :
    ofFunction (fun x => c â€¢ f x) (by
      change IsDistBounded (c â€¢ f)
      fun_prop) (by fun_prop) = c â€¢ ofFunction f hf hae := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [ofFunction_apply]
  simp only [Nat.succ_eq_add_one]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

open InnerProductSpace

lemma ofFunction_inner {dm1 n : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ EuclideanSpace â„ (Fin n))
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ğ“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) (y : EuclideanSpace â„ (Fin n)) :
    âŸªofFunction f hf hae Î·, yâŸ«_â„ = âˆ« x, Î· x * âŸªf x, yâŸ«_â„ := by
  rw [ofFunction_apply]
  trans âˆ« x, âŸªy, Î· x â€¢ f xâŸ«_â„; swap
  Â· congr
    funext x
    rw [real_inner_comm]
    simp [inner_smul_left]
  rw [integral_inner, real_inner_comm]
  exact IsDistBounded.schwartzMap_smul_integrable hf hae

TODO "LV5RM" "Add a general lemma specifying the derivative of
  functions from distributions."
end Distribution
