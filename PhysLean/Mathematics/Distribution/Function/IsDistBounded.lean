/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Mathematics.Distribution.Function.InvPowMeasure
/-!

## Bounded functions for distributions

In this module we define the property `IsDistBounded f` for a function `f`.
It says that `f` is bounded by a finite sum of terms of the form `c * â€–x + gâ€– ^ p` for
constants `c`, `g` and `-d â‰¤ p ` where `d` is the dimension of the space minus 1.

We prove a number of properties of these functions, inparticular that they
are integrable when multiplied by a Schwartz map. This allows us to define distributions
from such functions.

-/
open SchwartzMap NNReal
noncomputable section

variable (ð•œ : Type) {E F F' : Type} [RCLike ð•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F']

namespace Distribution

variable [NormedSpace â„ E]

open MeasureTheory

/-!

## IsBounded

-/

/-- The boundedness condition on a function ` EuclideanSpace â„ (Fin dm1.succ) â†’ F`
  for it to form a distribution. -/
@[fun_prop]
def IsDistBounded {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) : Prop :=
    âˆƒ n, âˆƒ c : Fin n â†’ â„, âˆƒ g : Fin n â†’ EuclideanSpace â„ (Fin dm1.succ),
    âˆƒ p : Fin n â†’ â„¤,
    (âˆ€ i, 0 â‰¤ c i) âˆ§
    (âˆ€ i, -dm1 â‰¤ p i) âˆ§
    âˆ€ x, â€–f xâ€– â‰¤ âˆ‘ i, c i * â€–x + g iâ€– ^ p i

@[fun_prop]
lemma IsDistBounded.add {dm1 : â„•} {f g : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (hg : IsDistBounded g) : IsDistBounded (f + g) := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  rcases hg with âŸ¨n2, c2, g2, p2, c2_nonneg, p2_bound, bound2âŸ©
  refine âŸ¨n1 + n2, Fin.append c1 c2, Fin.append g1 g2, Fin.append p1 p2, ?_, ?_, ?_âŸ©
  Â· intro i
    obtain âŸ¨i, rflâŸ© := finSumFinEquiv.surjective i
    match i with
    | .inl i =>
      simp only [finSumFinEquiv_apply_left, Fin.append_left, ge_iff_le]
      exact c1_nonneg i
    | .inr i =>
      simp only [finSumFinEquiv_apply_right, Fin.append_right, ge_iff_le]
      exact c2_nonneg i
  Â· intro i
    obtain âŸ¨i, rflâŸ© := finSumFinEquiv.surjective i
    match i with
    | .inl i =>
      simp only [finSumFinEquiv_apply_left, Fin.append_left, ge_iff_le]
      exact p1_bound i
    | .inr i =>
      simp only [finSumFinEquiv_apply_right, Fin.append_right, ge_iff_le]
      exact p2_bound i
  Â· intro x
    apply (norm_add_le _ _).trans
    apply (add_le_add (bound1 x) (bound2 x)).trans
    apply le_of_eq
    rw [â† finSumFinEquiv.sum_comp]
    simp

@[fun_prop]
lemma IsDistBounded.const_smul {dm1 : â„•} [NormedSpace â„ F] {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (c : â„) : IsDistBounded (c â€¢ f) := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, â€–câ€– â€¢ c1, g1, p1, ?_, p1_bound, ?_âŸ©
  Â· intro i
    simp only [Real.norm_eq_abs, Pi.smul_apply, smul_eq_mul]
    have hi := c1_nonneg i
    positivity
  Â· intro x
    simp [norm_smul]
    conv_rhs => enter [2, x]; rw [mul_assoc]
    rw [â† Finset.mul_sum]
    refine mul_le_mul (by rfl) (bound1 x) ?_ ?_
    Â· exact norm_nonneg (f x)
    Â· exact abs_nonneg c

lemma IsDistBounded.pi_comp {dm1 n : â„•}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ EuclideanSpace â„ (Fin n)}
    (hf : IsDistBounded f) (j : Fin n) : IsDistBounded (fun x => f x j) := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, ?_âŸ©
  intro x
  apply le_trans ?_ (bound1 x)
  simp only [Real.norm_eq_abs]
  rw [@PiLp.norm_eq_of_L2]
  refine Real.abs_le_sqrt ?_
  trans âˆ‘ i âˆˆ {j}, â€–(f x) iâ€– ^ 2
  Â· simp
  apply Finset.sum_le_univ_sum_of_nonneg
  intro y
  exact sq_nonneg â€–f x yâ€–

lemma IsDistBounded.comp_add_right {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) (c : EuclideanSpace â„ (Fin dm1.succ)) :
    IsDistBounded (fun x => f (x + c)) := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, c1, fun i => g1 i + c, p1, c1_nonneg, p1_bound, ?_âŸ©
  intro x
  apply (bound1 (x + c)).trans
  apply le_of_eq
  simp only [Nat.succ_eq_add_one]
  congr 1
  funext x
  congr 3
  module

@[fun_prop]
lemma IsDistBounded.const_mul_fun {dm1 : â„•}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„}
    (hf : IsDistBounded f) (c : â„) : IsDistBounded (fun x => c * f x) := by
  convert hf.const_smul c using 1

lemma IsDistBounded.congr {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    {g : EuclideanSpace â„ (Fin dm1.succ) â†’ F'}
    (hf : IsDistBounded f) (hfg : âˆ€ x, â€–g xâ€– = â€–f xâ€–) : IsDistBounded g := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, ?_âŸ©
  intro x
  rw [hfg x]
  exact bound1 x

lemma IsDistBounded.mono {dm1 : â„•} {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    {g : EuclideanSpace â„ (Fin dm1.succ) â†’ F'}
    (hf : IsDistBounded f) (hfg : âˆ€ x, â€–g xâ€– â‰¤ â€–f xâ€–) : IsDistBounded g := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, ?_âŸ©
  intro x
  exact (hfg x).trans (bound1 x)

open InnerProductSpace
@[fun_prop]
lemma IsDistBounded.inner_left {dm1 n : â„•}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ EuclideanSpace â„ (Fin n) }
    (hf : IsDistBounded f) (y : EuclideanSpace â„ (Fin n)) :
    IsDistBounded (fun x => âŸªf x, yâŸ«_â„) := by
  rcases hf with âŸ¨n1, c1, g1, p1, c1_nonneg, p1_bound, bound1âŸ©
  refine âŸ¨n1, fun i => â€–yâ€– * c1 i, g1, p1, ?_, p1_bound, ?_âŸ©
  Â· intro i
    simp only
    have hi := c1_nonneg i
    positivity
  Â· intro x
    apply (norm_inner_le_norm (f x) y).trans
    rw [mul_comm]
    simp only [Nat.succ_eq_add_one]
    conv_rhs => enter [2, i]; rw [mul_assoc]
    rw [â† Finset.mul_sum]
    refine mul_le_mul (by rfl) (bound1 x) ?_ ?_
    Â· exact norm_nonneg (f x)
    Â· exact norm_nonneg y

/-!

## Specific cases

-/

TODO "LSLHW" "The proof `IsDistBounded.pow` needs golfing."

@[fun_prop]
lemma IsDistBounded.const {dm1 : â„•} (f : F) :
    IsDistBounded (dm1 := dm1) (fun _ : EuclideanSpace â„ (Fin dm1.succ) => f) := by
  use 1, fun _ => â€–fâ€–, fun _ => 0, fun _ => 0
  simp

@[fun_prop]
lemma IsDistBounded.pow {dm1 : â„•} (n : â„¤) (hn : - dm1 â‰¤ n) :
    IsDistBounded (dm1 := dm1) (fun x => â€–xâ€– ^ n) := by
  refine âŸ¨1, fun _ => 1, fun _ => 0, fun _ => n, ?_, ?_, ?_âŸ©
  Â· simp
  Â· simp
    exact hn
  Â· intro x
    simp

@[fun_prop]
lemma IsDistBounded.pow_shift {dm1 : â„•} (n : â„¤)
    (g : EuclideanSpace â„ (Fin dm1.succ)) (hn : - dm1 â‰¤ n) :
    IsDistBounded (dm1 := dm1) (fun x => â€–x - gâ€– ^ n) := by
  refine âŸ¨1, fun _ => 1, fun _ => (- g), fun _ => n, ?_, ?_, ?_âŸ©
  Â· simp
  Â· simp
    exact hn
  Â· intro x
    simp
    rfl

@[fun_prop]
lemma IsDistBounded.inv {n : â„•} :
    IsDistBounded (dm1 := n.succ) (fun x => â€–xâ€–â»Â¹) := by
  convert IsDistBounded.pow (dm1 := n.succ) (-1) (by simp) using 1
  ext1 x
  simp

/-!

## Integrability

-/

private lemma schwartzMap_mul_pow_integrable {dm1 : â„•} (Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„))
    (p : â„¤) (hp : - (dm1 : â„¤) â‰¤ p) :
    Integrable (fun x => â€–Î· xâ€– * â€–xâ€– ^ p) volume := by
  by_cases hp : p = 0
  Â· subst hp
    simp only [Nat.succ_eq_add_one, zpow_zero, mul_one]
    apply Integrable.norm
    exact Î·.integrable
  suffices h1 : Integrable (fun x => â€–Î· xâ€– * â€–xâ€– ^ (p + dm1)) (invPowMeasure (dm1 := dm1)) by
    rw [invPowMeasure] at h1
    erw [integrable_withDensity_iff_integrable_smulâ‚€ (by fun_prop)] at h1
    convert h1 using 1
    funext x
    simp only [Nat.succ_eq_add_one, Real.norm_eq_abs, one_div]
    rw [Real.toNNReal_of_nonneg, NNReal.smul_def]
    simp only [inv_nonneg, norm_nonneg, pow_nonneg, coe_mk, smul_eq_mul]
    ring_nf
    rw [mul_assoc]
    congr
    have hx : 0 â‰¤ â€–xâ€– := norm_nonneg x
    generalize â€–xâ€– = r at *
    by_cases hr : r = 0
    Â· subst hr
      simp only [inv_zero]
      rw [zero_pow_eq, zero_zpow_eq, zero_zpow_eq]
      split_ifs <;> simp
      any_goals omega
    Â· field_simp
      rw [zpow_addâ‚€ hr]
      simp
    Â· simp
  convert integrable_pow_mul_iteratedFDeriv invPowMeasure Î· (p + dm1).toNat 0 using 1
  funext x
  simp only [Nat.succ_eq_add_one, Real.norm_eq_abs, norm_iteratedFDeriv_zero]
  rw [mul_comm]
  congr 1
  rw [â† zpow_natCast]
  congr
  refine Int.eq_natCast_toNat.mpr ?_
  omega

lemma IsDistBounded.schwartzMap_mul_integrable_norm {dm1 : â„•}
    {Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) :
    Integrable (fun x => â€–Î· xâ€– * â€–f xâ€–) volume := by
  rcases hf with âŸ¨n, c, g, p, c_nonneg, p_bound, hboundâŸ©
  apply Integrable.mono' (g := fun x => âˆ‘ i, â€–Î· xâ€– * (c i * â€–x + g iâ€– ^ p i)) _
  Â· fun_prop
  Â· filter_upwards with x
    rw [â† Finset.mul_sum]
    simp
    exact mul_le_mul (by rfl) (hbound x) (norm_nonneg _) (abs_nonneg _)
  Â· apply MeasureTheory.integrable_finset_sum
    intro i _
    conv =>
      enter [1, x]
      rw [â† mul_assoc, mul_comm _ (c i), mul_assoc]
    apply Integrable.const_mul
    suffices h1 : Integrable (fun x => â€–Î· ((x + g i) - g i)â€– * â€–x + g iâ€– ^ p i) volume by
      convert h1 using 1
      simp
    apply MeasureTheory.Integrable.comp_add_right (g := g i)
      (f := fun x => â€–Î· (x - g i)â€– * â€–xâ€– ^ p i)
    let Î·' := SchwartzMap.compCLM (ð•œ := â„)
      (g := fun x => x - g i)
      (by
        apply Function.HasTemperateGrowth.of_fderiv (k := 1) (C := 1 + â€–g iâ€–)
        Â· have hx : (fderiv â„ (fun x => x - g i)) =
              fun _ => ContinuousLinearMap.id â„ (EuclideanSpace â„ (Fin (dm1 + 1))) := by
            funext x
            simp only [Nat.succ_eq_add_one]
            erw [fderiv_sub]
            simp only [fderiv_id', fderiv_fun_const, Pi.zero_apply, sub_zero]
            fun_prop
            fun_prop
          rw [hx]
          exact
            Function.HasTemperateGrowth.const
              (ContinuousLinearMap.id â„ (EuclideanSpace â„ (Fin (dm1 + 1))))
        Â· fun_prop
        Â· intro x
          simp only [Nat.succ_eq_add_one, pow_one]
          trans â€–xâ€– + â€–g iâ€–
          Â· apply norm_sub_le
          simp [mul_add, add_mul]
          trans 1 + (â€–xâ€– + â€–g iâ€–)
          Â· simp
          trans (1 + (â€–xâ€– + â€–g iâ€–)) + â€–xâ€– * â€–g iâ€–
          Â· simp
            positivity
          ring_nf
          rfl) (by
          simp only [Nat.succ_eq_add_one]
          use 1, (1 + â€–g iâ€–)
          intro x
          simp only [Nat.succ_eq_add_one, pow_one]
          apply (norm_le_norm_add_norm_sub' x (g i)).trans
          trans 1 + (â€–g iâ€– + â€–x - g iâ€–)
          Â· simp
          trans (1 + (â€–g iâ€– + â€–x - g iâ€–)) + â€–g iâ€– * â€–x - g iâ€–
          Â· simp
            positivity
          ring_nf
          rfl) Î·
    exact schwartzMap_mul_pow_integrable Î·' (p i) (p_bound i)

lemma IsDistBounded.schwartzMap_smul_integrable {dm1 : â„•}
    {Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)}
    {f : EuclideanSpace â„ (Fin dm1.succ) â†’ F}
    (hf : IsDistBounded f) [NormedSpace â„ F]
    (hae: AEStronglyMeasurable (fun x => f x) volume) :
    Integrable (fun x => Î· x â€¢ f x) volume := by
  rw [â† MeasureTheory.integrable_norm_iff]
  convert hf.schwartzMap_mul_integrable_norm (Î· := Î·) hae using 1
  funext x
  simp [norm_smul]
  fun_prop

@[fun_prop]
lemma IsDistBounded.schwartzMap_mul_integrable {dm1 : â„•} (f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„)
    (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => Î· x * f x) := by
  convert hf.schwartzMap_smul_integrable (Î· := Î·) hae using 1

@[fun_prop]
lemma IsDistBounded.integrable_fderviv_schwartzMap_mul {dm1 : â„•}
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ â„) (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume)
    (Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) (y : EuclideanSpace â„ (Fin dm1.succ)) :
    Integrable (fun x : EuclideanSpace â„ (Fin dm1.succ) => fderiv â„ Î· x y * f x) := by
  exact hf.schwartzMap_smul_integrable hae
    (Î· := ((SchwartzMap.evalCLM (ð•œ := â„) y) ((fderivCLM â„) Î·)))

/-!

## Integrability of 1/(1 + â€–xâ€–)
-/

lemma intergrable_pow {dm1 : â„•} (p: â„¤) (r : â„•) (p_bound : -dm1 â‰¤ p)
    (r_bound : (p + â†‘dm1).toNat + (invPowMeasure (dm1 := dm1)).integrablePower â‰¤ r)
    (v : EuclideanSpace â„ (Fin dm1.succ)) :
    Integrable (fun x => â€–x + vâ€– ^ p * â€–((1 + â€–xâ€–) ^ r)â»Â¹â€–) volume := by
  let m := (invPowMeasure (dm1 := dm1)).integrablePower
  have h0 (q : â„•) : Integrable (fun x => â€–xâ€– ^ q * â€–((1 + â€–x - vâ€–) ^ (q + m))â»Â¹â€–)
      invPowMeasure := by
    have hr1 (x : EuclideanSpace â„ (Fin dm1.succ)) :
        â€–((1 + â€–x - vâ€–) ^ (q + m))â»Â¹â€– = ((1 + â€–x - vâ€–) ^ (q + m))â»Â¹ := by
      simp only [Nat.succ_eq_add_one, norm_inv, norm_pow, Real.norm_eq_abs, inv_inj]
      rw [abs_of_nonneg (by positivity)]
    apply integrable_of_le_of_pow_mul_le (Câ‚ := 1) (Câ‚‚ :=2 ^ (q + m - 1) * (â€–vâ€– ^ (q + m) + 1))
    Â· simp
      intro x
      refine inv_le_one_of_one_leâ‚€ ?_
      rw [abs_of_nonneg (by positivity)]
      refine one_le_powâ‚€ ?_
      simp
    Â· intro x
      rw [hr1]
      refine mul_inv_le_of_le_mulâ‚€ ?_ (by positivity) ?_
      Â· positivity
      change â€–xâ€–^ (q + m) â‰¤ _
      by_cases hzero : m = 0 âˆ§ q = 0
      Â· rcases hzero with âŸ¨hm, hqâŸ©
        generalize hm : m = m' at *
        subst hm hq
        rw [pow_zero, pow_zero]
        simp
      trans (â€–vâ€– + â€–x - vâ€–) ^ (q + m)
      Â· rw [pow_le_pow_iff_leftâ‚€]
        Â· apply norm_le_norm_add_norm_sub'
        Â· positivity
        Â· positivity
        simp only [ne_eq, Nat.add_eq_zero, not_and]
        intro hq
        omega
      apply (add_pow_le _ _ _).trans
      trans 2 ^ (q + m - 1) * (â€–vâ€– ^ (q + m) + â€–x - vâ€– ^ (q + m)) + (2 ^ (q + m - 1)
        + 2 ^ (q + m - 1) * â€–vâ€– ^ (q + m) * â€–x - vâ€– ^ (q + m))
      Â· simp
        positivity
      trans (2 ^ (q + m - 1) * (â€–vâ€– ^ (q + m) + 1)) * (1 + â€–x - vâ€– ^ (q + m))
      Â· ring_nf
        apply le_of_eq
        rfl
      refine mul_le_mul_of_nonneg (by rfl) ?_ ?_ ?_
      Â· trans 1 ^ (q + m) + â€–x - vâ€– ^ (q + m)
        Â· simp
        apply pow_add_pow_le
        Â· simp
        Â· positivity
        Â· simp
          omega
      Â· positivity
      Â· positivity
      Â· positivity
      Â· positivity
    Â· refine Measurable.aestronglyMeasurable ?_
      fun_prop
  have h0' (q : â„•) :
      Integrable (fun x => â€–xâ€– ^ (q - dm1 : â„¤) * â€–((1 + â€–x - vâ€–) ^ (q + m))â»Â¹â€–) volume := by
    specialize h0 q
    rw [invPowMeasure] at h0
    rw [MeasureTheory.integrable_withDensity_iff] at h0
    apply Integrable.congr h0
    rw [Filter.eventuallyEq_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp only [Nat.succ_eq_add_one, norm_inv, norm_pow, Real.norm_eq_abs, one_div, inv_nonneg,
      norm_nonneg, pow_nonneg, ENNReal.toReal_ofReal]
    simp at hx
    field_simp
    have hx': â€–xâ€– â‰  0 := by simpa using hx
    rw [zpow_subâ‚€ hx']
    field_simp
    ring
    Â· fun_prop
    Â· filter_upwards with x
      simp
  have h2 (q : â„•) : Integrable (fun x => â€–x + vâ€– ^ (q - dm1 : â„¤)
      * â€–((1 + â€–xâ€–) ^ (q + (invPowMeasure (dm1 := dm1)).integrablePower))â»Â¹â€–) volume := by
    convert (h0' q).comp_add_right v using 1
    funext x
    simp [m]
  apply Integrable.mono' (h2 (p + dm1).toNat)
  Â· fun_prop
  rw [Filter.eventually_iff_exists_mem]
  use {0}á¶œ
  constructor
  Â· rw [compl_mem_ae_iff, measure_singleton]
  intro x hx
  refine norm_mul_le_of_le ?_ ?_
  simp only [Nat.succ_eq_add_one, norm_zpow, norm_norm, Int.ofNat_toNat]
  apply le_of_eq
  congr
  rw [max_eq_left]
  simp only [add_sub_cancel_right]
  omega
  simp only [Nat.succ_eq_add_one, norm_inv, norm_pow, Real.norm_eq_abs, abs_abs]
  refine inv_pow_le_inv_pow_of_le ?_ ?_
  Â· rw [abs_of_nonneg (by positivity)]
    simp
  Â· exact r_bound

lemma IsDistBounded.norm_inv_mul_exists_pow_integrable {dm1 : â„•}
    (f : EuclideanSpace â„ (Fin dm1.succ) â†’ F) (hf : IsDistBounded f)
    (hae: AEStronglyMeasurable (fun x => f x) volume) :
    âˆƒ r, Integrable (fun x => â€–f xâ€– * â€–((1 + â€–xâ€–) ^ r)â»Â¹â€–) volume := by
  rcases hf with âŸ¨n, c, g, p, c_nonneg, p_bound, hboundâŸ©
  induction' n with n ih
  Â· simp at hbound
    conv =>
      enter [1, r, 1, x]
      rw [hbound]
    simp
  let pMax := Finset.max' (Finset.image p Finset.univ) (by simp)
  have pMax_max (i : Fin n.succ) : p i â‰¤ pMax := by
    simp [pMax]
    apply Finset.le_max'
    simp
  use (pMax + â†‘dm1).toNat + (invPowMeasure (dm1 := dm1)).integrablePower
  apply Integrable.mono' (g := fun x => âˆ‘ i, c i * â€–x + g iâ€– ^ p i
      * â€–((1 + â€–xâ€–) ^ ((pMax + â†‘dm1).toNat + (invPowMeasure (dm1 := dm1)).integrablePower))â»Â¹â€–) _
  Â· apply AEStronglyMeasurable.mul
    Â· fun_prop
    Â· refine Measurable.aestronglyMeasurable ?_
      fun_prop
  Â· filter_upwards with x
    simp [Nat.succ_eq_add_one, norm_inv, norm_pow, Real.norm_eq_abs, norm_mul, abs_abs]
    rw [â† Finset.sum_mul]
    refine mul_le_mul_of_nonneg (hbound x) ?_ ?_ ?_
    Â· rfl
    Â· exact norm_nonneg (f x)
    Â· positivity
  apply MeasureTheory.integrable_finset_sum
  intro i _
  conv =>
    enter [1, x]
    rw [mul_assoc]
  apply Integrable.const_mul
  apply intergrable_pow (p i) _ (p_bound i)
  simp only [Nat.succ_eq_add_one, add_le_add_iff_right, Int.toNat_le, Int.ofNat_toNat, le_sup_iff]
  left
  exact pMax_max i

open InnerProductSpace

end Distribution
