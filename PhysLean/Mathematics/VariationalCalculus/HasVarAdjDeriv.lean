/-
Copyright (c) 2025 Tomas Skrivan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tomas Skrivan, Joseph Tooby-Smith
-/
import PhysLean.Mathematics.VariationalCalculus.HasVarAdjoint
/-!
# Variational adjoint derivative

Variational adjoint derivative of `F` at `u` is a generalization of `(fderiv â„ F u).adjoint`
to function spaces. In particular, variational gradient is an analog of
`gradient F u := (fderiv â„ F u).adjoint 1`.

The definition of `HasVarAdjDerivAt` is constructed exactly such that we can prove composition
theorem saying
```
  HasVarAdjDerivAt F F' (G u)) â†’ HasVarAdjDerivAt G G' u â†’
    HasVarAdjDerivAt (F âˆ˜ G) (G' âˆ˜ F') u
```
This theorem is the main tool to mechanistically compute variational gradient.
-/

open MeasureTheory ContDiff InnerProductSpace

variable
  {X} [NormedAddCommGroup X] [NormedSpace â„ X] [MeasureSpace X]
  {Y} [NormedAddCommGroup Y] [NormedSpace â„ Y] [MeasureSpace Y]
  {Z} [NormedAddCommGroup Z] [NormedSpace â„ Z] [MeasureSpace Z]
  {U} [NormedAddCommGroup U] [NormedSpace â„ U] [InnerProductSpace' â„ U]
  {V} [NormedAddCommGroup V] [NormedSpace â„ V] [InnerProductSpace' â„ V]
  {W} [NormedAddCommGroup W] [NormedSpace â„ W] [InnerProductSpace' â„ W]

/-- This is analogue of saying `F' = (fderiv â„ F u).adjoint`.

This definition is useful as we can prove composition theorem for it and `HasVarGradient F grad u`
can be computed by `grad := F' (fun _ => 1)`. -/
structure HasVarAdjDerivAt (F : (X â†’ U) â†’ (Y â†’ V)) (F' : (Y â†’ V) â†’ (X â†’ U)) (u : X â†’ U) : Prop
    where
  smooth_at : ContDiff â„ âˆž u
  diff : âˆ€ (Ï† : â„ â†’ X â†’ U), ContDiff â„ âˆž â†¿Ï† â†’
    ContDiff â„ âˆž (fun sx : â„Ã—Y => F (Ï† sx.1) sx.2)
  linearize :
    âˆ€ (Ï† : â„ â†’ X â†’ U), ContDiff â„ âˆž â†¿Ï† â†’
      âˆ€ x,
        deriv (fun s' : â„ => F (Ï† s') x) 0
        =
        deriv (fun s' : â„ => F (fun x => Ï† 0 x + s' â€¢ deriv (Ï† Â· x) 0) x) 0
  adjoint : HasVarAdjoint (fun Î´u x => deriv (fun s : â„ => F (fun x' => u x' + s â€¢ Î´u x') x) 0) F'

namespace HasVarAdjDerivAt

variable {Î¼ : Measure X}

lemma apply_smooth_of_smooth {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u v : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) (hv : ContDiff â„ âˆž v) : ContDiff â„ âˆž (F v) := by
  have h1 := h.diff (fun _ => v) (by fun_prop)
  simp at h1
  have hf : F v = (fun (sx : â„ Ã— X) => F v sx.2) âˆ˜ fun x => (0, x) := by
    funext x
    simp
  rw [hf]
  apply ContDiff.comp h1
  fun_prop

lemma apply_smooth_self {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) : ContDiff â„ âˆž (F u) := by
  exact h.apply_smooth_of_smooth (h.smooth_at)

lemma smooth_R {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) {Ï† : â„ â†’ X â†’ U} (hÏ† : ContDiff â„ âˆž â†¿Ï†) (x : X) :
    ContDiff â„ âˆž (fun s : â„ => F (fun x' => Ï† s x') x) :=
  (h.diff (fun s x => Ï† s x) hÏ†).comp (by fun_prop : ContDiff â„ âˆž fun s => (s,x))

lemma smooth_linear {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) {Ï† : â„ â†’ X â†’ U} (hÏ† : ContDiff â„ âˆž â†¿Ï†) :
    ContDiff â„ âˆž (fun s' : â„ => F (fun x => Ï† 0 x + s' â€¢ deriv (Ï† Â· x) 0) x) := by
  apply h.smooth_R (Ï† := (fun s' x => Ï† 0 x + s' â€¢ deriv (Ï† Â· x) 0))
  fun_prop [deriv]

lemma smooth_adjoint {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) {Î´u : X â†’ U} (h' : ContDiff â„ âˆž Î´u) (x : X) :
    ContDiff (E:= â„) (F := V) â„ âˆž ((fun s : â„ => F (fun x' => u x' + s â€¢ Î´u x') x)) := by
  have h1 : ((fun s : â„ => F (fun x' => u x' + s â€¢ Î´u x') x))
      = (fun sx : â„ Ã— X => F ((fun r x' => u x' + r â€¢ Î´u x') sx.1) sx.2) âˆ˜ (Â·, x) := by
    funext x
    simp
  rw [h1]
  apply ContDiff.comp
  Â· apply h.diff (Ï† := (fun r x' => u x' + r â€¢ Î´u x'))
    have hx := h.smooth_at
    fun_prop
  Â· fun_prop

lemma differentiable_linear {F : (X â†’ U) â†’ (X â†’ V)} {F' : (X â†’ V) â†’ (X â†’ U)} {u : X â†’ U}
    (h : HasVarAdjDerivAt F F' u) {Ï† : â„ â†’ X â†’ U} (hÏ† : ContDiff â„ âˆž â†¿Ï†) (x : X) :
    Differentiable â„ (fun s' : â„ => F (fun x => Ï† 0 x + s' â€¢ deriv (Ï† Â· x) 0) x) := by
  exact fun x => (h.smooth_linear hÏ†).differentiable (ENat.LEInfty.out) x

omit [MeasureSpace X] [InnerProductSpace' â„ U] [InnerProductSpace' â„ V] in
lemma linearize_of_linear {F : (X â†’ U) â†’ (X â†’ V)}
    (add : âˆ€ Ï†1 Ï†2 : X â†’ U,
    ContDiff â„ âˆž Ï†1 â†’ ContDiff â„ âˆž Ï†2 â†’ F (Ï†1 + Ï†2) = F Ï†1 + F Ï†2)
    (smul : âˆ€ (c : â„) (Ï† : X â†’ U), ContDiff â„ âˆž Ï† â†’ F (c â€¢ Ï†) = c â€¢ F Ï†)
    (deriv_comm : âˆ€ {Ï† : â„ â†’ X â†’ U} (_ : ContDiff â„ âˆž â†¿Ï†) (x : X),
      deriv (fun s' => F (Ï† s') x) 0 = F (fun x' => deriv (fun x => Ï† x x') 0) x)
    {Ï† : â„ â†’ X â†’ U}
    (hÏ† : ContDiff â„ âˆž â†¿Ï†) (x : X) :
    deriv (fun s' : â„ => F (Ï† s') x) 0
    =
    deriv (fun s' : â„ => F (fun x' => Ï† 0 x' + s' â€¢ deriv (Ï† Â· x') 0) x) 0 := by
  have h1 (s' : â„) : F (fun x' => Ï† 0 x' + s' â€¢ deriv (Ï† Â· x') 0) =
    F (fun x' => Ï† 0 x') + s' â€¢ F (fun x' => deriv (Ï† Â· x') 0) := by
    rw [â† smul, â† add]
    rfl
    Â· fun_prop
    Â· apply ContDiff.smul
      fun_prop
      conv =>
        enter [3, x]
        rw [â† fderiv_deriv]
      apply ContDiff.fderiv_apply (n := âˆž) (m := âˆž)
      fun_prop
      fun_prop
      fun_prop
      simp
    Â· conv =>
        enter [3, x]
        rw [â† fderiv_deriv]
      apply ContDiff.fderiv_apply (n := âˆž) (m := âˆž)
      repeat fun_prop
      simp
  conv_rhs =>
    enter [1, s]
    rw [h1]
  simp only [Pi.add_apply, Pi.smul_apply, differentiableAt_const, differentiableAt_id',
    DifferentiableAt.smul, deriv_add, deriv_const', zero_add]
  rw [deriv_smul_const]
  simp only [deriv_id'', one_smul]
  rw [deriv_comm hÏ† x]
  fun_prop

lemma deriv_adjoint_of_linear {F'} {F : (X â†’ U) â†’ (X â†’ V)}
    (add : âˆ€ Ï†1 Ï†2 : X â†’ U,
    ContDiff â„ âˆž Ï†1 â†’ ContDiff â„ âˆž Ï†2 â†’ F (Ï†1 + Ï†2) = F Ï†1 + F Ï†2)
    (smul : âˆ€ (c : â„) (Ï† : X â†’ U), ContDiff â„ âˆž Ï† â†’ F (c â€¢ Ï†) = c â€¢ F Ï†)
    (u : X â†’ U) (smooth : ContDiff â„ âˆž u)
    (h : HasVarAdjoint F F') :
    HasVarAdjoint (fun Î´u x => deriv (fun s : â„ => F (fun x' => u x' + s â€¢ Î´u x') x) 0) F' := by
  apply HasVarAdjoint.congr_fun h
  intro Ï† hÏ†
  funext x
  have h1 (s : â„) : F (fun x' => u x' + s â€¢ Ï† x')
    = F u + s â€¢ F Ï† := by
    rw [â† smul, â† add]
    rfl
    Â· fun_prop
    Â· apply ContDiff.smul
      fun_prop
      exact IsTestFunction.contDiff hÏ†
    Â· exact IsTestFunction.contDiff hÏ†
  conv_lhs =>
    enter [1, s]
    rw [h1]
  simp only [Pi.add_apply, Pi.smul_apply, differentiableAt_const, differentiableAt_id',
    DifferentiableAt.smul, deriv_add, deriv_const', zero_add]
  rw [deriv_smul_const]
  simp only [deriv_id'', one_smul]
  fun_prop

lemma hasVarAdjDerivAt_of_hasVarAdjoint_of_linear
    {F'} {F : (X â†’ U) â†’ (X â†’ V)}
    (diff : âˆ€ (Ï† : â„ â†’ X â†’ U), ContDiff â„ âˆž â†¿Ï† â†’
      ContDiff â„ âˆž (fun sx : â„Ã—X => F (Ï† sx.1) sx.2))

    (add : âˆ€ Ï†1 Ï†2 : X â†’ U,
    ContDiff â„ âˆž Ï†1 â†’ ContDiff â„ âˆž Ï†2 â†’ F (Ï†1 + Ï†2) = F Ï†1 + F Ï†2)
    (smul : âˆ€ (c : â„) (Ï† : X â†’ U), ContDiff â„ âˆž Ï† â†’ F (c â€¢ Ï†) = c â€¢ F Ï†)
    (deriv_comm : âˆ€ {Ï† : â„ â†’ X â†’ U} (_ : ContDiff â„ âˆž â†¿Ï†) (x : X),
      deriv (fun s' => F (Ï† s') x) 0 = F (fun x' => deriv (fun x => Ï† x x') 0) x)
    (u : X â†’ U) (smooth : ContDiff â„ âˆž u)
    (h : HasVarAdjoint F F') :
    HasVarAdjDerivAt F F' u where
  smooth_at := smooth
  diff := diff
  linearize := fun _ a x => linearize_of_linear add smul deriv_comm a x
  adjoint := deriv_adjoint_of_linear add smul u smooth h

lemma id (u) (hu : ContDiff â„ âˆž u) : HasVarAdjDerivAt (fun Ï† : X â†’ U => Ï†) (fun Ïˆ => Ïˆ) u where
  smooth_at := hu
  diff := by intros; fun_prop
  linearize := by intro Ï† hÏ† x; simp [deriv_smul_const]
  adjoint := by simp [deriv_smul_const]; apply HasVarAdjoint.id

lemma const (u : X â†’ U) (v : X â†’ V) (hu : ContDiff â„ âˆž u) (hv : ContDiff â„ âˆž v) :
    HasVarAdjDerivAt (fun _ : X â†’ U => v) (fun _ => 0) u where

  smooth_at := hu
  diff := by intros; fun_prop
  linearize := by simp
  adjoint := by simp; exact HasVarAdjoint.zero

lemma comp {F : (Y â†’ V) â†’ (Z â†’ W)} {G : (X â†’ U) â†’ (Y â†’ V)} {u : X â†’ U}
    {F' G'} (hF : HasVarAdjDerivAt F F' (G u)) (hG : HasVarAdjDerivAt G G' u) :
    HasVarAdjDerivAt (fun u => F (G u)) (fun Ïˆ => G' (F' Ïˆ)) u where
  smooth_at := hG.smooth_at
  diff := by
    intro Ï† hÏ†
    apply hF.diff (Ï† := fun t x => G (Ï† t) x)
    exact hG.diff Ï† hÏ†
  linearize := by
    intro Ï† hÏ† x
    rw[hF.linearize (fun t x => G (Ï† t) x) (hG.diff Ï† hÏ†)]
    rw[hF.linearize (fun s' => G fun x => Ï† 0 x + s' â€¢ deriv (fun x_1 => Ï† x_1 x) 0)]
    simp[hG.linearize Ï† hÏ†]
    eta_expand; simp[Function.HasUncurry.uncurry]
    apply hG.diff (Ï† := fun a x => Ï† 0 x + a â€¢ deriv (fun x_1 => Ï† x_1 x) 0)
    fun_prop [deriv]
  adjoint := by

    have : ContDiff â„ âˆž u := hG.smooth_at
    have h := hF.adjoint.comp hG.adjoint
    apply h.congr_fun
    intro Ï† hÏ†; funext x
    rw[hF.linearize]
    Â· simp
    Â· simp [Function.HasUncurry.uncurry];
      apply hG.diff (Ï† := (fun s x => u x + s â€¢ Ï† x))
      fun_prop

lemma unique_on_test_functions
    [IsFiniteMeasureOnCompacts (@volume X _)] [(@volume X _).IsOpenPosMeasure]
    [OpensMeasurableSpace X]
    (F : (X â†’ U) â†’ (Y â†’ V)) (u : X â†’ U)
    (F' G') (hF : HasVarAdjDerivAt F F' u) (hG : HasVarAdjDerivAt F G' u)
    (Ï† : Y â†’ V) (hÏ† : IsTestFunction Ï†) :
    F' Ï† = G' Ï† := HasVarAdjoint.unique_on_test_functions hF.adjoint hG.adjoint Ï† hÏ†

lemma unique {X : Type*} [NormedAddCommGroup X] [InnerProductSpace â„ X]
    [MeasureSpace X] [OpensMeasurableSpace X]
    [IsFiniteMeasureOnCompacts (@volume X _)] [(@volume X _).IsOpenPosMeasure]
    {Y : Type*} [NormedAddCommGroup Y] [InnerProductSpace â„ Y]
    [FiniteDimensional â„ Y] [MeasureSpace Y]
    {F : (X â†’ U) â†’ (Y â†’ V)} {u : X â†’ U}
    {F' G'} (hF : HasVarAdjDerivAt F F' u) (hG : HasVarAdjDerivAt F G' u)
    (Ï† : Y â†’ V) (hÏ† : ContDiff â„ âˆž Ï†) :
    F' Ï† = G' Ï† :=
  HasVarAdjoint.unique hF.adjoint hG.adjoint Ï† hÏ†

lemma prod [OpensMeasurableSpace X] [IsFiniteMeasureOnCompacts (volume (Î± := X))]
    {F : (X â†’ U) â†’ (X â†’ V)} {G : (X â†’ U) â†’ (X â†’ W)} {F' G'}
    (hF : HasVarAdjDerivAt F F' u) (hG : HasVarAdjDerivAt G G' u) :
    HasVarAdjDerivAt
      (fun Ï† x => (F Ï† x, G Ï† x))
      (fun Ï† x => F' (fun x' => (Ï† x').1) x + G' (fun x' => (Ï† x').2) x) u where
  smooth_at := hF.smooth_at
  diff := by
    intro Ï† hÏ†
    have hF' := hF.diff Ï† hÏ†
    have hG' := hG.diff Ï† hÏ†
    apply ContDiff.prodMk hF' hG'
  linearize := by
    intro Ï† hÏ† x
    rw [@Prod.eq_iff_fst_eq_snd_eq]
    constructor
    Â· rw [â† fderiv_deriv, â† fderiv_deriv, DifferentiableAt.fderiv_prodMk,
        DifferentiableAt.fderiv_prodMk]
      simp only [ContinuousLinearMap.prod_apply, fderiv_eq_smul_deriv, one_smul]
      rw [hF.linearize]
      Â· exact hÏ†
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        apply hF.smooth_R _ x
        conv => enter [3, 1, x, y]; rw [â† fderiv_deriv]
        fun_prop
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        apply hG.smooth_R _ x
        conv => enter [3, 1, x, y]; rw [â† fderiv_deriv]
        fun_prop
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact smooth_R hF hÏ† x
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact smooth_R hG hÏ† x
    Â· rw [â† fderiv_deriv, â† fderiv_deriv, DifferentiableAt.fderiv_prodMk,
        DifferentiableAt.fderiv_prodMk]
      simp only [ContinuousLinearMap.prod_apply, fderiv_eq_smul_deriv, one_smul]
      rw [hG.linearize]
      Â· exact hÏ†
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact hF.smooth_linear hÏ†
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact hG.smooth_linear hÏ†
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact smooth_R hF hÏ† x
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        exact smooth_R hG hÏ† x
  adjoint := by
    apply HasVarAdjoint.congr_fun
      (G := fun Î´u x => (deriv (fun s => F (fun x' => u x' + s â€¢ Î´u x') x) (0 : â„),
        deriv (fun s => G (fun x' => u x' + s â€¢ Î´u x') x) (0 : â„)))
    apply HasVarAdjoint.prod
    Â· exact hF.adjoint
    Â· exact hG.adjoint
    intro Ï† hÏ†
    funext x
    rw [â† fderiv_deriv, â† fderiv_deriv, DifferentiableAt.fderiv_prodMk]
    simp only [ContinuousLinearMap.prod_apply, fderiv_eq_smul_deriv, one_smul]
    Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
      apply hF.smooth_adjoint
      exact hÏ†.contDiff
    Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
      apply hG.smooth_adjoint
      exact IsTestFunction.contDiff hÏ†

lemma fst {F : (X â†’ U) â†’ (X â†’ WÃ—V)}
    (hF : HasVarAdjDerivAt F F' u) :
    HasVarAdjDerivAt
      (fun Ï† x => (F Ï† x).1)
      (fun Ï† x => F' (fun x' => (Ï† x', 0)) x) u where
  smooth_at := hF.smooth_at
  diff := fun Ï† _ => ContDiff.comp contDiff_fst (hF.diff Ï† (by fun_prop))
  linearize := by
    intro Ï† hÏ† x
    have h1 := hF.linearize Ï† hÏ† x
    rw [â† fderiv_deriv, fderiv_comp']
    simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv, one_smul]
    rw [h1, fderiv_fst]
    simp only [ContinuousLinearMap.coe_fst']
    conv_rhs =>
      rw [â† fderiv_deriv]
    rw [fderiv_comp' _ (by fun_prop)]
    simp [fderiv_fst]
    Â· apply ContDiff.differentiable (n := âˆž) (hF.smooth_linear hÏ†) (by simp)
    Â· fun_prop
    Â· apply ContDiff.differentiable (n := âˆž) (hF.smooth_R hÏ† x) (by simp)
  adjoint := by
    apply HasVarAdjoint.congr_fun
      (G := (fun Î´u x => (deriv (fun s => (F (fun x' => u x' + s â€¢ Î´u x') x)) (0 :â„)).1))
    Â· exact HasVarAdjoint.fst hF.adjoint
    Â· intro Ï† hÏ†
      funext x
      rw [â† fderiv_deriv, fderiv_comp', fderiv_fst]
      simp only [ContinuousLinearMap.coe_comp', ContinuousLinearMap.coe_fst', Function.comp_apply,
        fderiv_eq_smul_deriv, one_smul]
      fun_prop
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        apply hF.smooth_adjoint
        exact IsTestFunction.contDiff hÏ†

lemma snd {F : (X â†’ U) â†’ (X â†’ WÃ—V)}
    (hF : HasVarAdjDerivAt F F' u) :
    HasVarAdjDerivAt
      (fun Ï† x => (F Ï† x).2)
      (fun Ï† x => F' (fun x' => (0, Ï† x')) x) u where
  smooth_at := hF.smooth_at
  diff := fun Ï† _ => ContDiff.comp contDiff_snd (hF.diff Ï† (by fun_prop))
  linearize := by
    intro Ï† hÏ† x
    have h1 := hF.linearize Ï† hÏ† x
    rw [â† fderiv_deriv, fderiv_comp']
    simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv, one_smul]
    rw [h1, fderiv_snd]
    simp only [ContinuousLinearMap.coe_snd']
    conv_rhs =>
      rw [â† fderiv_deriv]
    rw [fderiv_comp' _ (by fun_prop)]
    simp [fderiv_snd]
    Â· apply ContDiff.differentiable (n := âˆž) (hF.smooth_linear hÏ†) (by simp)
    Â· fun_prop
    Â· apply ContDiff.differentiable (n := âˆž) (hF.smooth_R hÏ† x) (by simp)
  adjoint := by
    apply HasVarAdjoint.congr_fun
      (G := (fun Î´u x => (deriv (fun s => (F (fun x' => u x' + s â€¢ Î´u x') x)) (0 :â„)).2))
    Â· exact HasVarAdjoint.snd hF.adjoint
    Â· intro Ï† hÏ†
      funext x
      rw [â† fderiv_deriv, fderiv_comp', fderiv_snd]
      simp only [ContinuousLinearMap.coe_comp', ContinuousLinearMap.coe_snd', Function.comp_apply,
        fderiv_eq_smul_deriv, one_smul]
      fun_prop
      Â· apply ContDiff.differentiable (n := âˆž) _ (by simp)
        apply hF.smooth_adjoint
        exact IsTestFunction.contDiff hÏ†

attribute [fun_prop] differentiableAt_id'

lemma deriv' (u : â„ â†’ U) (hu : ContDiff â„ âˆž u) :
    HasVarAdjDerivAt (fun Ï† : â„ â†’ U => deriv Ï†) (fun Ï† x => - deriv Ï† x) u where
  smooth_at := hu
  diff := by intros; fun_prop [deriv]
  linearize := by
    intro Ï† hÏ† x
    conv_rhs =>
      enter [1, s']
      rw [deriv_add (by
        apply function_differentiableAt_snd
        exact hÏ†.differentiable (by simp)) (by
        apply Differentiable.const_smul
        conv =>
          enter [2, x]
          rw [â† fderiv_deriv]
        apply fderiv_uncurry_differentiable_fst_comp_snd_apply
        apply ContDiff.of_le hÏ†
        exact ENat.LEInfty.out)]
      rw [deriv_const_smul _ (by
        conv =>
          enter [2, x]
          rw [â† fderiv_deriv]
        refine Differentiable.differentiableAt ?_
        apply fderiv_uncurry_differentiable_fst_comp_snd_apply
        apply ContDiff.of_le hÏ†
        exact ENat.LEInfty.out)]
    simp only [differentiableAt_const, differentiableAt_id', DifferentiableAt.smul, deriv_add,
      deriv_const', zero_add]
    rw [deriv_smul_const]
    simp only [deriv_id'', one_smul]
    rw [â† fderiv_deriv]
    conv_lhs =>
      enter [1, 2, s]
      rw [â† fderiv_deriv]
    rw [fderiv_swap]
    simp only [fderiv_eq_smul_deriv, one_smul]
    Â· apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
    Â· exact differentiableAt_id'
  adjoint := by
    apply HasVarAdjoint.congr_fun (G := (fun Î´u x => deriv (fun x' => Î´u x') x))
    Â· exact HasVarAdjoint.deriv
    Â· intro Ï† hÏ†
      funext x
      have := hÏ†.smooth.differentiable (ENat.LEInfty.out)
      have := hu.differentiable (ENat.LEInfty.out)
      simp (disch:=fun_prop) [deriv_add]
      conv_lhs =>
        enter [1, x]
        rw [deriv_const_smul _ (by fun_prop)]
      rw [deriv_smul_const]
      simp only [deriv_id'', one_smul]
      fun_prop

protected lemma deriv (F : (â„ â†’ U) â†’ (â„ â†’ V)) (F') (u) (hF : HasVarAdjDerivAt F F' u) :
    HasVarAdjDerivAt (fun Ï† : â„ â†’ U => deriv (F Ï†))
    (fun Ïˆ x => F' (fun x' => - deriv Ïˆ x') x) u :=
  comp (F:=deriv) (G:=F) (hF := deriv' (F u) hF.apply_smooth_self) (hG := hF)

lemma fmap
    {U} [NormedAddCommGroup U] [NormedSpace â„ U] [InnerProductSpace' â„ U]
    {V} [NormedAddCommGroup V] [NormedSpace â„ V] [InnerProductSpace' â„ V]
    [CompleteSpace U] [CompleteSpace V]
    (f : X â†’ U â†’ V) {f' : X â†’ U â†’ _ }
    (u : X â†’ U) (hu : ContDiff â„ âˆž u)
    (hf' : ContDiff â„ âˆž â†¿f) (hf : âˆ€ x u, HasAdjFDerivAt â„ (f x) (f' x u) u) :
    HasVarAdjDerivAt (fun (Ï† : X â†’ U) x => f x (Ï† x)) (fun Ïˆ x => f' x (u x) (Ïˆ x)) u where
  smooth_at := hu
  diff := by fun_prop
  linearize := by
    intro Ï† hÏ† x
    unfold deriv
    conv => lhs; rw[fderiv_comp' (ð•œ:=â„) (g:=(fun u : U => f _ u)) _
            (by fun_prop (config:={maxTransitionDepth:=3}) (disch:=aesop))
            (by fun_prop (config:={maxTransitionDepth:=3}) (disch:=aesop))]
    conv => rhs; rw[fderiv_comp' (ð•œ:=â„) (g:=(fun u : U => f _ u)) _
            (by fun_prop (config:={maxTransitionDepth:=3}) (disch:=aesop)) (by fun_prop)]
    simp[deriv_smul]
  adjoint := by
    apply HasVarAdjoint.congr_fun
    case h' =>
      intro Ï† hÏ†; funext x
      unfold deriv
      conv =>
        lhs
        rw[fderiv_comp' (ð•œ:=â„) (g:=_) (f:=fun s : â„ => u x + s â€¢ Ï† x) _
          (by fun_prop (config:={maxTransitionDepth:=3}) (disch:=aesop)) (by fun_prop)]
        simp[deriv_smul]
    case h =>
      constructor
      Â· intros;
        constructor
        Â· fun_prop
        Â· expose_names
          rw [â† exists_compact_iff_hasCompactSupport]
          have h1 := h.supp
          rw [â† exists_compact_iff_hasCompactSupport] at h1
          obtain âŸ¨K, cK, hKâŸ© := h1
          refine âŸ¨K, cK, ?_âŸ©
          intro x hx
          rw [hK x hx]
          simp
      Â· intro Ï† hÏ†
        constructor
        Â· apply ContDiff.fun_comp
            (g:= fun x : XÃ—UÃ—V => f' x.1 x.2.1 x.2.2)
            (f:= fun x => (x, u x, Ï† x))
          Â· apply HasAdjFDerivAt.contDiffAt_deriv <;> assumption
          Â· fun_prop
        Â· rw [â† exists_compact_iff_hasCompactSupport]
          have h1 := hÏ†.supp
          rw [â† exists_compact_iff_hasCompactSupport] at h1
          obtain âŸ¨K, cK, hKâŸ© := h1
          refine âŸ¨K, cK, ?_âŸ©
          intro x hx
          rw [hK x hx]
          have hfx := (hf x (u x)).hasAdjoint_fderiv
          exact HasAdjoint.adjoint_apply_zero hfx
      Â· intros
        congr 1; funext x
        rw[â† PreInnerProductSpace.Core.conj_inner_symm]
        rw[â† (hf x (u x)).hasAdjoint_fderiv.adjoint_inner_left]
        rw[PreInnerProductSpace.Core.conj_inner_symm]
      Â· intros K cK; use K; simp_all

lemma neg (F : (X â†’ U) â†’ (X â†’ V)) (F') (u) (hF : HasVarAdjDerivAt F F' u) :
    HasVarAdjDerivAt (fun Ï† x => -F Ï† x) (fun Ïˆ x => - F' Ïˆ x) u where
  smooth_at := hF.smooth_at
  diff := by intro Ï† hÏ†; apply ContDiff.neg; apply hF.diff; assumption
  linearize := by
    intros
    rw[deriv.neg']
    simp only [deriv.neg', neg_inj]
    rw[hF.linearize]
    assumption
  adjoint := by
    apply HasVarAdjoint.congr_fun
    case h' =>
      intro Ï† hÏ†; funext x
      have := hÏ†.smooth; have := hF.smooth_at
      conv =>
        lhs
        rw[deriv.neg']
        simp [hF.linearize (fun s x' => u x' + s â€¢ Ï† x') (by fun_prop)]
        simp[deriv_smul_const]
    case h =>
      apply HasVarAdjoint.neg
      apply hF.adjoint

section OnFiniteMeasures

variable [OpensMeasurableSpace X] [IsFiniteMeasureOnCompacts (@volume X _)]

lemma add
    (F G : (X â†’ U) â†’ (X â†’ V)) (F' G') (u)
    (hF : HasVarAdjDerivAt F F' u) (hG : HasVarAdjDerivAt G G' u) :
    HasVarAdjDerivAt (fun Ï† x => F Ï† x + G Ï† x) (fun Ïˆ x => F' Ïˆ x + G' Ïˆ x) u where
  smooth_at := hF.smooth_at
  diff := by
    intro Ï† hÏ†
    apply ContDiff.add
    Â· apply hF.diff; assumption
    Â· apply hG.diff; assumption
  linearize := by
    intro Ï† hÏ† x; rw[deriv_add]; rw[deriv_add]; rw[hF.linearize _ hÏ†, hG.linearize _ hÏ†]
    Â· exact hF.differentiable_linear hÏ† x 0
    Â· exact hG.differentiable_linear hÏ† x 0
    Â· apply ContDiff.differentiable _ ENat.LEInfty.out
      have hf := hF.diff Ï† hÏ†
      change ContDiff â„ âˆž ((fun sx : â„ Ã— X => F (Ï† sx.1) sx.2) âˆ˜ fun s' => (s', x))
      apply ContDiff.comp hf
      fun_prop
    Â· apply ContDiff.differentiable _ ENat.LEInfty.out
      have hf := hG.diff Ï† hÏ†
      change ContDiff â„ âˆž ((fun sx : â„ Ã— X => G (Ï† sx.1) sx.2) âˆ˜ fun s' => (s', x))
      apply ContDiff.comp hf
      fun_prop
  adjoint := by
    apply HasVarAdjoint.congr_fun
    case h' =>
      intro Ï† hÏ†; funext x
      have := hÏ†.smooth; have := hF.smooth_at
      have h1 : DifferentiableAt â„ (fun s => F (fun x' => u x' + s â€¢ Ï† x') x) (0 : â„) :=
        (hF.smooth_R (Ï†:=fun s x' => u x' + s â€¢ Ï† x') (by fun_prop) x)
          |>.differentiable ENat.LEInfty.out 0
      have h2 : DifferentiableAt â„ (fun s => G (fun x' => u x' + s â€¢ Ï† x') x) (0 : â„) :=
        (hG.smooth_R (Ï†:=fun s x' => u x' + s â€¢ Ï† x') (by fun_prop) x)
          |>.differentiable ENat.LEInfty.out 0
      conv =>
        lhs
        rw[deriv_add h1 h2]
        simp [hF.linearize (fun s x' => u x' + s â€¢ Ï† x') (by fun_prop)]
        simp [hG.linearize (fun s x' => u x' + s â€¢ Ï† x') (by fun_prop)]
        simp[deriv_smul_const]
    case h =>
      apply HasVarAdjoint.add
      apply hF.adjoint
      apply hG.adjoint

lemma mul
    (F G : (X â†’ â„) â†’ (X â†’ â„)) (F' G') (u)
    (hF : HasVarAdjDerivAt F F' u) (hG : HasVarAdjDerivAt G G' u) :
    HasVarAdjDerivAt (fun Ï† x => F Ï† x * G Ï† x)
      (fun Ïˆ x => F' (fun x' => Ïˆ x' * G u x') x + G' (fun x' => F u x' * Ïˆ x') x) u where
  smooth_at := hF.smooth_at
  diff := by
    intro Ï† hÏ†
    apply ContDiff.mul
    Â· apply hF.diff; assumption
    Â· apply hG.diff; assumption
  linearize := by
    intro Ï† hÏ† x; rw[deriv_mul]; rw[deriv_mul]; rw[hF.linearize _ hÏ†, hG.linearize _ hÏ†]; simp
    Â· exact hF.differentiable_linear hÏ† x 0
    Â· exact hG.differentiable_linear hÏ† x 0
    Â· apply ContDiff.differentiable _ ENat.LEInfty.out
      have hf := hF.diff Ï† hÏ†
      change ContDiff â„ âˆž ((fun sx : â„ Ã— X => F (Ï† sx.1) sx.2) âˆ˜ fun s' => (s', x))
      apply ContDiff.comp hf
      fun_prop
    Â· apply ContDiff.differentiable _ ENat.LEInfty.out
      have hf := hG.diff Ï† hÏ†
      change ContDiff â„ âˆž ((fun sx : â„ Ã— X => G (Ï† sx.1) sx.2) âˆ˜ fun s' => (s', x))
      apply ContDiff.comp hf
      fun_prop
  adjoint := by
    apply HasVarAdjoint.congr_fun
    case h' =>
      intro Ï† hÏ†; funext x
      have := hÏ†.smooth; have := hF.smooth_at
      -- Same two results as the `add` case
      have h1 : DifferentiableAt â„ (fun s => F (fun x' => u x' + s â€¢ Ï† x') x) (0 : â„) :=
        (hF.smooth_R (Ï†:=fun s x' => u x' + s â€¢ Ï† x') (by fun_prop) x)
          |>.differentiable ENat.LEInfty.out 0
      have h2 : DifferentiableAt â„ (fun s => G (fun x' => u x' + s â€¢ Ï† x') x) (0 : â„) :=
        (hG.smooth_R (Ï†:=fun s x' => u x' + s â€¢ Ï† x') (by fun_prop) x)
          |>.differentiable ENat.LEInfty.out 0
      conv =>
        lhs
        rw[deriv_mul h1 h2]
        simp [hF.linearize (fun s x' => u x' + s â€¢ Ï† x') (by fun_prop)]
        simp [hG.linearize (fun s x' => u x' + s â€¢ Ï† x') (by fun_prop)]
    case h =>
      apply HasVarAdjoint.add
      Â· apply HasVarAdjoint.mul_right
        apply hF.adjoint
        exact apply_smooth_self hG
      Â· apply HasVarAdjoint.mul_left
        apply hG.adjoint
        exact apply_smooth_self hF

omit [OpensMeasurableSpace X] [IsFiniteMeasureOnCompacts (@volume X _)] in
protected lemma fderiv (u : X â†’ U) (dx : X) (hu : ContDiff â„ âˆž u)
    [ProperSpace X] [BorelSpace X]
    [FiniteDimensional â„ X] [(@volume X _).IsAddHaarMeasure]:
    HasVarAdjDerivAt
      (fun (Ï† : X â†’ U) x => fderiv â„ Ï† x dx)
      (fun Ïˆ x => - fderiv â„ Ïˆ x dx) u := by
  apply hasVarAdjDerivAt_of_hasVarAdjoint_of_linear
  Â· intros; fun_prop [fderiv]
  Â· intro Ï†1 Ï†2 h1 h2
    funext x
    simp only [Pi.add_apply]
    erw [fderiv_add]
    simp only [ContinuousLinearMap.add_apply]
    Â· exact (h1.differentiable (by simp)).differentiableAt
    Â· exact (h2.differentiable (by simp)).differentiableAt
  Â· intro c Ï† hÏ†
    funext x
    simp only [Pi.smul_apply]
    erw [fderiv_const_smul]
    simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply]
    exact (hÏ†.differentiable (by simp)).differentiableAt
  Â· intro Ï† hÏ† x
    rw [â† fderiv_deriv]
    rw [fderiv_swap]
    simp only [fderiv_eq_smul_deriv, one_smul]
    Â· apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
  Â· exact hu
  Â· exact HasVarAdjoint.fderiv_apply

protected lemma gradient {d} (u : Space d â†’ â„) (hu : ContDiff â„ âˆž u) :
    HasVarAdjDerivAt
      (fun (Ï† : Space d â†’ â„) x => gradient Ï† x)
      (fun Ïˆ x => - Space.div Ïˆ x) u := by
  apply hasVarAdjDerivAt_of_hasVarAdjoint_of_linear
  Â· intro Ï† hÏ†
    simp [â† Space.grad_eq_gradiant, Space.grad_eq_sum]
    apply ContDiff.sum
    intro i _
    apply ContDiff.smul
    simp [Space.deriv]
    fun_prop
    fun_prop
  Â· intro Ï†1 Ï†2 h1 h2
    rw [â† Space.grad_eq_gradiant]
    rw [Space.grad_add, Space.grad_eq_gradiant, Space.grad_eq_gradiant]
    Â· exact h1.differentiable (by simp)
    Â· exact h2.differentiable (by simp)
  Â· intro c Ï† hÏ†
    rw [â† Space.grad_eq_gradiant]
    rw [Space.grad_smul, Space.grad_eq_gradiant]
    exact hÏ†.differentiable (by simp)
  Â· intro Ï† hÏ† x
    rw [â† Space.grad_eq_gradiant, Space.grad_eq_sum]
    conv_lhs => enter [1, x]; rw [â† Space.grad_eq_gradiant, Space.grad_eq_sum]
    rw [deriv_sum]
    congr
    funext i
    rw [deriv_smul_const]
    congr
    simp [Space.deriv]
    rw [â† fderiv_deriv]
    rw [fderiv_swap]
    simp only [fderiv_eq_smul_deriv, smul_eq_mul, one_mul]
    Â· apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
    Â· simp [Space.deriv]
      apply Differentiable.differentiableAt
      apply fderiv_uncurry_differentiable_snd_comp_fst_apply
      apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
    Â· intro i _
      apply Differentiable.differentiableAt
      apply Differentiable.smul_const
      simp [Space.deriv]
      apply fderiv_uncurry_differentiable_snd_comp_fst_apply
      apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
  Â· exact hu
  Â· exact HasVarAdjoint.gradient

lemma div {d} (u : Space d â†’ Space d) (hu : ContDiff â„ âˆž u) :
    HasVarAdjDerivAt
      (fun (Ï† : Space d â†’ Space d) x => Space.div Ï† x)
      (fun Ïˆ x => - gradient Ïˆ x) u := by
  apply hasVarAdjDerivAt_of_hasVarAdjoint_of_linear
  Â· intro Ï† hÏ†
    simp [Space.div]
    apply ContDiff.sum
    intro i _
    simp_rw [Space.deriv]
    fun_prop
  Â· intro Ï†1 Ï†2 h1 h2
    apply Space.div_add
    Â· exact h1.differentiable (by simp)
    Â· exact h2.differentiable (by simp)
  Â· intro c Ï† hÏ†
    apply Space.div_smul
    exact hÏ†.differentiable (by simp)
  Â· intro Ï† hÏ† x
    simp [Space.div]
    rw [deriv_sum]
    congr
    funext i
    simp [Space.deriv]
    rw [â† fderiv_deriv]
    rw [fderiv_swap]
    simp only [fderiv_eq_smul_deriv, smul_eq_mul, one_mul]
    congr
    funext y
    trans deriv (fun x' => Space.coordCLM i (Ï† x' y)) 0
    simp [Space.coordCLM_apply]
    rw [â† fderiv_deriv, fderiv_comp']
    simp [Space.coordCLM_apply]
    Â· fun_prop
    Â· apply function_differentiableAt_fst
      exact hÏ†.differentiable (by simp)
    Â· apply ContDiff.comp (g := Space.coord i)
      Â· change ContDiff â„ 2 (Space.coordCLM i)
        fun_prop
      Â· apply ContDiff.of_le hÏ†
        exact ENat.LEInfty.out
    Â· intro i _
      apply Differentiable.differentiableAt
      simp [Space.deriv]
      have h1 (s' : â„) : (fderiv â„ (fun x => Space.coord i (Ï† s' x)) x) =
          Space.coordCLM i âˆ˜L (fderiv â„ (fun x' => Ï† s' x') x) := by
        trans (fderiv â„ (fun x => Space.coordCLM i (Ï† s' x)) x)
        rfl
        rw [fderiv_comp']
        simp only [ContinuousLinearMap.fderiv]
        fun_prop
        apply function_differentiableAt_snd
        exact hÏ†.differentiable (by simp)
      conv =>
        enter [2, s]
        rw [h1]
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
      apply Differentiable.comp
      Â· fun_prop
      apply fderiv_uncurry_differentiable_snd_comp_fst_apply
      apply ContDiff.of_le hÏ†
      exact ENat.LEInfty.out
  Â· exact hu
  Â· exact HasVarAdjoint.div
