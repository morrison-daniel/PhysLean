/-
Copyright (c) 2025 Matteo Cipollina. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina
-/
import Mathlib.Analysis.Complex.Polynomial.Basic
import Mathlib.Analysis.Normed.Algebra.MatrixExponential
import PhysLean.Mathematics.DataStructures.Matrix.SchurTriangulation

/-!
# Lie's Trace Formula

This file proves the formula `det (exp A) = exp (tr A)` for matrices, also known as Lie's trace
formula.

## Main results

- `Matrix.det_exp`: The determinant of the exponential of a matrix is the exponential of its trace.
-/

/-!
We give a higher priority to the canonical `1` and `*` instances coming
from `Mathlib` so that every occurrence of `1 : Matrix _ _ _` and
`A * B` uses the very same definitions.
-/
attribute [instance 100] Matrix.instHMulOfFintypeOfMulOfAddCommMonoid
attribute [instance 100] Matrix.one
namespace Matrix
namespace IsUpperTriangular
variable {Œ± k n R ùïÇ m : Type*}
variable [LinearOrder n][CommRing Œ±]
variable [SMulZeroClass R Œ±]

/-- Scalar multiplication preserves the property of being upper-triangular. -/
lemma smul
    {A : Matrix n n Œ±} (hA : IsUpperTriangular A) (k : R) :
    IsUpperTriangular (k ‚Ä¢ A) := by
  intro i j hij
  simp [smul_apply, hA hij]

/-- The identity matrix is upper-triangular. -/
lemma one  : IsUpperTriangular (1 : Matrix n n Œ±) := by
  intro i j hij
  simp [one_apply, (ne_of_lt hij).symm];
  intro a
  subst a
  simp_all only [id_eq, lt_self_iff_false]

variable [Fintype n]
variable [SMulZeroClass R Œ±]

/-- The product of two upper-triangular matrices is upper-triangular. -/
lemma mul  {A B : Matrix n n Œ±}
    (hA : IsUpperTriangular A) (hB : IsUpperTriangular B) : IsUpperTriangular (A * B) := by
  intro i j hij
  rw [mul_apply]
  apply Finset.sum_eq_zero
  intro l _
  by_cases h‚ÇÅ : i ‚â§ l
  ¬∑ by_cases h‚ÇÇ : l ‚â§ j
    ¬∑ exfalso; exact not_lt_of_ge (le_trans h‚ÇÅ h‚ÇÇ) hij
    ¬∑ rw [not_le] at h‚ÇÇ; exact hB h‚ÇÇ ‚ñ∏ mul_zero (A i l)
  ¬∑ rw [not_le] at h‚ÇÅ; exact hA h‚ÇÅ ‚ñ∏ zero_mul (B l j)

/-- Powers of an upper-triangular matrix are upper-triangular. -/
lemma pow  {A : Matrix n n Œ±}
    (hA : IsUpperTriangular A) (k : ‚Ñï) : IsUpperTriangular (A ^ k) := by
  induction k with
  | zero =>
      rw [pow_zero]
      exact one
  | succ k ih =>
      rw [pow_succ]
      exact IsUpperTriangular.mul ih hA

lemma diag_mul_of {A B : Matrix n n Œ±}
    (hA : IsUpperTriangular A) (hB : IsUpperTriangular B) : (A * B).diag = A.diag * B.diag := by
  ext i
  simp only [diag_apply, mul_apply, Pi.mul_apply]
  have sum_eq : ‚àë j ‚àà Finset.univ, A i j * B j i = A i i * B i i := by
    apply Finset.sum_eq_single i
    ¬∑ intro j _ j_ne_i
      cases lt_or_gt_of_ne j_ne_i with
      | inl h => -- j < i
        rw [hA h, zero_mul]
      | inr h => -- j > i
        rw [hB h, mul_zero]
    ¬∑ intro hi_not_in
      exact absurd (Finset.mem_univ i) hi_not_in
  rw [sum_eq]

lemma diag_pow_of {A : Matrix n n Œ±}
    (hA : IsUpperTriangular A) (k : ‚Ñï) : (A ^ k).diag = A.diag ^ k := by
  induction k with
  | zero =>
      rw [pow_zero, pow_zero]
      ext i
      simp [diag_one]
  | succ k ih =>
      rw [pow_succ, pow_succ]
      have h_mul : (A ^ k * A).diag = (A ^ k).diag * A.diag :=
        diag_mul_of (pow hA k) hA
      rw [h_mul, ih]

end IsUpperTriangular

open scoped BigOperators Topology

instance [UniformSpace ùïÇ] : UniformSpace (Matrix m n ùïÇ) := by unfold Matrix; infer_instance

/-- If every term of a series is zero, then its sum is zero. -/
lemma tsum_eq_zero
    {Œ≤ : Type*} [TopologicalSpace Œ≤] [AddCommMonoid Œ≤]
    {f : ‚Ñï ‚Üí Œ≤} (h : ‚àÄ n, f n = 0) :
    (‚àë' n, f n) = 0 := by
  rw [‚Üê h Nat.zero]
  simp_all only [tsum_zero, Nat.zero_eq]

/-!
 ### The determinant of the matrix exponential
 -/
section DetExp

open IsUpperTriangular

variable [RCLike ùïÇ]--[IsAlgClosed ùïÇ] [Fintype m] [DecidableEq m] [LinearOrder m]
      --[LinearOrder ùïÇ] [Fintype ùïÇ]

/-- Apply a matrix `tsum` to a given entry.                                        -/
lemma matrix_tsum_apply
    {f : ‚Ñï ‚Üí Matrix m m ùïÇ} (hf : Summable f) (i j : m) :
    (‚àë' n, f n) i j = ‚àë' n, (f n) i j := by
  have h_row_summable : Summable (fun n ‚Ü¶ (f n) i) := by
    have h := Pi.summable.1 hf
    exact h i
  have h‚ÇÅ : ((‚àë' n, f n) : Matrix m m ùïÇ) i = (‚àë' n, (f n) i) := by
    exact tsum_apply hf
  have h‚ÇÇ : ((‚àë' n, (f n) i) : m ‚Üí ùïÇ) j = (‚àë' n, (f n) i j) := by
    exact tsum_apply h_row_summable
  rw [h‚ÇÅ, h‚ÇÇ]

private lemma Finset.prod_exp_eq_exp_sum [LinearOrder m] (s : Finset m) (f : m ‚Üí ùïÇ) :
    ‚àè i ‚àà s, NormedSpace.exp ùïÇ (f i) = NormedSpace.exp ùïÇ (‚àë i ‚àà s, f i) := by
  letI : CompleteSpace ùïÇ := by infer_instance
  induction' s using Finset.induction with a s ha ih
  ¬∑ simp [NormedSpace.exp_zero]
  ¬∑ rw [Finset.prod_insert ha, Finset.sum_insert ha, NormedSpace.exp_add, ih]

variable [Fintype m] [LinearOrder m]

attribute [local instance] Matrix.linftyOpNormedAlgebra
attribute [local instance] Matrix.linftyOpNormedRing
attribute [local instance] Matrix.instCompleteSpace

/-- Summability of the exponential series for matrices -/
lemma summable_exp_series [CompleteSpace ùïÇ] (A : Matrix m m ùïÇ) :
    Summable (fun n => ((n.factorial : ùïÇ)‚Åª¬π) ‚Ä¢ (A ^ n)) := by
  letI : NormedAddCommGroup (Matrix m m ùïÇ) := Matrix.linftyOpNormedAddCommGroup
  letI : NormedSpace ùïÇ (Matrix m m ùïÇ) := Matrix.linftyOpNormedSpace
  exact NormedSpace.expSeries_summable' A

lemma isUpperTriangular_exp_of_isUpperTriangular
    {A : Matrix m m ùïÇ} (hA : A.IsUpperTriangular) :
    (NormedSpace.exp ùïÇ A).IsUpperTriangular := by
  intro i j hij
  rw [NormedSpace.exp_eq_tsum]
  let exp_series := fun n ‚Ü¶ ((n.factorial : ùïÇ)‚Åª¬π) ‚Ä¢ (A ^ n)
  change (‚àë' (n : ‚Ñï), exp_series n) i j = 0
  rw [matrix_tsum_apply (summable_exp_series A) i j]
  apply tsum_eq_zero
  intro n
  have h_entry : (A ^ n) i j = 0 := by
    convert (IsUpperTriangular.pow hA n) hij
  simp [exp_series, smul_apply, h_entry]

/--
For an upper‚Äìtriangular matrix `A`, the `(i,i)` entry of the power `A ^ n`
is simply the `n`-th power of the original diagonal entry.
-/
lemma diag_pow_entry_eq_pow_diag_entry {A : Matrix m m ùïÇ}
    (hA : A.IsUpperTriangular) :
    ‚àÄ n : ‚Ñï, ‚àÄ i : m, (A ^ n) i i = (A i i) ^ n := by
  intro n i
  have h := diag_pow_of hA n
  calc (A ^ n) i i = ((A ^ n).diag) i := by simp [diag_apply]
    _ = (A.diag ^ n) i := by convert congrArg (fun d => d i) h
    _ = (A i i) ^ n   := by simp [Pi.pow_apply]

/-- Each term in the matrix exponential series equals the corresponding scalar term on the
diagonal -/
lemma exp_series_diag_term_eq {A : Matrix m m ùïÇ} (hA : A.IsUpperTriangular)
    (n : ‚Ñï) (i : m) :
    ((n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A ^ n)) i i = (n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A i i) ^ n := by
  simp only [smul_apply]
  rw [diag_pow_entry_eq_pow_diag_entry hA n i]

/-- The diagonal of the matrix exponential series equals the scalar exponential series -/
lemma matrix_exp_series_diag_eq_scalar_series {A : Matrix m m ùïÇ} (hA : A.IsUpperTriangular)
    (i : m) :
    (‚àë' n, ((n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A ^ n)) i i) = ‚àë' n, (n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A i i) ^ n := by
  exact tsum_congr (exp_series_diag_term_eq hA ¬∑ i)

theorem diag_exp_of_isUpperTriangular
    {A : Matrix m m ùïÇ} (hA : A.IsUpperTriangular) :
    (NormedSpace.exp ùïÇ A).diag = fun i => NormedSpace.exp ùïÇ (A i i) := by
  funext i
  have exp_def : (NormedSpace.exp ùïÇ A) = ‚àë' (n : ‚Ñï), (n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A ^ n) := by
    rw [NormedSpace.exp_eq_tsum (ùïÇ := ùïÇ)]
  rw [exp_def, diag_apply]
  rw [matrix_tsum_apply (summable_exp_series A) i i]
  rw [matrix_exp_series_diag_eq_scalar_series hA i]
  rw [NormedSpace.exp_eq_tsum (ùïÇ := ùïÇ)]

lemma det_of_isUpperTriangular {A : Matrix m m ùïÇ}
    (hA : A.IsUpperTriangular) : A.det = ‚àè i, A i i := by
  exact Matrix.det_of_upperTriangular hA

omit [LinearOrder m] in
lemma trace_of_isUpperTriangular {A : Matrix m m ùïÇ} : A.trace = ‚àë i, A i i := by
  rfl

/-- The trace is invariant under unitary conjugation. -/
lemma trace_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    trace ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) = trace A := by
  have h :=
    trace_mul_cycle
      (A := (U : Matrix m m ùïÇ))
      (B := A)
      (C := star (U : Matrix m m ùïÇ))
  simpa [Matrix.mul_assoc,
        Matrix.one_mul] using h

/-- The determinant is invariant under unitary conjugation. -/
lemma det_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    det ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) = det A := by
  have h_det_U : star (det (U : Matrix m m ùïÇ)) * det (U : Matrix m m ùïÇ) = 1 := by
    have h : star (U : Matrix m m ùïÇ) * (U : Matrix m m ùïÇ) = 1 :=
      UnitaryGroup.star_mul_self U
    have h_det := congrArg det h
    rw [det_mul, det_one] at h_det
    erw [det_conjTranspose] at h_det
    exact h_det
  calc
    det ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ))
        = det ((U : Matrix m m ùïÇ) * A) * det (star (U : Matrix m m ùïÇ)) := by
          exact det_mul ((U : Matrix m m ùïÇ) * A) (star (U : Matrix m m ùïÇ))
    _ = det (U : Matrix m m ùïÇ) * det A * det (star (U : Matrix m m ùïÇ)) := by
          rw [det_mul]
    _ = det (U : Matrix m m ùïÇ) * det A * star (det (U : Matrix m m ùïÇ)) := by
          rw [‚Üê det_mul, ‚Üê det_conjTranspose]; rfl
    _ = det A * (det (U : Matrix m m ùïÇ) * star (det (U : Matrix m m ùïÇ))) := by
          ring
    _ = det A * (star (det (U : Matrix m m ùïÇ)) * det (U : Matrix m m ùïÇ)) := by
          rw [mul_comm (det (U : Matrix m m ùïÇ)) (star (det (U : Matrix m m ùïÇ)))]
    _ = det A * 1 := by
          rw [h_det_U]
    _ = det A := by
          rw [mul_one]

/-- Lie's trace formula for upper triangular matrices. -/
lemma det_exp_of_isUpperTriangular {A : Matrix m m ùïÇ} (hA : IsUpperTriangular A) :
    (NormedSpace.exp ùïÇ A).det = NormedSpace.exp ùïÇ A.trace := by
  have h_exp_upper : IsUpperTriangular (NormedSpace.exp ùïÇ A) :=
    isUpperTriangular_exp_of_isUpperTriangular hA
  letI : LinearOrder m := by infer_instance
  rw [det_of_isUpperTriangular h_exp_upper];
  have h_diag_exp : (NormedSpace.exp ùïÇ A).diag = fun i => NormedSpace.exp ùïÇ (A i i) :=
    diag_exp_of_isUpperTriangular hA
  erw [‚Üê Finset.prod_exp_eq_exp_sum Finset.univ]
  exact congrArg Finset.univ.prod h_diag_exp

/-- The exponential of a matrix commutes with unitary conjugation. -/
lemma exp_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) =
      (U : Matrix m m ùïÇ) * NormedSpace.exp ùïÇ A * star (U : Matrix m m ùïÇ) := by
  let Uu : (Matrix m m ùïÇ)À£ :=
    { val     := (U : Matrix m m ùïÇ)
      inv     := star (U : Matrix m m ùïÇ)
      val_inv := by simp
      inv_val := by simp}
  have h_units := Matrix.exp_units_conj (ùïÇ := ùïÇ) Uu A
  simpa [Uu] using h_units

lemma det_exp_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    (NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ))).det =
    (NormedSpace.exp ùïÇ A).det := by
  have h_exp_conj := exp_unitary_conj A U
  have h‚ÇÅ : NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) =
      (U : Matrix m m ùïÇ) * NormedSpace.exp ùïÇ A * star (U : Matrix m m ùïÇ) := h_exp_conj
  have h‚ÇÇ : (NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ))).det =
      det ((U : Matrix m m ùïÇ) * NormedSpace.exp ùïÇ A * star (U : Matrix m m ùïÇ)) := by
    simp [h‚ÇÅ]
  have h‚ÇÉ :
      det ((U : Matrix m m ùïÇ) * NormedSpace.exp ùïÇ A * star (U : Matrix m m ùïÇ)) =
        (NormedSpace.exp ùïÇ A).det :=
    det_unitary_conj (NormedSpace.exp ùïÇ A) U
  simpa [h‚ÇÇ] using h‚ÇÉ

/-- The determinant of the exponential of a matrix is the exponential of its trace.
This is also known as **Lie's trace formula**. -/
theorem det_exp {ùïÇ m : Type*} [RCLike ùïÇ] [IsAlgClosed ùïÇ] [Fintype m] [LinearOrder m]
    (A : Matrix m m ùïÇ) :
    (NormedSpace.exp ùïÇ A).det = NormedSpace.exp ùïÇ A.trace := by
  let U := schurTriangulationUnitary A
  let T := schurTriangulation A
  have h_conj : A = U * T.val * star U := schur_triangulation A
  have h_trace_invariant : A.trace = T.val.trace := by
    simpa [h_conj] using trace_unitary_conj (A := T.val) U
  have h_det_invariant :
      (NormedSpace.exp ùïÇ A).det = (NormedSpace.exp ùïÇ T.val).det := by
    simpa [h_conj] using det_exp_unitary_conj T.val U
  have h_triangular_case :
      (NormedSpace.exp ùïÇ T.val).det = NormedSpace.exp ùïÇ T.val.trace :=
    det_exp_of_isUpperTriangular T.property
  simp [h_det_invariant, h_trace_invariant, h_triangular_case]

end DetExp

-- `Matrix.map` commutes with an absolutely convergent series.
lemma map_tsum {Œ± Œ≤ m n : Type*}
    [AddCommMonoid Œ±] [AddCommMonoid Œ≤] [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [T2Space Œ≤]
    (f : Œ± ‚Üí+ Œ≤) (hf : Continuous f) {s : ‚Ñï ‚Üí Matrix m n Œ±} (hs : Summable s) :
    (‚àë' k, s k).map f = ‚àë' k, (s k).map f := by
  let F : Matrix m n Œ± ‚Üí+ Matrix m n Œ≤ := AddMonoidHom.mapMatrix f
  have hF : Continuous F := Continuous.matrix_map continuous_id hf
  exact (hs.hasSum.map F hF).tsum_eq.symm

attribute [local instance] Matrix.linftyOpNormedAlgebra
attribute [local instance] Matrix.linftyOpNormedRing
attribute [local instance] Matrix.instCompleteSpace

lemma map_pow {Œ± Œ≤ m : Type*}
    [Fintype m] [DecidableEq m] [Semiring Œ±] [Semiring Œ≤]
    (f : Œ± ‚Üí+* Œ≤) (A : Matrix m m Œ±) (k : ‚Ñï) :
    (A ^ k).map f = (A.map f) ^ k := by
  induction k with
  | zero =>
    rw [pow_zero, pow_zero, Matrix.map_one]; all_goals aesop
  | succ k ih =>
    rw [pow_succ, pow_succ, Matrix.map_mul, ih]

end Matrix

lemma NormedSpace.exp_map_algebraMap {n : Type*} [Fintype n] [DecidableEq n]
    (A : Matrix n n ‚Ñù) :
    (exp ‚Ñù A).map (algebraMap ‚Ñù ‚ÑÇ) = exp ‚ÑÇ (A.map (algebraMap ‚Ñù ‚ÑÇ)) := by
  letI : SeminormedRing (Matrix n n ‚Ñù) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix n n ‚Ñù) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ‚Ñù (Matrix n n ‚Ñù) := Matrix.linftyOpNormedAlgebra
  letI : CompleteSpace (Matrix n n ‚Ñù) := inferInstance
  letI : SeminormedRing (Matrix n n ‚ÑÇ) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix n n ‚ÑÇ) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ‚ÑÇ (Matrix n n ‚ÑÇ) := Matrix.linftyOpNormedAlgebra
  letI : CompleteSpace (Matrix n n ‚ÑÇ) := inferInstance
  simp only [exp_eq_tsum]
  have hs : Summable (fun k => (k.factorial : ‚Ñù)‚Åª¬π ‚Ä¢ A ^ k) := by
    exact NormedSpace.expSeries_summable' A
  erw [Matrix.map_tsum (algebraMap ‚Ñù ‚ÑÇ).toAddMonoidHom RCLike.continuous_ofReal hs]
  apply tsum_congr
  intro k
  erw [Matrix.map_smul, Matrix.map_pow]
  all_goals aesop
section DetExp
namespace Matrix
/--
Lie's trace formula over ‚Ñù: det(exp(A)) = exp(tr(A)) for any real matrix A.
This is proved by transferring the result from ‚ÑÇ using the naturality of polynomial identities.
-/
theorem det_exp_real {n : Type*} [Fintype n] [LinearOrder n]
    (A : Matrix n n ‚Ñù) : (NormedSpace.exp ‚Ñù A).det = Real.exp A.trace := by
  let A_‚ÑÇ := A.map (algebraMap ‚Ñù ‚ÑÇ)
  have h_complex : (NormedSpace.exp ‚ÑÇ A_‚ÑÇ).det = Complex.exp A_‚ÑÇ.trace := by
    haveI : IsAlgClosed ‚ÑÇ := Complex.isAlgClosed
    rw [Complex.exp_eq_exp_‚ÑÇ, ‚Üê Matrix.det_exp]
  have h_trace_comm : A_‚ÑÇ.trace = (algebraMap ‚Ñù ‚ÑÇ) A.trace := by
    simp only [A_‚ÑÇ, trace, diag_map, map_sum]
    rfl
  have h_det_comm : (algebraMap ‚Ñù ‚ÑÇ) ((NormedSpace.exp ‚Ñù A).det) = (NormedSpace.exp ‚ÑÇ A_‚ÑÇ).det := by
    rw [@RingHom.map_det]
    rw [‚Üê NormedSpace.exp_map_algebraMap]
    rfl
  rw [‚Üê h_det_comm] at h_complex
  rw [h_trace_comm] at h_complex
  have h_exp_comm : Complex.exp ((algebraMap ‚Ñù ‚ÑÇ) A.trace) =
      (algebraMap ‚Ñù ‚ÑÇ) (Real.exp A.trace) := by
    erw [‚Üê Complex.ofReal_exp]
    simp_all only [Complex.coe_algebraMap, Algebra.id.map_eq_id, RingHom.id_apply,
      Complex.ofReal_exp, A_‚ÑÇ]
  rw [h_exp_comm] at h_complex
  exact Complex.ofReal_injective h_complex
