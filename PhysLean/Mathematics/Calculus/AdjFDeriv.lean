/-
Copyright (c) 2025 Tomas Skrivan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tomas Skrivan
-/
import Mathlib.Analysis.Calculus.Gradient.Basic
import PhysLean.Mathematics.FDerivCurry
import PhysLean.Mathematics.InnerProductSpace.Adjoint
import PhysLean.Mathematics.InnerProductSpace.Calculus
/-!

# Adjoint FrÃ©chet derivative

  `adjFDeriv ð•œ f x = (fderiv ð•œ f x).adjoint`

The main purpose of defining `adjFDeriv` is to compute `gradient f x = adjFDeriv ð•œ f x 1`.
The advantage of working with `adjFDeriv` is that we can formulate composition theorem.

The reason why we do not want to compute `fderiv` and then `adjoint` is that to compute `fderiv ð•œ f`
or `adjoint f` we decompose `f = fâ‚ âˆ˜ ... âˆ˜ fâ‚™` and then apply composition theorem. The problem is
that this decomposition has to be done differently for `fderiv` and `adjoint`. The problem is
that when working with `fderiv` the natural product type is `X Ã— Y` but when working with `adjoint`
the natural product is `WithLp 2 (X Ã— Y)`.
For example:
-/

noncomputable section

variable
  {ð•œ : Type*} [RCLike ð•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E] [InnerProductSpace' ð•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ð•œ F] [InnerProductSpace' ð•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ð•œ G] [InnerProductSpace' ð•œ G]
  {U : Type*} [NormedAddCommGroup U] [InnerProductSpace ð•œ U] [CompleteSpace U]

variable (ð•œ) in
/-- Adjoint FrÃ©chet derivative

  `adjFDeriv ð•œ f x = (fderiv ð•œ f x).adjoint`

The main purpose of this function is to compute `gradient f x = adjFDeriv ð•œ f x 1`. We provide
easy to use API to compute `adjFDeriv`.
-/
noncomputable
def adjFDeriv (f : E â†’ F) (x : E) (dy : F) : E := adjoint ð•œ (fderiv ð•œ f x) dy

variable (ð•œ) in
/-- Function `f` has adjoint FrÃ©chet derivative `f'` at `x`

  `f' = adjFDeriv ð•œ f x = (fderiv ð•œ f x).adjoint`

The main purpose is to compute `gradient f x = f' 1 = adjFDeriv ð•œ f x 1`.

This structure is analogous to `HasFDerivAt` and it is often easier to use as theorems for
`HasAdjFDeriv` do not require differentiability assumptions unlike theorems for `adjFDeriv`.
-/
@[fun_prop]
structure HasAdjFDerivAt (f : E â†’ F) (f' : F â†’ E) (x : E) where
  differentiableAt : DifferentiableAt ð•œ f x
  hasAdjoint_fderiv : HasAdjoint ð•œ (fderiv ð•œ f x) f'

protected lemma HasAdjFDerivAt.adjFDeriv {f : E â†’ F} {f'} {x} (hf : HasAdjFDerivAt ð•œ f f' x) :
    adjFDeriv ð•œ f x = f' := by
  unfold adjFDeriv; funext y;
  rw[hf.hasAdjoint_fderiv.adjoint]

open InnerProductSpace' in
protected lemma DifferentiableAt.hasAdjFDerivAt [CompleteSpace E] [CompleteSpace F]
    {f : E â†’ F} {x} (hf : DifferentiableAt ð•œ f x) :
    HasAdjFDerivAt ð•œ f (adjFDeriv ð•œ f x) x where
  differentiableAt := hf
  hasAdjoint_fderiv := by
    unfold adjFDeriv
    apply HasAdjoint.congr_adj
    Â· apply ContinuousLinearMap.hasAdjoint
    Â· funext y; simp[adjoint_eq_clm_adjoint]

namespace ContinuousLinearMap

variable
  {X : Type*} [NormedAddCommGroup X] [InnerProductSpace â„ X] [CompleteSpace X]
  {Y : Type*} [NormedAddCommGroup Y] [InnerProductSpace â„ Y] [CompleteSpace Y]

lemma adjoint.isBoundedBilinearMap_real :
    IsBoundedBilinearMap â„ (fun (fy : (X â†’L[â„] Y)Ã—Y) => fy.1.adjoint fy.2) :=
{
  add_left := by simp
  smul_left := by simp
  add_right := by simp
  smul_right := by simp
  bound := by
    simp only [gt_iff_lt]
    use 1
    constructor
    Â· simp
    Â· intro f y
      trans â€–f.adjointâ€– * â€–yâ€–
      apply ContinuousLinearMap.le_opNorm
      simp
}

end ContinuousLinearMap

open InnerProductSpace' in
protected lemma HasAdjFDerivAt.contDiffAt_deriv
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [InnerProductSpace' â„ F]
    {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G] [InnerProductSpace' â„ G]
    [CompleteSpace F] [CompleteSpace G]
    {f : E â†’ F â†’ G} {f' : E â†’ F â†’ _} (hf : âˆ€ x y, HasAdjFDerivAt â„ (f x) (f' x y) y)
    (hf' : ContDiff â„ (n+1) (â†¿f)) :
    ContDiff â„ n (fun x : EÃ—FÃ—G => f' x.1 x.2.1 x.2.2) := by
  simp[â† fun x y => (hf x y).adjFDeriv]
  unfold adjFDeriv
  simp[adjoint_eq_clm_adjoint]
  apply ContDiff.fun_comp
  Â· fun_prop
  Â· apply ContDiff.fun_comp (ð•œ:=â„) (n:=n)
      (g := fun fx : ((WithLp 2 F) â†’L[â„] (WithLp 2 G))Ã—(WithLp 2 G) => fx.1.adjoint fx.2)
      (f := fun x : EÃ—FÃ—G => (((toL2 â„) âˆ˜L
        ((fderiv â„ (f x.1) x.2.1) âˆ˜L (fromL2 â„))), (toL2 â„) x.2.2))
    Â· apply ContinuousLinearMap.adjoint.isBoundedBilinearMap_real.contDiff
    Â· fun_prop

lemma gradient_eq_adjFDeriv
    {f : U â†’ ð•œ} {x : U} (hf : DifferentiableAt ð•œ f x) :
    gradient f x = adjFDeriv ð•œ f x 1 := by
  apply ext_inner_right ð•œ
  unfold gradient
  simp [ContinuousLinearMap.adjoint_inner_left,
        hf.hasAdjFDerivAt.hasAdjoint_fderiv.adjoint_inner_left]

attribute [fun_prop] HasAdjFDerivAt.differentiableAt

lemma hasAdjFDerivAt_id (x : E) : HasAdjFDerivAt ð•œ (fun x : E => x) (fun dx => dx) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp; apply hasAdjoint_id

lemma adjFDeriv_id : adjFDeriv ð•œ (fun x : E => x) = fun _ dx => dx := by
  funext x
  rw[HasAdjFDerivAt.adjFDeriv (hasAdjFDerivAt_id x)]

lemma adjFDeriv_id' : adjFDeriv ð•œ (id : E â†’ E) = fun _ dx => dx := by
  exact adjFDeriv_id

lemma hasAdjFDerivAt_const (x : E) (y : F) :
    HasAdjFDerivAt ð•œ (fun _ : E => y) (fun _ => 0) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp; apply hasAdjoint_zero

lemma adjFDeriv_const (y : F) : adjFDeriv ð•œ (fun _ : E => y) = fun _ _ => 0 := by
  funext x
  rw[HasAdjFDerivAt.adjFDeriv (hasAdjFDerivAt_const x y)]

lemma HasAdjFDerivAt.comp {f : F â†’ G} {g : E â†’ F} {f' g'} {x : E}
    (hf : HasAdjFDerivAt ð•œ f f' (g x)) (hg : HasAdjFDerivAt ð•œ g g' x) :
    HasAdjFDerivAt ð•œ (fun x => f (g x)) (fun dz => g' (f' dz)) x where
  differentiableAt := by
    fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv_comp']
    exact hf.hasAdjoint_fderiv.comp hg.hasAdjoint_fderiv

lemma adjFDeriv_comp [CompleteSpace E] [CompleteSpace F] [CompleteSpace G]
    {f : F â†’ G} {g : E â†’ F} {x : E}
    (hf : DifferentiableAt ð•œ f (g x)) (hg : DifferentiableAt ð•œ g x) :
    adjFDeriv ð•œ (fun x => f (g x)) x = fun dy => adjFDeriv ð•œ g x (adjFDeriv ð•œ f (g x) dy) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAdjFDerivAt.comp
  apply hf.hasAdjFDerivAt
  apply hg.hasAdjFDerivAt

lemma HasAdjFDerivAt.prodMk {f : E â†’ F} {g : E â†’ G} {f' g'} {x : E}
    (hf : HasAdjFDerivAt ð•œ f f' x) (hg : HasAdjFDerivAt ð•œ g g' x) :
    HasAdjFDerivAt ð•œ (fun x => (f x, g x)) (fun dyz => f' dyz.fst + g' dyz.snd) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [DifferentiableAt.fderiv_prodMk]
    apply HasAdjoint.prodMk
    Â· exact hf.hasAdjoint_fderiv
    Â· exact hg.hasAdjoint_fderiv

lemma HasAjdFDerivAt.fst {f : E â†’ FÃ—G} {f'} {x : E} (hf : HasAdjFDerivAt ð•œ f f' x) :
    HasAdjFDerivAt ð•œ (fun x => (f x).fst) (fun dy => f' (dy, 0)) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv.fst]
    apply HasAdjoint.fst hf.hasAdjoint_fderiv

lemma adjFDeriv_fst [CompleteSpace E] [CompleteSpace F] [CompleteSpace G]
    {f : E â†’ FÃ—G} {x : E} (hf : DifferentiableAt ð•œ f x) :
    adjFDeriv ð•œ (fun x => (f x).fst) x = fun dy => adjFDeriv ð•œ f x (dy, 0) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAjdFDerivAt.fst hf.hasAdjFDerivAt

@[simp]
lemma adjFDeriv_prod_fst [CompleteSpace E] [CompleteSpace F] {x : F Ã— E} :
    adjFDeriv ð•œ (Prod.fst : F Ã— E â†’ F) x = fun a => (a, 0) := by
  change adjFDeriv ð•œ (fun x => (id x).fst) x = _
  rw [adjFDeriv_fst]
  funext dy
  rw [adjFDeriv_id']
  simp

lemma HasAjdFDerivAt.snd {f : E â†’ FÃ—G} {f'} {x : E} (hf : HasAdjFDerivAt ð•œ f f' x) :
    HasAdjFDerivAt ð•œ (fun x => (f x).snd) (fun dz => f' (0, dz)) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv.snd]
    apply HasAdjoint.snd hf.hasAdjoint_fderiv

lemma adjFDeriv_snd [CompleteSpace E] [CompleteSpace F] [CompleteSpace G]
    {f : E â†’ FÃ—G} {x : E} (hf : DifferentiableAt ð•œ f x) :
    adjFDeriv ð•œ (fun x => (f x).snd) x = fun dy => adjFDeriv ð•œ f x (0, dy) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAjdFDerivAt.snd hf.hasAdjFDerivAt

@[simp]
lemma adjFDeriv_prod_snd [CompleteSpace E] [CompleteSpace F] {x : F Ã— E} :
    adjFDeriv ð•œ (Prod.snd : F Ã— E â†’ E) x = fun a => (0, a) := by
  change adjFDeriv ð•œ (fun x => (id x).snd) x = _
  rw [adjFDeriv_snd]
  funext dy
  rw [adjFDeriv_id']
  simp

lemma hasAdjFDerivAt_uncurry {f : E â†’ F â†’ G} {xy} {fx' fy'}
    (hf : DifferentiableAt ð•œ (â†¿f) xy)
    (hfx : HasAdjFDerivAt ð•œ (f Â· xy.2) fx' xy.1) (hfy : HasAdjFDerivAt ð•œ (f xy.1 Â·) fy' xy.2) :
    HasAdjFDerivAt ð•œ (â†¿f) (fun dz => (fx' dz, fy' dz)) xy where
  differentiableAt :=hf
  hasAdjoint_fderiv := by
    eta_expand
    simp (disch:=fun_prop) [fderiv_uncurry]
    apply HasAdjoint.congr_adj
    case adjoint =>
      apply HasAdjoint.add
      apply HasAdjoint.comp (g:=Prod.fst) hfx.hasAdjoint_fderiv (HasAdjoint.fst hasAdjoint_id)
      apply HasAdjoint.comp (g:=Prod.snd) hfy.hasAdjoint_fderiv (HasAdjoint.snd hasAdjoint_id)
    case eq =>
      simp

lemma adjFDeriv_uncurry [CompleteSpace E] [CompleteSpace F] [CompleteSpace G]
    {f : E â†’ F â†’ G} {xy} (hfx : DifferentiableAt ð•œ (â†¿f) xy) :
    adjFDeriv ð•œ (â†¿f) xy = fun dz => (adjFDeriv ð•œ (f Â· xy.snd) xy.fst dz,
                                    adjFDeriv ð•œ (f xy.fst Â·) xy.snd dz) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply hasAdjFDerivAt_uncurry
  fun_prop
  apply DifferentiableAt.hasAdjFDerivAt (by fun_prop)
  apply DifferentiableAt.hasAdjFDerivAt (by fun_prop)

lemma HasAdjFDerivAt.neg {f : E â†’ F} {f'} {x : E} (hf : HasAdjFDerivAt ð•œ f f' x) :
    HasAdjFDerivAt ð•œ (fun x => - f x) (fun dy => - f' dy) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by simp; apply hf.hasAdjoint_fderiv.neg

lemma adjFDeriv_neg [CompleteSpace E] [CompleteSpace F]
    {f : E â†’ F} {x : E} (hf : DifferentiableAt ð•œ f x) :
    adjFDeriv ð•œ (fun x => - f x) x = fun dy => - adjFDeriv ð•œ f x dy := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAdjFDerivAt.neg hf.hasAdjFDerivAt

lemma HasAjdFDerivAt.add {f g : E â†’ F} {f' g'} {x : E}
    (hf : HasAdjFDerivAt ð•œ f f' x) (hg : HasAdjFDerivAt ð•œ g g' x) :
    HasAdjFDerivAt ð•œ (fun x => f x + g x) (fun dy => f' dy + g' dy) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv_fun_add]
    apply hf.hasAdjoint_fderiv.add hg.hasAdjoint_fderiv

lemma adjFDeriv_add [CompleteSpace E] [CompleteSpace F]
    {f g : E â†’ F} {x : E}
    (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    adjFDeriv ð•œ (fun x => f x + g x) x = fun dy => adjFDeriv ð•œ f x dy + adjFDeriv ð•œ g x dy := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAjdFDerivAt.add
  apply hf.hasAdjFDerivAt
  apply hg.hasAdjFDerivAt

lemma HasAdjFDerivAt.sub
    {f g : E â†’ F} {f' g'} {x : E}
    (hf : HasAdjFDerivAt ð•œ f f' x) (hg : HasAdjFDerivAt ð•œ g g' x) :
    HasAdjFDerivAt ð•œ (fun x => f x - g x) (fun dy => f' dy - g' dy) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv_fun_sub]
    apply hf.hasAdjoint_fderiv.sub hg.hasAdjoint_fderiv

lemma adjFDeriv_sub [CompleteSpace E] [CompleteSpace F] {f g : E â†’ F} {x : E}
    (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    adjFDeriv ð•œ (fun x => f x - g x) x = fun dy => adjFDeriv ð•œ f x dy - adjFDeriv ð•œ g x dy := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAdjFDerivAt.sub
  apply hf.hasAdjFDerivAt
  apply hg.hasAdjFDerivAt

open ComplexConjugate in
lemma HasAdjFDerivAt.smul {f : E â†’ F} {g : E â†’ ð•œ} {f' g'}
    (hf : HasAdjFDerivAt ð•œ f f' x) (hg : HasAdjFDerivAt ð•œ g g' x) :
    HasAdjFDerivAt ð•œ (fun x => g x â€¢ f x)
      (fun dy => conj (g x) â€¢ f' dy + g' (conj (inner ð•œ dy (f x)))) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    simp (disch:=fun_prop) [fderiv_fun_smul,-inner_conj_symm']
    apply HasAdjoint.add
    Â· apply hf.hasAdjoint_fderiv.smul_left
    Â· apply hg.hasAdjoint_fderiv.smul_right

open ComplexConjugate in
lemma adjFDeriv_smul [CompleteSpace E] [CompleteSpace F]
    {f : E â†’ F} {g : E â†’ ð•œ} {x : E}
    (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    adjFDeriv ð•œ (fun x => g x â€¢ f x) x = fun dy => conj (g x) â€¢ adjFDeriv ð•œ f x dy +
                                                  adjFDeriv ð•œ g x (conj (inner ð•œ dy (f x))) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAdjFDerivAt.smul
  apply hf.hasAdjFDerivAt
  apply hg.hasAdjFDerivAt

open InnerProductSpace
lemma HasAdjFDerivAt.inner {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [InnerProductSpace' â„ E] (x : E Ã— E) :
    HasAdjFDerivAt â„ (fun (x : E Ã— E) => âŸªx.1, x.2âŸ«_â„) (fun y => y â€¢ (x.2, x.1)) x where
  differentiableAt := by fun_prop
  hasAdjoint_fderiv := by
    conv =>
      enter [2]
      change fun t => fderiv â„ (fun x => âŸªx.1, x.2âŸ«_â„) x t
      enter [t]
      rw [fderiv_inner_apply' (by fun_prop) (by fun_prop)]
      simp [fderiv_snd, fderiv_fst]
    constructor
    intro a b
    simp [inner_smul_left']
    conv_lhs =>
      enter [1]
      rw [real_inner_comm']
    ring

lemma adjFDeriv_inner {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [InnerProductSpace' â„ E]
    (x : E Ã— E) :
    adjFDeriv â„ (fun (x : E Ã— E) => âŸªx.1, x.2âŸ«_â„) x =
      fun y => y â€¢ (x.2, x.1) := by
  apply HasAdjFDerivAt.adjFDeriv
  apply HasAdjFDerivAt.inner
