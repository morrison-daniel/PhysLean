/-
Copyright (c) 2025 Tomas Skrivan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tomas Skrivan
-/
import Mathlib.LinearAlgebra.Trace
import PhysLean.Mathematics.Calculus.AdjFDeriv
import PhysLean.SpaceAndTime.Space.Basic
/-!

# Divergence

In this module we define and create an API around the divergence of a map `f : E â†’ E`
where `E` is a normed space over a field `ğ•œ`.

-/
noncomputable section

variable
  {ğ•œ : Type*} [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ) in
/-- The divergence of a map `f : E â†’ E` where `E` is a normed space over `ğ•œ`. -/
noncomputable def divergence (f : E â†’ E) (x : E) : ğ•œ := (fderiv ğ•œ f x).toLinearMap.trace _ _

@[simp]
lemma divergence_zero : divergence ğ•œ (fun _ : E => 0) = fun _ => 0 := by
  unfold divergence
  simp

lemma divergence_eq_sum_fderiv {s : Finset E} (b : Basis s ğ•œ E) {f : E â†’ E} :
    divergence ğ•œ f = fun x => âˆ‘ i : s, b.repr (fderiv ğ•œ f x (b i)) i := by
  funext x
  unfold divergence
  rw[LinearMap.trace_eq_matrix_trace_of_finset (s:=s) _ b]
  simp[Matrix.trace,Matrix.diag,LinearMap.toMatrix]

lemma divergence_eq_sum_fderiv' {Î¹} [Fintype Î¹] (b : Basis Î¹ ğ•œ E) {f : E â†’ E} :
    divergence ğ•œ f = fun x => âˆ‘ i, b.repr (fderiv ğ•œ f x (b i)) i := by
  let s : Finset E := Finset.univ.map âŸ¨b, Basis.injective bâŸ©
  let f' : Î¹ â†’ s := fun i => âŸ¨b i, by simp [s]âŸ©
  have h : Function.Injective f' := by
    intro i j h
    simp [f'] at h
    exact Basis.injective b h
  have h' : Function.Surjective f' := by
    intro âŸ¨x, hxâŸ©
    simp [s] at hx
    obtain âŸ¨i, rflâŸ© := hx
    simp [f']
  let e : Î¹ â‰ƒ s := Equiv.ofBijective f' âŸ¨h, h'âŸ©
  let b' : Basis s ğ•œ E := b.reindex e
  rw [divergence_eq_sum_fderiv b']
  ext x
  rw [â† e.symm.sum_comp]
  simp [b']

lemma divergence_eq_space_div {d} (f : Space d â†’ Space d)
    (h : Differentiable â„ f) : divergence â„ f = Space.div f := by
  let b := (Space.basis (d:=d)).toBasis
  rw[divergence_eq_sum_fderiv' b]
  funext x
  simp +zetaDelta [Space.div,Space.deriv,Space.coord,Space.basis]
  congr
  funext i
  have h1 : (fderiv â„ (fun x => f x i) x)
    = fderiv â„ (Space.coordCLM i âˆ˜ f) x := by
    congr
    ext j
    simp only [Function.comp_apply]
    rw [Space.coordCLM_apply, Space.coord_apply]
  rw [h1]
  rw [fderiv_comp]
  simp [Space.coordCLM_apply, Space.coord_apply]
  Â· fun_prop
  Â· exact h x

lemma divergence_prodMk [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]
    {f : EÃ—F â†’ E} {g : EÃ—F â†’ F} {xy : EÃ—F}
    (hf : DifferentiableAt ğ•œ f xy) (hg : DifferentiableAt ğ•œ g xy) :
    divergence ğ•œ (fun xy : EÃ—F => (f xy, g xy)) xy
    =
    divergence ğ•œ (fun x' => f (x',xy.2)) xy.1
    +
    divergence ğ•œ (fun y' => g (xy.1,y')) xy.2 := by
  obtain âŸ¨s, âŸ¨bXâŸ©âŸ© := Basis.exists_basis ğ•œ E
  haveI : Fintype s := FiniteDimensional.fintypeBasisIndex bX
  obtain âŸ¨sY, âŸ¨bYâŸ©âŸ© := Basis.exists_basis ğ•œ F
  haveI : Fintype sY := FiniteDimensional.fintypeBasisIndex bY
  let bXY := bX.prod bY
  rw[divergence_eq_sum_fderiv' bX]
  rw[divergence_eq_sum_fderiv' bY]
  rw[divergence_eq_sum_fderiv' bXY]
  simp[hf.fderiv_prodMk hg,bXY,fderiv_wrt_prod hf,fderiv_wrt_prod hg]

lemma divergence_add {f g : E â†’ E} {x : E}
    (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    divergence ğ•œ (fun x => f x + g x) x
    =
    divergence ğ•œ f x + divergence ğ•œ g x := by
  unfold divergence
  simp [fderiv_fun_add hf hg]

lemma divergence_neg {f : E â†’ E} {x : E} :
    divergence ğ•œ (fun x => -f x) x
    =
    -divergence ğ•œ f x := by
  unfold divergence
  simp [fderiv_neg]

lemma divergence_sub {f g : E â†’ E} {x : E}
    (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    divergence ğ•œ (fun x => f x - g x) x
    =
    divergence ğ•œ f x - divergence ğ•œ g x := by
  unfold divergence
  simp [fderiv_fun_sub hf hg]

lemma divergence_const_smul {f : E â†’ E} {x : E} {c : ğ•œ}
    (hf : DifferentiableAt ğ•œ f x) :
    divergence ğ•œ (fun x => c â€¢ f x) x
    =
    c * divergence ğ•œ f x := by
  unfold divergence
  simp [fderiv_fun_const_smul hf]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

@[sorryful]
lemma divergence_smul [InnerProductSpace' ğ•œ E] {f : E â†’ ğ•œ} {g : E â†’ E} {x : E}
    (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x)
    [FiniteDimensional ğ•œ E] :
    divergence ğ•œ (fun x => f x â€¢ g x) x
    = f x * divergence ğ•œ g x + âŸªadjFDeriv ğ•œ f x 1, g xâŸ« := by
  unfold divergence
  simp [fderiv_fun_smul hf hg]
  sorry
