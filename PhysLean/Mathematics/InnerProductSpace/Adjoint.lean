/-
Copyright (c) 2025 Tomas Skrivan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tomas Skrivan
-/
import PhysLean.Mathematics.InnerProductSpace.Basic
import Mathlib.Analysis.InnerProductSpace.Adjoint
/-!

# Adjoint of a linear map

This module defines the adjoint of a linear map `f : E â†’ F` where
`E` and `F` carry the instances of `InnerProductSpace'` over a field `ğ•œ`.

This is a generalization of the usual adjoint defined on `InnerProductSpace` for
continuous linear maps.

-/
variable {ğ•œ : Type*} {E F G : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [InnerProductSpace' ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [InnerProductSpace' ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] [InnerProductSpace' ğ•œ G]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (ğ•œ) in
/-- Adjoint of a linear map `f` such that `âˆ€ x y, âŸªadjoint ğ•œ f y, xâŸ« = âŸªy, f xâŸ«`.

This computes adjoint of a liner map the same way as `ContinuousLinearMap.adjoint` but it is
defined over `InnerProductSpace'`, which is a generalization of `InnerProductSpace` that provides
instances for products and function types. These instances make it easier to perform computations
compared to using the standard `InnerProductSpace` class.
-/
structure HasAdjoint (f : E â†’ F) (f' : F â†’ E) where
  adjoint_inner_left (x : E) (y : F) : âŸªf' y, xâŸ« = âŸªy, f xâŸ«

open Classical in
variable (ğ•œ) in
@[inherit_doc HasAdjoint]
noncomputable
def adjoint (f : E â†’ F) :=
  if h : âˆƒ f', HasAdjoint ğ•œ f f' then
    choose h
  else 0

lemma HasAdjoint.adjoint_inner_right {f : E â†’ F} (hf : HasAdjoint ğ•œ f f') :
    âŸªx, f' yâŸ« = âŸªf x, yâŸ« := by
  rw [â† inner_conj_symm']
  rw [hf.adjoint_inner_left]
  rw [inner_conj_symm']

open InnerProductSpace' in
lemma ContinuousLinearMap.hasAdjoint [CompleteSpace E] [CompleteSpace F] (f : E â†’L[ğ•œ] F) :
    HasAdjoint ğ•œ f (fun y => fromL2 ğ•œ (((toL2 ğ•œ) âˆ˜L f âˆ˜L (fromL2 ğ•œ)).adjoint (toL2 ğ•œ y))) where
  adjoint_inner_left := by intros; simp[fromL2_inner_left,adjoint_inner_left]; rfl

open InnerProductSpace' in
lemma adjoint_eq_clm_adjoint [CompleteSpace E] [CompleteSpace F] (f : E â†’L[ğ•œ] F) :
    _root_.adjoint ğ•œ f = fromL2 ğ•œ âˆ˜L (toL2 ğ•œ âˆ˜L f âˆ˜L fromL2 ğ•œ).adjoint âˆ˜L (toL2 ğ•œ) := by
  ext y; apply ext_inner_right' ğ•œ; intro x; simp
  rw [f.hasAdjoint.adjoint_inner_left]
  have h : âˆƒ f', HasAdjoint ğ•œ f f' := âŸ¨_,f.hasAdjointâŸ©
  simp[_root_.adjoint,h,h.choose_spec.adjoint_inner_left]

lemma HasAdjoint.adjoint_apply_zero {f : E â†’ F} {f' : F â†’ E}
    (hf : HasAdjoint ğ•œ f f') : f' 0 = 0 := by
  simpa using hf.adjoint_inner_left (f' 0) 0

lemma HasAdjoint.adjoint
    {f : E â†’ F} {f' : F â†’ E}
    (hf : HasAdjoint ğ•œ f f') : adjoint ğ•œ f = f' := by
  funext y
  apply ext_inner_right' ğ•œ
  unfold _root_.adjoint
  have h : âˆƒ f', HasAdjoint ğ•œ f f' := âŸ¨f', hfâŸ©
  have := h.choose_spec.adjoint_inner_left
  simp_all
  simp [hf.adjoint_inner_left]

lemma HasAdjoint.congr_adj (f : E â†’ F) (f' g')
    (adjoint : HasAdjoint ğ•œ f g')
    (eq : g' = f') : HasAdjoint ğ•œ f f' := by simp[â† eq,adjoint]

lemma hasAdjoint_id : HasAdjoint ğ•œ (fun x : E => x) (fun x => x) := by
  constructor; intros; rfl

lemma hasAdjoint_zero : HasAdjoint ğ•œ (fun _ : E => (0 : F)) (fun _ => 0) := by
  constructor; intros; simp

lemma HasAdjoint.comp {f : F â†’ G} {g : E â†’ F} {f' g'}
    (hf : HasAdjoint ğ•œ f f') (hg : HasAdjoint ğ•œ g g') :
    HasAdjoint ğ•œ (fun x : E => f (g x)) (fun x => g' (f' x)) := by
  constructor; intros; simp[hf.adjoint_inner_left, hg.adjoint_inner_left]

lemma HasAdjoint.prodMk {f : E â†’ F} {g : E â†’ G} {f' g'}
    (hf : HasAdjoint ğ•œ f f') (hg : HasAdjoint ğ•œ g g') :
    HasAdjoint ğ•œ (fun x : E => (f x, g x)) (fun yz => f' yz.1 + g' yz.2) := by
  constructor; intros
  simp [inner,inner_add_left',
      hf.adjoint_inner_left, hg.adjoint_inner_left]

lemma HasAdjoint.fst {f : E â†’ FÃ—G} {f'} (hf : HasAdjoint ğ•œ f f') :
    HasAdjoint ğ•œ (fun x : E => (f x).1) (fun y => f' (y, 0)) := by
  constructor; intros
  simp[inner, hf.adjoint_inner_left]

lemma HasAdjoint.snd {f : E â†’ FÃ—G} {f'} (hf : HasAdjoint ğ•œ f f') :
    HasAdjoint ğ•œ (fun x : E => (f x).2) (fun z => f' (0, z)) := by
  constructor; intros
  simp[inner, hf.adjoint_inner_left]

lemma HasAdjoint.neg {f : E â†’ F} {f'} (hf : HasAdjoint ğ•œ f f') :
    HasAdjoint ğ•œ (fun x : E => -f x) (fun y => -f' y) := by
  constructor; intros
  simp[inner, hf.adjoint_inner_left]

lemma HasAdjoint.add {f g : E â†’ F} {f' g'}
    (hf : HasAdjoint ğ•œ f f') (hg : HasAdjoint ğ•œ g g') :
    HasAdjoint ğ•œ (fun x : E => f x + g x) (fun y => f' y + g' y) := by
  constructor; intros
  simp[inner, inner_add_left', inner_add_right',
      hf.adjoint_inner_left, hg.adjoint_inner_left]

lemma HasAdjoint.sub {f g : E â†’ F} {f' g'}
    (hf : HasAdjoint ğ•œ f f') (hg : HasAdjoint ğ•œ g g') :
    HasAdjoint ğ•œ (fun x : E => f x - g x) (fun y => f' y - g' y) := by
  constructor; intros
  simp[inner, sub_eq_add_neg, inner_add_left', inner_add_right',
      hf.adjoint_inner_left, hg.adjoint_inner_left]

open ComplexConjugate in
lemma HasAdjoint.smul_left {f : E â†’ F} {f'} (c : ğ•œ)
    (hf : HasAdjoint ğ•œ f f') :
    HasAdjoint ğ•œ (fun x : E => c â€¢ f x) (fun y => (conj c) â€¢ f' y) := by
  constructor; intros
  simp[inner, inner_smul_left', inner_smul_right', hf.adjoint_inner_left]

open ComplexConjugate in
lemma HasAdjoint.smul_right {f : E â†’ ğ•œ} {f'} (v : F)
    (hf : HasAdjoint ğ•œ f f') :
    HasAdjoint ğ•œ (fun x : E => f x â€¢ v) (fun y => f' (conj âŸªy, vâŸ«)) := by
  constructor; intros
  simp[inner, inner_smul_left', inner_smul_right', hf.adjoint_inner_left]
