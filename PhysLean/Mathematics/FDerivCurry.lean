/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import Mathlib.Analysis.Calculus.FDeriv.Mul
/-!
# fderiv currying lemmas

Various lemmas related to fderiv on curried/uncurried functions.

-/
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
    {X Y Z : Type} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
    [NormedAddCommGroup Y] [NormedSpace ğ•œ Y]
    [NormedAddCommGroup Z] [NormedSpace ğ•œ Z]

lemma fderiv_uncurry (f : X â†’ Y â†’ Z) (xy : X Ã— Y) (dy : Y)
    (h : DifferentiableAt ğ•œ (â†¿f) xy) :
    fderiv ğ•œ (â†¿f) xy (0, dy) =
    fderiv ğ•œ (fun x => f xy.1 x) xy.2 dy := by
  rw [show (fun x => f xy.1 x) =
    (â†¿f) âˆ˜ (fun x => (xy.1, x)) by {ext w; rfl}]
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  rw [(hasFDerivAt_prodMk_right (ğ•œ := ğ•œ) xy.1 xy.2).fderiv]
  rfl

lemma fderiv_uncurry' (f : X â†’ Y â†’ Z) (xy : X Ã— Y) (dx : X)
    (h : DifferentiableAt ğ•œ (â†¿f) xy) :
    fderiv ğ•œ (â†¿f) xy (dx, 0) =
    fderiv ğ•œ (fun x => f x xy.2) xy.1 dx := by
  rw [show (fun x => f x xy.2) =
    (â†¿f) âˆ˜ (fun x => (x, xy.2)) by {ext w; rfl}]
  rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
  rw [(hasFDerivAt_prodMk_left (ğ•œ := ğ•œ) xy.1 xy.2).fderiv]
  rfl

lemma fderiv_curry (f : X Ã— Y â†’ Z) (x : X) (y : Y)
    (h : DifferentiableAt ğ•œ f (x, y)) (dy : Y) :
    fderiv ğ•œ (Function.curry f x) y dy = fderiv ğ•œ (f) (x, y) (0, dy) := by
  have h1 : f = â†¿(Function.curry f) := by
    ext x
    rfl
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry]
  exact h

lemma fderiv_curry' (f : X Ã— Y â†’ Z) (x : X) (y : Y)
    (h : DifferentiableAt ğ•œ f (x, y)) (dx : X) :
    fderiv ğ•œ (fun x => Function.curry f x y) x dx = fderiv ğ•œ f (x, y) (dx, 0) := by
  have h1 : f = â†¿(Function.curry f) := by
    ext x
    rfl
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry']
  exact h

lemma fderiv_uncurry_sum (f : X â†’ Y â†’ Z) (xy dxy : X Ã— Y) :
    fderiv ğ•œ (â†¿f) xy dxy
    =
    fderiv ğ•œ (â†¿f) xy (dxy.1, 0) + fderiv ğ•œ (â†¿f) xy (0, dxy.2) := by
  rw [â† ContinuousLinearMap.map_add]
  simp

theorem fderiv_uncurry'' (f : X â†’ Y â†’ Z) (hf : Differentiable ğ•œ (â†¿f)) :
    fderiv ğ•œ â†¿f
    =
    fun xy =>
      (fderiv ğ•œ (f Â· xy.2) xy.1).comp (ContinuousLinearMap.fst ğ•œ X Y)
      +
      (fderiv ğ•œ (f xy.1 Â·) xy.2).comp (ContinuousLinearMap.snd ğ•œ X Y) := by
  funext xy
  apply ContinuousLinearMap.ext
  intro dxy
  rw [fderiv_uncurry_sum]
  rw [fderiv_uncurry, fderiv_uncurry']
  simp only [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_fst', Function.comp_apply, ContinuousLinearMap.coe_snd']
  fun_prop
  fun_prop

theorem fderiv_wrt_prod'' (f : X Ã— Y â†’ Z) (hf : Differentiable ğ•œ f) :
    fderiv ğ•œ f
    =
    fun xy =>
      (fderiv ğ•œ (fun x' => f (x',xy.2)) xy.1).comp (ContinuousLinearMap.fst ğ•œ X Y)
      +
      (fderiv ğ•œ (fun y' => f (xy.1,y')) xy.2).comp (ContinuousLinearMap.snd ğ•œ X Y) :=
  fderiv_uncurry'' (fun x y => f (x,y)) hf

theorem fderiv_clm_apply' (f : X â†’ Y â†’L[ğ•œ] Z) (y : Y) (x dx : X) (h : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ f x dx y
    =
    fderiv ğ•œ (f Â· y) x dx := by
  rw [fderiv_clm_apply] <;> first | simp | fun_prop
