/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, TomÃ¡Å¡ SkÅ™ivan, Joseph Tooby-Smith
-/
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import PhysLean.Meta.TODO.Basic
/-!
# fderiv currying lemmas

Various lemmas related to fderiv on curried/uncurried functions.

-/
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
    {X Y Z : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
    [NormedAddCommGroup Y] [NormedSpace ğ•œ Y]
    [NormedAddCommGroup Z] [NormedSpace ğ•œ Z]

lemma fderiv_uncurry (f : X â†’ Y â†’ Z) (xy dxy : X Ã— Y)
    (hf : DifferentiableAt ğ•œ (â†¿f) xy) :
    fderiv ğ•œ â†¿f xy dxy
    =
    fderiv ğ•œ (f Â· xy.2) xy.1 dxy.1 + fderiv ğ•œ (f xy.1 Â·) xy.2 dxy.2 := by
  have hx : (f Â· xy.2) = â†¿f âˆ˜ (fun x' => (x',xy.2)) := by rfl
  have hy : (f xy.1 Â·) = â†¿f âˆ˜ (fun y' => (xy.1,y')) := by rfl
  rw [hx,hy]
  repeat rw [fderiv_comp (hg := by fun_prop) (hf := by fun_prop)]
  dsimp
  rw [â† ContinuousLinearMap.map_add]
  congr
  repeat rw [DifferentiableAt.fderiv_prodMk (hfâ‚ := by fun_prop) (hfâ‚‚ := by fun_prop)]
  simp

lemma fderiv_curry_fst (f : X Ã— Y â†’ Z) (x : X) (y : Y)
    (h : DifferentiableAt ğ•œ f (x,y)) (dx : X) :
    fderiv ğ•œ (fun x' => Function.curry f x' y) x dx = fderiv ğ•œ f (x,y) (dx, 0) := by
  have h1 : f = â†¿(Function.curry f) := by
    ext x
    rfl
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry]
  simp only [Function.curry_apply, map_zero, add_zero]
  exact h

lemma fderiv_curry_snd (f : X Ã— Y â†’ Z) (x : X) (y : Y)
    (h : DifferentiableAt ğ•œ f (x,y)) (dy : Y) :
    fderiv ğ•œ (Function.curry f x) y dy = fderiv ğ•œ (f) (x,y) (0, dy) := by
  have h1 : f = â†¿(Function.curry f) := by
    ext x
    rfl
  conv_rhs =>
    rw [h1]
  rw [fderiv_uncurry]
  simp
  rfl
  exact h

lemma fderiv_uncurry_clm_comp (f : X â†’ Y â†’ Z) (hf : Differentiable ğ•œ (â†¿f)) :
    fderiv ğ•œ â†¿f
    =
    fun xy =>
      (fderiv ğ•œ (f Â· xy.2) xy.1).comp (ContinuousLinearMap.fst ğ•œ X Y)
      +
      (fderiv ğ•œ (f xy.1 Â·) xy.2).comp (ContinuousLinearMap.snd ğ•œ X Y) := by
  funext xy
  apply ContinuousLinearMap.ext
  intro dxy
  rw [fderiv_uncurry]
  simp only [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_fst', Function.comp_apply, ContinuousLinearMap.coe_snd']
  fun_prop

lemma fderiv_wrt_prod {f : X Ã— Y â†’ Z} {xy} (hf : DifferentiableAt ğ•œ f xy) :
    fderiv ğ•œ f xy
    =
    (fderiv ğ•œ (fun x' => f (x',xy.2)) xy.1).comp (ContinuousLinearMap.fst ğ•œ X Y)
    +
    (fderiv ğ•œ (fun y' => f (xy.1,y')) xy.2).comp (ContinuousLinearMap.snd ğ•œ X Y) := by
  apply ContinuousLinearMap.ext; intro (dx,dy)
  apply fderiv_uncurry (fun x y => f (x,y)) _ _ hf

lemma fderiv_wrt_prod_clm_comp (f : X Ã— Y â†’ Z) (hf : Differentiable ğ•œ f) :
    fderiv ğ•œ f
    =
    fun xy =>
      (fderiv ğ•œ (fun x' => f (x',xy.2)) xy.1).comp (ContinuousLinearMap.fst ğ•œ X Y)
      +
      (fderiv ğ•œ (fun y' => f (xy.1,y')) xy.2).comp (ContinuousLinearMap.snd ğ•œ X Y) :=
  fderiv_uncurry_clm_comp (fun x y => f (x,y)) hf

lemma fderiv_curry_clm_apply (f : X â†’ Y â†’L[ğ•œ] Z) (y : Y) (x dx : X) (h : Differentiable ğ•œ f) :
    fderiv ğ•œ f x dx y
    =
    fderiv ğ•œ (f Â· y) x dx := by
  rw [fderiv_clm_apply] <;> first | simp | fun_prop

TODO "AZ2QN" "Replace following helper lemmas with fun_prop after
    #24056 in Mathlib has gone through."

/-- Helper lemmas showing differentiability from ContDiff ğ•œ 2 â†¿f. -/
lemma ContDiff.two_differentiable (f : X â†’ Y â†’ Z) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (â†¿f) :=
  ContDiff.differentiable hf (by simp)

lemma ContDiff.uncurry (f : X â†’ Y â†’ Z) (x : X) (hf : ContDiff ğ•œ 2 â†¿f) :
    ContDiff ğ•œ 2 (f x) := by
  have h : f x = â†¿f âˆ˜ (x, Â·) := by rfl
  rw [h]
  apply ContDiff.comp
  Â· exact hf
  Â· exact contDiff_prodMk_right x

lemma ContDiff.two_fderiv_differentiable (f : X â†’ Y â†’ Z) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fderiv ğ•œ (â†¿f)) := by
  change ContDiff ğ•œ (1 + 1) â†¿f at hf
  rw [contDiff_succ_iff_fderiv] at hf
  have hd2 := hf.2.2
  apply ContDiff.differentiable hd2
  rfl

/- Helper rw lemmas for proving differentiablity conditions. -/
lemma fderiv_uncurry_comp_fst (f : X â†’ Y â†’ Z) (y : Y) (hf : Differentiable ğ•œ (â†¿f)) :
    fderiv ğ•œ (fun x' => (â†¿f) (x', y))
    =
    fun x => (fderiv ğ•œ (â†¿f) ((Â·, y) x)).comp (fderiv ğ•œ (Â·, y) x) := by
  have hl (y : Y) : (fun x' => (â†¿f) (x', y)) = â†¿f âˆ˜ (Â·, y) := by
    rfl
  rw [hl]
  funext x
  rw [fderiv_comp]
  Â· fun_prop
  Â· fun_prop

lemma fderiv_uncurry_comp_snd (f : X â†’ Y â†’ Z) (x : X) (hf : Differentiable ğ•œ (â†¿f)) :
    fderiv ğ•œ (fun y' => (â†¿f) (x, y'))
    =
    fun y => (fderiv ğ•œ (â†¿f) ((x, Â·) y)).comp (fderiv ğ•œ (x, Â·) y) := by
  have hl (x : X) : (fun y' => (â†¿f) (x, y')) = â†¿f âˆ˜ (x, Â·) := by
    rfl
  rw [hl]
  funext y
  rw [fderiv_comp]
  Â· fun_prop
  Â· fun_prop

lemma fderiv_curry_comp_fst (f : X â†’ Y â†’ Z) (x dx : X) (y : Y)
    (hf : Differentiable ğ•œ (â†¿f)) :
    (fderiv ğ•œ (fun x' => f x' y) x) dx
    =
    (fderiv ğ•œ (â†¿f) ((Â·, y) x)) ((fderiv ğ•œ (Â·, y) x) dx) := by
  have hl (y : Y) : (fun x' => f x' y) = â†¿f âˆ˜ (Â·, y) := by
    rfl
  rw [hl]
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
  Â· fun_prop
  Â· fun_prop

lemma fderiv_curry_comp_snd (f : X â†’ Y â†’ Z) (x : X) (y dy : Y)
    (hf : Differentiable ğ•œ (â†¿f)) :
    (fderiv ğ•œ (fun y' => f x y') y) dy
    =
    (fderiv ğ•œ (â†¿f) ((x, Â·) y)) ((fderiv ğ•œ (x, Â·) y) dy) := by
  have hl (x : X) : (fun y' => f x y') = â†¿f âˆ˜ (x, Â·) := by
    rfl
  rw [hl]
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
  Â· fun_prop
  Â· fun_prop

lemma fderiv_inr_fst_clm (x : X) (y : Y) :
    (fderiv ğ•œ (x, Â·) y) = ContinuousLinearMap.inr ğ•œ X Y := by
  rw [(hasFDerivAt_prodMk_right x y).fderiv]

lemma fderiv_inl_snd_clm (x : X) (y : Y) :
    (fderiv ğ•œ (Â·, y) x) = ContinuousLinearMap.inl ğ•œ X Y := by
  rw [(hasFDerivAt_prodMk_left x y).fderiv]

/- Differentiablity conditions. -/

lemma function_differentiableAt_fst (f : X â†’ Y â†’ Z) (x : X) (y : Y) (hf : Differentiable ğ•œ (â†¿f)) :
    DifferentiableAt ğ•œ (fun x' => f x' y) x := by
  have hl : (fun x' => f x' y) = â†¿f âˆ˜ (Â·, y) := by
    funext x'
    rfl
  rw [hl]
  apply Differentiable.differentiableAt
  apply Differentiable.comp
  Â· fun_prop
  Â· fun_prop

lemma function_differentiableAt_snd (f : X â†’ Y â†’ Z) (x : X) (y : Y) (hf : Differentiable ğ•œ (â†¿f)) :
    DifferentiableAt ğ•œ (fun y' => f x y') y := by
  have hl : (fun y' => f x y') = â†¿f âˆ˜ (x, Â·) := by
    funext y'
    rfl
  rw [hl]
  apply Differentiable.differentiableAt
  apply Differentiable.comp
  Â· fun_prop
  Â· fun_prop

@[fun_prop]
lemma fderiv_uncurry_differentiable_fst (f : X â†’ Y â†’ Z) (y : Y) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fderiv ğ•œ fun x' => (â†¿f) (x', y)) := by
  conv_rhs =>
    ext x
    rw [fderiv_uncurry_comp_fst (hf := hf.two_differentiable)]
  apply Differentiable.clm_comp
  Â· apply Differentiable.comp
    Â· exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_rhs =>
      enter [x]
      rw [fderiv_inl_snd_clm]
    fun_prop

@[fun_prop]
lemma fderiv_uncurry_differentiable_snd (f : X â†’ Y â†’ Z) (x : X) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fderiv ğ•œ fun y' => (â†¿f) (x, y')) := by
  conv_rhs =>
    ext y
    rw [fderiv_uncurry_comp_snd (hf := hf.two_differentiable)]
  apply Differentiable.clm_comp
  Â· apply Differentiable.comp
    Â· exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_rhs =>
      enter [y]
      rw [fderiv_inr_fst_clm]
    fun_prop

@[fun_prop]
lemma fderiv_uncurry_differentiable_fst_comp_snd (f : X â†’ Y â†’ Z) (x : X) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fun y' => fderiv ğ•œ (fun x' => (â†¿f) (x', y')) x) := by
  conv_rhs =>
    enter [y']
    rw [fderiv_uncurry_comp_fst (hf := hf.two_differentiable)]
  apply Differentiable.clm_comp
  Â· apply Differentiable.comp
    Â· exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_rhs =>
      enter [y]
      rw [fderiv_inl_snd_clm]
    fun_prop

lemma fderiv_uncurry_differentiable_fst_comp_snd_apply (f : X â†’ Y â†’ Z) (x Î´x : X)
    (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fun y' => fderiv ğ•œ (fun x' => (â†¿f) (x', y')) x Î´x) := by
  have h1 : (fun y' => fderiv ğ•œ (fun x' => (â†¿f) (x', y')) x Î´x)
    = (fun f => f Î´x) âˆ˜ (fun y' => fderiv ğ•œ (fun x' => (â†¿f) (x', y')) x) := by
    funext y'
    simp
  rw [h1]
  apply Differentiable.comp
  Â· fun_prop
  Â· apply fderiv_uncurry_differentiable_fst_comp_snd
    exact hf

@[fun_prop]
lemma fderiv_uncurry_differentiable_snd_comp_fst (f : X â†’ Y â†’ Z) (y : Y) (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fun x' => fderiv ğ•œ (fun y' => (â†¿f) (x', y')) y) := by
  conv_rhs =>
    enter [x']
    rw [fderiv_uncurry_comp_snd (hf := hf.two_differentiable)]
  apply Differentiable.clm_comp
  Â· apply Differentiable.comp
    Â· exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_rhs =>
      enter [x]
      rw [fderiv_inr_fst_clm]
    fun_prop

lemma fderiv_uncurry_differentiable_snd_comp_fst_apply (f : X â†’ Y â†’ Z) (y Î´y : Y)
    (hf : ContDiff ğ•œ 2 â†¿f) :
    Differentiable ğ•œ (fun x' => fderiv ğ•œ (fun y' => (â†¿f) (x', y')) y Î´y) := by
  have h1 : (fun x' => fderiv ğ•œ (fun y' => (â†¿f) (x', y')) y Î´y)
    = (fun f => f Î´y) âˆ˜ (fun x' => fderiv ğ•œ (fun y' => (â†¿f) (x', y')) y) := by
    funext y'
    simp
  rw [h1]
  apply Differentiable.comp
  Â· fun_prop
  Â· apply fderiv_uncurry_differentiable_snd_comp_fst
    exact hf

@[fun_prop]
lemma fderiv_curry_differentiableAt_fst_comp_snd (f : X â†’ Y â†’ Z) (x dx : X) (y : Y)
    (hf : ContDiff ğ•œ 2 â†¿f) :
    DifferentiableAt ğ•œ (fun y' => (fderiv ğ•œ (fun x' => f x' y') x) dx) y := by
  conv_lhs =>
    enter [x']
    rw [fderiv_curry_comp_fst (hf := hf.two_differentiable)]
  refine DifferentiableAt.clm_apply ?_ ?_
  Â· simp
    refine DifferentiableAt.comp y ?_ ?_
    Â· apply Differentiable.differentiableAt
      exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_lhs =>
      enter [x']
      rw [fderiv_inl_snd_clm]
    fun_prop

lemma fderiv_curry_differentiableAt_snd_comp_fst (f : X â†’ Y â†’ Z) (x : X) (y dy : Y)
    (hf : ContDiff ğ•œ 2 â†¿f) :
    DifferentiableAt ğ•œ (fun x' => (fderiv ğ•œ (fun y' => f x' y') y) dy) x := by
  conv_lhs =>
    enter [x']
    rw [fderiv_curry_comp_snd (hf := hf.two_differentiable)]
  refine DifferentiableAt.clm_apply ?_ ?_
  Â· simp
    refine DifferentiableAt.comp x ?_ ?_
    Â· apply Differentiable.differentiableAt
      exact hf.two_fderiv_differentiable
    Â· fun_prop
  Â· conv_lhs =>
      enter [x']
      rw [fderiv_inr_fst_clm]
    fun_prop

/- fderiv commutes on X Ã— Y. -/
lemma fderiv_swap [IsRCLikeNormedField ğ•œ] (f : X â†’ Y â†’ Z) (x dx : X) (y dy : Y)
    (hf : ContDiff ğ•œ 2 â†¿f) :
    fderiv ğ•œ (fun x' => fderiv ğ•œ (fun y' => f x' y') y dy) x dx
    =
    fderiv ğ•œ (fun y' => fderiv ğ•œ (fun x' => f x' y') x dx) y dy := by
  have hf' : IsSymmSndFDerivAt ğ•œ (â†¿f) (x,y) := by
    apply ContDiffAt.isSymmSndFDerivAt (n := 2)
    Â· exact ContDiff.contDiffAt hf
    Â· simp
  have h := IsSymmSndFDerivAt.eq hf' (dx,0) (0,dy)
  rw [fderiv_wrt_prod_clm_comp, fderiv_wrt_prod_clm_comp] at h
  simp only [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_fst', Function.comp_apply, ContinuousLinearMap.coe_snd', map_zero,
    add_zero, zero_add] at h
  rw [fderiv_curry_clm_apply, fderiv_curry_clm_apply] at h
  simp only [ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_fst', Function.comp_apply, map_zero, ContinuousLinearMap.coe_snd',
    zero_add, add_zero] at h
  exact h
  /- Start of differentiablity conditions. -/
  Â· refine Differentiable.add ?_ ?_
    Â· refine Differentiable.clm_comp ?_ ?_
      Â· apply fderiv_uncurry_differentiable_fst_comp_snd
        exact hf
      Â· fun_prop
    Â· refine Differentiable.clm_comp ?_ ?_
      Â· apply fderiv_uncurry_differentiable_snd
        exact hf
      Â· fun_prop
  Â· refine Differentiable.add ?_ ?_
    Â· refine Differentiable.clm_comp ?_ ?_
      Â· apply fderiv_uncurry_differentiable_fst
        exact hf
      Â· fun_prop
    Â· refine Differentiable.clm_comp ?_ ?_
      Â· apply fderiv_uncurry_differentiable_snd_comp_fst
        exact hf
      Â· fun_prop
  Â· exact hf.two_differentiable
  Â· exact hf.two_fderiv_differentiable
